;-------------------------------------------------
;背面座位アナル
;セックス系コマンド、レベル3
;-------------------------------------------------
@COM70
;前回の調教が3Pのとき3Pへ
IF PREVCOM == 71
	CALL COM_ABLE71
	SIF RESULT
		JUMP COM71

ENDIF
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER

;奉仕快楽フラグ
TCVAR:PLAYER:108 = 1

CSTR:PLAYER:10 += @"{TARGET}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:PLAYER:2 = 2
;強度
TCVAR:PLAYER:4 = 2000

CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:TARGET:2 = 12
;強度
TCVAR:TARGET:4 = 1300

DOWNBASE:PLAYER:体力 += 80

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 150
	SOURCE:PLAYER:快Ｃ = 400
	;SOURCE:快Ｃ = 250
	SOURCE:快Ａ = 350
	SOURCE:快Ｂ = 250
	SOURCE:苦痛 = 350

ELSE

	DOWNBASE:体力 += 70
	DOWNBASE:気力 += 80
	SOURCE:PLAYER:快Ｃ = 400

	;SOURCE:快Ｃ = 250
	SOURCE:快Ａ = 350
	SOURCE:快Ｂ = 250
	SOURCE:情愛 = 350
	SOURCE:苦痛 = 700
	SOURCE:露出 = 200
	SOURCE:逸脱 = 800
	SOURCE:反感 = 1200

	;上のほうで計算した汚れデータ
	SOURCE:不潔 = Y*10 + 60

	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;抑圧、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 900
ENDIF
;Aへの苦痛は先に処理しておく
CALL PAIN_CHECK_A(TARGET)

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;奴隷のＡ⇔調教者のＰの汚れが移動
CALL STAIN_アナルＰ挿入

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:Ａ経験 ++
EXP:PLAYER:挿入経験 ++
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
	ENDIF
		EXP:PLAYER:時姦経験 += 1
ELSE
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 4
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
	ENDIF
	EXP:Ａ性交経験 ++
	;愛情経験
	SIF !ASSIPLAY
		EXP:愛情経験 += TALENT:恋慕
ENDIF
RETURN 1

@CAN_COM70(ARG)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
IF FLAG:70 == 1
	SKIPDISP ARG
	PRINTL 背面座位でアナルを犯す
ELSE
	;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
	IF !TEQUIP:縄
		SKIPDISP ARG
		PRINTL 背面座位アナル
		A = 0
		S = 0
		;すべての命令に共通の要素を考慮
		;(従順が高いと命令に従いやすいなど)
		CALL COM_ORDER

		;ABL:欲望
		IF ABL:欲望
			SIF S
				PRINT  + 
			A += ABL:欲望 * 3
			PRINT 欲望
			PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
			S = 1
		ENDIF
		;ABL:Ａ感覚
		IF ABL:Ａ感覚
			SIF S
				PRINT  + 
			A += ABL:Ａ感覚 * 2
			PRINT Ａ感覚
			PRINTV 'LV,ABL:Ａ感覚,'(,ABL:Ａ感覚 * 2,')
			S = 1
		ENDIF
		;快楽刻印
		IF MARK:快楽刻印
			SIF S
				PRINT  + 
			A += MARK:快楽刻印 * 3
			PRINT 快楽刻印
			PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 3,')
			S = 1
		ENDIF
		;PALAM:潤滑（の不足）
		IF PALAM:潤滑 < PALAMLV:3
			PRINT  - 
			A -= 5
			PRINT 潤滑不足
			PRINTV '(,5,')
			S = 1
		ENDIF
		;PALAM:欲情
		IF GETPALAMLV(PALAM:欲情,5)
			SIF S
				PRINT  + 
			A += GETPALAMLV(PALAM:欲情,5) * 3
			PRINT 欲情
			PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 3,')
			S = 1
		ENDIF
		;恥じらい
		IF TALENT:羞恥心 > 0
			PRINT  - 
			A -= 2
			PRINT 恥じらい
			PRINTV '(,2,')
			S = 1
		ENDIF
		;快感の否定
		IF TALENT:快感応答 < 0 && !TALENT:淫乱
			PRINT  - 
			A -= 5
			PRINT 快感の否定
			PRINTV '(,5,')
			S = 1
		ENDIF
		;男嫌い（恋慕、親愛で無効）
		IF OTOKOGIRAI(TARGET)
			PRINT  - 
			A -= 12
			PRINT 男嫌い
			PRINTV '(,12,')
			S = 1
		ENDIF
		;淫乱
		IF TALENT:淫乱 && ASSIPLAY == 0
			SIF S
				PRINT  + 
			A += 15
			PRINT 淫乱
			PRINTV '(,15,')
			S = 15
		ENDIF
		;娼婦
		IF TALENT:淫乱 > 1 && ASSIPLAY == 0
			SIF S
				PRINT  + 
			A += 5
			PRINT 娼婦
			PRINTV '(,5,')
			S = 5
		ENDIF
		;A経験
		IF EXP:Ａ経験 < EXPLV:2
			PRINT  - 
			A -= 30
			PRINT Ａ経験不足
			PRINTV '(,30,')
			S = 1
		ENDIF

		;調教者がふたなり
		IF (TALENT:PLAYER:2 & 3) == 3
			SIF S
				PRINT  + 
			A += 4
			PRINT ふたなり
			PRINTV '(,4,')
			S = 1
		ENDIF

		;媚薬
		IF TCVAR:媚薬
			SIF S
				PRINT  + 
			A += 6
			PRINT 媚薬
			PRINTV '(,6,')
			S = 1
		ENDIF
		Y = 0
		;愛液の汚れ
		SIF STAIN:PLAYER:2 & 1
			Y += 1
		;精液の汚れ
		SIF STAIN:PLAYER:2 & 4
			Y += 3
		;アナルの汚れ
		SIF STAIN:PLAYER:2 & 8
			Y += 7
		SIF STAIN:PLAYER:2 & 16
			Y += 15

		Y = Y * (2 - TALENT:汚臭耐性) / 2

		;汚れあり
		IF Y
			PRINT  - 
			A -= Y
			PRINT 汚れあり
			;汚臭鈍感
			IF TALENT:汚臭耐性 > 0
				PRINT 、汚臭鈍感
			ELSEIF TALENT:汚臭耐性 < 0
				PRINT 、汚臭敏感
			ENDIF
			PRINTV '(,Y,')
			S = 1
		ENDIF

		;合計を表示(52以上で実行)
		PRINT  = 
		PRINTV A

		V = 52
		SIF A < V
			PRINT  < 
		SIF A == V
			PRINT  = 
		SIF A > V
			PRINT  > 
		PRINT 実行値
		PRINTV V
	ELSE
		SKIPDISP ARG
		PRINTL 束縛背面座位アナル
	ENDIF
	WAIT
	SKIPDISP 0
	;実行できない
	SIF A < V
		RETURN 0
	IF TALENT:妊娠 && (CFLAG:903 <= 30 || CFLAG:903 >= 71)
		PRINTFORMW お腹の子の為にも今は%CALLNAME:ARG%に無理はさせない方がいいだろう
		RETURN 0
	ENDIF
ENDIF
SKIPDISP 0
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE70
;背面座位アナル実行判定
SIF !TFLAG:100
	RETURN 0
SIF GLOBAL_COMABLE(70)
	RETURN RESULT
;調教者がオトコ、ふたなりの場合はある程度勃起してないとダメ
SIF (TALENT:PLAYER:2 & 2) && BASE:PLAYER:勃起 < 1000
	RETURN 0
;アナルバイブ挿入中はダメ
SIF TEQUIP:14
	RETURN 0
;アナルビーズ使用中は不可
SIF TEQUIP:15
	RETURN 0
;普通の浣腸使用中は不可
SIF TEQUIP:21
	RETURN 0
;バルーン使用中は不可
SIF TEQUIP:22
	RETURN 0
;電極使用中は不可
SIF TEQUIP:23
	RETURN 0
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;一定のＡ経験が必要
SIF EXP:2 < EXPLV:3
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
IF TEQUIP:31
	SIF ITEM:20 == 0 && NOITEM == 0
		RETURN 0
ENDIF
;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
;体格差がある場合は経験がないとダメ
SIF 挿入不可(TARGET) == 1 && EXP:Ａ経験 < 50 && !TALENT:PLAYER:禁断の知識
	RETURN 0
;さらに体格差があると禁断の知識が必要
SIF 挿入不可(TARGET) == 2 && (!TALENT:PLAYER:禁断の知識 || EXP:Ａ経験 < 50)
	RETURN 0
SIF 挿入不可(TARGET) == 3
	RETURN 0
;調教者がオトコ、半人半妖、ふたなり、もしくはペニスバンドがないとダメ
SIF !(TALENT:PLAYER:2 & 2) && ITEM:PBAND == 0
	RETURN 0
;潤滑不足で、従順3以下・レズっ気3以下のASSIの場合はできない（サドならＯＫ）
IF PALAM:潤滑 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:従順 <= 3 || ABL:ASSI:技巧 <= 3) && TALENT:ASSI:サド == 0
		RETURN 0
ENDIF
;対面座位、正常位からはダメ
SIF TEQUIP:体位 == 4 || TEQUIP:体位 == 1
	RETURN 0
;睡眠姦中
SIF CFLAG:イタズラ > 1
	RETURN 0
;室内でないかバスルームでプレイマットがないとダメ
SIF !INROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20)
	RETURN 0
RETURN 1


;-------------------------------------------------
;手淫する
;
;-------------------------------------------------
@COM124
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;奉仕快楽フラグ
TCVAR:PLAYER:108 = 1


CSTR:TARGET:10 += @"{PLAYER}/"
TCVAR:TARGET:2 = 3
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具）
TCVAR:TARGET:4 = 500

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;性技補正
IF ABL:PLAYER:指 < 4
	SOURCE:快Ｃ += 80
ELSE
	SOURCE:快Ｃ += ABL:PLAYER:指 * 20
ENDIF

;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 40
	SOURCE:快Ｃ = 80
ELSE
	DOWNBASE:PLAYER:体力 += 10
	DOWNBASE:PLAYER:気力 += 50
	DOWNBASE:気力 += 50
	SOURCE:快Ｃ = 80
	
;とりあえず手淫の数値のままで試行
	SOURCE:情愛 = 50
	SOURCE:性行動 = 250
	SOURCE:達成 = 50
	SOURCE:屈従 = 350
	SOURCE:逸脱 = 100
	SOURCE:反感 = 100

	;上のほうで計算した汚れデータ
	SOURCE:不潔 = Y*10 + 60

	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;抑圧、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 200
ENDIF
;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;調教者の指⇔奴隷のＰの汚れが移動
STAIN:PLAYER:1 |= STAIN:2
STAIN:2 |= STAIN:PLAYER:1

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;手淫経験
EXP:PLAYER:手淫経験 ++

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF FLAG:70 == 1 || CFLAG:イタズラ
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ELSE
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 1
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ENDIF
RETURN 1

@CAN_COM124(ARG)
SKIPDISP ARG
PRINTL 手淫する
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
IF FLAG:70 == 1 || CFLAG:イタズラ

ELSE
	A = 0
	S = 0

	;すべての命令に共通の要素を考慮
	;(従順が高いと命令に従いやすいなど)
	CALL COM_ORDER

	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		A += ABL:欲望 * 1
		PRINT 欲望
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 1,')
		S = 1
	ENDIF
	;MARK:快楽刻印
	IF MARK:快楽刻印
		SIF S
			PRINT  + 
		A += MARK:快楽刻印 * 1
		PRINT 快楽刻印
		PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 1,')
		S = 1
	ENDIF
	;PALAM:欲情
	IF GETPALAMLV(PALAM:欲情,5)
		SIF S
			PRINT  + 
		A += GETPALAMLV(PALAM:欲情,5) * 1
		PRINT 欲情
		PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 1,')
		S = 1
	ENDIF
	;恥じらい
	IF TALENT:羞恥心 > 0
		PRINT  - 
		A -= 1
		PRINT 恥じらい
		PRINTV '(,1,')
		S = 1
	ENDIF
	;快感の否定（淫乱系素質で無効）
	IF TALENT:快感応答 < 0 && !TALENT:淫乱
		PRINT  - 
		A -= 1
		PRINT 快感の否定
		PRINTV '(,1,')
		S = 1
	ENDIF
	;男嫌い（恋慕、親愛で無効）
	IF OTOKOGIRAI(TARGET)
		PRINT  - 
		A -= 5
		PRINT 男嫌い
		PRINTV '(,5,')
		S = 5
	ENDIF
	;恋慕
	IF TALENT:恋慕 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 5
		PRINT 恋慕
		PRINTV '(,5,')
		S = 5
	ENDIF
	;親愛
	IF TALENT:恋慕 > 1 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 5
		PRINT 親愛
		PRINTV '(,5,')
		S = 5
	ENDIF

	;調教者がふたなり
	;同姓にちんこ触られて嬉しいものだろうか？
	;試しにマイナスに反転させてみる
	IF (TALENT:PLAYER:2 & 3) == 3
		SIF S
			PRINT  - 
		A -= 8
		PRINT ふたなり
		PRINTV '(,8,')
		S = 1
	ENDIF

	;この付近は未着手
	Y = 0
	;愛液の汚れ
	SIF STAIN:PLAYER:2 & 1
		Y += 1
	;精液の汚れ
	SIF STAIN:PLAYER:2 & 4
		Y += 3
	;アナルの汚れ
	SIF STAIN:PLAYER:2 & 8
		Y += 7
	SIF STAIN:PLAYER:2 & 16
		Y += 15
	;手扱きでは汚れがそれほど影響しない
	Y /= 3
	Y = Y * (2 - TALENT:汚臭耐性) / 2

	;汚れあり
	IF Y
		PRINT  - 
		A -= Y
		PRINT 汚れあり
		;汚臭鈍感
		IF TALENT:汚臭耐性 > 0
			PRINT 、汚臭鈍感
		ELSEIF TALENT:汚臭耐性 < 0
			PRINT 、汚臭敏感
		ENDIF
		PRINTV '(,Y,')
		S = 1
	ENDIF


	;合計を表示(14以上で実行)
	PRINT  = 
	PRINTV A

	V = 14
	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V

	WAIT
	SKIPDISP 0
	;実行できない
	SIF A < V
		RETURN 0
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE124
;手淫する
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(124)
	RETURN RESULT
;調教者がオトコだとダメ
;SIF !(TALENT:PLAYER:2 & 1)
;	RETURN 0
;オトコ、半人半妖、ふたなりでないとダメ
SIF !(TALENT:2 & 2) 
	RETURN 0
RETURN 1


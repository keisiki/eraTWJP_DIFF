;-------------------------------------------------
;アナル舐め
;愛撫系コマンド、レベル1
;-------------------------------------------------
@COM4
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;奉仕快楽
TCVAR:PLAYER:108 = 1

CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:TARGET:2 = 12
;強度
TCVAR:TARGET:4 = 500

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 50
	SOURCE:快Ａ = 50
	SOURCE:液体 = 50
	SOURCE:快Ａ += ABL:PLAYER:舌 * 2

	;調教者のTALENT:舌使いをみる
	SIF TALENT:PLAYER:舌使い
		TIMES SOURCE:快Ａ , 1.50
		
	;調教者のTALENT:猫舌をみる
	SIF TALENT:PLAYER:猫舌
		TIMES SOURCE:快Ａ , 0.75
	;奴隷のＡ⇔調教者の口の汚れが移動
	STAIN:4 |= STAIN:PLAYER:0
	STAIN:PLAYER:0 |= STAIN:4

	;同性愛　レズなら戻り値1ゲイなら戻り値2
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 3
	ENDIF

	;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
	SIF TALENT:PLAYER:猫舌
		EXP:PLAYER:嗜虐快楽経験 += 1
	EXP:PLAYER:口淫経験 ++
ELSE

	DOWNBASE:体力 += 5
	DOWNBASE:気力 += 50

	SOURCE:快Ａ = 50
	SOURCE:液体 = 50
	SOURCE:逸脱 = 40
	SOURCE:反感 = 30

	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;抑圧、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 50

	SOURCE:快Ａ += ABL:PLAYER:舌 * 2

	;調教者のTALENT:舌使いをみる
	SIF TALENT:PLAYER:舌使い
		TIMES SOURCE:快Ａ , 1.50
		
	;調教者のTALENT:猫舌をみる
	SIF TALENT:PLAYER:猫舌
		TIMES SOURCE:快Ａ , 0.75

	;-------------------------------------------------
	;汚れの処理
	;-------------------------------------------------
	;奴隷のＡ⇔調教者の口の汚れが移動
	STAIN:4 |= STAIN:PLAYER:0
	STAIN:PLAYER:0 |= STAIN:4
	;-------------------------------------------------
	;経験の処理
	;-------------------------------------------------
	;同性愛　レズなら戻り値1ゲイなら戻り値2
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 3
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 3
	ENDIF

	;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
	SIF TALENT:PLAYER:猫舌
		EXP:PLAYER:嗜虐快楽経験 += 1
	EXP:PLAYER:口淫経験 ++
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE4
;アナル舐め実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(4)
	RETURN RESULT
;性器が汚れている
SIF STAIN:4 >= 2 && (STAIN:3 != 16 && STAIN:3 != 17) && ASSIPLAY == 0 && TALENT:PLAYER:汚臭耐性 < 2
	RETURN 0

;猫舌の調教者の場合は技巧Lv2以上必要
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 2
	RETURN 0

;下半身着衣
SIF TEQUIP:0 & 4 || TEQUIP:0 & 2
	RETURN 0
;アナルバイブ使用中は不可
SIF TEQUIP:14
	RETURN 0
;アナルビーズ使用中は不可
SIF TEQUIP:15
	RETURN 0
;普通の浣腸使用中は不可
SIF TEQUIP:21
	RETURN 0
;バルーン使用中は不可
SIF TEQUIP:22
	RETURN 0
;電極使用中は不可
SIF TEQUIP:23
	RETURN 0
RETURN 1


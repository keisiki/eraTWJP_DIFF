;-------------------------------------------------
;指挿入れ
;愛撫系コマンド、レベル1
;-------------------------------------------------
@COM3
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;奉仕快楽
TCVAR:PLAYER:108 = 1

;破瓜抑制
TFLAG:2 = 1

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 40
	IF !TALENT:処女 || TALENT:処女 == -1
		SOURCE:快Ｖ = 120
		SOURCE:苦痛 = 10
	ELSE
		SOURCE:快Ｖ = 60
		SOURCE:苦痛 = 5
	ENDIF
	SIF TALENT:PLAYER:器用な指
		SOURCE:快Ｖ += 20
	SOURCE:快Ｖ += ABL:PLAYER:指 * 5
	;Vへの苦痛
	CALL PAIN_CHECK_V(TARGET)
	;奴隷の膣内⇔調教者の指の汚れが移動
	STAIN:6 |= STAIN:PLAYER:1
	STAIN:PLAYER:1 |= STAIN:6
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
	EXP:Ｖ経験 ++
	EXP:PLAYER:手淫経験 ++
ELSE

	DOWNBASE:体力 += 30
	DOWNBASE:気力 += 80

	IF !TALENT:処女 || TALENT:処女 == -1
		SOURCE:快Ｖ = 120
		SOURCE:苦痛 = 15
		SOURCE:露出 = 10
		SOURCE:逸脱 = 20
		SOURCE:反感 = 20
	;V経験が0の時はV経験を伴わない物に派生
	;Vの珠は入らないが、快Vが高いと慣らしてあるため処女喪失時に影響
	ELSE
		SOURCE:快Ｖ = 60
		SOURCE:苦痛 = 10
		SOURCE:露出 = 30
		SOURCE:逸脱 = 30
		SOURCE:反感 = 30
	ENDIF

	SIF TALENT:PLAYER:器用な指
		SOURCE:快Ｖ += 20
	SOURCE:快Ｖ += ABL:PLAYER:指 * 5
	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;抑圧、抵抗持ちには、抑鬱ソース追加
	IF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 20
		;処女
		SIF TALENT:処女
			SOURCE:鬱屈 += 10
	ENDIF
	;Vへの苦痛
	CALL PAIN_CHECK_V(TARGET)

	;-------------------------------------------------
	;汚れの処理
	;-------------------------------------------------
	;奴隷の膣内⇔調教者の指の汚れが移動
		STAIN:6 |= STAIN:PLAYER:1
		STAIN:PLAYER:1 |= STAIN:6
	;-------------------------------------------------
	;経験の処理
	;-------------------------------------------------
	;同性愛　レズなら戻り値1ゲイなら戻り値2
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 1
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
	EXP:Ｖ経験 ++
	EXP:PLAYER:手淫経験 ++
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE3
;指挿入れ実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(3)
	RETURN RESULT
;オトコだとダメ
SIF !(TALENT:2 & 1)
	RETURN 0
;時間停止中は許可
IF FLAG:70 != 1
	;潤滑不足で、従順3以下・レズっ気3以下のASSIの場合アナル愛撫はできない（サドならＯＫ）
	IF PALAM:潤滑 < PALAMLV:2 && ASSIPLAY
		SIF (ABL:ASSI:従順 <= 3 || ABL:ASSI:レズっ気 <= 3) && !TALENT:ASSI:サド
			RETURN 0
	ENDIF
ENDIF

;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
RETURN 1


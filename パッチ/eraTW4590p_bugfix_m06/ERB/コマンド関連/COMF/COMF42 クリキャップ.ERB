;-------------------------------------------------
;クリキャップ
;道具使用コマンド、レベル2
;-------------------------------------------------
@COM42
;着脱の処理
TEQUIP:クリキャップ = !TEQUIP:クリキャップ

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;奉仕快楽フラグ
TCVAR:PLAYER:108 = 1

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 80
	SOURCE:快Ｃ += 200

ELSE

	DOWNBASE:体力 += 10
	DOWNBASE:気力 += 80

	SOURCE:快Ｃ += 200
	SOURCE:露出 += 20
	SOURCE:逸脱 += 30
	SOURCE:反感 += 30


	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;抑圧、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 30
ENDIF
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ELSE
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 1
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ENDIF
EXP:PLAYER:道具使用経験 ++

RETURN 1
;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM42(ARG)
PRINTL ＜クリキャップ装備中＞

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 20
	SOURCE:ARG:快Ｃ += 100

ELSE

	DOWNBASE:ARG:体力 += 5
	DOWNBASE:ARG:気力 += 20

	SOURCE:ARG:快Ｃ += 100
	SOURCE:ARG:露出 += 10
	SOURCE:ARG:逸脱 += 15
	SOURCE:ARG:反感 += 15
ENDIF
;-------------------------------------------------
;経験の処理
;-------------------------------------------------

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE42
;電動クリキャップ実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(42)
	RETURN RESULT
;解除はいつでも可能
SIF TEQUIP:11
	RETURN 1
;アイテムを持っているか、アイテムなし設定でないとダメ
SIF ITEM:2 == 0 && NOITEM == 0
	RETURN 0

;下半身着衣
SIF TEQUIP:0 > 1
	RETURN 0
;オトコやふたなりはダメ
SIF TALENT:2 & 2
	RETURN 0
;ASSIの場合、技巧3以上じゃないと使えない。
IF ASSIPLAY
	SIF ABL:ASSI:技巧 < 3
		RETURN 0
ENDIF
;シャワー中はダメ
SIF TEQUIP:32
	RETURN 0
RETURN 1


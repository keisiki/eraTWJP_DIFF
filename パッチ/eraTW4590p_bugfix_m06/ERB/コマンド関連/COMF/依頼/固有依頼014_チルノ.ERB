@固有依頼14(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE ARG % 1000
;ID101：あー！そー！ぼー！
CASE 101
	SELECTCASE O_DATA
	CASE "依頼名"
		CALLF MAKE_STR(V_NAME, "あー！そー！ぼー！")
	CASE "依頼内容表示"
		CALLF MAKE_STR(V_NAME, "一緒に遊ぶ")
	CASE "依頼報酬表示"
		CALLF MAKE_STR(V_NAME, "いいもの")
	CASE "依頼実行コマンド名"
		CALLF MAKE_STR(V_NAME, "遊びに付き合う")
		
	CASE "依頼内容"
		CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(0, "作業"))
	CASE "依頼サブ"
	CASE "依頼期限"
	CASE "依頼発生率"
	;これは他の固有依頼との相対確率なんで10％ではありません
		CALLF MAKE_INT(V_NAME, 10)
	CASE "依頼発生条件"
		CALLF MAKE_INT(V_NAME, 1)
	CASE "依頼受託可能条件"
	;受託可能は@COM_ABLE366も確認のこと
	;ここを参照するのは依頼人をTARGETにしてる時なので素通しでよし
		CALLF MAKE_INT(V_NAME, 1)
	CASE "依頼実行可能条件"
	;実行可能は@COM_ABLE367も確認のこと
		IF SHIRAHU(TARGET) != 1
		ELSE
			CALLF MAKE_INT(V_NAME, 1)
		ENDIF
	;成功/失敗処理はCARRYOUTで直接書く
	;SOURCEやITEM入手など、内容が複雑なので
	CASE "依頼消滅条件"
	;30％で消失
		SIF RAND:100 < 30
			CALLF MAKE_INT(V_NAME, 1)
	ENDSELECT
ENDSELECT



;固有依頼の設定用
;この関数は実行/成功/失敗時の
;報酬とペナルティと地の文のみを扱います
;各キャラのメッセージは別所にて
@IRAI_固有14(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
SELECTCASE STR_DATA_IRAI(IRAI_ID, "依頼名", CLIENT_ID)
;個々の依頼名で分岐
CASE "あー！そー！ぼー！"
	SELECTCASE SCENE
	CASE "依頼条件表示"
	;何か専用の表示情報を設けたい時用
	;受託済みの依頼表示で使用されます
	;
	;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも無しで
	;ここ以外の表示項目は
	;PRINTFORML 　　　　　：
	;となっているのでスペースを揃えたほうが見栄えが良いです
	
	CASE "依頼実行時"
	;このCASEに限り、RESULTが意味を持ちます
	;依頼実行時のRESULTにはIRAI_CONST_RESULT()を使用してください
	;可能な引数は"キャンセル", "実行して成功", "実行して失敗", "実行して未解決"です
	;独自処理を仕込みたい時は"実行して未解決"で先延ばしするなりしてください
		SOURCE:歓楽 = 1500
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:TARGET%とキャッチボールをする事にした
		PRINTFORMW %CALLNAME:TARGET%は終始笑顔だった
		PRINTL
		TIME += 20
		DOWNBASE:体力 = 150
		DOWNBASE:気力 = 100
		DOWNBASE:MASTER:体力 = 150
		DOWNBASE:MASTER:気力 = 100
		RETURN IRAI_CONST_RESULT("実行して成功")
	CASE "依頼成功時"
	;成功報告時の報酬など
		ITEM:カエル ++
		TALENT:TARGET:機嫌 = LIMIT(TALENT:機嫌 + 1, -1, 1)
		CALL CHANGE_CFLAG(2, 14, 30)
		CALL CHANGE_CFLAG(4, 14, 15)
		PRINTFORML %CALLNAME:MASTER%は依頼報酬を受け取った
		PRINTFORMW ……このカエル、どうしようか？
	CASE "依頼失敗時"
	;失敗報告時の処理
	ENDSELECT
ENDSELECT

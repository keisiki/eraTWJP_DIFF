@固有依頼76(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE ARG % 1000
;ID101：お盆マジつらい
;まー、幻想入りしてないだろうし独自解釈の塊だが
;忙しそうだよなぁと思ったのである
CASE 101
	SELECTCASE O_DATA
	CASE "依頼名"
		CALLF MAKE_STR(V_NAME, "お盆マジつらい")
	CASE "依頼内容表示"
		CALLF MAKE_STR(V_NAME, "三途の渡守の補佐をする")
	CASE "依頼報酬表示"
		CALLF MAKE_STR(V_NAME, @"\\5000")
	CASE "依頼実行コマンド名"
		CALLF MAKE_STR(V_NAME, "小町のお手伝い")
		
	CASE "依頼内容"
		CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(0, "作業"))
	CASE "依頼サブ"
	CASE "依頼期限"
	;発生時ではなく受託時に確定します
	CASE "依頼発生率"
	;これは他の固有依頼との相対確率なんで10％ではありません
		CALLF MAKE_INT(V_NAME, 10)
	CASE "依頼発生条件"
		SIF GET_MONTH() == "夏の月" && INRANGE(DAY:3, 1, 15)
			CALLF MAKE_INT(V_NAME, 1)
	CASE "依頼受託可能条件"
	;受託可能は@COM_ABLE366も確認のこと
		;夏の月の前半をお盆認定
		IF !(GET_MONTH() == "夏の月" && INRANGE(DAY:3, 1, 15))
		;土日は休む
		ELSEIF GROUPMATCH(GET_DAY(), "土", "日")
		;仕事の時間帯でない
		ELSEIF !INRANGE(TIME / 60, 10, 15)
		ELSEIF CFLAG:76:宴会参加
		;小町といっしょに三途の川にいる
		ELSEIF ALLSAMES(CFLAG:76:現在位置, CFLAG:MASTER:現在位置, 三途の川) 
			CALLF MAKE_INT(V_NAME, 1)
		ENDIF
	CASE "依頼実行可能条件"
	;実行可能は@COM_ABLE367も確認のこと
		SIF ALLSAMES(CFLAG:76:現在位置, CFLAG:MASTER:現在位置, 三途の川) 
			CALLF MAKE_INT(V_NAME, 1)
	;成功/失敗処理はCARRYOUTで直接書く
	;SOURCEやITEM入手など、内容が複雑なので
	CASE "依頼消滅条件"
		SIF !(GET_MONTH() == "夏の月" && INRANGE(DAY:3, 1, 15))
			CALLF MAKE_INT(V_NAME, 1)
	ENDSELECT
ENDSELECT



;固有依頼の設定用
;この関数は実行/成功/失敗時の
;報酬とペナルティと地の文のみを扱います
;各キャラのメッセージは別所にて
@IRAI_固有76(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
SELECTCASE STR_DATA_IRAI(IRAI_ID, "依頼名", CLIENT_ID)
;個々の依頼名で分岐
CASE "お盆マジつらい"
	SELECTCASE SCENE
	CASE "依頼条件表示"
	;何か専用の表示情報を設けたい時用
	;受託済みの依頼表示で使用されます
	;
	;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも無しで
	;ここ以外の表示項目は
	;PRINTFORML 　　　　　：
	;となっているのでスペースを揃えたほうが見栄えが良いです
	
	CASE "依頼実行時"
	;このCASEに限り、RESULTが意味を持ちます
	;依頼実行時のRESULTにはIRAI_CONST_RESULT()を使用してください
	;可能な引数は"キャンセル", "実行して成功", "実行して失敗", "実行して未解決"です
	;独自処理を仕込みたい時は"実行して未解決"で先延ばしするなりしてください
		DOWNBASE:MASTER:体力 += 100
		DOWNBASE:MASTER:気力 += 50
		PRINTFORMW %CALLNAME:MASTER%は亡者でごった返す河原で死神たちに混じって働いた
		TIME += 120
		RETURN IRAI_CONST_RESULT("実行して成功")
	CASE "依頼成功時"
	;成功報告時の報酬など
		SOURCE:TARGET:歓楽 += 500
		SOURCE:TARGET:征服 += 500
		;システムメッセージつき好感度変化
		;最後の引数だけ好感度が変化
		CALL CHANGE_CFLAG(2, TARGET, 20)
		;システムメッセージつき信頼度変化
		;最後の引数だけ信頼度が変化
		CALL CHANGE_CFLAG(4, TARGET, 10)
		MONEY += 5000
		PRINTFORMW %CALLNAME:MASTER%は依頼報酬の\\5000を受け取った
	CASE "依頼失敗時"
	;失敗報告時の処理
		;システムメッセージつき好感度変化
		;最後の引数だけ好感度が変化
		CALL CHANGE_CFLAG(2, TARGET, -20)
		;システムメッセージつき信頼度変化
		;最後の引数だけ信頼度が変化
		CALL CHANGE_CFLAG(4, TARGET, -10)
	ENDSELECT
	
ENDSELECT

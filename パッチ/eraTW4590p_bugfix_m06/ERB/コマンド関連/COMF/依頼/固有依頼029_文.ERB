@固有依頼29(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE ARG % 1000
;ID101：お酒をください
CASE 101
	SELECTCASE O_DATA
	CASE "依頼名"
		CALLF MAKE_STR(V_NAME, "お酒をください")
	CASE "依頼内容表示"
		CALLF MAKE_STR(V_NAME, "お酒を三つ以上納入する")
	CASE "依頼報酬表示"
		CALLF MAKE_STR(V_NAME, @"\\5000")
	CASE "依頼実行コマンド名"
		CALLF MAKE_STR(V_NAME, "お酒を渡す")
		
	;依頼対象が実行時に決定するので
	;とりあえず作業にしてCLIENTとの同室判定だけさせるよ
	CASE "依頼内容"
		CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(0, "作業"))
	CASE "依頼サブ"
	CASE "依頼期限"
	;発生時ではなく受託時に確定します
		CALLF MAKE_INT(V_NAME, 8)
	CASE "依頼発生率"
	;これは他の固有依頼との相対確率なんで10％ではありません
		CALLF MAKE_INT(V_NAME, 10)
	CASE "依頼発生条件"
		CALLF MAKE_INT(V_NAME, 1)
	CASE "依頼受託可能条件"
	;受託可能は@COM_ABLE366も確認のこと
	;ここを参照するのは依頼人をTARGETにしてる時なので素通しでよし
		CALLF MAKE_INT(V_NAME, 1)
	CASE "依頼実行可能条件"
	;実行可能は@COM_ABLE367も確認のこと
		SIF MAX(ITEM:51, ITEM:52, ITEM:53) >= 3
			CALLF MAKE_INT(V_NAME, 1)
	;成功/失敗処理はCARRYOUTで直接書く
	;SOURCEやITEM入手など、内容が複雑なので
	CASE "依頼消滅条件"
	;30％で消失
		SIF RAND:100 < 30
			CALLF MAKE_INT(V_NAME, 1)
	ENDSELECT
;ID102：街頭アンケート
CASE 102
	SELECTCASE O_DATA
	CASE "依頼名"
		CALLF MAKE_STR(V_NAME, "街頭アンケート")
	CASE "依頼内容表示"
		CALLF MAKE_STR(V_NAME, "人里広場での聞き込み")
	CASE "依頼報酬表示"
		CALLF MAKE_STR(V_NAME, @"\\3000")
	CASE "依頼実行コマンド名"
		CALLF MAKE_STR(V_NAME, "街頭アンケート")
		
	CASE "依頼内容"
		;人里中央広場(202)が対象
		CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(202, "伝令（場所）"))
	CASE "依頼サブ"
	CASE "依頼期限"
	;発生時ではなく受託時に確定します
		CALLF MAKE_INT(V_NAME, 7)
	CASE "依頼発生率"
	;これは他の固有依頼との相対確率なんで10％ではありません
		CALLF MAKE_INT(V_NAME, 10)
	CASE "依頼発生条件"
		CALLF MAKE_INT(V_NAME, 1)
	CASE "依頼受託可能条件"
	;受託可能は@COM_ABLE366も確認のこと
	;ここを参照するのは依頼人をTARGETにしてる時なので素通しでよし
		CALLF MAKE_INT(V_NAME, 1)
	CASE "依頼実行可能条件"
	;実行可能は@COM_ABLE367も確認のこと
		IF FLAG:70
		ELSE
			CALLF MAKE_INT(V_NAME, 1)
		ENDIF
	;成功/失敗処理はCARRYOUTで直接書く
	;SOURCEやITEM入手など、内容が複雑なので
	CASE "依頼消滅条件"
	;30％で消失
		SIF RAND:100 < 30
			CALLF MAKE_INT(V_NAME, 1)
	ENDSELECT
ENDSELECT



;固有依頼の設定用
;この関数は実行/成功/失敗時の
;報酬とペナルティと地の文のみを扱います
;各キャラのメッセージは別所にて
@IRAI_固有29(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
SELECTCASE STR_DATA_IRAI(IRAI_ID, "依頼名", CLIENT_ID)
;個々の依頼名で分岐
CASE "お酒をください"
	SELECTCASE SCENE
	CASE "依頼条件表示"
	;何か専用の表示情報を設けたい時用
	;受託済みの依頼表示で使用されます
	;
	;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも無しで
	;ここ以外の表示項目は
	;PRINTFORML 　　　　　：
	;となっているのでスペースを揃えたほうが見栄えが良いです
	
	CASE "依頼実行時"
	;このCASEに限り、RESULTが意味を持ちます
	;依頼実行時のRESULTにはIRAI_CONST_RESULT()を使用してください
	;可能な引数は"キャンセル", "実行して成功", "実行して失敗", "実行して未解決"です
	;独自処理を仕込みたい時は"実行して未解決"で先延ばしするなりしてください
		PRINTL どれを渡そうか？
		CALL 酒一覧表示(3)
		IF INRANGE(RESULT, 51, 53)
			PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:TARGET%にお酒を渡した
			ITEM:RESULT -= 3
			;TFLAGに渡した酒番号を保存。報酬などを分岐させたい場合に。
			TFLAG:194 = RESULT
			RETURN IRAI_CONST_RESULT("実行して成功")
		ELSE
			RETURN IRAI_CONST_RESULT("キャンセル")
		ENDIF
	CASE "依頼成功時"
	;成功報告時の報酬など
		CALL KIGEN_CHANGE(TARGET, 100, 1)
		SOURCE:TARGET:歓楽 += 2000
		;システムメッセージつき好感度変化
		;最後の引数だけ好感度が変化
		CALL CHANGE_CFLAG(2, TARGET, 30)
		;システムメッセージつき信頼度変化
		;最後の引数だけ信頼度が変化
		CALL CHANGE_CFLAG(4, TARGET, 10)
		MONEY += 5000
		PRINTFORMW %CALLNAME:MASTER%は依頼報酬の\\5000を受け取った
	CASE "依頼失敗時"
	;失敗報告時の処理
		;システムメッセージつき好感度変化
		;最後の引数だけ好感度が変化
		CALL CHANGE_CFLAG(2, TARGET, -20)
		;システムメッセージつき信頼度変化
		;最後の引数だけ信頼度が変化
		CALL CHANGE_CFLAG(4, TARGET, -10)
	ENDSELECT
	
CASE "街頭アンケート"
	SELECTCASE SCENE
	CASE "依頼条件表示"
	;何か専用の表示情報を設けたい時用
	;受託済みの依頼表示で使用されます
	;
	;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも無しで
	;ここ以外の表示項目は
	;PRINTFORML 　　　　　：
	;となっているのでスペースを揃えたほうが見栄えが良いです
	
	CASE "依頼実行時"
	;このCASEに限り、RESULTが意味を持ちます
	;依頼実行時のRESULTにはIRAI_CONST_RESULT()を使用してください
	;可能な引数は"キャンセル", "実行して成功", "実行して失敗", "実行して未解決"です
	;独自処理を仕込みたい時は"実行して未解決"で先延ばしするなりしてください
		DOWNBASE:MASTER:気力 += 50
		PRINTFORMW %CALLNAME:MASTER%は道行く里人たちをつかまえてはアンケート調査を行なった
		TIME += 120
		RETURN IRAI_CONST_RESULT("実行して成功")
	CASE "依頼成功時"
	;成功報告時の報酬など
		CALL KIGEN_CHANGE(TARGET, 100, 1)
		SOURCE:TARGET:歓楽 = 1000
		SOURCE:TARGET:征服 = 500
		;システムメッセージつき好感度変化
		;最後の引数だけ好感度が変化
		CALL CHANGE_CFLAG(2, TARGET, 30)
		;システムメッセージつき信頼度変化
		;最後の引数だけ信頼度が変化
		CALL CHANGE_CFLAG(4, TARGET, 15)
		MONEY += 3000
		PRINTFORMW %CALLNAME:MASTER%は依頼報酬の\\3000を受け取った
	CASE "依頼失敗時"
	;失敗報告時の処理
		;システムメッセージつき好感度変化
		;最後の引数だけ好感度が変化
		CALL CHANGE_CFLAG(2, TARGET, -20)
		;システムメッセージつき信頼度変化
		;最後の引数だけ信頼度が変化
		CALL CHANGE_CFLAG(4, TARGET, -10)
	ENDSELECT
ENDSELECT

;存否関数
;F関数はTRYCALL系が使えないので通常関数で存否配列を作っちゃう
@EXIST_一般依頼2
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
@一般依頼2(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "依頼名"
	CALLF MAKE_STR(V_NAME, "お花に水をあげて")
CASE "依頼内容表示"
	CALLF MAKE_STR(V_NAME, @"%CALLNAME:ARG%の自宅の草木に水をあげる")
CASE "依頼報酬表示"
	CALLF MAKE_STR(V_NAME, @"\\500")
CASE "依頼報告不要"
	CALLF MAKE_STR(V_NAME, "報告不要（報酬受理には報告必須）")
CASE "依頼実行コマンド名"
	CALLF MAKE_STR(V_NAME, "水やり")

CASE "依頼内容"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(GET_CHARAHOME(ARG), "伝令（場所）"))
CASE "依頼サブ"
CASE "依頼期限"
;発生時ではなく受託時に確定します
CASE "依頼発生率"
;これは他の一般依頼との相対確率なんで15％ではありません
	CALLF MAKE_INT(V_NAME, 15)
CASE "依頼発生条件"
;自宅があなたと同一マップのキャラのみ
	SIF MAIN_MAP == GET_MAPID(GET_CHARAHOME(ARG)) && MAP_CAN_MOVE(GET_CHARAHOME(ARG), MASTER) && ABL:ARG:親密 >= 2
		CALLF MAKE_INT(V_NAME, 1)
CASE "依頼受託可能条件"
;その依頼者の自宅にいるとき
	SIF MAIN_MAP == GET_MAPID(GET_CHARAHOME(ARG)) && MAP_CAN_MOVE(GET_CHARAHOME(ARG), MASTER)
		CALLF MAKE_INT(V_NAME, 1)
CASE "依頼実行可能条件"
		CALLF MAKE_INT(V_NAME, 1)
;成功/失敗処理はCARRYOUTで直接書く
;SOURCEやITEM入手、消費が煩雑になるので
CASE "依頼消滅条件"
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
ENDSELECT



@IRAI_一般2(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
SELECTCASE SCENE
CASE "依頼条件表示"
;何か専用の表示情報を設けたい時用
;受託済みの依頼表示で使用されます
;
;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも無しで
;ここ以外の表示項目は
;PRINTFORML 　　　　　：
;となっているのでスペースを揃えたほうが見栄えが良いです

CASE "依頼実行時"
;このCASEに限り、RESULTが意味を持ちます
;依頼実行時のRESULTにはIRAI_CONST_RESULT()を使用してください
;可能な引数は"キャンセル", "実行して成功", "実行して失敗", "実行して未解決"です
;独自処理を仕込みたい時は"実行して未解決"で先延ばしするなりしてください
	PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:CLIENT_ID%の家の鉢植えや植木に水をあげた
	PRINTFORML 
	RETURN IRAI_CONST_RESULT("実行して成功")
CASE "依頼成功時"
;成功報告時の報酬など
	MONEY += 500
	SOURCE:歓楽 = 500
	CALL CHANGE_CFLAG(2, TARGET, 20)
	CALL CHANGE_CFLAG(4, TARGET, 15)
	PRINTFORMW %CALLNAME:MASTER%は依頼報酬の\\500を受け取った
CASE "依頼失敗時"
;失敗報告時の処理
	CALL CHANGE_CFLAG(2, TARGET, -10)
	CALL CHANGE_CFLAG(4, TARGET, -5)
ENDSELECT

;-------------------------------------------------
;バイブ
;道具使用コマンド、レベル2
;-------------------------------------------------
@COM44
;着脱の処理
TEQUIP:バイブ = !TEQUIP:バイブ
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;奉仕快楽フラグ
TCVAR:PLAYER:108 = 1

CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:TARGET:2 = 13
;強度
TCVAR:TARGET:4 = 900

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 100
	SOURCE:快Ｖ = 250
	SOURCE:苦痛 = 25

	;処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:処女 == 1
		SOURCE:苦痛 += 1000
	ENDIF

	;再生処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:処女 == 2
		SOURCE:苦痛 += 250
	ENDIF

ELSE

	DOWNBASE:体力 += 30
	DOWNBASE:気力 += 100

	SOURCE:快Ｖ = 250
	SOURCE:苦痛 = 50
	SOURCE:露出 = 20
	SOURCE:逸脱 = 40
	SOURCE:反感 = 40


	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;抑圧、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 40

	;処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:処女 == 1
		SOURCE:苦痛 += 2000
		SOURCE:反感 += 3000
	ENDIF

	;再生処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:処女 == 2
		SOURCE:苦痛 += 500
		SOURCE:反感 += 1000
	ENDIF
ENDIF
;Vへの苦痛は先に処理しておく
CALL PAIN_CHECK_V(TARGET)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;挿入時はＶ経験+
EXP:Ｖ経験 ++
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ELSE
	;異常経験
	IF TALENT:処女 == 1 && !GETBIT(CFLAG:3,3)
		EXP:異常経験 ++
		SETBIT CFLAG:3,3
	ENDIF

	;同性愛　レズなら戻り値1ゲイなら戻り値2
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 1
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ENDIF
EXP:PLAYER:道具使用経験 ++

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM44(ARG)
;バイブ挿入中
PRINTL ＜バイブ挿入中＞

CSTR:TARGET:10 += @"{PLAYER}/"
TCVAR:TARGET:2 = 13
TCVAR:TARGET:4 = 450

IF FLAG:70 == 1
BASE:MASTER:TSP -= 50
SOURCE:ARG:快Ｖ += 125

ELSE


DOWNBASE:ARG:体力 += 10
DOWNBASE:ARG:気力 += 50

SOURCE:ARG:快Ｖ += 125
SOURCE:ARG:露出 += 10
SOURCE:ARG:逸脱 += 20

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:ARG:自己愛 < 0 || TALENT:ARG:抵抗
	SOURCE:ARG:鬱屈 += 20

;Vへの苦痛は先に処理しておく
;EXP:Ｖ経験をみる（処女はありえない）
LOCAL = 25
LOCAL:1 = 20
IF EXP:ARG:Ｖ経験 < EXPLV:2
	TIMES LOCAL , 0.80
	TIMES LOCAL:1 , 1.20
ELSEIF EXP:ARG:Ｖ経験 < EXPLV:3
	TIMES LOCAL , 0.40
ELSEIF EXP:ARG:Ｖ経験 < EXPLV:4
	TIMES LOCAL , 0.10
ELSEIF EXP:ARG:Ｖ経験 < EXPLV:5
	TIMES LOCAL , 0.00
ELSEIF EXP:ARG:Ｖ経験 >= EXPLV:5
	TIMES LOCAL , 0.00
ENDIF

;PALAM:潤滑をみる
IF PALAM:ARG:潤滑 < PALAMLV:1
	TIMES LOCAL , 1.20
	TIMES LOCAL:1 , 1.50
ELSEIF PALAM:ARG:潤滑 < PALAMLV:2
	TIMES LOCAL , 0.70
	TIMES LOCAL:1 , 1.20
ELSEIF PALAM:ARG:潤滑 < PALAMLV:3
	TIMES LOCAL , 0.40
ELSEIF PALAM:ARG:潤滑 < PALAMLV:4
	TIMES LOCAL , 0.10
ELSEIF PALAM:ARG:潤滑 >= PALAMLV:4
	TIMES LOCAL , 0.10
ENDIF
SOURCE:ARG:苦痛 += LOCAL
SOURCE:ARG:反感 += LOCAL:1

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;自慰実行時は経験＋１
SIF EXP_UP(22,TARGET)
	EXP:ARG:Ｖ経験 += 1
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE44
;バイブ実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(44)
	RETURN RESULT
;解除はいつでも可能
SIF TEQUIP:13
	RETURN 1
;オトコだとダメ
SIF !(TALENT:2 & 1)
	RETURN 0
;アイテムを持っているか、アイテムなし設定でないとダメ
SIF ITEM:4 == 0 && NOITEM == 0
	RETURN 0

;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
;処女で、従順4以下・レズっ気4以下のASSIの場合バイブはできない（サドならＯＫ）
IF TALENT:処女 && ASSIPLAY
	SIF (ABL:ASSI:従順 <= 4 || ABL:ASSI:レズっ気 <= 4) && TALENT:ASSI:サド == 0
		RETURN 0
ENDIF
;潤滑不足で、従順3以下・レズっ気3以下のASSIの場合バイブはできない（サドならＯＫ）
IF PALAM:潤滑 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:従順 <= 3 || ABL:ASSI:レズっ気 <= 3) && TALENT:ASSI:サド == 0
		RETURN 0
ENDIF
RETURN 1


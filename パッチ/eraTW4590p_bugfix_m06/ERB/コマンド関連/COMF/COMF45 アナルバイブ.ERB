;-------------------------------------------------
;アナルバイブ
;道具使用コマンド、レベル2
;-------------------------------------------------
@COM45
;着脱の処理
TEQUIP:アナルバイブ = !TEQUIP:アナルバイブ 
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;奉仕快楽フラグ
TCVAR:PLAYER:108 = 1

CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:TARGET:2 = 12
;強度
TCVAR:TARGET:4 = 900

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 150
	SOURCE:快Ａ = 250
	SOURCE:苦痛 = 130

ELSE

	DOWNBASE:体力 += 60
	DOWNBASE:気力 += 150

	SOURCE:快Ａ = 250
	SOURCE:苦痛 = 250
	SOURCE:露出 = 50
	SOURCE:屈従 = 50
	SOURCE:逸脱 = 50
	SOURCE:反感 = 200
	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;抑圧、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 70
ENDIF
;Aへの苦痛は先に処理しておく
CALL PAIN_CHECK_A(TARGET)

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
STAIN:7 |= STAIN:4
STAIN:4 |= STAIN:7
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;Ａ経験
EXP:Ａ経験 ++
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ELSE
	;同性愛　レズなら戻り値1ゲイなら戻り値2
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 1
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ENDIF
EXP:PLAYER:道具使用経験 ++

RETURN 1
;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM45(ARG)
;アナルバイブ挿入中
PRINTL ＜アナルバイブ挿入中＞

CSTR:TARGET:10 += @"{PLAYER}/"
TCVAR:TARGET:2 = 12
TCVAR:TARGET:4 = 450

IF FLAG:70 == 1
	BASE:MASTER:TSP -= 80
	SOURCE:ARG:快Ａ += 125

ELSE


	DOWNBASE:ARG:体力 += 30
	DOWNBASE:ARG:気力 += 80

	SOURCE:ARG:快Ａ += 125
	SOURCE:ARG:苦痛 += 100
	SOURCE:ARG:露出 += 25
	SOURCE:ARG:逸脱 += 25
	SOURCE:ARG:反感 += 40

	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;抑圧、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:ARG:自己愛 < 0 || TALENT:ARG:抵抗
		SOURCE:ARG:鬱屈 += 35

	;Aへの苦痛は先に処理しておく
	;EXP:Ａ経験をみる
	LOCAL = 100
	LOCAL:1 = 40
	IF EXP:ARG:Ａ経験 < EXPLV:1
		TIMES LOCAL , 3.00
		TIMES LOCAL:1 , 3.00
	ELSEIF EXP:ARG:Ａ経験 < EXPLV:2
		TIMES LOCAL , 1.20
		TIMES LOCAL:1 , 2.00
	ELSEIF EXP:ARG:Ａ経験 < EXPLV:3
		TIMES LOCAL , 0.80
		TIMES LOCAL:1 , 1.50
	ELSEIF EXP:ARG:Ａ経験 < EXPLV:4
		TIMES LOCAL , 0.60
		TIMES LOCAL:1 , 1.20
	ELSEIF EXP:ARG:Ａ経験 < EXPLV:5
		TIMES LOCAL , 0.40
	ELSEIF EXP:ARG:Ａ経験 >= EXPLV:5
		TIMES LOCAL , 0.20
	ENDIF

	;PALAM:潤滑をみる
	IF PALAM:ARG:潤滑 < PALAMLV:1
		TIMES LOCAL , 1.20
		TIMES LOCAL:1 , 2.00
	ELSEIF PALAM:ARG:潤滑 < PALAMLV:2
		TIMES LOCAL , 1.00
		TIMES LOCAL:1 , 1.50
	ELSEIF PALAM:ARG:潤滑 < PALAMLV:3
		TIMES LOCAL , 0.60
	ELSEIF PALAM:ARG:潤滑 < PALAMLV:4
		TIMES LOCAL , 0.30
	ELSEIF PALAM:ARG:潤滑 >= PALAMLV:4
		TIMES LOCAL , 0.20
	ENDIF
	SOURCE:ARG:苦痛 += LOCAL
	SOURCE:ARG:反感 += LOCAL:1

	;-------------------------------------------------
	;経験の処理
	;-------------------------------------------------
	;自慰実行時は経験＋１
	SIF EXP_UP(22,TARGET)
		EXP:ARG:Ａ経験 ++
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE45
;アナルバイブ実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(45)
	RETURN RESULT
;解除はいつでも可能
SIF TEQUIP:14
	RETURN 1
;アイテムを持っているか、アイテムなし設定でないとダメ
SIF ITEM:5 == 0 && NOITEM == 0
	RETURN 0
;潤滑不足で、従順3以下・レズっ気3以下のASSIの場合アナルバイブはできない（サドならＯＫ）
IF PALAM:潤滑 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:従順 <= 3 || ABL:ASSI:レズっ気 <= 3) && TALENT:ASSI:サド == 0
		RETURN 0
ENDIF

;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
;浣腸＋アナルプラグ使用中はダメ
SIF TEQUIP:21
	RETURN 0
;アナルビーズ使用中は不可
SIF TEQUIP:15
	RETURN 0
;バルーン使用中は不可
SIF TEQUIP:22
	RETURN 0
;電極使用中は不可
SIF TEQUIP:23
	RETURN 0
RETURN 1


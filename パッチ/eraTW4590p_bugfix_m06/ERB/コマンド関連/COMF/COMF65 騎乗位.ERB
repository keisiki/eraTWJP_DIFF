;-------------------------------------------------
;騎乗位
;セックス系コマンド、レベル2
;-------------------------------------------------
@COM65
;前回の調教が3Pのとき3Pへ
IF PREVCOM == 71
	CALL COM_ABLE71
	SIF RESULT
		JUMP COM71
ENDIF
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = TARGET
;奉仕快楽フラグ
TCVAR:108 = 1

CSTR:PLAYER:10 += @"{TARGET}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:PLAYER:2 = 1
;強度
TCVAR:PLAYER:4 = 2000

CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:TARGET:2 = 13
;強度
TCVAR:TARGET:4 = 1300

DOWNBASE:PLAYER:体力 += 50

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 150
	SOURCE:PLAYER:快Ｃ = 400

	SOURCE:快Ｖ = 500
	SOURCE:苦痛 = 250

	;処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:処女 == 1
		SOURCE:苦痛 += 1000
	ENDIF

	;再生処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:処女 == 2
		SOURCE:苦痛 += 250
	ENDIF

ELSE

	DOWNBASE:体力 += 80
	DOWNBASE:気力 += 50
	SOURCE:PLAYER:快Ｃ = 400

	SOURCE:快Ｖ = 500
	SOURCE:情愛 = 250
	SOURCE:性行動 = 250
	SOURCE:達成 = 250
	SOURCE:苦痛 = 500
	SOURCE:露出 = 120
	SOURCE:逸脱 = 600
	SOURCE:反感 = 800

	;上のほうで計算した汚れデータ
	SOURCE:不潔 = Y*10 + 60
	CALL 特殊ソース処理
ENDIF
;Vへの苦痛は先に処理しておく
CALL PAIN_CHECK_V(TARGET)

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
CALL STAIN_膣内Ｐ挿入

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;性交経験
EXP:PLAYER:挿入経験 ++
EXP:Ｖ経験 ++
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 7
	ENDIF
		EXP:PLAYER:時姦経験 += 1
ELSE
	;異常経験
	IF TALENT:処女 && TALENT:処女 != -1 && !GETBIT(CFLAG:3,1)
		IF ASSIPLAY && (TALENT:PLAYER:2 & 1)
			EXP:異常経験 += 3
		ELSE
			EXP:異常経験 += 2
		ENDIF
		SETBIT CFLAG:3,1
	ENDIF
	;同性愛　レズなら戻り値1ゲイなら戻り値2
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 7
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 7
	ENDIF
	EXP:Ｖ性交経験 ++
	IF !ASSIPLAY
		EXP:愛情経験 += TALENT:恋慕 * 2
		;処女
		SIF TALENT:処女 && TALENT:処女 != -1 && TALENT:恋慕
			EXP:愛情経験 += 5
	ENDIF
ENDIF
RETURN 1

@CAN_COM65(ARG)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
IF FLAG:70 == 1
	SKIPDISP ARG
	PRINTL 騎乗位で犯す
ELSE
	SKIPDISP ARG
	PRINTL 騎乗位
	A = 0
	S = 0

	;すべての命令に共通の要素を考慮
	;(従順が高いと命令に従いやすいなど)
	CALL COM_ORDER

	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		A += ABL:欲望 * 3
		PRINT 欲望
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
		S = 1
	ENDIF
	;ABL:Ｖ感覚
	IF ABL:Ｖ感覚
		SIF S
			PRINT  + 
		A += ABL:Ｖ感覚 * 2
		PRINT Ｖ感覚
		PRINTV 'LV,ABL:Ｖ感覚,'(,ABL:Ｖ感覚 * 2,')
		S = 1
	ENDIF
	;ABL:奉仕精神
	IF ABL:奉仕精神
		SIF S
			PRINT  + 
		A += ABL:奉仕精神 * 4
		PRINT 奉仕精神
		PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
		S = 1
	ENDIF
	IF MARK:快楽刻印
		SIF S
			PRINT  + 
		A += MARK:快楽刻印 * 3
		PRINT 快楽刻印
		PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 3,')
		S = 1
	ENDIF

	;PALAM:潤滑（の不足）
	IF PALAM:潤滑 < PALAMLV:3
		PRINT  - 
		A -= 5
		PRINT 潤滑不足
		PRINTV '(,5,')
		S = 1
	ENDIF
	;PALAM:欲情
	IF GETPALAMLV(PALAM:欲情,5)
		SIF S
			PRINT  + 
		A += GETPALAMLV(PALAM:欲情,5) * 3
		PRINT 欲情
		PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 3,')
		S = 1
	ENDIF
	;恥じらい
	IF TALENT:羞恥心 > 0
		PRINT  - 
		A -= 2
		PRINT 恥じらい
		PRINTV '(,2,')
		S = 1
	ENDIF
	;快感の否定（淫乱、娼婦で無効）
	IF TALENT:快感応答 < 0 && !TALENT:恋慕
		PRINT  - 
		A -= 5
		PRINT 快感の否定
		PRINTV '(,5,')
		S = 1
	ENDIF
	;男嫌い（恋慕、親愛で無効）
	IF OTOKOGIRAI(TARGET)
		PRINT  - 
		A -= 12
		PRINT 男嫌い
		PRINTV '(,12,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:恋慕 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 15
		PRINT 恋慕
		PRINTV '(,15,')
		S = 15
	ENDIF
	;親愛
	IF TALENT:恋慕 > 1 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 5
		PRINT 親愛
		PRINTV '(,5,')
		S = 5
	ENDIF


	;処女
	LOCAL = MAX(10,40 - EXP:Ｖ経験)
	IF TALENT:処女 && TALENT:処女 != -1
		PRINT  - 
		A -= LOCAL
		PRINT 処女
		PRINTV '(,LOCAL,')
		S = 1
	ELSEIF EXP:Ｖ経験 < EXPLV:2
		PRINT  - 
		A -= 15
		PRINT Ｖ経験不足
		PRINTV '(,15,')
		S = 1
	ENDIF

	;調教者がふたなり
	IF (TALENT:PLAYER:2 & 3) == 3
		SIF S
			PRINT  + 
		A += 4
		PRINT ふたなり
		PRINTV '(,4,')
		S = 1
	ENDIF

	;媚薬
	IF TCVAR:媚薬
		SIF S
			PRINT  + 
		A += 6
		PRINT 媚薬
		PRINTV '(,6,')
		S = 1
	ENDIF

	Y = 0
	;愛液の汚れ
	SIF STAIN:PLAYER:2 & 1
		Y += 1
	;精液の汚れ
	SIF STAIN:PLAYER:2 & 4
		Y += 3
	;アナルの汚れ
	SIF STAIN:PLAYER:2 & 8
		Y += 7
	SIF STAIN:PLAYER:2 & 16
		Y += 15

	Y = Y * (2 - TALENT:汚臭耐性) / 2

	;汚れあり
	IF Y
		PRINT  - 
		A -= Y
		PRINT 汚れあり
		;汚臭鈍感
		IF TALENT:汚臭耐性 > 0
			PRINT 、汚臭鈍感
		ELSEIF TALENT:汚臭耐性 < 0
			PRINT 、汚臭敏感
		ENDIF
		PRINTV '(,Y,')
		S = 1
	ENDIF


	;合計を表示(35以上で実行)
	PRINT  = 
	PRINTV A

	V = 35
	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V

	WAIT
	SKIPDISP 0
	;実行できない
	SIF A < V
		RETURN 0

	;その他諸元判定
	IF ARG == 1
		SIF TFLAG:17 != TARGET
			TFLAG:16 = 0
		;初体験許すか判定
		IF GROUPMATCH(TALENT:TARGET:処女, 1, -1)
			CALL LOST_VIRGIN_STOP(TARGET)
			SIF RESULT
				RETURN 0
		ENDIF
		;危険日避妊要求判定
		CALL 避妊要求(TARGET)
		IF TFLAG:16 && !(TEQUIP:PLAYER:コンドーム || TCVAR:ピル)
			TFLAG:16 -= 1
			RETURN 0
		ENDIF
	ENDIF
ENDIF
SKIPDISP 0
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE65
;騎乗位実行判定
SIF !TFLAG:100
	RETURN 0
SIF GLOBAL_COMABLE(65)
	RETURN RESULT
;調教者がオトコ、ふたなりの場合はある程度勃起してないとダメ
SIF (TALENT:PLAYER:2 & 2) && BASE:PLAYER:勃起 < 1000
	RETURN 0
;バイブ挿入中はダメ
SIF TEQUIP:13
	RETURN 0
;オトコだとダメ
SIF !(TALENT:2 & 1)
	RETURN 0
;体格差がある場合は経験がないとダメ
SIF 挿入不可(TARGET) == 1 && EXP:Ｖ経験 < 50 && !TALENT:PLAYER:禁断の知識
	RETURN 0
;さらに体格差があると禁断の知識が必要
SIF 挿入不可(TARGET) == 2 && (!TALENT:PLAYER:禁断の知識 || EXP:Ｖ経験 < 50)
	RETURN 0
SIF 挿入不可(TARGET) == 3
	RETURN 0
;調教者がオトコか半人半妖かふたなりじゃないとダメ
SIF !(TALENT:PLAYER:2 & 2)
	RETURN 0

;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
IF TEQUIP:31
	SIF ITEM:20 == 0 && NOITEM == 0
		RETURN 0
ENDIF
;処女で、従順4以下・レズっ気4以下のASSIの場合はできない（サドならＯＫ）
IF TALENT:処女 && ASSIPLAY
	SIF (ABL:ASSI:従順 <= 4 || ABL:ASSI:技巧 <= 4) && TALENT:ASSI:サド == 0
		RETURN 0
ENDIF
;時間停止中はスキップ
IF FLAG:70 != 1
	;処女かつ潤滑不足はだめ
	SIF PALAM:潤滑 < PALAMLV:2 && TALENT:処女 && TALENT:処女 != -1
		RETURN 0
	;縄使用中は不可
	SIF TEQUIP:縄
		RETURN 0
ENDIF
;睡眠姦中
SIF CFLAG:イタズラ > 1
	RETURN 0
;ベッドルームorバスルームでプレイマットがないとダメ
SIF !BEDROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20)
	RETURN 0
RETURN 1


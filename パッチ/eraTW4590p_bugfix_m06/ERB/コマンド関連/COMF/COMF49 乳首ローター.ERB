;-------------------------------------------------
;乳首ローター
;道具使用コマンド、レベル2
;-------------------------------------------------
@COM49
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
PRINTL 乳首ローター
STR:0 = 乳首ローター
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;奉仕快楽フラグ
TCVAR:PLAYER:108 = 1

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 80
	SOURCE:快Ｂ = 250

ELSE

	DOWNBASE:体力 += 10
	DOWNBASE:気力 += 80

	SOURCE:快Ｂ = 250
	SOURCE:露出 = 20
	SOURCE:逸脱 = 30
	SOURCE:反感 = 30
	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;抑圧、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 30
ENDIF
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ELSE
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 1
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ENDIF
EXP:PLAYER:道具使用経験 ++
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE49
;乳首ローター実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(49)
	RETURN RESULT
;アイテムを持っているか、アイテムなし設定でないとダメ
SIF ITEM:0 == 0 && NOITEM == 0
	RETURN 0
;上半身着衣
SIF (TEQUIP:1 & 2) && TEQUIP:3 == 0
	RETURN 0
;搾乳器使用中は不可
SIF TEQUIP:17
	RETURN 0
;乳房電極装着中はダメ
SIF TEQUIP:24
	RETURN 0
RETURN 1


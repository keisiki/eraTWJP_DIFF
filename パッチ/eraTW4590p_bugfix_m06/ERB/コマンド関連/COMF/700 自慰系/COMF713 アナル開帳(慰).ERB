;-------------------------------------------------
;アナル開帳
;愛撫系コマンド、レベル2
;-------------------------------------------------
@COM713
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = TARGET
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力

DOWNBASE:PLAYER:体力 += 50
DOWNBASE:PLAYER:気力 += 90

SOURCE:PLAYER:快Ａ = 43
SOURCE:PLAYER:達成 = 20
SOURCE:PLAYER:恐怖 = 120
SOURCE:PLAYER:液体 = 10
SOURCE:PLAYER:露出 = 300
SOURCE:PLAYER:屈従 = 450
SOURCE:PLAYER:逸脱 = 150
SOURCE:PLAYER:反感 = 150
;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:PLAYER:自己愛 < 0 || TALENT:PLAYER:抵抗
	SOURCE:PLAYER:鬱屈 += 150

;処女、貞操観念
IF TALENT:PLAYER:処女 == 1 || TALENT:PLAYER:処女 == -1
	TIMES SOURCE:PLAYER:恐怖 , 1.20
	TIMES SOURCE:PLAYER:屈従 , 1.20
	TIMES SOURCE:PLAYER:反感 , 1.20
	SOURCE:PLAYER:露出 += 300
	IF TALENT:PLAYER:貞操 > 0
		TIMES SOURCE:PLAYER:露出 , 2.00
		TIMES SOURCE:PLAYER:逸脱 , 2.00
	ENDIF
ENDIF
;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のＶ⇔指の汚れが移動
STAIN:PLAYER:4 |= STAIN:PLAYER:1
STAIN:PLAYER:1 |= STAIN:PLAYER:4
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
SIF !FLAG:時間停止
	TIME += 5
;露出癖Lv3以上で自慰経験加算
SIF ABL:PLAYER:露出癖 >= 3
	CFLAG:PLAYER:オナニーした ++
RETURN 1


@CAN_COM713(PLAYER)
SKIPDISP PLAYER
PRINTL アナル開帳
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
	A = 0
	S = 0
	;すべての命令に共通の要素を考慮
	;(従順が高いと命令に従いやすいなど)
	CALL COM_ORDER

	;ABL:PLAYER:欲望
	IF ABL:PLAYER:欲望
		SIF S
			PRINT  + 
		A += ABL:PLAYER:欲望 * 3
		PRINT 欲望
		PRINTV 'LV,ABL:PLAYER:欲望,'(,ABL:PLAYER:欲望 * 3,')
		S = 1
	ENDIF
	;ABL:PLAYER:Ａ感覚
	IF ABL:PLAYER:Ａ感覚
		SIF S
			PRINT  + 
		A += ABL:PLAYER:Ａ感覚 * 2
		PRINT Ａ感覚
		PRINTV 'LV,ABL:PLAYER:Ａ感覚,'(,ABL:PLAYER:Ａ感覚 * 2,')
		S = 1
	ENDIF
	;ABL:PLAYER:奉仕精神
	IF ABL:PLAYER:奉仕精神
		SIF S
			PRINT  + 
		A += ABL:PLAYER:奉仕精神 * 4
		PRINT 奉仕精神
		PRINTV 'LV,ABL:PLAYER:奉仕精神,'(,ABL:PLAYER:奉仕精神 * 4,')
		S = 1
	ENDIF
	;ABL:PLAYER:露出癖
	IF ABL:PLAYER:露出癖
		SIF S
			PRINT  + 
		A += ABL:PLAYER:露出癖 * 3
		PRINT 露出癖
		PRINTV 'LV,ABL:PLAYER:露出癖,'(,ABL:PLAYER:露出癖 * 3,')
		S = 1
	ENDIF
	;ABL:PLAYER:自慰中毒
	IF ABL:PLAYER:自慰中毒
		SIF S
			PRINT  + 
		A += ABL:PLAYER:自慰中毒 * 3
		PRINT 自慰中毒
		PRINTV 'LV,ABL:PLAYER:自慰中毒,'(,ABL:PLAYER:自慰中毒 * 3,')
		S = 1
	ENDIF

	IF MARK:PLAYER:快楽刻印
		SIF S
			PRINT  + 
		A += MARK:PLAYER:快楽刻印 * 3
		PRINT 快楽刻印
		PRINTV 'LV,MARK:PLAYER:快楽刻印,'(,MARK:PLAYER:快楽刻印 * 3,')
		S = 1
	ENDIF

	;PALAM:PLAYER:潤滑（の不足）
	IF PALAM:PLAYER:潤滑 < PALAMLV:3
		PRINT  - 
		A -= 5
		PRINT 潤滑不足
		PRINTV '(,5,')
		S = 1
	ENDIF
	;PALAM:PLAYER:欲情
	IF GETPALAMLV(PALAM:PLAYER:欲情,5)
		SIF S
			PRINT  + 
		A += GETPALAMLV(PALAM:PLAYER:欲情,5) * 3
		PRINT 欲情
		PRINTV 'LV,GETPALAMLV(PALAM:PLAYER:欲情,5),'(,GETPALAMLV(PALAM:PLAYER:欲情,5) * 3,')
		S = 1
	ENDIF

	;恥じらい
	IF TALENT:PLAYER:羞恥心 > 0
		PRINT  - 
		A -= 2
		PRINT 恥じらい
		PRINTV '(,2,')
		S = 1
	ENDIF
	;快感の否定（淫乱、娼婦で無効）
	IF TALENT:PLAYER:快感応答 < 0 && !TALENT:PLAYER:淫乱
		PRINT  - 
		A -= 5
		PRINT 快感の否定
		PRINTV '(,5,')
		S = 1
	ENDIF
	;男嫌い（恋慕、親愛で無効）
	IF OTOKOGIRAI(TARGET) == 1
		PRINT  - 
		A -= 12
		PRINT 男嫌い
		PRINTV '(,12,')
		S = 1
	ENDIF

	;処女
	IF TALENT:PLAYER:処女 == 1
		PRINT  - 
		A -= 20
		PRINT 処女
		PRINTV '(,20,')
		S = 1
	ELSEIF EXP:PLAYER:Ａ経験 < EXPLV:2
		PRINT  - 
		A -= 5
		PRINT Ａ経験
		PRINT 不足
		PRINTV '(,5,')
		S = 1
	ENDIF

	;調教者がふたなり
	IF (TALENT:PLAYER:2 & 3) == 3
		PRINT  + 
		A += 4
		PRINT ふたなり
		PRINTV '(,4,')
		S = 1
	ENDIF

	;媚薬
	IF TCVAR:PLAYER:媚薬
		SIF S
			PRINT  + 
		A += 6
		PRINT 媚薬
		PRINTV '(,6,')
		S = 1
	ENDIF

	;'合計を表示(18以上で実行)
	PRINT  = 
	PRINTV A

	V = 18
	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V

	WAIT
	SKIPDISP 0
	;実行できない
	SIF A < V
		RETURN 0
RETURN 1

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
@COM_ABLE713
;アナル開帳実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(713)
	RETURN RESULT
;時間停止中は許可
IF FLAG:70 != 1
	;従順が2未満だとダメ
	SIF ABL:従順< 2
		RETURN 0
	;処女の場合、従順3以上または露出癖3以上ないとダメ
	SIF TALENT:処女 && ABL:従順 < 3 && ABL:露出癖 < 3
		RETURN 0
	;縄使用中は不可
	SIF TEQUIP:縄
		RETURN 0
ENDIF

;下半身着衣
SIF TEQUIP:0 & 4 || TEQUIP:0 & 2
	RETURN 0
;アナルバイブ、ビーズ挿入してる時はダメ
SIF TEQUIP:14 || TEQUIP:15
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
IF TEQUIP:31
	SIF ITEM:20 == 0 && NOITEM == 0
		RETURN 0
ENDIF
RETURN 1


;-------------------------------------------------
;愛撫
;愛撫系コマンド　レベル2
;-------------------------------------------------
@COM0
#DIM キスする
IF (FIRSTTIME(312,1) && FIRSTTIME(20,1)) || (MASTER_POSE(SET_M, SET_M) != TARGET && MASTER_POSE(0,4)) || (STAIN:0 >= 2 && (STAIN:0 != 16 && STAIN:0 != 17) && ASSIPLAY == 0 && TALENT:PLAYER:汚臭耐性 < 2) || TEQUIP:20
;奴隷の口の汚れがあるときキスはない（愛液と母乳よごれはＯＫ、汚れ無視か助手調教時は問題なし）
;ボールギャグ使用中もなし
	キスする = 0
ELSE
	キスする = 1
ENDIF
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;奉仕快楽
TCVAR:PLAYER:108 = 1

;TARGETがふたなりの場合には射精可能に
CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:TARGET:2 = 3
;強度
TCVAR:TARGET:4 = 1500

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 20
	SOURCE:快Ｃ = 40
	SOURCE:快Ｂ = 40
	IF キスする
		SOURCE:快Ｍ = 20
		SOURCE:快Ｍ += ABL:PLAYER:舌 * 3
	ENDIF
	;性技補正
	SOURCE:快Ｃ += ABL:PLAYER:指 * 4
	SOURCE:快Ｂ += ABL:PLAYER:指 * 4


	;指を使う調教では、調教者の指使いによって、快感のソース増加
	IF TALENT:PLAYER:器用な指
		TIMES SOURCE:快Ｃ , 1.50
		TIMES SOURCE:快Ｂ , 1.50
	ENDIF
	SIF キスする && TALENT:PLAYER:舌使い
		TIMES SOURCE:快Ｍ , 1.50

	;キス経験
	EXP:PLAYER:キス経験 ++
	;奴隷の口⇔調教者の口の汚れが移動
	STAIN:0 |= STAIN:PLAYER:0
	STAIN:PLAYER:0 |= STAIN:0
	;同性愛　レズなら戻り値1ゲイなら戻り値2
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 5
	ENDIF

	;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
	SIF TALENT:PLAYER:猫舌
		EXP:PLAYER:嗜虐快楽経験 += 1
	;経験
	EXP:PLAYER:手淫経験 ++
ELSE

	DOWNBASE:体力 += 10
	DOWNBASE:気力 += 50

	SOURCE:快Ｃ = 40
	SOURCE:快Ｂ = 40
	IF キスする
		SOURCE:快Ｍ = 20 + ABL:PLAYER:舌 * 3
		SOURCE:PLAYER:快Ｍ = 20 + ABL:TARGET:舌 * 3
	ENDIF
	
	SOURCE:情愛 = 50
	SOURCE:性行動 = 60
	SOURCE:露出 = 20
	SOURCE:不潔 = 30
	SOURCE:逸脱 = 20
	SOURCE:反感 = 20

	;性技補正
	SOURCE:快Ｃ += ABL:PLAYER:指 * 4
	SOURCE:快Ｂ += ABL:PLAYER:指 * 4

	IF TALENT:PLAYER:器用な指
		TIMES SOURCE:快Ｃ , 1.50
		TIMES SOURCE:快Ｂ , 1.50
	ENDIF
	SIF キスする && TALENT:PLAYER:舌使い
		TIMES SOURCE:快Ｍ , 1.50

	SIF TALENT:PLAYER:舌使い
		TIMES SOURCE:情愛 , 1.50
	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;抑圧、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 20
	;-------------------------------------------------
	;汚れの処理
	;-------------------------------------------------
	IF !キスする
		SOURCE:不潔 = 0
		SOURCE:情愛 /= 4
		SOURCE:液体 /= 2
	ELSE
		;キス経験
		EXP:キス経験 ++
		EXP:PLAYER:キス経験 ++
		;汚臭鈍感
		SIF TALENT:汚臭耐性 > 0
			SOURCE:不潔 /= 4
		;汚臭敏感
		SIF TALENT:汚臭耐性 < 0
			SOURCE:不潔 *= 3
		;プライド高い
		SIF TALENT:プライド > 0
			SOURCE:不潔 *= 2
		IF STAIN:PLAYER:0
		;主人の口の汚れがあると不潔150%
			SOURCE:不潔 *= 3
			SOURCE:不潔 /= 2
		ENDIF
		;奴隷の口⇔調教者の口の汚れが移動
		STAIN:0 |= STAIN:PLAYER:0
		STAIN:PLAYER:0 |= STAIN:0
	ENDIF


	;-------------------------------------------------
	;経験の処理
	;-------------------------------------------------
	;同性愛　レズなら戻り値1ゲイなら戻り値2
	CALL ADD_EXP_LESNIAN(5)

	;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
	SIF TALENT:PLAYER:猫舌
		EXP:PLAYER:嗜虐快楽経験 += 1
	;経験
	EXP:PLAYER:手淫経験 ++
ENDIF
RETURN 1

;-------------------------------------------------
;愛撫実行判定
;-------------------------------------------------
@COM_ABLE0
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(0)
	RETURN RESULT
SIF !TARGET
	RETURN 0
RETURN 1


;-------------------------------------------------
;豆まき
;日常系コマンド、レベル1
;-------------------------------------------------
@COM364

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
TFLAG:98 = -1
TFLAG:193 = 1
LOCAL:2 = TARGET

FOR LOCAL, 1, GET_TARGETNUM() + 1
	SIF CFLAG:(TARGET:LOCAL):睡眠
		CONTINUE
	SIF TALENT:(TARGET:LOCAL):吸血鬼
		CONTINUE
	SIF GROUPMATCH(TARGET:LOCAL, 10, 64)
		CONTINUE
	TFLAG:193 ++
	TARGET = TARGET:LOCAL
	CALL COM364_1
NEXT

TARGET = LOCAL:2

DOWNBASE:体力 += TFLAG:193 * 200
DOWNBASE:気力 += TFLAG:193 * 200

SOURCE:苦痛 = TFLAG:193 * 1000
SOURCE:恐怖 = TFLAG:193 * 2000
SOURCE:反感 = TFLAG:193 * 500

DOWNBASE:MASTER:気力 += 50
;仕事扱いで金が入る
TFLAG:207 += TFLAG:193 * 300
;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;苦痛のパラメータによる苦痛のソース増加は先に処理しておく
SOURCE:苦痛 = SOURCE:苦痛 * (10 + 5 * MAX(GETPALAMLV(PALAM:苦痛,5) - 1, 0)) / 10
TIME += 30
RETURN 1


@COM364_1
DOWNBASE:気力 += 50
SOURCE:歓楽 = 1500 + 300 * ABL:サドっ気
SOURCE:征服 = 500 + 200 * ABL:サドっ気

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE364
;停止中は不可
SIF FLAG:70
	RETURN 0
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(301)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;添い寝中
SIF CFLAG:添い寝中
	RETURN 0
SIF FESTIVAL() != "節分"
	RETURN 0
;TARGETが鬼じゃないと駄目
SIF TARGET != 64 && TARGET != 10 && !TALENT:TARGET:吸血鬼
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
RETURN 1


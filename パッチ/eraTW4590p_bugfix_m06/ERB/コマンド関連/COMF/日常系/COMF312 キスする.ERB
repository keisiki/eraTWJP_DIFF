;-------------------------------------------------
;キスする
;日常系コマンド　レベル2
;-------------------------------------------------
@COM312
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 10
	SOURCE:快Ｍ = ABL:PLAYER:舌 * 2 + 10
	EXP:MASTER:キス経験 += 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 2
	ENDIF
ELSE
	IF FLAG:甲斐性無 && !GETBIT(CFLAG:既成事実 ,0)
		PRINTFORMW %CALLNAME:MASTER%にはそんな度胸はない
		RETURN -1
	ENDIF
	;拒絶判定
	IF REJECT_CHECK(SELECTCOM) && !CFLAG:睡眠
		TFLAG:193 = 99
		LOCAL = 0
		;固定獲得ソース
		SOURCE:反感 = 500
		SOURCE:恐怖 = 300
		;好感度が低いと好感度低下
		IF CFLAG:2 <= 100
			LOCAL -= 50
		ELSEIF CFLAG:2 <= 300
			LOCAL -= 30
		ELSEIF CFLAG:2 <= 500
			LOCAL -= 10
		ENDIF
		;好感度変化
		TFLAG:97 += LOCAL
		TIME += 10
		RETURN 1
	ENDIF

	LOCAL = RAND:110
	LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
	SIF LOCAL:1 > 109
		LOCAL:1 = 109
	IF LOCAL <= 9
		;10％で大成功
		TFLAG:193 = 1
		IF CHK_DATENOW(CFLAG:TARGET:デート中)
			EXP:MASTER:デート経験 ++
			EXP:TARGET:デート経験 ++
		ENDIF
	ELSEIF LOCAL >= LOCAL:1
		;10〜1％で失敗
		TFLAG:193 = -1
	ELSE
		;残りは成功
		TFLAG:193 = 0
	ENDIF
	;先制発生時は失敗しない
	SIF TFLAG:102 == 3
		TFLAG:193 = MAX(TFLAG:193,0)

	IF CFLAG:TARGET:添い寝中
		DOWNBASE:MASTER:気力 += 50
	ELSEIF CHK_DATENOW(CFLAG:TARGET:デート中)
		DOWNBASE:気力 += 30
		DOWNBASE:MASTER:気力 += 50
	ELSE
		DOWNBASE:気力 += 70
		DOWNBASE:MASTER:気力 += 100
	ENDIF

	;固定で獲得するソース
	SOURCE:快Ｍ = ABL:PLAYER:舌 * 2 + 10
	SOURCE:PLAYER:快Ｍ = ABL:舌 * 2 + 10
	SOURCE:歓楽 = 150
	SOURCE:情愛 = 150
	SOURCE:露出 = 80
	SOURCE:反感 = 300
	SIF CFLAG:睡眠
		SOURCE:反感 = 200

	;ABL:親密をみる
	IF ABL:親密 <= 1
		SOURCE:情愛 += (ABL:親密 * 30)
	ELSEIF ABL:親密 <= 3
		SOURCE:歓楽 += 150 + (ABL:親密 * 30)
		SOURCE:情愛 += 150 + (ABL:親密 * 30)
		SOURCE:欲情 += 65 + (ABL:親密 * 20)
	ELSEIF ABL:親密 <= 5
		SOURCE:歓楽 += 300 + (ABL:親密 * 40)
		SOURCE:情愛 += 280 + (ABL:親密 * 30)
		SOURCE:欲情 += 130 + (ABL:親密 * 26)
		SOURCE:露出 += 80 + (ABL:親密 * 15)
	ELSEIF ABL:親密 <= 8
		SOURCE:歓楽 += 450 + (ABL:親密 * 70)
		SOURCE:情愛 += 380 + (ABL:親密 * 40)
		SOURCE:欲情 += 170 + (ABL:親密 * 30)
		SOURCE:露出 += 110 + (ABL:親密 * 18)
	ELSEIF ABL:親密 <= 11
		SOURCE:歓楽 += 500 + (ABL:親密 * 85)
		SOURCE:情愛 += 480 + (ABL:親密 * 50)
		SOURCE:欲情 += 230 + (ABL:親密 * 35)
		SOURCE:露出 += 160 + (ABL:親密 * 20)
	ELSE
		SOURCE:歓楽 += 800 + (ABL:親密 * 80)
		SOURCE:情愛 += 760 + (ABL:親密 * 45)
		SOURCE:欲情 += 350 + (ABL:親密 * 30)
		SOURCE:露出 += 260 + (ABL:親密 * 17)
	ENDIF

	;好感度をみる
	IF CFLAG:2 <= 500
		SOURCE:情愛 += CFLAG:2
	ELSEIF CFLAG:2 <= 5000
		SOURCE:情愛 += 400 + (CFLAG:2 - 500) / 5
		SOURCE:歓楽 += 400 + (CFLAG:2 - 500) / 4
	ELSE
		SOURCE:情愛 += 1200 + (CFLAG:2 - 5000) / 7
		SOURCE:歓楽 += 1200 + (CFLAG:2 - 5000) / 4
	ENDIF
	SIF SOURCE:情愛 < 0
		SOURCE:情愛 = 0

	LOCAL = 0
	;好感度が低いと好感度低下
	IF CFLAG:2 <= 100
		LOCAL -= 3
	ELSEIF CFLAG:2 <= 300
		LOCAL -= 2
	ELSEIF CFLAG:2 <= 500
		LOCAL -= 1
	ENDIF


	SOURCE:受動 = 150 + 300 * ABL:従順
	SOURCE:征服 = 150 + 300 * ABL:サドっ気

	;好感度変化
	SIF !CFLAG:睡眠
		TFLAG:97 += LOCAL

	;二人きりだと効果アップ、そうでないと露出以外ダウン=============
	FOR LOCAL,1,CHARANUM
		SIF CFLAG:LOCAL:同室
			LOCAL:2 ++
	NEXT
	IF LOCAL:2 == 1
		TIMES SOURCE:歓楽 , 1.20
		TIMES SOURCE:情愛 , 1.20
		TIMES SOURCE:征服 , 1.20
		TIMES SOURCE:受動 , 1.20
	ELSE
		TIMES SOURCE:歓楽 , 0.70
		TIMES SOURCE:情愛 , 0.80
		TIMES SOURCE:征服 , 0.80
		TIMES SOURCE:受動 , 0.90
		TIMES SOURCE:露出 , 1.10
	ENDIF
	;ムードの影響=====================================================
	IF BASE:ムード == MAXBASE:ムード
		TIMES SOURCE:歓楽 , 1.30
		TIMES SOURCE:情愛 , 1.30
		TIMES SOURCE:征服 , 1.20
		TIMES SOURCE:受動 , 1.20
		TIMES SOURCE:露出 , 1.30
	ELSEIF BASE:ムード < 600 && BASE:ムード > 200 && !TALENT:キス魔
		TIMES SOURCE:歓楽 , 0.70
		TIMES SOURCE:情愛 , 0.75
		TIMES SOURCE:征服 , 0.70
		TIMES SOURCE:受動 , 0.60
		TIMES SOURCE:露出 , 0.50
	ELSEIF BASE:ムード <= 200 && !TALENT:キス魔
		TIMES SOURCE:歓楽 , 0.40
		TIMES SOURCE:情愛 , 0.40
		TIMES SOURCE:征服 , 0.40
		TIMES SOURCE:受動 , 0.40
		TIMES SOURCE:露出 , 0.35
	ENDIF
	;キス魔===============================================================
	IF TALENT:キス魔
		TIMES SOURCE:歓楽 , 1.40
		TIMES SOURCE:情愛 , 1.40
	ENDIF
	;=====================================================================
	IF TFLAG:193 == -1
		TIMES SOURCE:歓楽 , 0.10
		TIMES SOURCE:情愛 , 0.50
		TIMES SOURCE:征服 , 0.50
		TIMES SOURCE:受動 , 0.50
		TIMES SOURCE:露出 , 0.40
	ELSEIF TFLAG:193 == 0
		TIMES SOURCE:歓楽 , 1.00
		TIMES SOURCE:情愛 , 1.00
		TIMES SOURCE:征服 , 1.00
		TIMES SOURCE:受動 , 1.00
		TIMES SOURCE:露出 , 0.90
	ELSE
		TIMES SOURCE:歓楽 , 2.00
		TIMES SOURCE:情愛 , 2.00
		TIMES SOURCE:征服 , 2.00
		TIMES SOURCE:受動 , 2.00
		TIMES SOURCE:露出 , 1.80
	ENDIF
	TIME += 10
	SIF !CFLAG:睡眠
		EXP:キス経験 += 1
		EXP:Ｍ経験 += 1
		EXP:MASTER:Ｍ経験 += 1
	EXP:MASTER:キス経験 += 1
	IF CHK_DATENOW(CFLAG:TARGET:デート中)
		EXP:MASTER:デート経験 ++
		EXP:TARGET:デート経験 ++
		CALL DATE_GOES_ON(1)
	ENDIF
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 2
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 2
	ENDIF
	;キスしたフラグ
	SIF !TALENT:恋慕 && !TALENT:恋人 && !TALENT:愛欲 && !TALENT:セフレ
		TCVAR:314 = 1440 * DAY + TIME
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE312
;キスする実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(312)
	RETURN RESULT
;時間停止中はスキップ
IF !FLAG:70
	;馴れ合い強度1
	SIF (TCVAR:MASTER:馴れ合い < 1) && !CFLAG:睡眠
		RETURN 0
	;睡眠中
	SIF CFLAG:睡眠 && !CFLAG:イタズラ && !CFLAG:添い寝中
		RETURN 0
	;仕事中
	SIF CFLAG:行動 == 5
		RETURN 0
	;1時間間隔
	SIF TIME_PROGRESS(TCVAR:314) < 60
		RETURN 0
ENDIF
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
;口使用中
SIF TFLAG:60 && MASTER_POSE(SET_M, SET_M) != TARGET
	RETURN 0
SIF GETBIT (TCVAR:26,2)
	RETURN 0
;シックスナイン体勢
SIF TEQUIP:シックスナイン
	RETURN 0
RETURN 1


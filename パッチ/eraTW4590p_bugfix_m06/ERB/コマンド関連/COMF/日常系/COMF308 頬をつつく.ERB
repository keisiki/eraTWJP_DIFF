;-------------------------------------------------
;頬をつつく
;日常系コマンド　レベル1
;-------------------------------------------------
@COM308
LOCAL = RAND:110
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 109
	LOCAL:1 = 109
IF LOCAL <= 9
	;大成功
	TFLAG:193 = 1
ELSEIF LOCAL >= LOCAL:1
	;失敗
	TFLAG:193 = -1
ELSE
	;残りは成功
	TFLAG:193 = 0
ENDIF

IF CFLAG:TARGET:添い寝中
	DOWNBASE:MASTER:気力 += 50
ELSE
	DOWNBASE:気力 += 50
	DOWNBASE:MASTER:気力 += 50
ENDIF

;固定で獲得するソース
SOURCE:歓楽 = 80
SOURCE:情愛 = 50
SOURCE:反感 = 200

;ABL:親密をみる
IF ABL:親密 <= 3
	SOURCE:歓楽 += 100 + (ABL:親密 * 20)
	SOURCE:情愛 += 150 + (ABL:親密 * 30)
ELSEIF ABL:親密 <= 5
	SOURCE:歓楽 += 300 + (ABL:親密 * 40)
	SOURCE:情愛 += 350 + (ABL:親密 * 30)
ELSEIF ABL:親密 <= 8
	SOURCE:歓楽 += 600 + (ABL:親密 * 50)
	SOURCE:情愛 += 500 + (ABL:親密 * 40)
ELSEIF ABL:親密 <= 11
	SOURCE:歓楽 += 800 + (ABL:親密 * 70)
	SOURCE:情愛 += 700 + (ABL:親密 * 60)
ELSE
	SOURCE:歓楽 += 1200 + (ABL:親密 * 40)
	SOURCE:情愛 += 1500 + (ABL:親密 * 20)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:歓楽 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:歓楽 += 200 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:歓楽 += 1000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:歓楽 < 0
	SOURCE:歓楽 = 0

SOURCE:屈従 = 500
SOURCE:受動 = 100 + 200 * ABL:従順

IF TFLAG:193 == -1
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:情愛 , 0.50
	TIMES SOURCE:受動 , 0.50
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:反感 , 1.50
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:歓楽 , 1.00
	TIMES SOURCE:情愛 , 1.00
	TIMES SOURCE:受動 , 1.00
	TIMES SOURCE:屈従 , 1.00
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:情愛 , 2.00
	TIMES SOURCE:受動 , 2.00
	TIMES SOURCE:屈従 , 0.50
	TIMES SOURCE:反感 , 0.75
ENDIF
TIME += 10
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE308
;頬をつつく実行判定
SIF !TFLAG:100
	RETURN 0
;睡眠姦中
SIF CFLAG:イタズラ > 1
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(308)
	RETURN RESULT
;睡眠中でない
SIF !CFLAG:睡眠 && !CFLAG:添い寝中
	RETURN 0
;イタズラ中でない
SIF !CFLAG:イタズラ && !CFLAG:添い寝中
	RETURN 0
;仕事中
SIF CFLAG:行動 == 5
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
RETURN 1

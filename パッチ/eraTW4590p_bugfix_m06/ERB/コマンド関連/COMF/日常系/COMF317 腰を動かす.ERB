;-------------------------------------------------
;腰を動かす(V)
;日常系コマンド　レベル3
;-------------------------------------------------
@COM317

IF ABL:MASTER:腰 >= 3
	CALL ASK_M("激しく",1,"ねちっこく",1,"グラインド",1)
	TFLAG:194 = RESULT + 1

	IF EFFECTIVE_PATTERN_V:(TEQUIP:Ｖコンボ) == TFLAG:194
		TEQUIP:Ｖコンボ ++
		TFLAG:193 = TEQUIP:Ｖコンボ
		SIF TEQUIP:Ｖコンボ == 3
			TEQUIP:Ｖコンボ = 0
	ENDIF
ENDIF
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER

CSTR:PLAYER:10 += @"{TARGET}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:PLAYER:2 = 1
;強度
TCVAR:PLAYER:4 = 500 + ABL:腰 * 50 + ABL:膣 * 50

CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:TARGET:2 = 13
;強度
TCVAR:TARGET:4 = 1500

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 80
	SOURCE:MASTER:快Ｃ = 40 + ABL:膣 * 50
	SOURCE:快Ｖ = 40 + ABL:PLAYER:腰 * 30
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
	ENDIF
	EXP:Ｖ性交経験 ++
	EXP:PLAYER:挿入経験 ++
ELSE
	DOWNBASE:PLAYER:体力 += 50
	DOWNBASE:PLAYER:気力 += 80

	SOURCE:MASTER:快Ｃ = 40 + ABL:腰 * 50 + ABL:膣 * 50
	SOURCE:快Ｖ = 50 + ABL:PLAYER:腰 * 20
	SOURCE:情愛 = 200
	SOURCE:苦痛 = 250
	SOURCE:欲情 = 250
	SOURCE:露出 = 150
	SOURCE:逸脱 = 150
	SOURCE:反感 = 100

	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;抑圧、抵抗持ちには、抑鬱ソース追加
	IF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 20
	ENDIF
	;Vへの苦痛
	CALL PAIN_CHECK_V(TARGET)
	;-------------------------------------------------
	;経験の処理
	;-------------------------------------------------
	EXP:PLAYER:挿入経験 ++
	;同性愛　レズなら戻り値1ゲイなら戻り値2
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 4
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
	ENDIF
	EXP:Ｖ性交経験 ++
	;-------------------------------------------------
	;コンボ
	;-------------------------------------------------
	IF TFLAG:193 == 2
		SOURCE:快Ｖ += 10 + ABL:PLAYER:腰 * 15
		SOURCE:欲情 += 100
		SOURCE:屈従 += 100
		SOURCE:情愛 += 50
		TCVAR:PLAYER:4 += ABL:膣 * 50
		EXP:Ｖ性交経験 ++
	ELSEIF TFLAG:193 == 3
		SOURCE:快Ｖ += 50 + ABL:PLAYER:腰 * 30
		SOURCE:欲情 += 200
		SOURCE:屈従 += 300
		SOURCE:情愛 += 200
		TCVAR:PLAYER:4 += ABL:膣 * 150
		SIF ABL:Ｖ感覚 >= 5
		SOURCE:快Ｃ += 30
		EXP:Ｖ性交経験 ++
		TFLAG:好感度ボーナス = 100
	ENDIF
	TIME += 5
ENDIF
;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の膣内⇔調教者のＰの汚れが移動
STAIN:PLAYER:2 |= STAIN:3
STAIN:PLAYER:2 |= STAIN:6
STAIN:3 |= STAIN:PLAYER:2
STAIN:6 |= STAIN:PLAYER:2
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE317
;実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(317)
	RETURN RESULT
;停止中は不可
SIF FLAG:70
	RETURN 0
;馴れ合い強度2
SIF (TCVAR:MASTER:馴れ合い < 2) && CFLAG:睡眠 == 0
	RETURN 0
;着衣
SIF TEQUIP:0 & 4
	RETURN 0
;睡眠中
SIF CFLAG:睡眠 && CFLAG:イタズラ < 2
	RETURN 0
;仕事中
SIF CFLAG:行動 == 5
	RETURN 0
;デート中
SIF CHK_DATENOW(CFLAG:デート中) && CFLAG:現在位置 != OMANEKIBEYA()
	RETURN 0
;添い寝中
SIF CFLAG:添い寝中
	RETURN 0
SIF TCVAR:TARGET:100 != PLAYER
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
SIF GETBIT (TCVAR:26,8)
	RETURN 0
SIF CFLAG:うふふ != 2
	RETURN 0
;TARGETが挿入されてない
SIF TEQUIP:Ｖ接触部位 != 1
	RETURN 0
RETURN 1


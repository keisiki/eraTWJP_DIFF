;-------------------------------------------------
;出掛ける
;日常系コマンド、レベル1
;-------------------------------------------------
@COM405
VARSET LOCAL
FOR LOCAL,1,CHARANUM
	IF CFLAG:(LOCAL):同行
		LOCAL:1 ++
		SIF CFLAG:LOCAL:衰弱 || BASE:LOCAL:気力 == 0
			LOCAL:2 ++
		SIF !CHARA_HOLIDAY(LOCAL) && BASE:LOCAL:仕事量
			LOCAL:3 ++
	ENDIF
NEXT

IF NEMUKE() >= 720
;	PRINTL 門限を超えているため出掛けることは出来ません
	PRINTL 眠気があるので、出掛けない方が良いでしょう
	RETURN -1
ELSEIF LOCAL:1 > 1
	PRINTL 二人以上連れて出掛けることは出来ません
	RETURN -1
ELSEIF LOCAL:2 || BASE:MASTER:気力 == 0
	PRINTFORML 体調不良のため出掛けることは出来ません\@ BASE:MASTER:気力 == 0 ? （気力ゼロ） # （同行者が体調不良）\@
	RETURN -1
ELSEIF LOCAL:3 && FESTIVAL() == ""
	PRINTL 同行者が仕事日のため出掛けることは出来ません
	RETURN -1
ELSEIF TIME:5 == 9 || TIME:5 == 5
	PRINTL 悪天候のため出掛けることは出来ません
	RETURN -1
ELSEIF TALENT:MASTER:100 < -4 
	PRINTL この身体で出掛けるのは危険だ
	RETURN -1
ENDIF
LOCAL:3 = 0
DRAWLINE
CALL OTHERREGIONS
LOCAL:2 = RESULT
LOCAL = 0
IF LOCAL:2 == MAIN_MAP || LOCAL:2 == 100
	RETURN -1
ENDIF
RETURN 1

@COM_ABLE405
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(405)
	RETURN RESULT
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
SIF CFLAG:うふふ == 2
	RETURN 0
;参道設定
SIF !CHK_ENTRANCE(CFLAG:MASTER:現在位置, MAIN_MAP)
	RETURN 0
RETURN 1
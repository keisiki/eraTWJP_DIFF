;-------------------------------------------------
;料理を作る
;日常系コマンド、レベル1
;-------------------------------------------------
@COM413
;食材がない
IF !HAS_INGREDIENT()
	PRINTFORMW 食材がない
	RETURN -1
ENDIF
CALL COOKING_MENU
SIF !RESULT
	RETURN -1
IF TFLAG:194 == 1
	DOWNBASE:MASTER:体力 = 50
	DOWNBASE:MASTER:気力 = 50
	TIME += 15
	SIF CFLAG:同行
		CFLAG:同行 += 15
ELSEIF TFLAG:194 == 2
	DOWNBASE:MASTER:体力 = 100
	DOWNBASE:MASTER:気力 = 100
	TIME += 45
	SIF CFLAG:同行
		CFLAG:同行 += 45
ELSEIF TFLAG:194 == 3
	DOWNBASE:MASTER:体力 = 100
	DOWNBASE:MASTER:気力 = 100
	TIME += 45
	SIF CFLAG:同行
		CFLAG:同行 += 45
ELSEIF TFLAG:194 == 4
	DOWNBASE:MASTER:体力 = 150
	DOWNBASE:MASTER:気力 = 150
	TIME += 45
	SIF CFLAG:同行
		CFLAG:同行 += 45
ENDIF

IF TARGET > 0 && !CFLAG:睡眠 && !FLAG:70
	IF TARGET && ABL:親密 * 500 + ABL:従順 * 500 + ABL:料理技能 * 1000 > CFLAG:310 - CFLAG:MASTER:310
		TFLAG:193 = 1
		EXP:料理経験 ++
	ELSE
		TFLAG:193 = 0
	ENDIF
	;固定で獲得するソース
	SOURCE:歓楽 = 200
	
	;信頼
	TFLAG:98 = 3


	;ABL:従順をみる
	IF ABL:従順 <= 1
		SOURCE:歓楽 += (ABL:従順 * 60)
	ELSEIF ABL:従順 <= 3
		SOURCE:歓楽 += 200 + (ABL:従順 * 60)
	ELSEIF ABL:従順 <= 5
		SOURCE:歓楽 += 500 + (ABL:従順 * 60)
	ELSEIF ABL:従順 <= 8
		SOURCE:歓楽 += 750 + (ABL:従順 * 75)
	ELSEIF ABL:従順 <= 11
		SOURCE:歓楽 += 1000 + (ABL:従順 * 75)
	ELSE
		SOURCE:歓楽 += 2000 + (ABL:従順 * 30)
	ENDIF

	;好感度をみる
	IF CFLAG:2 <= 500
		SOURCE:歓楽 += CFLAG:2
	ELSEIF CFLAG:2 <= 5000
		SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
	ELSE
		SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
	ENDIF
	SIF SOURCE:歓楽 < 0
		SOURCE:歓楽 = 0
ENDIF
TCVAR:MASTER:308 = 0
SIF TARGET < 1
	CALL MIX_MENU

SIF ITEM:102 && !RAND:2
	EXP:MASTER:料理経験 ++
DISHTIME = 1440 * DAY + TIME

STAIN:MASTER:0 = 0
STAIN:MASTER:1 = 0
CFLAG:MASTER:口に精液 = 0
CFLAG:MASTER:顔に精液 = 0
CFLAG:MASTER:手に精液 = 0
IF TARGET
	STAIN:0 = 0
	STAIN:1 = 0
	CFLAG:口に精液 = 0
	CFLAG:顔に精液 = 0
	CFLAG:手に精液 = 0
ENDIF

RETURN 1



@COOKING_MENU
PRINTL 何を作りますか？
CALL ASK_M("やっぱりやめる",1,"軽食",1,"主食",ABL:MASTER:料理技能 > 1,"デザート",ABL:MASTER:料理技能 > 2,"禁断の料理",ABL:MASTER:料理技能 > 2, "魚料理", ABL:MASTER:料理技能 > 1)
SELECTCASE RESULT
CASE 1
	DISHTYPE = RESULT
	CALL INGREDIENT_CHOICE
	SIF RESULT == 0
		RETURN 0
	TFLAG:194 = 1
	CALL COOKING
	RETURN 1
CASE 2
	DISHTYPE = RESULT
	CALL INGREDIENT_CHOICE
	SIF RESULT == 0
		RETURN 0
	TFLAG:194 = 2
	CALL COOKING
	RETURN 1
CASE 3
	DISHTYPE = RESULT
	CALL INGREDIENT_CHOICE
	SIF RESULT == 0
		RETURN 0
	TFLAG:194 = 3
	CALL COOKING
	RETURN 1
CASE 4
	IF !ITEM:0
		PRINTFORMW 食材が足りません
		TIME += 5
		RETURN 0
	ENDIF
	
	ITEM:0 --
	DISHTYPE = 4
	CALL COOKING
	TFLAG:194 = FLAG:料理
	RETURN 1
CASE 5
	PRINTFORML 食材を選んでください
	CALL ASK_M(@"魚　{ITEM:魚}尾所持", ITEM:魚, @"うまい魚　{ITEM:うまい魚}尾所持", ITEM:うまい魚,@"カエル　{ITEM:カエル}匹所持", ITEM:カエル,"やめる",1)
	SELECTCASE RESULT
		CASE 0
			DISHQUALITY = 1
			ITEM:魚 --
		DISHTYPE = 5
		CASE 1
			DISHQUALITY = 2
			ITEM:うまい魚 --
		DISHTYPE = 5
		CASE 2
			DISHQUALITY = 1
			ITEM:カエル --
		DISHTYPE = 6
		CASE 3
			RETURN 0
	ENDSELECT
	TFLAG:194 = 2
	CALL COOKING
	RETURN 1
CASE 6

CASE 0
	RETURN 0
ENDSELECT





;料理実行関数
;料理依頼に流用できるように作成
;先に呼び出し元でDISHTYPEとDISHQUALITYを決めておくこと
;食材の消費処理も先にすませておくこと
@COOKING(CHARA)
#DIM  CHARA ;キャラを渡すと手伝ってくれる（依頼用、COM413は実行後に料理経験入るので不要）
#DIMS CONST MENU_TYPE, 7 = "", "SNACK", "MAINDISH", "DESSERT", "FORBIDDEN","FISH", "FROG"
CALL SET_DISHNAME(MENU_TYPE:DISHTYPE)
PRINTFORMW %DISHNAME%を作ります
SELECTCASE DISHTYPE
CASE 1
	;ゆゆ様
	IF !RAND:20 && VISIT(66) && AT_HOME(MASTER) && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
		SETBIT FLAG:50,55
		CFLAG:66:現在位置 = CFLAG:MASTER:現在位置
		PRINTFORML 料理してる気配を嗅ぎつけて%CALLNAME:66%がやってきた……
	ENDIF
CASE 2
	;ゆゆ様
	IF !RAND:10 && VISIT(66) && AT_HOME(MASTER) && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
		SETBIT FLAG:50,55
		CFLAG:66:現在位置 = CFLAG:MASTER:現在位置
		PRINTFORML 料理してる気配を嗅ぎつけて%CALLNAME:66%がやってきた……
	ENDIF
	EXP:MASTER:料理経験 ++
	SIF CHARA
		EXP:CHARA:料理経験 ++
CASE 3
CASE 4
CASE 5
	YOGORE:(CFLAG:MASTER:現在位置) += 500
ENDSELECT
YOGORE:(CFLAG:MASTER:現在位置) += 500
EXP:MASTER:料理経験 ++
SIF CHARA
	EXP:CHARA:料理経験 ++



;料理抽選関数
;料理の決定とFLAGへの記録、DISHNAME決定まで
;DISHQUALITYの設定は呼び出し元で行うこと
@SET_DISHNAME(MENU_TYPE)
#DIMS MENU_TYPE
#DIM  FOOD_ID
SELECTCASE MENU_TYPE
CASE "SNACK"
	DISHTYPE = 1
	FLAG:料理 = RAND:MENUS_SNACK + 1
	DISHNAME = %FOOD_SNACK:(FLAG:料理)%
CASE "MAINDISH"
	DISHTYPE = 2
	FOOD_ID = RAND:MENUS_MAINDISH
	FLAG:料理 = FOOD_ID + 10
	DISHNAME = %FOOD_MAINDISH:(FOOD_ID)%
	IF FESTIVAL_MENU() != ""
		DISHNAME = %FESTIVAL_MENU()%
		FLAG:料理 = 999
		DISHTYPE = 5
	ENDIF
CASE "DESSERT"
	DISHTYPE = 3
	FOOD_ID = RAND:MENUS_DESSERT
	DISHNAME = %FOOD_DESSERT:(FOOD_ID)%
	FLAG:料理 = FOOD_ID + 50
CASE "FORBIDDEN"
	DISHTYPE = 4
	DISHNAME = %FOOD_FORBIDDEN:(DAY:2)%
	FLAG:料理 = 99 + DAY:2
CASE "FISH"
	DISHTYPE = 2
	DISHNAME = %FOOD_FISH:(RAND:MENUS_FISH)%
CASE "FROG"
	DISHTYPE = 2
	DISHNAME = %FOOD_FROG:(RAND:MENUS_FROG)%
ENDSELECT




@食事(ARG)
#DIM 精力回復量
#DIM 気力回復量
#DIM 体力回復量
#DIM 精力回復率
#DIM 気力回復率
#DIM 体力回復率

;回復率は0.1%単位

精力回復量 = 0
気力回復量 = 0
体力回復量 = 0
精力回復率 = 0
気力回復率 = 0
体力回復率 = 0

SELECTCASE DISHTYPE
	CASE 1
		体力回復率 = 65
		気力回復率 = 200
		精力回復率 = 50
		CALL 満腹度上昇(ARG,"軽食")
	CASE 2
		体力回復率 = 200
		気力回復率 = 300
		精力回復率 = 200
		CALL 満腹度上昇(ARG,"主食")
	CASE 3
		体力回復率 = 50
		気力回復率 = 500
		精力回復率 = 50
		CALL 満腹度上昇(ARG,"デザート")
	CASE 4
		体力回復率 = 300
		気力回復率 = 300
		精力回復率 = 300
		CALL 満腹度上昇(ARG,"主食")
		EXP:ARG:Ｃ経験 += 30
	CASE 5
		体力回復率 = 100
		気力回復率 = 150
		精力回復率 = 100
		CALL 満腹度上昇(ARG,"主食")
ENDSELECT
体力回復量 = (MAXBASE:ARG:体力 * 体力回復率 / 1000) * (ABL:MASTER:料理技能 + 5 + DISHQUALITY) / 10
気力回復量 = (MAXBASE:ARG:気力 * 気力回復率 / 1000) * (ABL:MASTER:料理技能 + 5 + DISHQUALITY) / 10
精力回復量 = (MAXBASE:ARG:精力 * 精力回復率 / 1000) * (ABL:MASTER:料理技能 + 5 + DISHQUALITY) / 10

CALL RECOVER(ARG,体力回復量,"体力")
CALL RECOVER(ARG,気力回復量,"気力")
SIF ARG == MASTER
	CALL RECOVER(ARG,精力回復量,"精力")

;イタズラを仕込まれている
IF !TCVAR:MASTER:308 < 8
	IF GETBIT(TCVAR:MASTER:308, 21)
		TCVAR:TARGET:媚薬 = 210
	ELSEIF GETBIT(TCVAR:MASTER:308, 22)
		TCVAR:TARGET:利尿剤 = 210
	ELSEIF GETBIT(TCVAR:MASTER:308, 23)
		TCVAR:TARGET:惚れ薬 = 210
	ELSEIF GETBIT(TCVAR:MASTER:308, 24)
		IF TCVAR:TARGET:睡眠薬強度 < 2
			TCVAR:TARGET:睡眠薬強度 = 2
			TCVAR:TARGET:睡眠薬お茶使用 = 1
		ENDIF
	ENDIF
ENDIF

@満腹度上昇(ARG,ARGS)
SELECTCASE ARGS
	CASE "軽食"
		TCVAR:ARG:空腹時刻 = 1440 * DAY + TIME + 180
		SIF TALENT:ARG:健啖
			TCVAR:ARG:空腹時刻 -= 30
	CASE "主食"
		TCVAR:ARG:空腹時刻 = 1440 * DAY + TIME + 360
		SIF TALENT:ARG:健啖
			TCVAR:ARG:空腹時刻 -= 90
	CASE "デザート"
		TCVAR:ARG:デザート空腹時刻 = 1440 * DAY + TIME + 180
		SIF TALENT:ARG:健啖
			TCVAR:ARG:デザート空腹時刻 -= 30
ENDSELECT

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE413
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;掃除実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(413)
	RETURN RESULT

;厨房なのか
SIF !KITCHEN(CFLAG:MASTER:現在位置)
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
SIF CFLAG:うふふ
	RETURN 0
;添い寝中
SIF CFLAG:添い寝中
	RETURN 0
;イタズラ中
SIF CFLAG:MASTER:イタズラ
	RETURN 0
;既に料理を作っている
SIF DISHNAME != ""
	RETURN 0
RETURN 1

@HUNGER(ARG)
#FUNCTIONS
LOCAL = TCVAR:ARG:空腹時刻 - TIME:1
SELECTCASE LOCAL
	CASE IS > 270
	SETCOLOR C_GREEN
		RETURNF "満腹"
	CASE IS > 180
	SETCOLOR C_P_GREEN
		RETURNF "腹八分"
	CASE IS > 0
	SETCOLOR C_P_YELLOW
		RETURNF "普通"
	CASEELSE
		RETURNF "食事可"
ENDSELECT

@INGREDIENT_CHOICE
PRINTFORML 食材を選んでください
CALL ASK_M(@"雑穀({ITEM:48})", ITEM:48, @"古米({ITEM:50})", ITEM:50, @"白米({ITEM:47})", ITEM:47,"やめる",1)
SELECTCASE RESULT
	CASE 0
		ITEM:48 --
	CASE 1
		ITEM:50 --
	CASE 2
		ITEM:47 --
	CASE 3
		DISHTYPE = 0
		RETURN 0
ENDSELECT
DISHQUALITY = RESULT + 1
RETURN DISHQUALITY

@RESET_DISH
DISHTIME = 0
DISHSPOIL = 0
FLAG:料理 = 0
DISHQUALITY = 0
DISHTYPE = 0
DISHNAME =

;食材所持判定
@HAS_INGREDIENT
#FUNCTION
;こいつらは料理技能関係なし
SIF MAX(ITEM:白米, ITEM:雑穀, ITEM:古米) > 0
	RETURNF 1
;魚料理は料理技能2〜
SIF ABL:MASTER:料理技能 > 1 && MAX(ITEM:魚, ITEM:うまい魚, ITEM:カエル) > 0
	RETURNF 1
;禁断の料理は料理技能3〜
SIF ABL:MASTER:料理技能 > 2 && ITEM:ローター > 0
	RETURNF 1
RETURNF 0

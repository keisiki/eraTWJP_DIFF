;-------------------------------------------------
;頭を撫でる
;日常系コマンド　レベル2
;-------------------------------------------------
@COM309
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 10
ELSE
	LOCAL = RAND:110
	LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
	SIF LOCAL:1 > 109
		LOCAL:1 = 109
	IF LOCAL <= 9
		;大成功
		TFLAG:193 = 1
		IF CHK_DATENOW(CFLAG:TARGET:デート中)
			EXP:MASTER:デート経験 ++
			EXP:TARGET:デート経験 ++
		ENDIF
		TFLAG:98 = 2
	ELSEIF LOCAL >= LOCAL:1
		;失敗
		TFLAG:193 = -1
		TFLAG:98 = -2
	ELSE
		;残りは成功
		TFLAG:193 = 0
		TFLAG:98 = 0
	ENDIF

	;先制発生時は失敗しない
	SIF TFLAG:102 == 3
		TFLAG:193 = MAX(TFLAG:193,0)
	IF CFLAG:添い寝中
		DOWNBASE:MASTER:気力 += 50
	ELSEIF CHK_DATENOW(CFLAG:TARGET:デート中)
		DOWNBASE:気力 += 30
		DOWNBASE:MASTER:気力 += 30
	ELSE
		DOWNBASE:気力 += 50
		DOWNBASE:MASTER:気力 += 50
	ENDIF

	TFLAG:98 += 1

	;固定で獲得するソース
	SOURCE:歓楽 = 150
	SOURCE:情愛 = 150
	SOURCE:反感 = 100

	;ABL:親密をみる
	IF ABL:親密 <= 3
		SOURCE:歓楽 += 150 + (ABL:親密 * 50)
		SOURCE:情愛 += 180 + (ABL:親密 * 40)
	ELSEIF ABL:親密 <= 5
		SOURCE:歓楽 += 500 + (ABL:親密 * 50)
		SOURCE:情愛 += 450 + (ABL:親密 * 30)
	ELSEIF ABL:親密 <= 8
		SOURCE:歓楽 += 800 + (ABL:親密 * 50)
		SOURCE:情愛 += 700 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 11
		SOURCE:歓楽 += 1000 + (ABL:親密 * 90)
		SOURCE:情愛 += 900 + (ABL:親密 * 50)
	ELSE
		SOURCE:歓楽 += 1800 + (ABL:親密 * 40)
		SOURCE:情愛 += 2000 + (ABL:親密 * 20)
	ENDIF

	;好感度をみる
	IF CFLAG:2 <= 500
		SOURCE:歓楽 += CFLAG:2
	ELSEIF CFLAG:2 <= 5000
		SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
	ELSE
		SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
	ENDIF
	SIF SOURCE:歓楽 < 0
		SOURCE:歓楽 = 0

	;寝てる場合効果半額
	IF CFLAG:睡眠 && SOURCE:歓楽 > 0
		SOURCE:歓楽 = SOURCE:歓楽 / 2
		SOURCE:情愛 = SOURCE:情愛 / 2
	ENDIF

	LOCAL = 0
	;睡眠中は好感度変化無し
	IF !CFLAG:睡眠
		;親密が低いと好感度低下
		IF ABL:親密 == 0
			LOCAL -= 3
		ELSEIF ABL:親密 == 1
			LOCAL -= 2
		ELSEIF ABL:親密 == 2
			LOCAL -= 1
		ENDIF
		;好感度が低いと好感度低下
		IF CFLAG:2 <= 50
			LOCAL -= 3
		ELSEIF CFLAG:2 <= 100
			LOCAL -= 2
		ELSEIF CFLAG:2 <= 250
			LOCAL -= 1
		ENDIF
	ENDIF

	SOURCE:欲情 = 50 + 150 * ABL:欲望
	SOURCE:受動 = 100 + 200 * ABL:従順

	;好感度変化
	TFLAG:97 += LOCAL

	IF TFLAG:193 == -1
		TIMES SOURCE:歓楽 , 0.10
		TIMES SOURCE:情愛 , 0.50
		TIMES SOURCE:受動 , 0.50
		TIMES SOURCE:欲情 , 0.50
		TIMES SOURCE:反感 , 1.50
	ELSEIF TFLAG:193 == 0
		TIMES SOURCE:歓楽 , 1.00
		TIMES SOURCE:情愛 , 1.00
		TIMES SOURCE:受動 , 1.00
		TIMES SOURCE:欲情 , 1.00
	ELSE
		TIMES SOURCE:歓楽 , 2.00
		TIMES SOURCE:情愛 , 2.00
		TIMES SOURCE:受動 , 2.00
		TIMES SOURCE:欲情 , 2.00
		TIMES SOURCE:反感 , 0.75
	ENDIF

	IF CHK_DATENOW(CFLAG:TARGET:デート中)
		TIME += 10
		EXP:MASTER:デート経験 ++
		EXP:TARGET:デート経験 ++
		;頭を撫でたフラグ
		TCVAR:304 = 1440 * DAY + TIME
		CALL DATE_GOES_ON(1)
	ELSE
		TIME += 20
		;頭を撫でたフラグ
		TCVAR:304 = 1440 * DAY + TIME
	ENDIF
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE309
;頭を撫でる実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(309) && !CFLAG:イタズラ && !CFLAG:添い寝中
	RETURN RESULT
;時間停止中はスキップ
IF !FLAG:70
	;馴れ合い強度1
	SIF (TCVAR:MASTER:馴れ合い < 1) && !CFLAG:睡眠
		RETURN 0
	;睡眠中
	SIF CFLAG:睡眠 && !CFLAG:イタズラ && !CFLAG:添い寝中
		RETURN 0
	;睡眠姦中
	SIF CFLAG:イタズラ > 1
		RETURN 0
	;仕事中
	SIF CFLAG:行動 == 5
		RETURN 0
	;1時間間隔
	SIF TIME_PROGRESS(TCVAR:304) < 60 && !CFLAG:イタズラ && !CFLAG:添い寝中
		RETURN 0
ENDIF
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0

RETURN 1


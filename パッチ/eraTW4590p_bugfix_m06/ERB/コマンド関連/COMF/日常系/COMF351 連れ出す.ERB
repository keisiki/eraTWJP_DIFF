;-------------------------------------------------
;連れ出す
;日常系コマンド、レベル2
;-------------------------------------------------
@COM351
#DIM NUM_TARGET
NUM_TARGET = GET_TARGETNUM()
;連れ出す
VARSET LOCAL
SIF !NUM_TARGET
	RETURN -1
FOR LOCAL, 1, NUM_TARGET + 1
	IF COM_351_CONDITION(TARGET:LOCAL) >= 2
		LOCAL:97 += 1
	ENDIF
NEXT
IF NUM_TARGET == 1 || LOCAL:97 == 1
	IF COM_351_CONDITION(TARGET) < 1
		RETURN -1
	ELSEIF COM_351_CONDITION(TARGET) < 2
		PRINTFORMW %CALLNAME:(TARGET)%は取り込み中です。
		RETURN -1
	ENDIF
	IF CFLAG:TARGET:同行
		CFLAG:TARGET:同行 = 0
		CFLAG:MASTER:同行 = 0
		PRINTFORMW 同行解除します。
	ELSE
		CFLAG:TARGET:同行 = 60
		;雨＋外＋傘or相手が傘の時は相合い傘
		SIF TIME:5 > 3 && TIME:5 < 14 && OUTROOF(CFLAG:MASTER:現在位置) && (ITEM:36 || EQUIP:TARGET:アクセサリ == 6)
			TEQUIP:TARGET:201 = 1
		PRINTFORMW %CALLNAME:TARGET%を連れ出します
		CFLAG:MASTER:同行 = 60
	ENDIF
	TIME += 1
	SIF CFLAG:TARGET:同行
		CALL KOJO_MESSAGE_SEND("DIRECT",351,TARGET,-1,-1)
	RETURN 1
ENDIF


PRINTL 誰を誘いますか？
FOR LOCAL, 1, NUM_TARGET + 1
;	SIF CFLAG:(TARGET:LOCAL):態度 > 0 || TCVAR:(TARGET:LOCAL):泥酔 == 1
	SIF COM_351_CONDITION(TARGET:LOCAL) <= 0
		CONTINUE
	PRINTFORML [{LOCAL}] - %CALLNAME:(TARGET:LOCAL)%
NEXT
PRINTL [98] - まとめて連れ出す
PRINTL [99] - 同行解除
PRINTL [100] - やめる
DO
	INPUT
	SELECTCASE RESULT
	CASE 100
		RETURN -1
	CASE 99
		FOR LOCAL, 1, NUM_TARGET + 1
			SIF CFLAG:(TARGET:LOCAL):同行 
				CFLAG:(TARGET:LOCAL):同行 =  0
			;相合い傘もなくなる
			TEQUIP:(TARGET:LOCAL):201 = 0
		NEXT
		CFLAG:MASTER:同行 = 0
		RETURN 1
	CASE 98
		FOR LOCAL, 1, NUM_TARGET + 1
			IF COM_351_CONDITION(TARGET:LOCAL) >= 3
				CFLAG:(TARGET:LOCAL):同行 = 60
				LOCAL:98 += 1
			ENDIF
		NEXT
		PRINTFORMW {LOCAL:98}人まとめて連れ出します
		CFLAG:MASTER:同行 = 60
		TIME += LOCAL:98
		SIF CFLAG:TARGET:同行
			CALL KOJO_MESSAGE_SEND("DIRECT",351,TARGET,-1,-1)
		RETURN 1
	CASEELSE
		IF !INRANGE(RESULT, 1, NUM_TARGET)
			CLEARLINE 1
			CONTINUE
		ELSEIF COM_351_CONDITION(TARGET:RESULT) < 1
			CLEARLINE 1
			CONTINUE
		ELSEIF COM_351_CONDITION(TARGET:RESULT) < 2
			PRINTFORMW %CALLNAME:(TARGET:RESULT)%は取り込み中です。
			CLEARLINE 2
			CONTINUE
		ENDIF
		BREAK
	ENDSELECT
LOOP 1

CFLAG:(TARGET:RESULT):同行 = 60
;雨＋外＋傘or相手が傘の時は相合い傘
SIF TIME:5 > 3 && TIME:5 < 14 && OUTROOF(CFLAG:MASTER:現在位置) && (ITEM:36 || EQUIP:TARGET:アクセサリ == 6)
	TEQUIP:TARGET:201 = 1
PRINTFORMW %CALLNAME:(TARGET:RESULT)%を連れ出します
CFLAG:MASTER:同行 = 60
TIME += 1
;SOURCEまでを回さないのでここで口上を表示させる
SIF CFLAG:TARGET:同行
	CALL KOJO_MESSAGE_SEND("DIRECT",351,TARGET,-1,-1)

RETURN 1

@EQUIP_COM351(ARG)
;ソースは控えめに適当。愛情入りすぎたので自重
IF FLAG:70 == 1

ELSE
	SOURCE:歓楽 += ABL:親密 * 5
	SOURCE:情愛 += ABL:親密 * 5
	SOURCE:反感 += 100 - MIN(ABL:親密 * 10,100)
	SIF ABL:親密 > 9 && TIME_PROGRESS(TIME:1) > 10
	EXP:ARG:愛情経験 += RAND:2
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE351
;停止中は不可
SIF FLAG:70
	RETURN 0
;連れ出す実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(351)
	RETURN RESULT
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
SIF CFLAG:うふふ
	RETURN 0
;デート中
SIF CHK_DATENOW(CFLAG:MASTER:デート中)
	RETURN 0
SIF IN_HOME(CFLAG:現在位置) == 0
	RETURN 0
;TARGETの馴れ合い強度1
FOR LOCAL,0,CHARANUM
	;TARGET:1〜をさがす
	SIF !TARGET:LOCAL
		CONTINUE
	;添い寝中
	SIF CFLAG:(TARGET:LOCAL):添い寝中
		CONTINUE
	;仕事中は除外
	SIF CFLAG:(TARGET:LOCAL):行動 == 5 && CFLAG:(TARGET:LOCAL):350 != 49 && CFLAG:(TARGET:LOCAL):350 != 51
		CONTINUE
	;可能なキャラが居れば1を返す
	SIF COM_351_CONDITION(TARGET:LOCAL) > 0
		RETURN 1
NEXT
RETURN 0



;戻り値
; 3 まとめて連れ出せる
; 2 個別選択で連れ出せる
; 1 連れ出せない（取り込み中メッセージ）
; 0 連れ出せない
@COM_351_CONDITION(CHARA)
#FUNCTION
#DIM CHARA
SIF CHARA == MASTER
	RETURNF 0
SIF CFLAG:CHARA:態度 > 0 && CFLAG:CHARA:睡眠 == 0 && CFLAG:CHARA:行動 != 5
	RETURNF 3

;現在同行中のキャラは許可(同行条件を満たさなくなった場合に手動で同行解除できるようにする)
SIF CFLAG:CHARA:同行
	RETURNF 2

SIF CFLAG:CHARA:態度 < 1 && TCVAR:CHARA:泥酔 == 0 && !CAN_IRAI_CHARA_GO_TOGATHER(CHARA)
	RETURNF 0
SIF CFLAG:CHARA:睡眠
	RETURNF 0
SIF CFLAG:CHARA:行動 == 5 && !TCVAR:CHARA:泥酔
	RETURNF 1

RETURNF 2




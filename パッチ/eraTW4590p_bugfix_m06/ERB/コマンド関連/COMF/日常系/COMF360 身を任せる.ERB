;-------------------------------------------------
;身を任せる
;日常系コマンド、レベル3
;-------------------------------------------------
@COM360
TIME += 10
;気力ゼロのときは変動しない（身を任せるしかできないため）
SIF BASE:MASTER:気力 <= 0
	RETURN 1
;通常はプラス,サドだと反転
IF TALENT:TARGET:サド
	LOCAL = -1
ELSE
	LOCAL = 1
ENDIF
;指向が反転中は3倍
IF COUNTER_SIKOU:TARGET:(TCVAR:TARGET:カウンター行動) > 0 && TALENT:TARGET:サド
	LOCAL *= 3
ELSEIF COUNTER_SIKOU:TARGET:(TCVAR:TARGET:カウンター行動) < 0
	LOCAL *= 3
ENDIF
;［素直］［習得早い］は2倍
SIF TALENT:TARGET:態度 < 0 || TALENT:TARGET:習得速度 > 0
	LOCAL *= 2
;最大影響度10
COUNTER_SIKOU:TARGET:(TCVAR:TARGET:カウンター行動) = LIMIT(COUNTER_SIKOU:TARGET:(TCVAR:TARGET:カウンター行動) + LOCAL, -10, 10)
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE360
;停止中は不可
SIF FLAG:70
	RETURN 0
;なすがまま実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(360)
	RETURN RESULT
SIF CFLAG:うふふ != 2
	RETURN 0
;おねだり中は不可
SIF INRANGE(TCVAR:カウンター行動, 40, 50)
	RETURN 0
RETURN 1

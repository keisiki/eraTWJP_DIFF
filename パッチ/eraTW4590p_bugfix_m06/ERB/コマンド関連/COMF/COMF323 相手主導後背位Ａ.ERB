;-------------------------------------------------
;相手主導後背位アナル
;セックス系コマンド、レベル1
;-------------------------------------------------
@COM323
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;室内でないorバスルームでプレイマットがないと立位に
SIF !INROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20)
	TFLAG:193 = 1

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;奉仕快楽フラグ
TCVAR:PLAYER:108 = 1

CSTR:PLAYER:10 += @"{TARGET}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:PLAYER:2 = 2
;強度
TCVAR:PLAYER:4 = 2000

CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:TARGET:2 = 12
;強度
TCVAR:TARGET:4 = 1500

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
DOWNBASE:体力 += 60
DOWNBASE:気力 += 60
DOWNBASE:PLAYER:体力 += 100
SOURCE:PLAYER:快Ｃ = 400
SOURCE:快Ａ = 500
SOURCE:情愛 = 300
SOURCE:苦痛 = 900
SOURCE:露出 = 100
SOURCE:逸脱 = 200

;上のほうで計算した汚れデータ
SOURCE:不潔 = Y*10 + 60

;Aへの苦痛は先に処理しておく
CALL PAIN_CHECK_A(TARGET)

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;奴隷のＡ⇔調教者のＰの汚れが移動
CALL STAIN_アナルＰ挿入

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:Ａ経験 ++
EXP:PLAYER:挿入経験 ++
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
	ENDIF
		EXP:PLAYER:時姦経験 += 1
ELSE
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 4
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
	ENDIF
	EXP:Ａ性交経験 ++
	;愛情経験
	SIF !ASSIPLAY
		EXP:愛情経験 += TALENT:恋慕
ENDIF
RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE323
SIF !TFLAG:100
	RETURN 0
SIF TCVAR:20 != 44
	RETURN 0
;調教者がオトコ、ふたなりの場合はある程度勃起してないとダメ
SIF (TALENT:PLAYER:2 & 2) && BASE:PLAYER:勃起 < 1000
	RETURN 0
;アナルバイブ挿入中はダメ
SIF TEQUIP:14
	RETURN 0
;アナルビーズ使用中は不可
SIF TEQUIP:15
	RETURN 0
;普通の浣腸使用中は不可
SIF TEQUIP:21
	RETURN 0
;バルーン使用中は不可
SIF TEQUIP:22
	RETURN 0
;電極使用中は不可
SIF TEQUIP:23
	RETURN 0

;体格差がある場合は経験がないとダメ
SIF 挿入不可(TARGET) == 1 && EXP:Ａ経験 < 50 && !TALENT:PLAYER:禁断の知識
	RETURN 0
;さらに体格差があると禁断の知識が必要
SIF 挿入不可(TARGET) == 2 && (!TALENT:PLAYER:禁断の知識 || EXP:Ａ経験 < 50)
	RETURN 0
SIF 挿入不可(TARGET) == 3
	RETURN 0
;調教者がオトコ、半人半妖、ふたなり、もしくはペニスバンドがないとダメ
SIF !(TALENT:PLAYER:2 & 2) && ITEM:PBAND == 0
	RETURN 0


;対面座位からはダメ
SIF TEQUIP:体位 == 4
	RETURN 0
;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
IF TEQUIP:31
	SIF ITEM:20 == 0 && NOITEM == 0
		RETURN 0
ENDIF

RETURN 1
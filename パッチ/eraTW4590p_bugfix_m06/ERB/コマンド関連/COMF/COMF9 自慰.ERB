;-------------------------------------------------
;自慰
;愛撫系コマンド、レベル3
;-------------------------------------------------
@COM9
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = TARGET

CSTR:TARGET:10 += @"{TARGET}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:TARGET:2 = 3
;強度
TCVAR:TARGET:4 = 500

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 5
DOWNBASE:気力 += 50

SOURCE:快Ｃ = 80
SOURCE:快Ｂ = 80
SOURCE:性行動 = 100
SOURCE:恐怖 = 200
SOURCE:露出 = 400
SOURCE:屈従 = 500
SOURCE:逸脱 = 300
SOURCE:反感 = 250
;バイブ
IF TEQUIP:バイブ
	SOURCE:快Ｖ += 80
	SOURCE:屈従 += 100
	SOURCE:反感 += 100
	DOWNBASE:気力 += 10
	TIMES SOURCE:快Ｃ , 0.70
	TIMES SOURCE:快Ｂ , 0.70
ENDIF
;アナルバイブ
IF TEQUIP:アナルバイブ
	SOURCE:屈従 += 150
	SOURCE:反感 += 150
	SOURCE:快Ａ += 80
	DOWNBASE:気力 += 50
	TIMES SOURCE:快Ｃ , 0.70
	TIMES SOURCE:快Ｂ , 0.70
ENDIF
;シャワー
IF TEQUIP:32
	SOURCE:快Ｃ += 50
	SOURCE:露出 += 100
	SOURCE:屈従 += 50
	SOURCE:反感 += 80
ENDIF
;ビデオ
IF TEQUIP:28
	SOURCE:液体 += 50
	SOURCE:欲情 += 100
	SOURCE:反感 += 200
ENDIF
;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:自己愛 < 0 || TALENT:抵抗
	SOURCE:鬱屈 += 400
;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の指⇔奴隷のＢの汚れが移動
STAIN:1 |= STAIN:5
STAIN:5 |= STAIN:1

;奴隷の指⇔奴隷のＶの汚れが移動
STAIN:1 |= STAIN:3
STAIN:3 |= STAIN:1

;シャワーオナニーの場合汚れをリセット、潤滑更に半分。
IF TEQUIP:32 == 1
	STAIN:1 = 0
	STAIN:2 = 2
	STAIN:3 = 1
	STAIN:4 = 8
	PALAM:4 /= 2
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;ビデオ撮影時は経験にプラス
IF TEQUIP:ビデオ撮影
	EXP:自慰経験 += 2
	EXP:調教自慰経験 += 2
ELSE
	EXP:自慰経験 += 1
	EXP:調教自慰経験 += 1
ENDIF
;公開オナニー時は異常経験付加
IF TEQUIP:ビデオ撮影 && !GETBIT(CFLAG:3,0)
	EXP:異常経験 ++
	SETBIT CFLAG:3,0
ENDIF
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 4
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
ENDIF
RETURN 1


@CAN_COM9(ARG)
SKIPDISP ARG
SIF TEQUIP:ビデオ撮影
	PRINT 公開
SIF TEQUIP:シャワー
	PRINT シャワー
IF TEQUIP:バイブ && TEQUIP:アナルバイブ
	PRINT 二穴バイブ
ELSEIF TEQUIP:バイブ
	PRINT バイブ
ELSEIF TEQUIP:アナルバイブ
	PRINT アナルバイブ
ENDIF

IF TEQUIP:ビデオ撮影 || TEQUIP:シャワー || TEQUIP:バイブ || TEQUIP:アナルバイブ
	PRINTL オナニー
ELSE
	PRINTL 自慰
ENDIF
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
VARSET A
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	SIF S
		PRINT  + 
	A += ABL:欲望 * 3
	PRINT 欲望
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
	S = 1
ENDIF
;ABL:露出癖
IF ABL:露出癖
	SIF S
		PRINT  + 
	A += ABL:露出癖 * 4
	PRINT 露出癖
	PRINTV 'LV,ABL:露出癖,'(,ABL:露出癖 * 4,')
	S = 1
ENDIF
;ABL:自慰中毒
IF ABL:自慰中毒
	SIF S
		PRINT  + 
	A += ABL:自慰中毒 * 3
	PRINT 自慰中毒
	PRINTV 'LV,ABL:自慰中毒,'(,ABL:自慰中毒 * 3,')
	S = 1
ENDIF

IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 3
	PRINT 快楽刻印
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 3,')
	S = 1
ENDIF

;自制心
IF TALENT:自制心
	PRINT  - 
	A -= 5
	PRINT 自制心
	PRINTV '(,5,')
	S = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	A -= 5
	PRINT 恥じらい
	PRINTV '(,5,')
	S = 1
ENDIF
;恥薄い
IF TALENT:羞恥心 < 0
	SIF S
		PRINT  + 
	A += 2
	PRINT 恥薄い
	PRINTV '(,2,')
	S = 1
ENDIF
;自慰しやすい
IF TALENT:自慰しやすい
	SIF S
		PRINT  + 
	A += 5
	PRINT 自慰しやすい
	PRINTV '(,5,')
	S = 1
ENDIF
;快感に素直
IF TALENT:快感応答 > 0
	SIF S
		PRINT  + 
	A += 5
	PRINT 快感に素直
	PRINTV '(,5,')
	S = 1
ENDIF
;快感の否定（淫乱、娼婦で無効）
IF TALENT:快感応答 < 0 && !TALENT:淫乱
	PRINT  - 
	A -= 5
	PRINT 快感の否定
	PRINTV '(,5,')
	S = 1
ENDIF

;媚薬
IF TCVAR:媚薬
	SIF S
		PRINT  + 
	A += 8
	PRINT 媚薬
	PRINTV '(,8,')
	S = 1
ENDIF

;合計を表示(33以上で実行)
PRINT  = 
PRINTV A

;難易度上昇
;公開で+10、バイブで+5、アナルバイブで+5、シャワーで＋3
V = 33
SIF TEQUIP:ビデオ撮影
	V += 10
SIF TEQUIP:シャワー
	V += 3
SIF TEQUIP:バイブ
	V += 5
SIF TEQUIP:アナルバイブ
	V += 5

SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT
SKIPDISP 0
;実行できない
SIF A < V
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE9
;自慰実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(9)
	RETURN RESULT
;従順3以下・レズっ気3以下のASSIの場合自慰はできない（小悪魔ならＯＫ）
IF ASSIPLAY
	SIF (ABL:ASSI:従順 <= 3 || ABL:ASSI:レズっ気 <= 3) && TALENT:ASSI:小悪魔 == 0
		RETURN 0
ENDIF
;睡眠姦中
SIF CFLAG:イタズラ > 1
	RETURN 0
;縄使用中は不可
SIF TEQUIP:縄
	RETURN 0
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
RETURN 1
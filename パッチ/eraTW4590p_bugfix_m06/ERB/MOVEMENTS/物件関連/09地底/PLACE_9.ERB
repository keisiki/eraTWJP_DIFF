@KICKOUT_9()
#FUNCTION
SELECTCASE CHK_INHOME(CFLAG:MASTER:現在位置)
	CASE 21
		RETURNF 922
	CASE 34
		RETURNF 932
ENDSELECT
RETURNF 0


@MAP_UNIQUEMESSAGE_9
CALL DRAW_INFORMATIONLINE("Info",1)
IF WITH_MOB()
	CALL MOB_MESSAGE_9
	RETURN 1
ELSE
	SELECTCASE CFLAG:MASTER:現在位置
		CASE 944,946
			PRINTFORML 繁華街の喧騒が聞こえてくる　その向こうには地霊殿が見える
		CASE 948
			PRINTFORML 道の端には小さな地蔵が見える
	ENDSELECT
ENDIF
IF CHK_FOCUS(944,CFLAG:MASTER:現在位置,948)
	SIF CFLAG:60:行動 == 5
		PRINTFORML 橋の上ではパルスィが通行者がこないか見張っている
ENDIF
RETURN 1
@MOB_MESSAGE_9
IF CHK_FOCUS(927,CFLAG:MASTER:現在位置,930)
	IF FLAG:70
		PRINTL 怨霊達が停止している
	ELSE
		IF !RAND:2
			PRINTL 怨霊がさまよっている
		ELSE
			PRINTL 怨霊が漂っている
		ENDIF
	ENDIF
ELSEIF CHK_FOCUS(931,CFLAG:MASTER:現在位置,939)
	IF FLAG:70
		PRINTL 鬼達が停止している
	ELSEIF GROUPMATCH(TIME:2,1,6,7)
		PRINTL 鬼達が眠っている…
	ELSE
		SELECTCASE RAND:6
			CASE 0
				PRINTL 鬼達が酒盛りしている
			CASE 1
				PRINTL 鬼達がたむろしている
			CASE 2
				PRINTL 鬼達が喧嘩をしている
			CASE 3
				PRINTL 鬼達がうろついている
			CASE 4
				PRINTL 鬼達が騒いでいる
			CASE 5
				PRINTL 鬼が寝そべっている…邪魔くさい
		ENDSELECT
	ENDIF
ELSE
	IF FLAG:70
		PRINTFORML %ANIMALNAME()%が停止している
	ELSEIF GROUPMATCH(TIME:2,1,6,7)
		PRINTFORML %ANIMALNAME()%が眠っている…
	ELSEIF CFLAG:MASTER:現在位置 == 923
		IF !RAND:2
			PRINTFORML %ANIMALNAME()%が水浴びをしている
		ELSE
			PRINTFORML %ANIMALNAME()%が泳いでいる
		ENDIF
	ELSE
		IF !RAND:2
			PRINTFORML %ANIMALNAME()%がうろついている
		ELSE
			PRINTFORML %ANIMALNAME()%が寝そべっている…邪魔くさい
		ENDIF
	ENDIF
ENDIF
;-----------------------------------------
;非自宅時に差し替える場所IDを返す関数
;ARG,対象ID
;RETURNF,差し替え済み対象ID
;-----------------------------------------

@MAP_SEARCH_REPLACEMENT_9(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 948
	;幻想風穴
		RETURNF 910
	CASE 940 TO 946
	;地獄の深道
		RETURNF 920
	CASE 934
	;旧地獄温泉
		RETURNF 940
	CASE 935
	;ラブホ
		RETURNF 950
	CASE 931 TO 939
	;旧地獄街道
		RETURNF 930
	CASE 901 TO 926
	;地霊殿
		RETURNF 960
	CASE 927 TO 930
	;灼熱地獄跡
		RETURNF 970
ENDSELECT
RETURNF 0



@MAP9_AREANAME(PLACE_ID)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM PLACE_ID
IF MAIN_MAP == 9
	SIF INRANGE(PLACE_ID, 901, 926)
		RETURNF "地霊殿"
	SIF INRANGE(PLACE_ID, 927, 930)
		RETURNF "旧地獄"
	SIF INRANGE(PLACE_ID, 931, 944)
		RETURNF "旧都"
	RETURNF "地底"
ELSE
	SELECTCASE PLACE_ID
	CASE 930, 940, 950
		RETURNF "旧都"
	CASE 960
		RETURNF "地霊殿"
	CASE 970
		RETURNF "旧地獄"
	CASEELSE
		RETURNF "地底"
	ENDSELECT
ENDIF

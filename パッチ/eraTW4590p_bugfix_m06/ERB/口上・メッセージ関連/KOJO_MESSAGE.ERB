;====================================================================
;KOJO表示総合管理
;@KOJO_MESSAGE_SEND
;ARGS   => 口上種類 ("ENCOUNTER" "SP_EVENT" "EVENT" "COMMAND" "COUNTER" "PALAM" "MARK" "DIRECT" "ENDING"）
;ARG:1 => 口上タイミング(イベント番号、PALAM系タイミング番号、COMMAND番号等)
;ARG:2 => 発言者（口上主）ナンバー
;ARG:3 => その他口上向け引数buffer
;ARG:4 => その他口上向け引数buffer2
;ARGS:1 => 文字列ver.口上タイミング(ARG:1は無視する)
;ARGS:2 => 文字列ver.その他口上向け引数(ARGS:1がある時のみ)
;RESULTSによる汎用化に関しては仕様が読めないので取り敢えずベタ移植
;====================================================================
@KOJO_MESSAGE_SEND( ARGS,ARG:1,ARG:2,ARG:3,ARG:4,ARGS:1,ARGS:2 )
SIF ARG:2 == MASTER && ARGS != "COMMAND"
	RETURN -1
RESULTS =
VARSET LOCAL
SELECTCASE ARGS
;------------------------------------------------------------
;ファーストインプレッション系KOJO出力処理（俗に言うエクストラ構文）
;------------------------------------------------------------
	CASE "ENCOUNTER"
		;TARGETに発言者設定（現在のTARGETはLOCALへ退避）
		LOCAL = TARGET
		TARGET = ARG:2
		;口上存在判定
		TRYCALLFORM M_KOJO_K{NO:TARGET}
		;口上存在しないなら終了
		IF !RESULT
			TARGET = LOCAL
			RETURN 0
		ENDIF
		;口上表示設定なら表示
		IF FLAG:口上テキスト設定 > 0
			SIF FLAG:口上色
			TRYCALLFORM M_KOJO_COLOR_K{NO:TARGET}
			TRYCALLFORM M_KOJO%RESULTS%_ENCOUNTER_K{NO:TARGET}
		ENDIF
		RESETCOLOR
		TARGET = LOCAL
;------------------------------------------------------------
;SPイベント系KOJO出力処理
;------------------------------------------------------------
	CASE "SP_EVENT"
		;TARGETに発言者設定（現在のTARGETはLOCALへ退避）
		LOCAL = TARGET
		TARGET = ARG:2
		;口上未表示なら共通口上へ
		IF FLAG:口上テキスト設定 <= 0
			;共通口上出力
			CALLFORM SPEVENT_MESSAGE_{ARG:1}(ARG:3,ARG:4)
			TARGET = LOCAL
			RETURN 0
		ENDIF
		RESULT = 0
		RESULTS =
		;TARGETの発言
		;@M_KOJO_KXにてRESULTSを定義すると口上が被らず起動する
		SIF FLAG:口上色
			TRYCALLFORM M_KOJO_COLOR_K{NO:TARGET}
		TRYCALLFORM M_KOJO_K{NO:TARGET}
		RESETCOLOR
		;口上が存在しないなら共通口上へ
		IF !RESULT
			;口上表示設定なら共通表示
			IF FLAG:口上テキスト設定 > 0
				;共通口上出力
				CALLFORM SPEVENT_MESSAGE_{ARG:1}(ARG:3,ARG:4)
				TARGET = LOCAL
				RETURN 0
			ENDIF
		ENDIF
		;口上表示
		IF FLAG:口上テキスト設定 > 0
			;TARGET口上出力
			;M_KOJO_KXでRESULTSを定義すると切り替え可
			SIF FLAG:口上色
				TRYCALLFORM M_KOJO_COLOR_K{NO:TARGET}
			TRYCCALLFORM M_KOJO%RESULTS%_SPEVENT_K{NO:TARGET}_{ARG:1}(ARG:3,ARG:4)
			CATCH
				RESETCOLOR
				IF FLAG:口上テキスト設定 > 0
					;共通口上出力
					CALLFORM SPEVENT_MESSAGE_{ARG:1}(ARG:3,ARG:4)
					TARGET = LOCAL
					RETURN 0
				ENDIF
			ENDCATCH
		ENDIF
		RESETCOLOR
		TARGET = LOCAL
;------------------------------------------------------------
;イベント系KOJO出力処理
;------------------------------------------------------------
	CASE "EVENT"
		;TARGETに発言者設定（現在のTARGETはLOCALへ退避）
		LOCAL = TARGET
		TARGET = ARG:2
		;口上未表示なら共通口上へ
		IF FLAG:口上テキスト設定 <= 0
			;共通口上出力
			TRYCALLFORM M_KOJO_EVENT_{ARG:1}(ARG:3,ARG:4)
			TARGET = LOCAL
			RETURN 0
		ENDIF
		RESULT = 0
		RESULTS =
		;TARGETの発言
		;@M_KOJO_KXにてRESULTSを定義すると口上が被らず起動する
		TRYCALLFORM M_KOJO_K{NO:TARGET}
		;口上が存在しないなら共通口上へ
		IF !RESULT || (FLAG:70 && !CFLAG:(NO:TARGET):時間停止口上有) || (FLAG:なりきり && !CFLAG:(NO:TARGET):なりきり口上有)
			;口上表示設定なら共通表示
			IF FLAG:口上テキスト設定 > 0
				;共通口上出力
				TRYCALLFORM M_KOJO_EVENT_{ARG:1}(ARG:3,ARG:4)
				TARGET = LOCAL
				RETURN 0
			ENDIF
		ENDIF
		;口上表示
		IF FLAG:口上テキスト設定 > 0
			;TARGET口上出力
			SIF FLAG:口上色
				TRYCALLFORM M_KOJO_COLOR_K{NO:TARGET}
			IF ARGS:1 != ""
				TRYCALLFORM M_KOJO%RESULTS%_EVENT_K{NO:TARGET}_%ARGS:1%(ARGS:2,ARG:3,ARG:4)
			ELSE
				TRYCALLFORM M_KOJO%RESULTS%_EVENT_K{NO:TARGET}_{ARG:1}(ARG:3,ARG:4)
			ENDIF
		ENDIF
		RESETCOLOR
		TARGET = LOCAL
;------------------------------------------------------------
;コマンド系KOJO出力処理
;------------------------------------------------------------
	CASE "COMMAND"
		;テキスト設定
		IF FLAG:口上テキスト設定 <= 0
			;情景テキスト出力
			SIF FLAG:6 > 0
				CALL TRAIN_MESSAGE
			RETURN 0
		ENDIF
		;ボールギャグ時
		IF TEQUIP:(ARG:2):20 && SELECTCOM != 107
			SIF FLAG:6 > 0
				CALL TRAIN_MESSAGE
		ELSEIF (FLAG:70 && !CFLAG:(ARG:2):時間停止口上有) || (CFLAG:(ARG:2):イタズラ >= 1 && !CFLAG:(ARG:2):眠姦口上有) || (FLAG:なりきり && !CFLAG:(ARG:2):なりきり口上有)
			SIF FLAG:6 > 0
				CALL TRAIN_MESSAGE
		ELSE
			RESULT = 0
			RESULTS =
			;口上の存在判定 and RESULTSに文字列代入
			TRYCALLFORM M_KOJO_K{NO:TARGET}
			IF !RESULT
				SIF FLAG:6 > 0
					CALL TRAIN_MESSAGE
				RETURN 0
			ENDIF
			;SIF FLAG:口上色
				;TRYCALLFORM M_KOJO_COLOR_K{NO:TARGET}
			IF TFLAG:50
				LOCAL = TFLAG:50
				LOCALS = SCOM
			ELSE
				LOCAL = SELECTCOM
				LOCALS = COM
			ENDIF
			TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_%LOCALS%_K{NO:TARGET}_{LOCAL}
			CATCH
				;埋めていないところは汎用的な処理が可能
				TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_COM_K{NO:TARGET}_00
				CATCH
					IF FLAG:6 > 0
						CALL TRAIN_MESSAGE
						RESETCOLOR
						RETURN 0
					ENDIF
				ENDCATCH
			ENDCATCH
		ENDIF
		RESETCOLOR
;------------------------------------------------------------
;カウンター系KOJO出力処理
;------------------------------------------------------------
	CASE "COUNTER"
		LOCAL = TARGET
		TARGET = ARG:2
		IF FLAG:口上テキスト設定 <= 0
			IF FLAG:6 > 0
				CALL EVENT_COUNTER_MESSAGE
				TARGET = LOCAL
			ENDIF
			RETURN 0
		ENDIF
		
		;ボールギャグ
		IF TEQUIP:(ARG:2):20 && FLAG:6 > 0
			CALL EVENT_COUNTER_MESSAGE
			TARGET = LOCAL
		ELSE
			RESULT = 0
			RESULTS =
			;口上の存在判定 and RESULTSに文字列代入
			TRYCALLFORM M_KOJO_K{NO:TARGET}
			IF !RESULT || (FLAG:なりきり && !CFLAG:(ARG:2):なりきり口上有)
				SIF FLAG:6 > 0
					CALL EVENT_COUNTER_MESSAGE
				TARGET = LOCAL
				RESETCOLOR
				RETURN 0
			ENDIF
			TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_COUNTER_K{NO:TARGET}_{TCVAR:20}
				PRINTL
				PRINTL
			CATCH
				IF FLAG:6 > 0
					CALL EVENT_COUNTER_MESSAGE
					TARGET = LOCAL
					RESETCOLOR
					RETURN 0
				ENDIF
			ENDCATCH
			TARGET = LOCAL
		ENDIF
		RESETCOLOR
;------------------------------------------------------------
;パラメータ系KOJO出力処理
;------------------------------------------------------------
	CASE "PALAM"
		SELECTCASE ARG:1
		;==== 射精時口上 ============
			CASE 1
				VARSET LOCAL
				IF FLAG:口上テキスト設定 <= 0
					SIF FLAG:6 > 0
						CALL PALAM_MESSAGE_A
					RETURN 0
				ENDIF
				FOR LOCAL, 0, GET_TARGETNUM() + 1
					SIF TARGET:LOCAL < 1
						CONTINUE
					;TARGET,TARGET:1,…の順に出力
					SIF LOCAL > 0 && TARGET:LOCAL == TARGET
						CONTINUE
					;口上出力用のフラグがないと飛ばす
					SIF !TCVAR:(TARGET:LOCAL):50
						CONTINUE
					LOCAL:1 = TARGET
					TARGET = TARGET:LOCAL
					RESULT = 0
					RESULTS =
					;口上の存在判定 and RESULTSに文字列代入
					SIF FLAG:口上色
						TRYCALLFORM M_KOJO_COLOR_K{NO:TARGET}
					TRYCALLFORM M_KOJO_K{NO:TARGET}
					RESETCOLOR
					IF (!RESULT &&  FLAG:6 > 0) || (FLAG:70 && !CFLAG:(NO:TARGET):時間停止口上有) || (FLAG:なりきり && !CFLAG:(NO:TARGET):なりきり口上有)
						CALL PALAM_MESSAGE_A
						TARGET = LOCAL:1
						RETURN 0
					ENDIF
					;射精口上出力ギミック
					CALL KOJO_MESSAGE_PALAMCNG_A2(LOCAL)
					SIF !RESULT
						LOCAL:2 ++
					TARGET = LOCAL:1
					;コンドーム口上は対象一人のみ
					SIF GETBIT(TFLAG:1,0)
						BREAK
				NEXT
		;==== 絶頂時口上 ============
			CASE 2
				IF FLAG:口上テキスト設定 <= 0
					SIF FLAG:6 > 0
						CALL PALAM_MESSAGE_B
					RETURN 0
				ENDIF
				RESULT = 0
				RESULTS =
				;口上の存在判定 and RESULTSに文字列代入
				SIF FLAG:口上色
					TRYCALLFORM M_KOJO_COLOR_K{NO:TARGET}
				TRYCALLFORM M_KOJO_K{NO:TARGET}
				RESETCOLOR
				IF (!RESULT &&  FLAG:6 > 0) || (FLAG:70 && !CFLAG:(NO:TARGET):時間停止口上有) || (FLAG:なりきり && !CFLAG:(NO:TARGET):なりきり口上有)
					CALL PALAM_MESSAGE_B
					RETURN 0
				ENDIF
				IF CFLAG:(NO:TARGET):イタズラ
					TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_B2_K{NO:TARGET}
					CATCH
						CALL PALAM_MESSAGE_B
					ENDCATCH
				ELSE
					TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_B_K{NO:TARGET}
					CATCH
						CALL PALAM_MESSAGE_B
					ENDCATCH
				ENDIF
				RESETCOLOR
		;==== 調教後口上 ============
			CASE 3
				IF FLAG:口上テキスト設定 <= 0
					SIF FLAG:6 > 0
						CALL PALAM_MESSAGE_C
					RETURN 0
				ENDIF
				RESULT = 0
				RESULTS =
				;口上の存在判定 and RESULTSに文字列代入
				SIF FLAG:口上色
				TRYCALLFORM M_KOJO_COLOR_K{NO:TARGET}
				TRYCALLFORM M_KOJO_K{NO:TARGET}
				RESETCOLOR
				IF (!RESULT &&  FLAG:6 > 0) || (FLAG:70 && !CFLAG:(NO:TARGET):時間停止口上有) || (FLAG:なりきり && !CFLAG:(NO:TARGET):なりきり口上有)
					CALL PALAM_MESSAGE_C
					RETURN 0
				ENDIF
				TRYCALLFORM M_KOJO_MESSAGE_PALAMCNG_C_K{NO:TARGET}
				RESETCOLOR
		;==== パラメータ変動口上 ====
			CASE 4
				IF FLAG:口上テキスト設定 <= 0
					SIF FLAG:情景テキスト設定 > 0
						CALL PALAM_MESSAGE_D
					RETURN 0
				ENDIF
				RESULT = 0
				RESULTS =
				;口上の存在判定 and RESULTSに文字列代入
				SIF FLAG:口上色
				TRYCALLFORM M_KOJO_COLOR_K{NO:TARGET}
				TRYCALLFORM M_KOJO_K{NO:TARGET}
				RESETCOLOR
				IF (!RESULT &&  FLAG:6 > 0) || (FLAG:70 && !CFLAG:(NO:TARGET):時間停止口上有) || (FLAG:なりきり && !CFLAG:(NO:TARGET):なりきり口上有)
					CALL PALAM_MESSAGE_D
					RETURN 0
				ENDIF
				TRYCALLFORM M_KOJO_COLOR_K{NO:TARGET}
				TRYCALLFORM M_KOJO_MESSAGE_PALAMCNG_D_K{NO:TARGET}
				RESETCOLOR
		;==== 触手の射精 ============
			CASE 5
				IF FLAG:口上テキスト設定 <= 0
					SIF FLAG:情景テキスト設定 > 0
						CALL PALAM_MESSAGE_E
					RETURN 0
				ENDIF
				RESULT = 0
				RESULTS =
				;口上の存在判定 and RESULTSに文字列代入
				TRYCALLFORM M_KOJO_K{NO:TARGET}
				IF !RESULT && FLAG:情景テキスト設定 > 0
					CALL PALAM_MESSAGE_E
					RETURN 0
				ENDIF
				TRYCALLFORM M_KOJO_MESSAGE_PALAMCNG_E_K{NO:TARGET}
		;==== 焦らし ============
			CASE 6
				IF FLAG:口上テキスト設定 <= 0
					SIF FLAG:情景テキスト設定 > 0
						CALL PALAM_MESSAGE_F
					RETURN 0
				ENDIF
				RESULT = 0
				RESULTS =
				;口上の存在判定 and RESULTSに文字列代入
				TRYCALLFORM M_KOJO_K{NO:TARGET}
				IF !RESULT && FLAG:情景テキスト設定 > 0
					CALL PALAM_MESSAGE_F
					RETURN 0
				ENDIF
				TRYCALLFORM M_KOJO_MESSAGE_PALAMCNG_F_K{NO:TARGET}
		ENDSELECT
;	;刻印系KOJO出力処理
	CASE "MARK"
		IF FLAG:口上テキスト設定 <= 0
			SIF FLAG:情景テキスト設定 > 0
				CALL MARK_MESSAGE
			RETURN 0
		ENDIF
		RESULT = 0
		RESULTS =
		;口上の存在判定 and RESULTSに文字列代入
		SIF FLAG:口上色
			TRYCALLFORM M_KOJO_COLOR_K{NO:TARGET}
		TRYCALLFORM M_KOJO_K{NO:TARGET}
		RESETCOLOR
		IF !RESULT && FLAG:情景テキスト設定 > 0
			CALL MARK_MESSAGE
			RETURN 0
		ENDIF
		TRYCALLFORM M_KOJO_MESSAGE_MARKCNG_K{NO:TARGET}
		RESETCOLOR
	;コマンド系直接呼び出し
	CASE "DIRECT"
		SELECTCASE ARG:1
			CASE 351
				;ボールギャグ
				IF TEQUIP:(ARG:2):20 && SELECTCOM != 107 && FLAG:情景テキスト設定 > 0
					RETURN 1
				ELSE
					RESULT = 0
					RESULTS =
					
					IF FLAG:口上テキスト設定 > 0
						IF FLAG:口上色
							TRYCALLFORM M_KOJO_COLOR_K{NO:TARGET}
						ELSE
							RESETCOLOR
						ENDIF
					ELSE
						RESETCOLOR
						RETURN 1
					ENDIF
					TRYCALLFORM M_KOJO_MESSAGE_COM_K{NO:TARGET}_351
					RESETCOLOR
				ENDIF
		ENDSELECT
	;いつか来る終わりの為に
	CASE "ENDING"
	SIF FLAG:口上色
		TRYCALLFORM M_KOJO_COLOR_K{NO:TARGET}
		TRYCALLFORM M_KOJO_MESSAGE_ENDING_K{NO:TARGET}
		RESETCOLOR
ENDSELECT

;-------------------------------------------------
;@KOJO_MESSAGE_PALAMCNG_A2
;射精口上出力のギミック
;-------------------------------------------------
@KOJO_MESSAGE_PALAMCNG_A2(ARG)
;ARG Wフェラ等でTARGETならARG=0、二人目ならARG=1

;-------------------------------------------------
;射精対象が非ターゲットかつ非複数キャラSCOMF射精
;-------------------------------------------------
IF MASTER_POSE(0,SET_P) != TARGET && !ARG
	IF CFLAG:(NO:TARGET):イタズラ 
		TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A2_K{NO:TARGET}_00(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ELSE
		TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A_K{NO:TARGET}_00(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ENDIF
ENDIF
;-------------------------------------------------
;コンドーム
;-------------------------------------------------
IF GETBIT(TCVAR:50,0)
	IF CFLAG:(NO:TARGET):イタズラ 
		TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A2_K{NO:TARGET}_0(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ELSE
		TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A_K{NO:TARGET}_0(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ENDIF
ENDIF
;-------------------------------------------------
;イラマチオ
;-------------------------------------------------
IF GETBIT(TCVAR:50,23)
	IF CFLAG:(NO:TARGET):イタズラ 
		TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A2_K{NO:TARGET}_23(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ELSE
		TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A_K{NO:TARGET}_23(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ENDIF
ENDIF
;-------------------------------------------------
;シックスナイン
;-------------------------------------------------
IF GETBIT(TCVAR:50,22)
	TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A_K{NO:TARGET}_22(ARG)
		RETURN RESULT
	CATCH
	ENDCATCH
ENDIF
;-------------------------------------------------
;パイズリフェラ
;-------------------------------------------------
IF GETBIT(TCVAR:50,21)
	TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A_K{NO:TARGET}_21(ARG)
		RETURN RESULT
	CATCH
	ENDCATCH
ENDIF
;-------------------------------------------------
;手淫フェラ
;-------------------------------------------------
IF GETBIT(TCVAR:50,20)
	TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A_K{NO:TARGET}_20(ARG)
		RETURN RESULT
	CATCH
	ENDCATCH
ENDIF
;-------------------------------------------------
;アナル奉仕
;-------------------------------------------------
IF GETBIT(TCVAR:50,9)
	TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A_K{NO:TARGET}_9(ARG)
		RETURN RESULT
	CATCH
	ENDCATCH
ENDIF
;-------------------------------------------------
;泡踊り
;-------------------------------------------------
IF GETBIT(TCVAR:50,8)
	TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A_K{NO:TARGET}_8(ARG)
		RETURN RESULT
	CATCH
	ENDCATCH
ENDIF
;-------------------------------------------------
;足コキ
;-------------------------------------------------
IF GETBIT(TCVAR:50,7)
	IF CFLAG:(NO:TARGET):イタズラ 
		TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A2_K{NO:TARGET}_7(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ELSE
		TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A_K{NO:TARGET}_7(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ENDIF
ENDIF
;-------------------------------------------------
;素股
;-------------------------------------------------
IF GETBIT(TCVAR:50,6)
	TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A_K{NO:TARGET}_6(ARG)
		RETURN RESULT
	CATCH
	ENDCATCH
ENDIF
;-------------------------------------------------
;パイズリ
;-------------------------------------------------
IF GETBIT(TCVAR:50,5)
	IF CFLAG:(NO:TARGET):イタズラ 
		TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A2_K{NO:TARGET}_5(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ELSE
		TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A_K{NO:TARGET}_5(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ENDIF
ENDIF
;-------------------------------------------------
;フェラチオ
;-------------------------------------------------
IF GETBIT(TCVAR:50,4)
	IF CFLAG:(NO:TARGET):イタズラ 
		TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A2_K{NO:TARGET}_4(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ELSE
		TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A_K{NO:TARGET}_4(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ENDIF
ENDIF
;-------------------------------------------------
;手淫
;-------------------------------------------------
IF GETBIT(TCVAR:50,3)
	IF CFLAG:(NO:TARGET):イタズラ 
		TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A2_K{NO:TARGET}_3(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ELSE
		TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A_K{NO:TARGET}_3(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ENDIF
ENDIF
;-------------------------------------------------
;アナル
;-------------------------------------------------
IF GETBIT(TCVAR:50,2)
	IF CFLAG:(NO:TARGET):イタズラ 
		TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A2_K{NO:TARGET}_2(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ELSE
		TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A_K{NO:TARGET}_2(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ENDIF
ENDIF
;-------------------------------------------------
;膣内
;-------------------------------------------------
IF GETBIT(TCVAR:50,1)
	IF CFLAG:(NO:TARGET):イタズラ 
		TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A2_K{NO:TARGET}_1(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ELSE
		TRYCCALLFORM M_KOJO_MESSAGE_PALAMCNG_A_K{NO:TARGET}_1(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ENDIF
ENDIF
CALL PALAM_MESSAGE_A
RESETCOLOR
RETURN 0

@SILENCE_TSTOP(ARG)
#FUNCTION
;時間停止中には喋らないキャラを指定
SIF GROUPMATCH(ARG,39,41,54,86,87,88)
	RETURNF 1
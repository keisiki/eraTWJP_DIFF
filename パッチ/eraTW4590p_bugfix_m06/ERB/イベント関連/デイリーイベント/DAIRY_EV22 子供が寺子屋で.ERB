@DAIRY_CONDITION_EV22
#DIM YOME
#DIM CHILDNUM
SIF !TALENT:MASTER:育児中
	RETURN -1
SIF RAND:5 > 0
	RETURN -1
YOME = 0
CALL FISHER_YATES_SHAFFLE(CHARANUM - 1)
FOR LOCAL:10, 0, CHARANUM - 1
	LOCAL = SHAFFLE_ARRAY:(LOCAL:10) + 1
	IF CHK_HAVE_CHILD(LOCAL,CHILD_就学,CHILD_自立) > 0
		YOME = LOCAL
		CHILDNUM = CHK_HAVE_CHILD(LOCAL,CHILD_就学,CHILD_自立)
		BREAK
	ENDIF
NEXT
IF YOME
	CALL DAIRY_EV22(YOME,CHILDNUM)
	RETURN 1
ELSE
	RETURN -1
ENDIF


@DAIRY_EV22(YOME,CHILDNUM)
#DIM YOME
#DIM CHILDNUM
#DIM 慰謝料

PRINTFORM %CALLNAME:YOME%から、寺子屋で%CHILDNAME:YOME:CHILDNUM%が

IF !RAND:4
	PRINTFORMW よその子に怪我をさせたと伝えられた……
	PRINTFORML たとえ悪気がなかったとしても、常人相手に%CHILDNAME:YOME:CHILDNUM%が力を振るえば容易く傷つけてしまうだろう
	PRINTFORML 親御さんに謝罪したうえで%CHILDNAME:YOME:CHILDNUM%にもよくよくいい聞かせなければ……
	PRINTFORML %CALLNAME:MASTER%は気が重くなった
	PRINTFORML 
	CALL KOJO_MESSAGE_SEND("EVENT", 0, YOME, CHILDNUM, 0, "CHILD_DAIRY_寺子屋", "怪我させた")
	慰謝料 = MIN(10000,MONEY)
	MONEY -= 慰謝料
	CALL COLORMESSAGE(@"先方へ治療費として{慰謝料}払った",C_YELLOW,1,1)
	CALL COLORMESSAGE(@"%CALLNAME:MASTER%の気力が一時的に減った",C_YELLOW,1,1)
	CALL BUFF_BASE(MASTER,BASE_気力,-1000)
ELSE
	PRINTFORMW 褒められたと伝えられた……
	PRINTFORML さすがはうちの子だ！
	PRINTFORML 
	CALL KOJO_MESSAGE_SEND("EVENT", 0, YOME, CHILDNUM, 0, "CHILD_DAIRY_寺子屋", "褒められた")
	CALL COLORMESSAGE(@"%CALLNAME:MASTER%の気力が一時的に増えた",C_YELLOW,1,1)
	CALL BUFF_BASE(MASTER,BASE_気力,1000)
ENDIF
PRINTW 
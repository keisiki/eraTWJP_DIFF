
@PRINT_STATE_JUEL(選択中キャラID)
#DIM 選択中キャラID
#DIM 表示項目数
PRINTL
PRINTL □ 宝珠 □---------------------------------------------------------------------------
表示項目数 = 0
FOR LOCAL, 0, 22
	;PALAM.CSVに名称が登録されていないとCONTINUE
	SIF !STRLENS(PALAMNAME:LOCAL)
		CONTINUE
	;1000,1000000,単位で区切る
	LOCALS = {JUEL:選択中キャラID:LOCAL}
	SIF JUEL:選択中キャラID:LOCAL > 1000000
		LOCALS = {JUEL:選択中キャラID:LOCAL / 1000}k
	SIF JUEL:選択中キャラID:LOCAL > 1000000000
		LOCALS = {JUEL:選択中キャラID:LOCAL / 1000000}m
	PRINTFORM %PALAMNAME:LOCAL,6%の珠:%LOCALS,7,LEFT%
	表示項目数 += 1
	;改行or表示位置あけ
	SIF 表示項目数 % 4 == 0
		PRINTL 
NEXT
;否定の珠
LOCALS = {JUEL:選択中キャラID:100}
SIF JUEL:選択中キャラID:100 > 1000000
	LOCALS = {JUEL:選択中キャラID:100 / 1000}k
PRINTFORM %PALAMNAME:100,6%の珠:%LOCALS,7,LEFT%

@PRINT_STATE_EX(選択中キャラID)
#DIM 選択中キャラID
#DIM 表示項目数

SIF EX:選択中キャラID:Ｃ絶頂 + EX:選択中キャラID:Ｖ絶頂 + EX:選択中キャラID:Ａ絶頂 + EX:選択中キャラID:Ｂ絶頂 + EX:選択中キャラID:Ｍ絶頂 + EX:選択中キャラID:噴乳 + EX:選択中キャラID:射精 == 0
	RETURN 0
表示項目数 = 0
PRINTL
PRINTL □ 絶頂回数 □-----------------------------------------------------------------------
FOR LOCAL, 0, VARSIZE("EX")
	SIF !EX:選択中キャラID:LOCAL
		CONTINUE
	SIF !GROUPMATCH(EXNAME:LOCAL, "Ｃ絶頂", "Ｖ絶頂", "Ａ絶頂", "Ｂ絶頂", "Ｍ絶頂", "噴乳", "射精", "放尿")
		CONTINUE
	PRINTFORM %EXNAME:LOCAL,12%:{EX:選択中キャラID:LOCAL,5,LEFT} 
	表示項目数 ++
	SIF 表示項目数 % 4 == 0
		PRINTL
NEXT


@PRINT_STATE_PERSONAL(選択中キャラID)
#DIM 選択中キャラID

PRINTL
PRINTL □ 個人情報 □-----------------------------------------------------------------------
PRINTL  
IF 選択中キャラID == MASTER
	IF CFLAG:MASTER:睡眠
		PRINTFORML 昨日の就寝時刻：%時刻表示(CFLAG:MASTER:就寝時間 - DAY * 1440)%
	ELSE
		PRINTFORML 昨日の就寝時刻：%時刻表示(CFLAG:MASTER:就寝時間 + 1440 - DAY * 1440)%
	ENDIF
	PRINTFORML 起床時刻：%時刻表示(TIME:3)%
	IF FLAG:地主
		PRINTFORML 
		CALL SHOW_NOUMIN
	ENDIF
ELSE
	PRINT 活動時間帯:
	SELECTCASE 選択中キャラID
	CASE 1
		PRINTL 6〜13時 14〜23時
	CASE 2
		PRINTL 5〜13時 14〜24時
	CASE 3
		PRINTL 0〜10時 17〜24時
	CASE 4
		PRINTL 0〜10時 16〜24時
	CASE 5
		PRINTL 6〜24時
	CASE 6
		PRINTL 0〜2時 8〜24時
	CASE 7
		PRINTL 0〜1時 6〜13時 14〜24時
	CASE 8
		PRINTL 0〜1時 6〜13時 14〜24時
	CASE 9
		PRINTL 0〜5時 10〜13時 14〜24時
	CASE 10
		PRINTL 0〜3時 10〜24時
	CASEELSE
		IF CFLAG:選択中キャラID:神社在住 || CFLAG:MASTER:現在位置 == OMANEKIBEYA()
			CALL 起床時間表示(選択中キャラID)
		ELSE
			CALL 滞在時間表示(選択中キャラID)
		ENDIF
	ENDSELECT
	IF CFLAG:選択中キャラID:よく行く地域 >= 0
		PRINT よく行く地域：
		SELECTCASE CFLAG:選択中キャラID:よく行く地域
		CASE 0
			PRINTL 博麗神社
		CASE 1
			PRINTL 命蓮寺
		CASE 2
			PRINTL 人間の里
		CASE 3
			PRINTL 霧の湖〜紅魔館
		CASE 4
			PRINTL 迷いの竹林
		CASE 5
			PRINTL 魔法の森
		CASE 6
			PRINTL 三途の川〜冥界
		CASE 7
			PRINTL 妖怪の山（山麓）
		CASE 8
			PRINTL 妖怪の山（山頂）
		CASE 9
			PRINTL 地底
		ENDSELECT
	ENDIF
	IF CFLAG:選択中キャラID:自宅位置 && !CFLAG:選択中キャラID:神社在住
		LOCAL = CFLAG:選択中キャラID:自宅位置 / 10 + 6000
		PRINT 自宅位置：
		PRINTFORML %STR:LOCAL%
	ELSEIF CFLAG:選択中キャラID:神社在住
		PRINT 自宅位置：
		PRINTFORML %GET_MAPNAME(MAIN_MAP)%
	ENDIF
	PRINT 仕事情報：
	PRINTFORML %CSTR:選択中キャラID:1%
	PRINT 職場：
	PRINTFORML %CSVCSTR(選択中キャラID, 2)%
ENDIF

PRINT 味覚嗜好:
SIF !CFLAG:選択中キャラID:332 & 1
	PRINT 普通
SIF CFLAG:選択中キャラID:332 & 1
	PRINT 濃い味
SIF CFLAG:選択中キャラID:332 & 2
	PRINT 薄味
SIF CFLAG:選択中キャラID:332 & 3
	PRINT 　辛味耐性
SIF CFLAG:選択中キャラID:332 & 4
	PRINT 　甘味耐性
SIF CFLAG:選択中キャラID:332 & 5
	PRINT 　わさび耐性
SIF CFLAG:選択中キャラID:332 & 6
	PRINT 　酸味耐性
PRINTL 　
PRINTL 　

IF CFLAG:選択中キャラID:好きな体位
	PRINT 弱点：
	SELECTCASE CFLAG:選択中キャラID:好きな体位
	CASE 1
		PRINTL 正常位
	CASE 2 
		PRINTL 後背位
	CASE 3 
		PRINTL 騎乗位
	CASE 4 
		PRINTL 対面座位 
	CASE 5 
		PRINTL 背面座位
	CASE 6
		PRINTL 正常位アナル
	CASE 7 
		PRINTL 後背位アナル
	CASE 8 
		PRINTL 騎乗位アナル
	CASE 9 
		PRINTL 対面座位アナル
	CASE 10 
		PRINTL 背面座位アナル
	ENDSELECT
	PRINTL 　
ENDIF

PRINTL 　
IF 選択中キャラID == RANDOM_CHARANUM
	;現状、複雑な種族分岐などは無しで
	PRINTL □ キャラ紹介 □---------------------------------------------------------------------
	PRINTL 
	PRINTFORML 〜モブ娘〜　●種族：人間　●能力：%CSTR:RANDOM_CHARANUM:15%
ELSEIF CSVCSTR(選択中キャラID, 10) != ""
	PRINTL □ キャラ紹介 □---------------------------------------------------------------------
	PRINTL 
	;キャラ紹介文を口上の展開によって改変したい場合「@CHARA_INFO_KOJO_KX」を使用する
	TRYCALLFORM CHARA_INFO_KOJO_K{選択中キャラID}
	IF !RESULT
		FOR LOCAL,10,20
			SIF CSVCSTR(選択中キャラID, LOCAL) != ""
				PRINTFORML %CSVCSTR(選択中キャラID, LOCAL,)%
		NEXT
	ENDIF
ENDIF
IF 選択中キャラID == MASTER && FLAG:甲斐性無
	PRINTFORML 甲斐性なしだ
	PRINTFORML 
ENDIF
		
IF CFLAG:選択中キャラID:引き継ぎ
	SIF GETBIT (CFLAG:選択中キャラID:引き継ぎ, 0)
		PRINTL 以前の周回で恋慕になったことがある
	SIF GETBIT (CFLAG:選択中キャラID:引き継ぎ, 1)
		PRINTL 以前の周回で恋人になったことがある
ENDIF
SIF CFLAG:選択中キャラID:清い交際
	PRINTFORML 清いお付き合いを経て%CALLNAME:MASTER%と恋仲になった
SIF CFLAG:選択中キャラID:既成事実 & 合意_キス
	PRINTFORML %CALLNAME:MASTER%に唇を許している
SIF CFLAG:選択中キャラID:既成事実 & 合意_うふふ
	PRINTFORML %CALLNAME:MASTER%に体を許している
SIF TALENT:選択中キャラID:処女 && TALENT:選択中キャラID:ヤリマン
	PRINTFORML 一応処女だが、求められれば簡単に体を開くだろう

IF TALENT:選択中キャラID:恋慕
	IF TALENT:選択中キャラID:無知
		PRINTFORML %CALLNAME:選択中キャラID%は%CALLNAME:MASTER%のお嫁さんになると密かに決めている
	ELSEIF !CFLAG:選択中キャラID:既成事実 & 合意_うふふ
		IF TALENT:選択中キャラID:貞操 > 0
			PRINTFORML 肉体関係などふしだらな事と考えているにも関わらず、%CALLNAME:選択中キャラID%は心の奥底では%CALLNAME:MASTER%と交わりたいと思っている
		ELSE
			PRINTFORML %CALLNAME:MASTER%が未だに手をつけないことに苛立ちと不安を感じている
		ENDIF
	ENDIF
ENDIF

IF CFLAG:選択中キャラID:バレンタイン
	SELECTCASE CFLAG:選択中キャラID:バレンタイン % 100
		CASE 1
			LOCALS = ブラックサンダー
		CASE 2
			LOCALS = 普通のチョコ
		CASE 3
			LOCALS = 高級チョコ
		CASE 4
			LOCALS = 手作りチョコ
	ENDSELECT
	PRINTFORML バレンタインに%LOCALS%をくれた\@ CFLAG:選択中キャラID:バレンタイン > 100? （返礼済）# \@
ENDIF
@滞在時間表示(選択中キャラID)
#DIM 選択中キャラID

IF CFLAG:選択中キャラID:来訪時間 < CFLAG:選択中キャラID:帰宅時間
	PRINTFORML {CFLAG:選択中キャラID:来訪時間 / 60}時〜{CFLAG:選択中キャラID:帰宅時間 / 60}時
ELSE
	PRINTFORML 0時〜{CFLAG:選択中キャラID:帰宅時間 / 60}時 {CFLAG:選択中キャラID:来訪時間 / 60}時〜24時
ENDIF



@起床時間表示(選択中キャラID)
#DIM 選択中キャラID

IF CFLAG:選択中キャラID:起床時間 < CFLAG:選択中キャラID:就寝時間
	PRINTFORML {CFLAG:選択中キャラID:起床時間 / 60}時〜{CFLAG:選択中キャラID:就寝時間 / 60}時
ELSE
	PRINTFORML 0時〜{CFLAG:選択中キャラID:就寝時間 / 60}時 {CFLAG:選択中キャラID:起床時間 / 60}時〜24時
ENDIF



@肉体情報(選択中キャラID)
#DIM 選択中キャラID
VARSET LOCALS
VARSET LOCAL
PRINTL
PRINTL □ 肉体情報 □-----------------------------------------------------------------------
PRINTL 
PRINTL 【口】　
SIF GETBIT (CFLAG:選択中キャラID:引き継ぎ, 6)
	PRINTFORML どうすれば感じるのか%CALLNAME:MASTER%に知り尽くされている
IF !EXP:選択中キャラID:キス経験
	PRINTL まだ誰にも唇を許していない
ELSEIF CFLAG:選択中キャラID:ファーストキス喪失記念日
	PRINTFORM {CFLAG:選択中キャラID:ファーストキス喪失記念日}日目、
	SIF CSTR:選択中キャラID:ファーストキス位置 != ""
		PRINTFORM %CSTR:選択中キャラID:ファーストキス位置%で
	PRINTFORM %CALLNAME:(CFLAG:選択中キャラID:ファーストキス喪失相手)%に
	IF CFLAG:選択中キャラID:ファーストキス喪失対象 & (喪失_自分で | 喪失_恋慕 | 喪失_思慕)
		PRINTFORML ファーストキスを捧げた
	ELSE
		SIF CFLAG:選択中キャラID:ファーストキス喪失対象 & 喪失_なし崩し
			PRINTFORML なし崩し的に
		PRINTFORML ファーストキスを奪われた
	ENDIF
ENDIF
SIF EXP:選択中キャラID:キス経験
	PRINTFORML 今まで{EXP:選択中キャラID:キス経験}回の口づけを経験した
SIF CFLAG:選択中キャラID:累計精飲
	PRINTFORML 今まで{CFLAG:選択中キャラID:累計精飲}の精液を飲み干した
IF EXP:選択中キャラID:精飲経験 > 150 && ABL:選択中キャラID:精液中毒 >= 5
	PRINTFORML 精液を味わうと身体が疼いてしまう
ELSEIF ABL:選択中キャラID:精液中毒 >= 3
	PRINTFORML 精液を抵抗なく飲むことが出来る
ENDIF
IF 選択中キャラID == MASTER
	IF ABL:選択中キャラID:舌 >= 5
		PRINTFORML 卓越した舌技で少女を虜にする
	ELSEIF ABL:選択中キャラID:舌 >= 3
		PRINTFORML 巧みな舌使いで少女を悦ばせる
	ENDIF
ENDIF
PRINTL 
PRINTL 【胸】
SELECTCASE ABL:選択中キャラID:Ｂ感覚
	CASE IS >= 10
		PRINT 感度抜群　
	CASE IS >= 5
		PRINT 感度良好　
ENDSELECT
SIF GETBIT (CFLAG:選択中キャラID:引き継ぎ, 5)
	PRINTFORML どうすれば感じるのか%CALLNAME:MASTER%に知り尽くされている
SIF CFLAG:選択中キャラID:累計ぶっかけ
	PRINTFORML 今まで{CFLAG:選択中キャラID:累計ぶっかけ}の精液にまみれた　
IF TALENT:選択中キャラID:母乳体質
	;PRINTFORML \@ ABL:選択中キャラID:噴乳中毒 >= 3 ? 甘みのあるミルクがたっぷりと詰まっている# 刺激してやれば甘みのあるミルクが滲みだすだろう\@
	PRINTFORML 刺激してやれば甘みのあるミルクが滲みだすだろう
ENDIF
SELECTCASE ABL:選択中キャラID:胸
	CASE IS >= 5
	CASE IS >= 3
ENDSELECT
PRINTL

PRINTL 【指】
SELECTCASE ABL:選択中キャラID:指
	CASE IS >= 5
		PRINT 凶器　
	CASE IS >= 3
		PRINT 達人　
ENDSELECT
IF 選択中キャラID == MASTER
	IF ABL:選択中キャラID:指 >= 5
		PRINTFORML しなやかな指での愛撫はたちまち少女を蕩けさせてしまう
	ELSEIF ABL:選択中キャラID:指 >= 3
		PRINTFORML 巧みな指裁きで少女を悦ばせる
	ENDIF
ENDIF
PRINTL
PRINTL 【アナル】
	IF CFLAG:選択中キャラID:アナル処女喪失記念日 > 0
		PRINTFORM {CFLAG:選択中キャラID:アナル処女喪失記念日}日目、
		SIF CSTR:選択中キャラID:Ａ処女喪失位置 != ""
			PRINTFORM %CSTR:選択中キャラID:Ａ処女喪失位置%で
		SIF CFLAG:選択中キャラID:アナル処女喪失対象 & 喪失_時間停止
			PRINT 時間停止中に
		IF GETBIT (CFLAG:選択中キャラID:アナル処女喪失対象,3)
			PRINTFORM %CALLNAME:(CFLAG:選択中キャラID:アナル処女喪失相手)%にペニスバンドで
		ELSEIF GETBIT (CFLAG:選択中キャラID:アナル処女喪失対象,2)
			PRINTFORM %CALLNAME:(CFLAG:選択中キャラID:アナル処女喪失相手)%にアナルバイブで
		ELSEIF GETBIT (CFLAG:選択中キャラID:アナル処女喪失対象,1)
			PRINTFORM %CALLNAME:(CFLAG:選択中キャラID:アナル処女喪失相手)%のペニスに
		ENDIF
		IF CFLAG:選択中キャラID:アナル処女喪失対象 & 喪失_時間停止
			PRINTFORML 尻穴処女を奪われた
		ELSE
			PRINTFORML 尻穴処女を捧げた
		ENDIF
	ENDIF
SELECTCASE ABL:選択中キャラID:Ａ感覚
	CASE IS >= 10
		PRINT 感度抜群　
	CASE IS >= 5
		PRINT 感度良好　
ENDSELECT
SIF GETBIT (CFLAG:選択中キャラID:引き継ぎ, 4)
	PRINTFORML どうすれば感じるのか%CALLNAME:MASTER%に知り尽くされている
IF 選択中キャラID
	SELECTCASE 挿入不可(選択中キャラID)
		CASE 3
			PRINTFORML とても%CALLNAME:MASTER%のペニスを受け入れられるようなサイズではない
			LOCAL:6 ++
		CASE 2
			IF EXP:選択中キャラID:Ａ経験 < 50 && TALENT:MASTER:禁断の知識
				PRINTFORML もう少しほぐさないと%CALLNAME:MASTER%のペニスは入らないだろう
				LOCAL:6 ++
			ELSEIF !TALENT:MASTER:禁断の知識
				PRINTFORML %CALLNAME:MASTER%のペニスを挿入するのは酷だろう
				LOCAL:6 ++
			ENDIF
		CASE 1
			IF EXP:選択中キャラID:Ａ経験 < 50 || !TALENT:MASTER:禁断の知識
				PRINTFORML もう少しほぐさないと%CALLNAME:MASTER%のペニスは入らないだろう
				LOCAL:6 ++
			ENDIF
		CASE -1
			IF ABL:選択中キャラID:アナル < 2
				PRINTFORML %CALLNAME:MASTER%のペニスでは少々サイズが合わない
				LOCAL:6 ++
			ENDIF
		CASE -2
			IF ABL:選択中キャラID:アナル < 3
				PRINTFORML 大き過ぎて入れてもあまり気持ちよくなさそうだ…
				LOCAL:6 ++
			ENDIF
	ENDSELECT
	SELECTCASE ABL:選択中キャラID:アナル
		CASE IS >= 5
			PRINTL 極上名器
			PRINTFORML 完全に%CALLNAME:MASTER%のカタチに作り替えられたアナルは肉ヒダを蠢かせて絡みついてくる
		CASE IS >= 3
			PRINTL 名器
			PRINTFORML 使い込んだ甲斐あって大変具合のいい穴になった
		CASE 2
			PRINTFORML %CALLNAME:MASTER%を受け入れることに慣れきっている
		CASE 1
			PRINTL ほぐれ始めて具合が良くなってきた
		CASE 0
			IF !CFLAG:選択中キャラID:アナル処女喪失記念日
				PRINTL 未開通
			ELSEIF EXP:選択中キャラID:Ａ経験 < 20
					PRINTL まだ異物を受け入れることに慣れていない
			ENDIF
	ENDSELECT
ENDIF
SIF CFLAG:選択中キャラID:累計アナル精液
	PRINTFORML 今まで{CFLAG:選択中キャラID:累計アナル精液}の精液を注ぎ込まれた

SELECTCASE ABL:選択中キャラID:肛射中毒
	CASE IS >= 7
		PRINTFORML アナル内射精されると多幸感を感じる
	CASE IS >= 3
		PRINTFORML アナル内射精されると腸内が疼いてしまう
ENDSELECT

SIF ABL:選択中キャラID:Ａ感覚 >= 5 && CFLAG:選択中キャラID:溜まってる度 > 300 && 選択中キャラID
	PRINTFORML %CALLNAME:MASTER%に抱かれたくてうずうずしている

IF 充填率(選択中キャラID,2) >= 100
	PRINTFORML 直腸は重みを感じるほどの量の精液で完全に満たされている
ELSEIF 充填率(選択中キャラID,2) >= 50
	PRINTFORML 直腸で夥しい数の精子が蠢いている
ELSEIF 充填率(選択中キャラID,2) >= 20
	PRINTFORML 直腸で精子が元気に泳ぎ回っている
ENDIF

PRINTL
IF TALENT:選択中キャラID:2 & 1
	PRINTL 【膣】
	IF CFLAG:選択中キャラID:処女喪失記念日
		PRINTFORM {CFLAG:選択中キャラID:処女喪失記念日}日目、
		SIF CSTR:選択中キャラID:処女喪失位置 != ""
			PRINTFORM %CSTR:選択中キャラID:処女喪失位置%で
		SIF CFLAG:選択中キャラID:処女喪失対象 & 喪失_時間停止
			PRINT 時間停止中に
		SIF GETBIT (CFLAG:選択中キャラID:処女喪失対象,9)
			PRINT 酔いつぶれているとき
		SIF GETBIT (CFLAG:選択中キャラID:処女喪失対象,16)
			PRINT 眠っている間に
		
		IF GETBIT (CFLAG:選択中キャラID:処女喪失対象,3)
			PRINTFORM %CALLNAME:(CFLAG:選択中キャラID:処女喪失相手)%にペニスバンドで
		ELSEIF GETBIT (CFLAG:選択中キャラID:処女喪失対象,2)
			PRINTFORM %CALLNAME:(CFLAG:選択中キャラID:処女喪失相手)%にバイブで
		ELSEIF GETBIT (CFLAG:選択中キャラID:処女喪失対象,1)
			SIF CFLAG:選択中キャラID:処女喪失対象 & 喪失_恋慕
				PRINT 愛する
			PRINTFORM %CALLNAME:(CFLAG:選択中キャラID:処女喪失相手)%の\@ GROUPMATCH(TALENT:MASTER:形状,5,6)? 凶悪な# \@ペニスに
		ENDIF
		
		SIF CFLAG:選択中キャラID:処女喪失対象 & 喪失_なし崩し
			PRINTFORML なし崩し的に
		;処女喪失対象が恋慕か思慕であり、かつ、泥酔でも無理矢理でも睡眠でもないか自分での場合
		IF (GETBIT (CFLAG:選択中キャラID:処女喪失対象, 11) || GETBIT (CFLAG:選択中キャラID:処女喪失対象, 12)) && ((!GETBIT (CFLAG:選択中キャラID:処女喪失対象,9) && !GETBIT (CFLAG:選択中キャラID:処女喪失対象,13) && !GETBIT (CFLAG:選択中キャラID:処女喪失対象,16)) || GETBIT (CFLAG:選択中キャラID:処女喪失対象,14))
			PRINTFORML 処女を捧げた
		ELSE
			SIF CFLAG:選択中キャラID:処女喪失対象 & 喪失_無理矢理
				PRINT 力ずくで
			IF 選択中キャラID == MASTER
				PRINTFORML 処女を与えた
			ELSE
				PRINTFORML 処女を奪われた
			ENDIF
		ENDIF
	ENDIF
	
	SELECTCASE ABL:選択中キャラID:Ｖ感覚
		CASE IS >= 10
			PRINT 感度抜群　
		CASE IS >= 5
			PRINT 感度良好　
	ENDSELECT
	IF 選択中キャラID
		SELECTCASE 挿入不可(選択中キャラID)
			CASE 3
				PRINTFORML とても%CALLNAME:MASTER%のペニスを受け入れられるようなサイズではない
				LOCAL:5 ++
			CASE 2
				IF EXP:選択中キャラID:Ｖ経験 < 50 && TALENT:MASTER:禁断の知識
					PRINTFORML もう少しほぐさないと%CALLNAME:MASTER%のペニスは入らないだろう
					LOCAL:5 ++
				ELSEIF !TALENT:MASTER:禁断の知識
					PRINTFORML %CALLNAME:MASTER%のペニスを挿入するのは酷だろう
					LOCAL:5 ++
				ENDIF
			CASE 1
				IF EXP:選択中キャラID:Ｖ経験 < 50 || !TALENT:MASTER:禁断の知識
					PRINTFORML もう少しほぐさないと%CALLNAME:MASTER%のペニスは入らないだろう
					LOCAL:5 ++
				ENDIF
			CASE -1
				IF ABL:選択中キャラID:膣 < 2
					PRINTFORML %CALLNAME:MASTER%のペニスでは少々サイズが合わない
					LOCAL:5 ++
				ENDIF
			CASE -2
				IF ABL:選択中キャラID:膣 < 3
					PRINTFORML 大き過ぎて入れてもあまり気持ちよくなさそうだ…
					LOCAL:5 ++
				ENDIF
		ENDSELECT
		
		IF TALENT:選択中キャラID:処女 == 1
			PRINTFORML まだ誰一人として受け入れたことがない\@ ABL:選択中キャラID:Ｖ感覚 >= 5 ? にもかかわらず、刺激を与えられれば容易く絶頂するほど淫らに開発されている# \@
		ELSEIF TALENT:選択中キャラID:処女 == -1
			PRINTFORML すでに処女を奪われていることに気付いていない
		ENDIF
		SIF ABL:選択中キャラID:Ｖ感覚 >= 5 && CFLAG:選択中キャラID:溜まってる度 > 300 && 選択中キャラID
			PRINTFORML %CALLNAME:MASTER%に抱かれたくてうずうずしている
		IF !LOCAL:5
			SELECTCASE ABL:選択中キャラID:膣
				CASE IS >= 5
					PRINTL 極上名器
					PRINTFORML 完全に%CALLNAME:MASTER%のカタチに作り替えられた膣内は肉ヒダを蠢かせて絡みついてくる
				CASE IS >= 3
					PRINTL 名器
					IF TALENT:選択中キャラID:処女 == -1
						PRINTFORML 知らぬ間に%CALLNAME:MASTER%を気持ちよくさせるための穴に作り変えられてしまった
					ELSE
						PRINTFORML 使い込んだ甲斐あって大変具合のいい穴になった
					ENDIF
				CASE 2
					PRINTFORML %CALLNAME:MASTER%を受け入れることに慣れきっている
				CASE 1
					PRINTL ほぐれ始めて具合が良くなってきた
				CASE 0
					;IF EXP:選択中キャラID:Ｖ経験
					IF TALENT:選択中キャラID:処女 == 1
						PRINTL 性を知らない未使用の膣
					ELSEIF EXP:選択中キャラID:Ｖ経験 < 10
						PRINTL 性を知り大人の階段を恐る恐る登り始めたばかりの膣は強くしがみついてくる
					ELSEIF EXP:選択中キャラID:Ｖ経験 < 20
						PRINTL 未使用に近い膣はまだ硬さを残している
					ENDIF
			ENDSELECT
		ENDIF
	ENDIF
	
	SIF CFLAG:選択中キャラID:累計膣内精液
		PRINTFORML 今まで{CFLAG:選択中キャラID:累計膣内精液}の精液を注ぎ込まれた
	SELECTCASE ABL:選択中キャラID:膣射中毒
		CASE IS >= 7
			PRINTFORML 膣内射精されると多幸感を感じる
		CASE IS >= 3
			PRINTFORML 膣内射精されると子宮が疼いてしまう
	ENDSELECT
	SIF CFLAG:選択中キャラID:無理矢理膣射
		PRINTFORML %CALLNAME:MASTER%に無理矢理中出しされたことがある
	SIF CFLAG:選択中キャラID:睡眠姦 == 1
		PRINTFORML %CALLNAME:MASTER%に眠っている間にイタズラされたことがある
	SIF CFLAG:選択中キャラID:睡眠姦 == 2
		PRINTFORML %CALLNAME:MASTER%に眠っている間に犯されたことがある
	IF CFLAG:選択中キャラID:泥酔姦 == 2
		PRINTFORML %CALLNAME:MASTER%に酔いつぶれているところを犯されたことがある
	ELSEIF CFLAG:選択中キャラID:泥酔姦 == 1
		PRINTFORML %CALLNAME:MASTER%に酔いつぶれているところを犯されたことがあるが気づいていない
	ENDIF
	
	IF 充填率(選択中キャラID,1) >= 100
		PRINTFORML 膣内は%CALLNAME:MASTER%の精液で完全に満たされている
	ELSEIF 充填率(選択中キャラID,1) >= 50
		PRINTFORML 膣内で夥しい数の精子が蠢いている
	ELSEIF 充填率(選択中キャラID,1) >= 20
		PRINTFORML 膣内で精子が元気に泳ぎ回っている
	ELSEIF CFLAG:選択中キャラID:生理周期 == 7 && FLAG:生理周期可視化 && 妊娠可否(選択中キャラID)
		PRINTFORML 危険日の子宮は瑞々しい卵子を用意して受精を待っている
	ENDIF
	;PRINTFORML {EX:選択中キャラID:子宮内精液}
	IF TALENT:選択中キャラID:妊娠
		IF CFLAG:選択中キャラID:妊娠経過日数 < 20
			PRINTFORML 生理が止まっている
		ELSE
			PRINTFORML 子宮内では%CALLNAME:MASTER%との子供がすくすくと育っている
			PRINTFORML 現在妊娠{CFLAG:選択中キャラID:妊娠経過日数}日目だ
			CALL LOOK_BELLY(選択中キャラID)
			SIF INRANGE(CFLAG:選択中キャラID:妊娠経過日数,20,29)
					PRINT まだ見た目に変化は見られない
		ENDIF
	ENDIF
ENDIF
PRINTL
SIF 選択中キャラID == MASTER
	CALL ちん遍歴(MASTER)



@ちん遍歴(選択中キャラID)
#DIM 選択中キャラID
#DIM 暫定一位の相手
#DIM 暫定一位の量
#DIM 暫定一位の種類
SIF !GETBIT(TALENT:選択中キャラID:2, 1)
	RETURN
PRINTL 【男性器】
SELECTCASE TALENT:選択中キャラID:形状
CASE 1
	LOCALS = 子供のような可愛らしい
CASE 2
	LOCALS = 貧相な
CASE 3
	LOCALS = 何の変哲もない
CASE 4
	LOCALS = 発光する
CASE 5
	LOCALS = イボだらけの
CASE 6
	LOCALS = 長大な
ENDSELECT
PRINTFORM %CALLNAME:選択中キャラID%の%LOCALS%ペニスは、
LOCAL:1 = SUMCARRAY(CFLAG:LOCAL:累計精飲,       1, CHARANUM)
LOCAL:2 = SUMCARRAY(CFLAG:LOCAL:累計ぶっかけ,   1, CHARANUM)
LOCAL:3 = SUMCARRAY(CFLAG:LOCAL:累計膣内精液,   1, CHARANUM)
LOCAL:4 = SUMCARRAY(CFLAG:LOCAL:累計アナル精液, 1, CHARANUM)
IF !TALENT:PLAYER:1
	PRINTFORML 未使用だ
ELSE
	SIF FLAG:処女喰い
		PRINTFORML 今までに{FLAG:処女喰い}人の処女を散らし、
	PRINTFORML 少女たちの口内に{LOCAL:1}、肢体に{LOCAL:2}、膣内に{LOCAL:3}、アナルに{LOCAL:4}の精液を吐き出してきた
	;各射精シチュの最大値調査
	;→もっとも大量に出したシチュの決定
	;→もっとも大量に出した相手の決定
	LOCAL:1 = MAXCARRAY(CFLAG:LOCAL:累計精飲,       1, CHARANUM)
	LOCAL:2 = MAXCARRAY(CFLAG:LOCAL:累計ぶっかけ,   1, CHARANUM)
	LOCAL:3 = MAXCARRAY(CFLAG:LOCAL:累計膣内精液,   1, CHARANUM)
	LOCAL:4 = MAXCARRAY(CFLAG:LOCAL:累計アナル精液, 1, CHARANUM)
	暫定一位の量 = 0
	FOR LOCAL:0, 1, 5
		SIF 暫定一位の量 > LOCAL:(LOCAL:0)
			CONTINUE
		暫定一位の種類 = LOCAL:0
		暫定一位の量   = LOCAL:(LOCAL:0)
	NEXT
	SELECTCASE 暫定一位の種類
	CASE 1
		暫定一位の相手 = FINDCHARA(CFLAG:LOCAL:累計精飲,       LOCAL:1, 1, CHARANUM)
		LOCALS = 口内
	CASE 2
		暫定一位の相手 = FINDCHARA(CFLAG:LOCAL:累計ぶっかけ,   LOCAL:2, 1, CHARANUM)
		LOCALS = 肢体
	CASE 3
		暫定一位の相手 = FINDCHARA(CFLAG:LOCAL:累計膣内精液,   LOCAL:3, 1, CHARANUM)
		LOCALS = 膣内
	CASE 4
		暫定一位の相手 = FINDCHARA(CFLAG:LOCAL:累計アナル精液, LOCAL:4, 1, CHARANUM)
		LOCALS = アナル
	ENDSELECT
	PRINTFORML 特に%CALLNAME:暫定一位の相手%の%LOCALS%がお気に入りだ
ENDIF
SIF FLAG:行きずり孕ませ数
	PRINTFORML 行きずりに孕ませた数：{FLAG:行きずり孕ませ数}人
SIF TALENT:MASTER:巫女孕ませ
	PRINTL 巫女を孕ませた罰当たりなペニスだ
SIF TALENT:MASTER:妖精孕ませ
	PRINTL 妖精を孕ませた犯罪的なペニスだ
SIF TALENT:MASTER:妖怪孕ませ
	PRINTL 妖怪を孕ませた恐れ知らずのペニスだ
SIF TALENT:MASTER:天狗孕ませ
	PRINTL 天狗を孕ませた神速のペニスだ
SIF TALENT:MASTER:鬼孕ませ
	PRINTL 鬼を孕ませた鬼をもひしぐペニスだ
SIF TALENT:MASTER:付喪神孕ませ
	PRINTL 付喪神を孕ませた節操のないペニスだ
SIF TALENT:MASTER:神霊孕ませ
	PRINTL 神霊を孕ませた神をも恐れぬペニスだ
SIF TALENT:MASTER:蓬莱孕ませ
	PRINTL 蓬莱人すら孕ませた時を超えるペニスだ
SIF TALENT:MASTER:万能孕ませ
	PRINTL 機械だろうが幽霊だろうがいかなる少女でも孕ませる人智を超えたペニスだ
SIF TALENT:MASTER:濃厚精液
	PRINTFORML 何日分もの精を溜め込んだペニスはむせ返るような雄の臭いを放っている
SIF BASE:選択中キャラID:精力 >= 1500
	PRINTL ずっしりと重い陰嚢には大量の精子が蓄えられている




@妊娠関連(ARG)
PRINTL
PRINTL □ 妊娠関連 □-----------------------------------------------------------------------
;PRINTL
;PRINTFORM 前回体内射精量 : {EX:ARG:14}
;PRINTL
PRINTFORM 妊娠後経過時間: {CFLAG:ARG:903}
;CFLAG:ARG:903 = 40
;PRINTL
;PRINTFORM 自覚 : {CFLAG:(ARG):910}
;PRINTL
;PRINTFORM 無自覚妊娠 : {CFLAG:ARG:無自覚妊娠}
PRINTL
PRINTFORM 現在育児中子供数 : {CFLAG:ARG:育児人数}
PRINTL
PRINTFORM 現在育児経過時間 : {CFLAG:ARG:908}
PRINTFORML 
PRINTFORM 　　　　子供の数 : {CFLAG:ARG:子供人数}
PRINTFORML 
IF CFLAG:ARG:子供人数
	FOR LOCAL,1,11
		IF CHILDNAME:ARG:LOCAL != ""
			PRINTFORM 第{LOCAL}子：%CHILDNAME:ARG:LOCAL, 15, LEFT%　\@ CHILD_SEX:ARG:LOCAL == 1 ? ♀ # ♂ \@ 
			CALL PRINT_DATE(CHILD_BIRTHDAY:ARG:LOCAL)
			PRINTFORM ({CHILD_BIRTHDAY:ARG:LOCAL}日目) 
			SELECTCASE CHILD_WEATHER:ARG:LOCAL
			CASE "Ｃ敏感"
				PRINTFORM みんなのクリトリスが敏感な
			CASE "Ｖ敏感"
				PRINTFORM みんなの女性器が敏感な
			CASE "Ａ敏感"
				PRINTFORM みんなの肛門が敏感な
			CASE "Ｂ敏感"
				PRINTFORM みんなの胸が敏感な
			CASE "Ｍ敏感"
				PRINTFORM みんなの唇が敏感な
			CASE "スカート"
				PRINTFORM スカートをめくられて頭にくると気持ちよくなる
			CASE "パンツ"
				PRINTFORM パンツが飛び交う
			CASEELSE
				PRINTFORM %CHILD_WEATHER:ARG:LOCAL%の
			ENDSELECT
			PRINTFORM 日に生まれた　
			IF DAY - CHILD_BIRTHDAY:ARG:LOCAL < 120
				IF CFLAG:ARG:出産経過日 < CHILD_語彙
					PRINTFORML 幼児期
				ELSEIF CFLAG:ARG:出産経過日 < CHILD_自立前
					PRINTFORML 幼少期
				ELSE
					PRINTFORML 思春期
				ENDIF
			ELSE
				PRINTFORML 現在{(DAY - CHILD_BIRTHDAY:ARG:LOCAL) / 124}才
			ENDIF
		ENDIF
	NEXT
ENDIF



@予定
#DIM 依頼番号

PRINTL □ 予定 □---------------------------------------------------------------------------
PRINTL 
SELECTCASE FLAG:抱負
	CASE 1
		LOCALS = オナニー奨励
	CASE 2
		LOCALS = 同性愛推進
	CASE 3
		LOCALS = 牝穴開拓
	CASE 4
		LOCALS = 尻穴開発
	CASE 5
		LOCALS = 乳挟振興
	CASE 6
		LOCALS = 子孫繁栄
ENDSELECT
IF FLAG:抱負
	PRINTFORML 今年の抱負: %LOCALS%
	PRINTL
ENDIF
IF FLAG:宴会開催フラグ
	IF FLAG:開始日 - DAY <= 0
		PRINT 本日
	ELSE
		PRINTFORM {FLAG:開始日 - DAY}日後
	ENDIF
	CALL 宴会予定表示
	PRINTL 
	PRINT 宴会規模：
		SELECTCASE FLAG:宴会規模
			CASE IS >= 5000
				PRINT 特大
			CASE IS >= 2500
				PRINT 大
			CASE IS >= 1500
				PRINT 中
			CASEELSE
				PRINT 小
		ENDSELECT

	SIF FLAG:36 > 0
		PRINTL 　雨天中止
ENDIF
PRINTL
IF FLAG:貸本返却期限 >= 2
	FOR LOCAL,1,5
		SIF ITEM:(100 + LOCAL)
			PRINTFORML 借りている本：%ITEMNAME:(100 + LOCAL)%
	NEXT
	PRINTL 
	IF  FLAG:貸本返却期限 == 2
		PRINTFORML 貸本の返却日：本日
	ELSE
		PRINTFORML 貸本の返却日：{FLAG:貸本返却期限 - 2}日後
	ENDIF
PRINTL
ENDIF

CALL SHOW_ALL_IRAI_SLOT

SELECTCASE CFLAG:MASTER:小槌
	CASE 4
		PRINT 小槌で一物を大きくし
		IF CFLAG:MASTER:魔力回収 == 6
			PRINTL ている
		ELSE
			PRINTL た
		ENDIF
		SIF CFLAG:MASTER:魔力回収 < 6
			PRINTFORML 魔力回収完済まであと{CFLAG:MASTER:魔力回収}日
	CASE 5
		PRINT 小槌で一物を二本にし
		IF CFLAG:MASTER:魔力回収 == 6
			PRINTL ている
		ELSE
			PRINTL た
		ENDIF
		SIF CFLAG:MASTER:魔力回収 < 6
			PRINTFORML 魔力回収完済まであと{CFLAG:MASTER:魔力回収}日
	CASE 6
		PRINT 小槌で小人になっ
		IF CFLAG:MASTER:魔力回収 == 3
			PRINTL ている
		ELSE
			PRINTL た
		ENDIF
		SIF CFLAG:MASTER:魔力回収 < 3
			PRINTFORML 魔力回収完済まであと{CFLAG:MASTER:魔力回収}日
	CASE 7
		PRINTL 小槌で小人から元に戻った
		SIF CFLAG:MASTER:魔力回収 < 3
			PRINTFORML 魔力回収完済まであと{CFLAG:MASTER:魔力回収}日
ENDSELECT
SIF CFLAG:MASTER:小槌
	PRINTL 



@パンツ収集状況(選択中キャラID)
#DIM 選択中キャラID
PRINTL □ パンツ収集状況 □-----------------------------------------------------------------
CALL DISPLAY_COLLECTION(選択中キャラID)
PRINTFORML 総数：{RESULT:0,4}枚　　種別：{RESULT:1,2}種　　ダブり：{RESULT:0 - RESULT:1,4}枚

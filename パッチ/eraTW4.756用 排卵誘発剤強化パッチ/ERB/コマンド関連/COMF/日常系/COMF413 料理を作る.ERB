;-------------------------------------------------
;料理を作る
;日常系コマンド、レベル1
;-------------------------------------------------
@COM413
;食材がない
IF !CAN_COOK("全て", MASTER)
	DRAWLINE
	PRINTFORMW 食材がない
	RETURN -1
ENDIF
;先に助手取得
IF TARGET <= 0
ELSEIF SHIRAHU(TARGET) == 1 && ABL:親密 * 500 + ABL:従順 * 500 + ABL:料理技能 * 1000 > CFLAG:310 - CFLAG:MASTER:310
	DISHASSI = TARGET
ENDIF
CALL COOKING_MENU
IF !RESULT
	CALL RESET_DISH
	RETURN -1
ENDIF

;まな板補正で美味しくできます
IF ITEM:イチョウのまな板
	SELECTCASE ITEM:イチョウのまな板
		CASE 1
			DISHQUALITY += RAND:3
		CASE 2
			DISHQUALITY += MAX(RAND:3, 1)
		CASE 3
			DISHQUALITY += MAX(RAND:4, 1)
	ENDSELECT
	PRINTFORML 包丁が打つまな板が心地よい音を立てている…
ENDIF

IF TFLAG:194 == 1
	DOWNBASE:MASTER:体力 = 50
	DOWNBASE:MASTER:気力 = 50
	TIME += 15
	SIF CFLAG:同行
		CFLAG:同行 += 15
ELSEIF TFLAG:194 == 2
	DOWNBASE:MASTER:体力 = 100
	DOWNBASE:MASTER:気力 = 100
	TIME += 45
	SIF CFLAG:同行
		CFLAG:同行 += 45
ELSEIF TFLAG:194 == 3
	DOWNBASE:MASTER:体力 = 100
	DOWNBASE:MASTER:気力 = 100
	TIME += 45
	SIF CFLAG:同行
		CFLAG:同行 += 45
ELSEIF TFLAG:194 == 4
	DOWNBASE:MASTER:体力 = 150
	DOWNBASE:MASTER:気力 = 150
	TIME += 45
	SIF CFLAG:同行
		CFLAG:同行 += 45
ENDIF

IF TARGET > 0 && !CFLAG:睡眠 && !FLAG:70
	IF DISHASSI
		TFLAG:193 = 1
		EXP:料理経験 ++
	ELSE
		TFLAG:193 = 0
	ENDIF
	;固定で獲得するソース
	SOURCE:歓楽 = 200
	
	;信頼
	TFLAG:98 = 3


	;ABL:従順をみる
	IF ABL:従順 <= 1
		SOURCE:歓楽 += (ABL:従順 * 60)
	ELSEIF ABL:従順 <= 3
		SOURCE:歓楽 += 200 + (ABL:従順 * 60)
	ELSEIF ABL:従順 <= 5
		SOURCE:歓楽 += 500 + (ABL:従順 * 60)
	ELSEIF ABL:従順 <= 8
		SOURCE:歓楽 += 750 + (ABL:従順 * 75)
	ELSEIF ABL:従順 <= 11
		SOURCE:歓楽 += 1000 + (ABL:従順 * 75)
	ELSE
		SOURCE:歓楽 += 2000 + (ABL:従順 * 30)
	ENDIF

	;好感度をみる
	IF CFLAG:2 <= 500
		SOURCE:歓楽 += CFLAG:2
	ELSEIF CFLAG:2 <= 5000
		SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
	ELSE
		SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
	ENDIF
	SIF SOURCE:歓楽 < 0
		SOURCE:歓楽 = 0
ENDIF
TCVAR:MASTER:308 = 0
SIF TARGET < 1 && (!GET_OPTION_COOKING("自動味付け設定") || GET_OPTION_COOKING("異物混入可能") || GET_OPTION_COOKING("薬物混入可能"))
	CALL MIX_MENU
SIF GET_OPTION_COOKING("自動味付け設定")
	CALL AUTO_SEASONING

IF ITEM:調理指南書
	SIF !RAND:2
		EXP:MASTER:料理経験 ++
	SIF !RAND:2 && TARGET
		EXP:TARGET:料理経験 ++
ENDIF
DISHTIME = 1440 * DAY + TIME

STAIN:MASTER:0 = 0
STAIN:MASTER:1 = 0
CFLAG:MASTER:口に精液 = 0
CFLAG:MASTER:顔に精液 = 0
CFLAG:MASTER:手に精液 = 0
IF TARGET
	STAIN:0 = 0
	STAIN:1 = 0
	CFLAG:口に精液 = 0
	CFLAG:顔に精液 = 0
	CFLAG:手に精液 = 0
ENDIF

RETURN 1


@COOKING_MENU
PRINTL 何を作りますか？
{
CALL ASK_M(
	"やっぱりやめる", 1,
	"軽食",           CAN_COOK("軽食", MASTER),
	"主食",           CAN_COOK("主食", MASTER),
	"デザート",       CAN_COOK("デザート", MASTER),
	"禁断の料理",     CAN_COOK("禁断の料理", MASTER), 
	"魚料理",         (CAN_COOK("魚料理", MASTER) || CAN_COOK("カエル料理", MASTER)))
}
SELECTCASE RESULT
CASE 1
	DISHTYPE = RESULT
	CALL INGREDIENT_CHOICE
	SIF RESULT == 0
		RETURN 0
	IF GET_OPTION_COOKING("料理内容自動決定")
		CALL COOKING(, "「料理内容自動決定」")
	ELSE
		CALL COOKING
		SIF RESULT == 0
			RETURN 0
	ENDIF
	TFLAG:194 = 1
	CALL VEGE_CHOICE
	CALL REMOVE_VEGE
	CALL SHOW_DISHDATA
	WAIT
	RETURN 1
CASE 2
	DISHTYPE = RESULT
	CALL INGREDIENT_CHOICE
	SIF RESULT == 0
		RETURN 0
	IF GET_OPTION_COOKING("料理内容自動決定")
		CALL COOKING(, "「料理内容自動決定」")
	ELSE
		CALL COOKING
		SIF RESULT == 0
			RETURN 0
	ENDIF
	TFLAG:194 = 2
	CALL VEGE_CHOICE
	CALL REMOVE_VEGE
	CALL SHOW_DISHDATA
	WAIT
	RETURN 1
CASE 3
	DISHTYPE = RESULT
	CALL INGREDIENT_CHOICE
	SIF RESULT == 0
		RETURN 0
	IF GET_OPTION_COOKING("料理内容自動決定")
		CALL COOKING(, "「料理内容自動決定」")
	ELSE
		CALL COOKING
		SIF RESULT == 0
			RETURN 0
	ENDIF
	TFLAG:194 = 3
	CALL VEGE_CHOICE
	CALL REMOVE_VEGE
	CALL SHOW_DISHDATA
	WAIT
	RETURN 1
CASE 4
	IF !ITEM:0
		PRINTFORMW 食材が足りません
		TIME += 5
		RETURN 0
	ENDIF
	
	ITEM:0 --
	DISHTYPE = 4
	CALL COOKING
	TFLAG:194 = FLAG:料理
	CALL VEGE_CHOICE
	CALL REMOVE_VEGE
	CALL SHOW_DISHDATA
	WAIT
	RETURN 1
CASE 5
	PRINTFORML 食材を選んでください
	CALL ASK_M(@"魚　{ITEM:魚}尾所持", ITEM:魚, @"うまい魚　{ITEM:うまい魚}尾所持", ITEM:うまい魚,@"カエル　{ITEM:カエル}匹所持", ITEM:カエル >= 3,"やめる",1)
	SELECTCASE RESULT
		CASE 0
			DISHQUALITY = 1
			DISHTYPE = 5
			;@COOKING用に一時的に5と6が代入されるが後でDISHTYPE = 2で上書きされる
			DISHTASTE += GET_TASTE_INGREDIENT("魚")
		CASE 1
			DISHQUALITY = 2
			DISHTYPE = 5
			DISHTASTE += GET_TASTE_INGREDIENT("うまい魚")
		CASE 2
			DISHQUALITY = 1
			DISHTYPE = 6
			DISHTASTE += GET_TASTE_INGREDIENT("カエル")
		CASE 3
			RETURN 0
	ENDSELECT
	IF GET_OPTION_COOKING("料理内容自動決定")
		CALL COOKING(, "「料理内容自動決定」")
	ELSE
		CALL COOKING
		SIF RESULT == 0
			RETURN 0
	ENDIF
	TFLAG:194 = 2
	CALL VEGE_CHOICE
	CALL REMOVE_VEGE
	CALL SHOW_DISHDATA
	WAIT
	RETURN 1
CASE 0
	RETURN 0
ENDSELECT





;料理実行関数
;料理依頼に流用できるように作成
;先に呼び出し元でDISHTYPEとDISHQUALITYを決めておくこと
;食材の消費処理も先にすませておくこと
@COOKING(CHARA, OP)
#DIM  CHARA ;キャラを渡すと手伝ってくれる（依頼用、COM413は実行後に料理経験入るので不要）
#DIMS CONST MENU_TYPE, 7 = "", "SNACK", "MAINDISH", "DESSERT", "FORBIDDEN","FISH", "FROG"
#DIMS OP
SIF CHARA
	DISHASSI = CHARA
IF STRCOUNT(OP, "「料理内容自動決定」") || MENU_TYPE:DISHTYPE == "FORBIDDEN"
	CALL SET_DISHNAME(MENU_TYPE:DISHTYPE)
ELSE
	CALL SELECT_DISHNAME(MENU_TYPE:DISHTYPE)
	SIF RESULT == 0
		RETURN
ENDIF
PRINTFORMW %DISHNAME%を作ります
SELECTCASE DISHTYPE
CASE 1
	CALL COOKING_TAKARI([[幽々子]], 5)
	CALL COOKING_TAKARI([[紫苑]], 5)
CASE 2
	CALL COOKING_TAKARI([[幽々子]], 10)
	CALL COOKING_TAKARI([[紫苑]], 10)
	EXP:MASTER:料理経験 ++
	IF CHARA
		EXP:CHARA:料理経験 ++
		SIF EXP:MASTER:料理経験 > EXP:CHARA:料理経験
			EXP:CHARA:料理経験 ++
	ENDIF
CASE 3
CASE 4
CASE 5
	YOGORE:(CFLAG:MASTER:現在位置) += 500
ENDSELECT
YOGORE:(CFLAG:MASTER:現在位置) += 500
	EXP:MASTER:料理経験 ++
	IF CHARA
		EXP:CHARA:料理経験 ++
		SIF EXP:MASTER:料理経験 > EXP:CHARA:料理経験
			EXP:CHARA:料理経験 ++
	ENDIF
SIF DISHASSI
	DISHTASTE += GET_TASTE_ASSIBONUS(DISHASSI)
DISHTASTE += GET_TASTE_ASSIBONUS(MASTER)
IF STRCOUNT(DISHTASTE, ESCAPE("雑穀/"))
	ITEM:雑穀 --
ELSEIF STRCOUNT(DISHTASTE, ESCAPE("古米/"))
	ITEM:古米 --
ELSEIF STRCOUNT(DISHTASTE, ESCAPE("白米/"))
	ITEM:白米 --
ELSEIF STRCOUNT(DISHTASTE, ESCAPE("うまい魚/"))
	ITEM:うまい魚 --
ELSEIF STRCOUNT(DISHTASTE, ESCAPE("魚/"))
	ITEM:魚 --
ELSEIF STRCOUNT(DISHTASTE, ESCAPE("カエル料理/"))
	ITEM:カエル -= 3
ENDIF
RETURN 1


;料理名手動設定関数
@SELECT_DISHNAME(MENU_TYPE)
#DIMS         MENU_TYPE      ;作りたいメニューの種類
#DIM          NUM_MENUS      ;
#DIM          FOOD_ID        ;
#DIMS DYNAMIC FOOD_NAME, 100 ;
#DIMS DYNAMIC FES_NAME       ;
#DIM  DYNAMIC CNT_SHOW       ;
FES_NAME '= FESTIVAL_MENU()
PRINTFORML 何を作ろうか？
SELECTCASE MENU_TYPE
CASE "SNACK"
	PRINTFORML ＜軽食メニュー＞
	ARRAYCOPY "FOOD_SNACK", "FOOD_NAME"
	NUM_MENUS = MENUS_SNACK

CASE "MAINDISH"
	PRINTFORML ＜主食メニュー＞
	ARRAYCOPY "FOOD_MAINDISH", "FOOD_NAME"
	NUM_MENUS = MENUS_MAINDISH

CASE "DESSERT"
	PRINTFORML ＜デザートメニュー＞
	ARRAYCOPY "FOOD_DESSERT", "FOOD_NAME"
	NUM_MENUS = MENUS_DESSERT

CASE "FISH"
	PRINTFORML ＜魚料理メニュー＞
	ARRAYCOPY "FOOD_FISH", "FOOD_NAME"
	NUM_MENUS = MENUS_FISH

CASE "FROG"
	PRINTFORML ＜カエル料理メニュー＞
	ARRAYCOPY "FOOD_FROG", "FOOD_NAME"
	NUM_MENUS = MENUS_FROG
ENDSELECT
FOR FOOD_ID, NUM_MENUS - 1, -1, -1
	SIF !STRLENS(FOOD_NAME:FOOD_ID)
		CONTINUE
	LOCALS = [{FOOD_ID, 2}]%FOOD_NAME:FOOD_ID, 24%
	PRINTFORM 　
	PRINTBUTTON LOCALS, FOOD_ID
	CNT_SHOW ++
	SIF CNT_SHOW % 3 == 0
		PRINTFORML 
NEXT
SIF CNT_SHOW % 3
	PRINTFORML 
SIF STRLENS(FES_NAME) && MENU_TYPE == "MAINDISH"
	PRINTFORML 　[90]%FES_NAME, 24%
PRINTFORML 　[99]やめる
DO
	INPUT
	SELECTCASE RESULT
	CASE 99
		RETURN
	CASE 90
		SIF !(STRLENS(FES_NAME) && MENU_TYPE == "MAINDISH")
			GOTO RETRY
		DISHNAME  '= FES_NAME
		MENU_TYPE '= "FESTIVAL"
	CASE 0 TO NUM_MENUS - 1
		FOOD_ID = RESULT
		SIF !STRLENS(FOOD_NAME:FOOD_ID)
			GOTO RETRY
		DISHNAME  '= FOOD_NAME:FOOD_ID
	CASEELSE
		$RETRY
		PRINTFORML 無効な入力です
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1
SELECTCASE MENU_TYPE
CASE "SNACK"
	DISHTYPE = 1
	FLAG:料理 = FOOD_ID
CASE "MAINDISH"
	DISHTYPE = 2
	FLAG:料理 = FOOD_ID + 10
CASE "DESSERT"
	DISHTYPE = 3
	FLAG:料理 = FOOD_ID + 50
CASE "FISH"
	DISHTYPE = 2
CASE "FROG"
	DISHTYPE = 2
CASE "FESTIVAL"
	DISHTYPE = 5
	FLAG:料理 = 999
ENDSELECT
DISHTASTE += GET_TASTE_MENUTYPE(MENU_TYPE)
DISHTASTE += DISHNAME + "/" + GET_TASTE_DISH(DISHNAME)
RETURN 1


;料理抽選関数
;料理の決定とFLAGへの記録、DISHNAME決定まで
;DISHQUALITYの設定は呼び出し元で行うこと
@SET_DISHNAME(MENU_TYPE)
#DIMS MENU_TYPE
#DIM  FOOD_ID
SELECTCASE MENU_TYPE
CASE "SNACK"
	DISHTYPE = 1
	FLAG:料理 = RAND:MENUS_SNACK + 1
	DISHNAME = %FOOD_SNACK:(FLAG:料理)%
CASE "MAINDISH"
	DISHTYPE = 2
	FOOD_ID = RAND:MENUS_MAINDISH
	FLAG:料理 = FOOD_ID + 10
	DISHNAME = %FOOD_MAINDISH:(FOOD_ID)%
	IF FESTIVAL_MENU() != ""
		DISHNAME = %FESTIVAL_MENU()%
		FLAG:料理 = 999
		DISHTYPE = 5
	ENDIF
CASE "DESSERT"
	DISHTYPE = 3
	FOOD_ID = RAND:MENUS_DESSERT
	DISHNAME = %FOOD_DESSERT:(FOOD_ID)%
	FLAG:料理 = FOOD_ID + 50
CASE "FORBIDDEN"
	DISHTYPE = 4
	DISHNAME = %FOOD_FORBIDDEN:(DAY:2)%
	FLAG:料理 = 99 + DAY:2
CASE "FISH"
	DISHTYPE = 2
	DISHNAME = %FOOD_FISH:(RAND:MENUS_FISH)%
CASE "FROG"
	DISHTYPE = 2
	DISHNAME = %FOOD_FROG:(RAND:MENUS_FROG)%
ENDSELECT

IF DISHTYPE == 5
	DISHTASTE += GET_TASTE_MENUTYPE("FESTIVAL")
ELSE
	DISHTASTE += GET_TASTE_MENUTYPE(MENU_TYPE)
ENDIF
DISHTASTE += DISHNAME + "/" + GET_TASTE_DISH(DISHNAME)




@食事(ARG)
#DIM 精力回復量
#DIM 気力回復量
#DIM 体力回復量
#DIM 精力回復率
#DIM 気力回復率
#DIM 体力回復率
#DIMS CONST DISH_TO_満腹度 = "", "軽食", "主食", "デザート", "主食", "主食"
#DIM COOK_POWER		;キャラの調理力、または外食時の味
;回復率は0.1%単位

精力回復量 = 0
気力回復量 = 0
体力回復量 = 0
精力回復率 = 0
気力回復率 = 0
体力回復率 = 0

SELECTCASE DISHTYPE
	CASE 1
	;軽食
		体力回復率 = 65
		気力回復率 = 200
		精力回復率 = 50
	CASE 2
	;主食
		体力回復率 = 200
		気力回復率 = 300
		精力回復率 = 200
	CASE 3
	;デザート
		体力回復率 = 50
		気力回復率 = 500
		精力回復率 = 50
	CASE 4
	;禁断の料理
		体力回復率 = 300
		気力回復率 = 300
		精力回復率 = 300
		EXP:ARG:Ｃ経験 += 30
	CASE 5
	;祭日メニュー
		体力回復率 = 100
		気力回復率 = 150
		精力回復率 = 100
ENDSELECT

IF !DISH_RADICAL()
ELSEIF TASTE_GIVEUP(ARG, DISH_RADICAL())
	TIMES 体力回復率, 0.1
	TIMES 気力回復率, 0.1
	TIMES 精力回復率, 0.1
ELSE
	TIMES 体力回復率, 1.2
	TIMES 気力回復率, 1.2
	TIMES 精力回復率, 1.2
ENDIF
CALL 満腹度上昇(ARG, DISH_TO_満腹度:DISHTYPE)

体力回復量 = (MAXBASE:ARG:体力 * 体力回復率 / 1000) * (ABL:MASTER:料理技能 + 5 + DISHQUALITY) / 10
気力回復量 = (MAXBASE:ARG:気力 * 気力回復率 / 1000) * (ABL:MASTER:料理技能 + 5 + DISHQUALITY) / 10
精力回復量 = (MAXBASE:ARG:精力 * 精力回復率 / 1000) * (ABL:MASTER:料理技能 + 5 + DISHQUALITY) / 10
;追加食材ボーナス
IF DISHVEGE > 0
	体力回復量 += 50
	気力回復量 += 50
	精力回復量 += 50
ENDIF

;料理の腕による上方補正
COOK_POWER = 8 + ABL:MASTER:料理技能 * 2
SIF DISHASSI
	COOK_POWER += ABL:DISHASSI:料理技能

CALL RECOVER(ARG, 体力回復量, "体力", "食事による回復")
CALL RECOVER(ARG, 気力回復量, "気力", "食事による回復")
IF ARG == MASTER
	CALL RECOVER(ARG, 精力回復量, "精力", "食事による回復")
ELSEIF STRLENS(DISHTASTE) && GET_OPTION_COOKING("採点機能オン")
	CALL FOOD_TASTING(ARG, DISHTASTE, COOK_POWER)
	PRINTFORML %CALLNAME:ARG + "の評価…………", 40, LEFT%{TCVAR:ARG:料理総評価値, 3}点
ENDIF

;イタズラを仕込まれている
SELECTCASE DISH_MIXTURE()
CASE DISH_DRAG_媚薬
	TCVAR:TARGET:媚薬 = 210
CASE DISH_DRAG_利尿剤
	TCVAR:TARGET:利尿剤 = 210
CASE DISH_DRAG_惚れ薬
	TCVAR:TARGET:惚れ薬 = 210
CASE DISH_DRAG_睡眠薬
	IF TCVAR:TARGET:睡眠薬強度 < 2
		TCVAR:TARGET:睡眠薬強度 = 2
		TCVAR:TARGET:睡眠薬お茶使用 = 1
	ENDIF
CASE DISH_DRAG_排卵誘発剤
	TCVAR:TARGET:強制排卵 = 210
ENDSELECT

@満腹度上昇(ARG,ARGS)
SIF TASTE_GIVEUP(ARG, DISH_RADICAL())
	RETURN
SELECTCASE ARGS
	CASE "軽食"
		TCVAR:ARG:空腹時刻 = 1440 * DAY + TIME + 180
		SIF TALENT:ARG:健啖
			TCVAR:ARG:空腹時刻 -= 30
	CASE "主食"
		TCVAR:ARG:空腹時刻 = 1440 * DAY + TIME + 360
		SIF TALENT:ARG:健啖
			TCVAR:ARG:空腹時刻 -= 90
	CASE "デザート"
		TCVAR:ARG:デザート空腹時刻 = 1440 * DAY + TIME + 180
		SIF TALENT:ARG:健啖
			TCVAR:ARG:デザート空腹時刻 -= 30
ENDSELECT


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE413
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;掃除実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(413)
	RETURN RESULT

;厨房なのか
SIF !KITCHEN(CFLAG:MASTER:現在位置)
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
SIF CFLAG:うふふ
	RETURN 0
;添い寝中
SIF CFLAG:添い寝中
	RETURN 0
;イタズラ中
SIF CFLAG:MASTER:イタズラ
	RETURN 0
;既に料理を作っている
SIF DISHNAME != ""
	RETURN 0
RETURN 1

@HUNGER(ARG)
#FUNCTIONS
LOCAL = TCVAR:ARG:空腹時刻 - TIME:1
SELECTCASE LOCAL
	CASE IS > 270
	SETCOLOR C_GREEN
		RETURNF "満腹"
	CASE IS > 180
	SETCOLOR C_P_GREEN
		RETURNF "腹八分"
	CASE IS > 0
	SETCOLOR C_P_YELLOW
		RETURNF "普通"
	CASEELSE
		RETURNF "食事可"
ENDSELECT


@INGREDIENT_CHOICE
PRINTFORML 食材を選んでください
CALL ASK_M(@"雑穀({ITEM:48})", ITEM:48, @"古米({ITEM:50})", ITEM:50, @"白米({ITEM:47})", ITEM:47,"やめる",1)
SELECTCASE RESULT
	CASE 0
		DISHTASTE += GET_TASTE_INGREDIENT("雑穀")
	CASE 1
		DISHTASTE += GET_TASTE_INGREDIENT("古米")
	CASE 2
		DISHTASTE += GET_TASTE_INGREDIENT("白米")
	CASE 3
		DISHTYPE = 0
		RETURN 0
ENDSELECT
DISHQUALITY = RESULT + 1
RETURN DISHQUALITY


;追加する野菜の選択
@VEGE_CHOICE
#DIMS CONST VEGE_NAME = "", "キャベツ", "イチゴ", "カブ", "キュウリ", "トマト", "スイカ", "ナス", "サツマイモ", "ブドウ", "ユリネ", "ニンジン", "ネギ", "山菜"
#DIM  VEGE_ID
#DIM  TEMP_INPUT
;野菜がない
{
IF MAX(
	ITEM:キャベツ, ITEM:イチゴ,     ITEM:カブ,   
	ITEM:キュウリ, ITEM:トマト,     ITEM:スイカ, 
	ITEM:ナス,     ITEM:サツマイモ, ITEM:ブドウ, 
	ITEM:ユリネ,   ITEM:ニンジン,   ITEM:ネギ,   
	ITEM:山菜                                    ) <= 0
}
	RETURN
ENDIF
DO
	PRINTFORML 料理に追加する食材を選んで下さい
	FOR VEGE_ID, VARSIZE("VEGE_NAME") - 1, 0, -1
		IF ITEM:(GETNUM(ITEM, VEGE_NAME:VEGE_ID)) > 0
			PRINTBUTTON @"{VEGE_ID, 2, RIGHT}[%VEGE_NAME:VEGE_ID, 10%]", VEGE_ID
		ELSE
			SETCOLOR 0x606060
			PRINTPLAINFORM {VEGE_ID, 2, RIGHT}[%VEGE_NAME:VEGE_ID, 10%]
			RESETCOLOR
		ENDIF
		IF VEGE_ID % 3 == 1
			PRINTFORML 
		ELSE
			PRINTFORM 　
		ENDIF
	NEXT
	PRINTBUTTON @" 0[%"やめる", 10%]", 0
	PRINTFORML 
	INPUT
	TEMP_INPUT = RESULT
	SELECTCASE TEMP_INPUT
	CASE 0
		RETURN
	CASE 1 TO VARSIZE("VEGE_NAME") - 1
		;行儀が悪いがループ用に使ったのを使いまわしてます
		VEGE_ID = GETNUM(ITEM, VEGE_NAME:TEMP_INPUT)
		SIF ITEM:VEGE_ID <= 0
			GOTO RETRY
		BREAK
	CASEELSE
		$RETRY
		PRINTFORML 無効な入力です
		CONTINUE
	ENDSELECT
LOOP 1
ITEM:VEGE_ID --
DISHVEGE  = VEGE_ID
DISHTASTE += ITEMNAME:VEGE_ID + "/" + GET_TASTE_VEGE(ITEMNAME:VEGE_ID)


@RESET_DISH
DISHTIME = 0
DISHSPOIL = 0
FLAG:料理 = 0
DISHQUALITY = 0
DISHTYPE = 0
DISHNAME =

DISHTASTE =
DISHVEGE = 0
DISHASSI = 0



;野菜除去処理実行判定関数
;サニークラピのコンビで料理すると7割野菜抜き
@CAN_REMOVE_VEGE
#FUNCTION
#DIM DYNAMIC VEGE_HATE_MAS
#DIM DYNAMIC VEGE_HATE_ASI
SIF !STRCOUNT(DISHTASTE, ESCAPE("野菜/"))
	RETURNF 0
SIF FLAG:なりきり
	VEGE_HATE_MAS = STRCOUNT(GET_STR(, "キャラデータ", FLAG:なりきり, "料理：嫌いな味"), ESCAPE("野菜/"))
SIF RAND(1, 11) <= VEGE_HATE_MAS
	RETURNF 1
SIF !DISHASSI
	RETURNF 0
VEGE_HATE_ASI = STRCOUNT(GET_STR(, "キャラデータ", DISHASSI, "料理：嫌いな味"), ESCAPE("野菜/"))
SIF RAND(1, 11) <= VEGE_HATE_ASI
	RETURNF 2



;野菜除去処理実行関数
;元々の料理の味覚にも影響が出てしまうが
;なにかこう、料理のバランス的なサムシングが崩れてしまうと脳内補完してください
;
;クラピがベースであるため
;キュウリ/サツマイモ/ネギ、は処理から抜けてるがめんどくさいしこのまま
;
;従来はトマト/ニンジンが相当強力だった感があるのである種バランスはとれるかも
;野菜抜き処理で高評価を受けやすい「紅い/鮮やか」が抜けてしまうよ
;そして影響を受けないイチゴは相変わらず強力
@REMOVE_VEGE
SELECTCASE CAN_REMOVE_VEGE()
CASE 0
	RETURN
CASE 1
	PRINTFORML %CALLNAME:MASTER%は料理からこっそりと苦手な野菜を取り除いた……
CASE 2
	PRINTFORML %CALLNAME:DISHASSI%は料理からこっそりと苦手な野菜を取り除いた……
ENDSELECT

SIF STRCOUNT(DISHTASTE, ESCAPE("キャベツ/"))
	DISHTASTE '= REPLACE(DISHTASTE, "キャベツ\/|さっぱり\/", "")

SIF STRCOUNT(DISHTASTE, ESCAPE("カブ/"))
	DISHTASTE '= REPLACE(DISHTASTE, "カブ\/|なめらか\/", "")

SIF STRCOUNT(DISHTASTE, ESCAPE("トマト/"))
	DISHTASTE '= REPLACE(DISHTASTE, "トマト\/|紅い\/|鮮やか\/", "")

SIF STRCOUNT(DISHTASTE, ESCAPE("ナス/"))
	DISHTASTE '= REPLACE(DISHTASTE, "ナス\/|さっぱり\/", "")

SIF STRCOUNT(DISHTASTE, ESCAPE("ユリネ/"))
	DISHTASTE '= REPLACE(DISHTASTE, "ユリネ\/|上品\/", "")

SIF STRCOUNT(DISHTASTE, ESCAPE("ニンジン/"))
	DISHTASTE '= REPLACE(DISHTASTE, "ニンジン\/|鮮やか\/", "")

SIF STRCOUNT(DISHTASTE, ESCAPE("山菜/"))
	DISHTASTE '= REPLACE(DISHTASTE, "山菜\/|野趣\/", "")

DISHTASTE '= REPLACE(DISHTASTE, "野菜\/", "野菜抜き\/")

IF DISHNAME == "野菜炒め"
	DISHNAME = もやし炒め
	DISHTASTE += "質素/貧相/"
	DISHTASTE '= REPLACE(DISHTASTE, "野菜炒め\/", "もやし炒め\/")
ELSEIF DISHNAME == "おひたし"
	DISHNAME = おひたさない
	DISHTASTE += "貧相/" * 3
	DISHTASTE '= REPLACE(DISHTASTE, "おひたし\/", "おひたさない\/")
ELSEIF DISHNAME == "漬物に大盛り飯"
	DISHNAME = 大盛り飯
	DISHTASTE '= REPLACE(DISHTASTE, "漬物に大盛り飯\/", "大盛り飯\/")
ELSEIF DISHNAME == "モッツァレラチーズとトマトのサラダ"
	DISHNAME = モッツァレラチーズだけのサラダ
	DISHTASTE += "チーズ/" * 2
	DISHTASTE '= REPLACE(DISHTASTE, "モッツァレラチーズとトマトのサラダ\/", "モッツァレラチーズだけのサラダ\/")
ENDIF
DISHVEGE = 0


;特定のメニューの実行条件を満たしているかを判定
;一応、MASTER以外も渡せるようにした
@CAN_COOK(MENU_TYPE, C_ID)
#FUNCTION
#DIM  DYNAMIC C_ID      ;調理実行者のC_ID
#DIMS DYNAMIC MENU_TYPE ;判定対象となる調理メニュー
;祝日系(FESTIVAL)は判定なしなのでそこだけ注意（MAINDISHからの派生になる）
SELECTCASE MENU_TYPE
CASE "SNACK", "軽食"
	SIF MAX(ITEM:白米, ITEM:雑穀, ITEM:古米) > 0
		RETURNF 1
CASE "MAINDISH", "主食"
	SIF ABL:C_ID:料理技能 > 1 && MAX(ITEM:白米, ITEM:雑穀, ITEM:古米) > 0
		RETURNF 1
CASE "DESSERT", "デザート"
	SIF ABL:C_ID:料理技能 > 2 && MAX(ITEM:白米, ITEM:雑穀, ITEM:古米) > 0
		RETURNF 1
CASE "FORBIDDEN", "禁断の料理"
	SIF ABL:C_ID:料理技能 > 2 && ITEM:ローター > 0
		RETURNF 1
CASE "FISH", "魚料理"
	SIF ABL:C_ID:料理技能 > 1 && MAX(ITEM:魚, ITEM:うまい魚) > 0
		RETURNF 1
CASE "FROG", "カエル料理"
	SIF ABL:C_ID:料理技能 > 1 && ITEM:カエル >= 3
		RETURNF 1
CASE "ALL", "全て", ""
;DYNAMICを利用した再帰呼び出し
	SIF CAN_COOK("SNACK", C_ID)
		RETURNF 1
	SIF CAN_COOK("MAINDISH", C_ID)
		RETURNF 1
	SIF CAN_COOK("DESSERT", C_ID)
		RETURNF 1
	SIF CAN_COOK("FORBIDDEN", C_ID)
		RETURNF 1
	SIF CAN_COOK("FISH", C_ID)
		RETURNF 1
	SIF CAN_COOK("FROG", C_ID)
		RETURNF 1
ENDSELECT
RETURNF 0



;料理名表示用F関数
@EX_DISHNAME
#FUNCTIONS
#DIMS DYNAMIC EX_DISHNAME
SIF DISHASSI
	EX_DISHNAME += CALLNAME:DISHASSI + "と作った"
SIF DISHVEGE
	EX_DISHNAME += ITEMNAME:DISHVEGE + "風味の"
SIF STRCOUNT(DISHTASTE, ESCAPE("月見/"))
	EX_DISHNAME += "月見"
EX_DISHNAME += DISHNAME
IF !STRCOUNT(DISHTASTE, ESCAPE("野菜抜き/"))
ELSEIF !GROUPMATCH(DISHNAME, "もやし炒め", "おひたさない", "大盛り飯", "モッツァレラチーズだけのサラダ")
	EX_DISHNAME += "（野菜抜き）"
ENDIF
RETURNF EX_DISHNAME


;こんな料理ができたよ！を表示する関数
@SHOW_DISHDATA
PRINTFORML ＜%EX_DISHNAME()%＞
CALL SHOW_TASTE(DISHTASTE)


;味の表示用関数
;味にかぎらず使いまわせそう？
;横幅の限界に注意
@SHOW_TASTE(TASTE_STR)
#DIMS TASTE_STR               ;表示対象の味データ
#DIMS DYNAMIC TASTE_LIST, 100 ;味リスト
#DIM  DYNAMIC TASTE_CNT,  100 ;味リスト各要素の重複個数
#DIM  DYNAMIC TASTE_NUM       ;味の種類の総数
#DIM  DYNAMIC SHOW_NUM        ;表示した味の数
#DIM  TASTE_ID                ;ループ用
;味リストの取得
CALLF CNT_SPLIT(TASTE_STR, TASTE_LIST, TASTE_CNT, TASTE_NUM)
;味リストの表示
FOR TASTE_ID, 0, TASTE_NUM
	SIF !STRLENS(TASTE_LIST:TASTE_ID)
		CONTINUE
	LOCALS = [%TASTE_LIST:TASTE_ID%]Lv{TASTE_CNT:TASTE_ID, 2}
	;システム側で6文字、味名称に14文字分をわりあて
	PRINTFORM 　%LOCALS, 20%
	SHOW_NUM ++
	SIF SHOW_NUM % 5
		CONTINUE
	PRINTFORML 
NEXT
SIF SHOW_NUM % 5
	PRINTFORML 



;-------------------------------------------------
;味覚評価処理関数
;一応は文字列だけで評価できるようにはしたがTCVARも参照する
;-------------------------------------------------
@FOOD_TASTING(C_ID, TASTE_FOOD, COOK_POWER)
#DIM          C_ID              ;対象キャラ
#DIM          COOK_POWER		;キャラの調理力、または外食時の味
#DIMS         TASTE_FOOD        ;出来た料理の味
TCVAR:C_ID:料理好評価値 = TASTE_GOOD(C_ID, TASTE_FOOD, COOK_POWER)
TCVAR:C_ID:料理悪評価値 = TASTE_BAD(C_ID, TASTE_FOOD)
TCVAR:C_ID:料理総評価値 = TCVAR:C_ID:料理好評価値 * 100 / (TCVAR:C_ID:料理好評価値 + TCVAR:C_ID:料理悪評価値)

SELECTCASE TCVAR:C_ID:料理総評価値
	CASE IS >= 80
		CALL KIGEN_CHANGE(C_ID,100,1)
	CASE IS < 40
		CALL KIGEN_CHANGE(C_ID,100, -1)
ENDSELECT

;味覚評価関数GOOD
@TASTE_GOOD(C_ID, TASTE_FOOD, COOK_POWER)
#FUNCTION
#DIM          C_ID              ;対象キャラ
#DIMS         TASTE_FOOD        ;出来た料理の味
#DIMS DYNAMIC TASTE_CHARA       ;キャラの好み
#DIM  DYNAMIC TASTE_GOOD        ;いいね！と思った度合い
#DIM  COOK_POWER				;キャラの調理力、または外食時の味

TASTE_CHARA '= GET_STR(, "キャラデータ", C_ID, "料理：好きな味")
TASTE_GOOD = 30 + 10 * STR_MULTI_COUNT(TASTE_CHARA, TASTE_FOOD)
SIF STRCOUNT(TASTE_FOOD, ESCAPE("ごちそう/"))
	TASTE_GOOD += 20
SIF STRCOUNT(TASTE_FOOD, ESCAPE("メイド流/"))
	TASTE_GOOD += 5
SIF STRCOUNT(TASTE_FOOD, ESCAPE("家庭的/"))
	TASTE_GOOD += 5
;野菜嫌いは野菜抜くと喜ぶ（個々の野菜は判定しない）
SIF STRCOUNT(TASTE_FOOD, ESCAPE("野菜抜き/"))
	TASTE_GOOD += STRCOUNT(GET_STR(, "キャラデータ", C_ID, "料理：嫌いな味"), ESCAPE("野菜/"))

;満月ボーナス
SIF GROUPMATCH(NAME_MOON(DAY:3), "望月", "十五夜 -中秋の名月- ") && STRCOUNT(TASTE_FOOD, ESCAPE("月見/"))
	TASTE_GOOD += 3

SIF !STRLENS(DISHNAME)
	RETURNF MAX(TASTE_GOOD, 1)

;ここより↓は通常の料理処理専用の評価処理です
IF !DISH_RADICAL()
ELSEIF TASTE_GIVEUP(C_ID, DISH_RADICAL())
	TIMES TASTE_GOOD, 0.1
ELSE
	TIMES TASTE_GOOD, 1.5
ENDIF

TASTE_GOOD = TASTE_GOOD * COOK_POWER

;食事結果大成功の時
SIF TFLAG:193 == 1
	TIMES TASTE_GOOD, 1.5
;つくりたて
SIF DISHSPOIL < 1
	TIMES TASTE_GOOD, 1.2

;自分が作った料理は贔屓する
SIF DISHASSI == C_ID
	TIMES TASTE_GOOD, 1.1

;恋慕系ぼーなす
IF TALENT:C_ID:恋人
	TIMES TASTE_GOOD, 2.0
ELSEIF TALENT:C_ID:恋慕
	TIMES TASTE_GOOD, 1.3
ELSEIF TALENT:C_ID:思慕
	TIMES TASTE_GOOD, 1.1
ENDIF

;助手効果による追加処理
SIF STRCOUNT(TASTE_FOOD, ESCAPE("貧相/"))
		TIMES TASTE_GOOD, 0.8
SIF STRCOUNT(TASTE_FOOD, ESCAPE("瀟洒/"))
		TIMES TASTE_GOOD, 1.2
SELECTCASE GET_TALENTNAME(GETNUM(TALENT, "神霊"), TALENT:C_ID:神霊)
CASE "死神", "閻魔"
CASEELSE
	SIF TALENT:C_ID:神霊 && STRCOUNT(TASTE_FOOD, ESCAPE("霊験/"))
		TIMES TASTE_GOOD, 1.1
ENDSELECT

SELECTCASE GET_TALENTNAME(GETNUM(TALENT, "人間"), TALENT:C_ID:人間)
CASE "人間"
	SIF STR_MULTI_COUNT(TASTE_FOOD, ESCAPE("怪しい/不穏/"))
		TIMES TASTE_GOOD, 0.8
CASE "月人"
	SIF STRCOUNT(TASTE_FOOD, ESCAPE("月見/"))
		TIMES TASTE_GOOD, 1.1
ENDSELECT

SIF STRCOUNT(TASTE_FOOD, ESCAPE("パワフル/")) && GET_TALENTNAME(GETNUM(TALENT, "回復速度"), TALENT:C_ID:回復速度) == "回復遅い"
		TIMES TASTE_GOOD, 1.1
SIF STRCOUNT(TASTE_FOOD, ESCAPE("繊細/")) && STR_MULTI_COUNT(TASTE_FOOD, ESCAPE("デザート/軽食/"))
		TIMES TASTE_GOOD, 1.1
SIF STRCOUNT(TASTE_FOOD, ESCAPE("大火力/")) && STR_MULTI_COUNT(TASTE_FOOD, ESCAPE("濃厚/油/"))
		TIMES TASTE_GOOD, 1.1
SIF STRCOUNT(TASTE_FOOD, ESCAPE("絢爛/")) && STRCOUNT(TASTE_FOOD, ESCAPE("上品/"))
		TIMES TASTE_GOOD, 1.1
SIF STRCOUNT(TASTE_FOOD, ESCAPE("風流/")) && STRCOUNT(TASTE_FOOD, ESCAPE("和食/"))
		TIMES TASTE_GOOD, 1.1
SIF STRCOUNT(TASTE_FOOD, ESCAPE("実験的/")) && GET_TALENTNAME(GETNUM(TALENT, "性的興味"), TALENT:C_ID:性的興味) == "好奇心"
		TIMES TASTE_GOOD, 1.1
SIF STRCOUNT(TASTE_FOOD, ESCAPE("妖精風/")) && TALENT:C_ID:妖精
		TIMES TASTE_GOOD, 1.1
SIF STRCOUNT(TASTE_FOOD, ESCAPE("官能的/")) && (TALENT:C_ID:セフレ || TALENT:C_ID:愛欲)
		TIMES TASTE_GOOD, 1.1

SIF STRCOUNT(TASTE_FOOD, ESCAPE("豊穣/")) && STR_MULTI_COUNT(TASTE_FOOD, ESCAPE("野菜/お米/"))
		TIMES TASTE_GOOD, 1.2
SIF STRCOUNT(TASTE_FOOD, ESCAPE("琉球風/")) && STR_MULTI_COUNT(TASTE_FOOD, ESCAPE("濃厚/油/揚げ物/"))
		TIMES TASTE_GOOD, 1.2
SIF STRCOUNT(TASTE_FOOD, ESCAPE("華やか/")) && STRCOUNT(TASTE_FOOD, ESCAPE("鮮やか/"))
		TIMES TASTE_GOOD, 1.5
SIF STRCOUNT(TASTE_FOOD, ESCAPE("カリスマ/")) && STRCOUNT(TASTE_FOOD, ESCAPE("紅い/"))
		TIMES TASTE_GOOD, 1.5
SIF STRCOUNT(TASTE_FOOD, ESCAPE("水兵風/")) && STRCOUNT(TASTE_FOOD, ESCAPE("魚/カレーライス/"))
		TIMES TASTE_GOOD, 1.5
SIF STRCOUNT(TASTE_FOOD, ESCAPE("華人小娘/")) && STRCOUNT(TASTE_FOOD, ESCAPE("中華/"))
		TIMES TASTE_GOOD, 2.0
SIF STRCOUNT(TASTE_FOOD, ESCAPE("名刀/")) && STR_MULTI_COUNT(TASTE_FOOD, ESCAPE("刺身/生け作り/スシ/"))
		TIMES TASTE_GOOD, 2.0

IF STRCOUNT(TASTE_FOOD, ESCAPE("天狗流/"))
	SIF ALCOHOL_FACE(C_ID) != "　素面　"
		TIMES TASTE_GOOD, 1.1
ENDIF
IF STRCOUNT(TASTE_FOOD, ESCAPE("鬼の隠し味/"))
	SELECTCASE ALCOHOL_FACE(C_ID)
	CASE "ほろ酔い"
		TIMES TASTE_GOOD, 1.1
	CASE "酔いどれ"
		TIMES TASTE_GOOD, 1.3
	CASE "ぐでぐで"
		TIMES TASTE_GOOD, 1.5
	ENDSELECT
ENDIF
SIF STRCOUNT(TASTE_FOOD, ESCAPE("反逆/"))
	TASTE_GOOD = TASTE_GOOD * (MARK:C_ID:反発刻印 + 1)

IF STRCOUNT(TASTE_FOOD, ESCAPE("氷結/"))
	SIF GET_MONTH() == "夏の月"
		TIMES TASTE_GOOD, 2.0
ELSEIF STRCOUNT(TASTE_FOOD, ESCAPE("春風/"))
	SIF GROUPMATCH(GET_MONTH(), "冬の月", "春の月")
		TIMES TASTE_GOOD, 1.5
ELSEIF STRCOUNT(TASTE_FOOD, ESCAPE("秋風/"))
	SIF GROUPMATCH(GET_MONTH(), "夏の月", "秋の月")
		TIMES TASTE_GOOD, 1.5
ENDIF

SIF STRCOUNT(TASTE_FOOD, ESCAPE("嫉妬/")) && (TALENT:C_ID:思慕 || TALENT:C_ID:恋慕 || TALENT:C_ID:恋人)
		TIMES TASTE_GOOD, 0.5

SIF DISHASSI > 0 && CSVRELATION(C_ID, DISHASSI)
	TASTE_GOOD = TASTE_GOOD * CSVRELATION(C_ID, DISHASSI) / 100

TASTE_GOOD = TASTE_GOOD / 10
RETURNF MAX(TASTE_GOOD, 1)

;味覚評価関数BAD
@TASTE_BAD(C_ID, TASTE_FOOD)
#FUNCTION
#DIM          C_ID              ;対象キャラ
#DIMS         TASTE_FOOD        ;出来た料理の味
#DIMS DYNAMIC TASTE_CHARA       ;キャラの好み
#DIM  DYNAMIC TASTE_BAD         ;ヤダね！と思った度合い
TASTE_CHARA '= GET_STR(, "キャラデータ", C_ID, "料理：嫌いな味")
TASTE_BAD = 30 + 15 * STR_MULTI_COUNT(TASTE_CHARA, TASTE_FOOD)

SIF !STRLENS(DISHNAME)
	RETURNF MAX(TASTE_BAD, 1)

;精度確保用
TASTE_BAD = TASTE_BAD * 10

IF !DISH_RADICAL()
ELSEIF TASTE_GIVEUP(C_ID, DISH_RADICAL())
	TIMES TASTE_BAD, 3.0
ELSE
	TIMES TASTE_BAD, 0.8
ENDIF

;食事結果大失敗の時
SIF TFLAG:193 == -1
	TIMES TASTE_BAD, 1.5
;調理経過時間
SELECTCASE DISHSPOIL
CASE 1
	TIMES TASTE_BAD, 1.2
CASE 2
	TIMES TASTE_BAD, 1.8
ENDSELECT

SIF DISHASSI == C_ID
	TIMES TASTE_BAD, 0.9

;助手効果による追加処理
SIF STRCOUNT(TASTE_FOOD, ESCAPE("貧相/"))
		TIMES TASTE_BAD, 1.2
SIF GET_TALENTNAME(GETNUM(TALENT, "人間"), TALENT:C_ID:人間) == "人間" && STR_MULTI_COUNT(TASTE_FOOD, ESCAPE("怪しい/不穏/"))
		TIMES TASTE_BAD, 1.1
SIF STRCOUNT(TASTE_FOOD, ESCAPE("気配り/"))
		TIMES TASTE_BAD, 0.9
SIF STRCOUNT(TASTE_FOOD, ESCAPE("メイド流/"))
		TIMES TASTE_BAD, 0.9
SIF STRCOUNT(TASTE_FOOD, ESCAPE("家庭的/"))
		TIMES TASTE_BAD, 0.9
SIF STRCOUNT(TASTE_FOOD, ESCAPE("幸運/"))
		TIMES TASTE_BAD, 0.8

SIF DISHASSI > 0 && CSVRELATION(C_ID, DISHASSI)
	TASTE_BAD = TASTE_BAD * 100 / CSVRELATION(C_ID, DISHASSI)

TASTE_BAD = TASTE_BAD / 10

RETURNF MAX(TASTE_BAD, 1)


;-------------------------------------------------
;イタズラ関連判定
;-------------------------------------------------
;イタズラによる食事の中断処理
;一応判定BIT返してるがあんま意味はなかろう
@TASTE_GIVEUP(C_ID, TASTE_TYPE)
#FUNCTION
#DIM         C_ID			;判定対象キャラ
#DIM         TASTE_TYPE		;イタズラのタイプ
SELECTCASE TASTE_TYPE
CASE DISH_RADICAL_激辛
	SIF GETBIT(CFLAG:C_ID:味覚嗜好, DISH_RADICAL_激辛)
		RETURNF 0
	SIF STRCOUNT(GET_STR(, "キャラデータ", C_ID, "料理：好きな味"), ESCAPE("スパイス/"))
		RETURNF 0
	RETURNF DISH_RADICAL_激辛
CASE DISH_RADICAL_激甘
	SIF GETBIT(CFLAG:C_ID:味覚嗜好, DISH_RADICAL_激甘)
		RETURNF 0
	SIF STRCOUNT(GET_STR(, "キャラデータ", C_ID, "料理：好きな味"), ESCAPE("甘い/"))
		RETURNF 0
	RETURNF DISH_RADICAL_激甘
CASE DISH_RADICAL_わさび
	SIF GETBIT(CFLAG:C_ID:味覚嗜好, DISH_RADICAL_わさび)
		RETURNF 0
	SIF STRCOUNT(GET_STR(, "キャラデータ", C_ID, "料理：好きな味"), ESCAPE("スパイス/"))
		RETURNF 0
	RETURNF DISH_RADICAL_わさび
CASE DISH_RADICAL_レモン
	SIF GETBIT(CFLAG:C_ID:味覚嗜好, DISH_RADICAL_レモン)
		RETURNF 0
	RETURNF DISH_RADICAL_レモン
ENDSELECT

;料理にイタズラが施されているか否か
;濃い/薄いの味付けは除外
@DISH_MIXTURE()
#FUNCTION
LOCAL = DISH_RADICAL()
SIF LOCAL
	RETURNF LOCAL
LOCAL = DISH_DRAG()
	RETURNF LOCAL
RETURNF

;イタズラにより過激な味のする料理である
@DISH_RADICAL()
#FUNCTION
SIF GETBIT(TCVAR:MASTER:308, DISH_RADICAL_激辛)
	RETURNF DISH_RADICAL_激辛
SIF GETBIT(TCVAR:MASTER:308, DISH_RADICAL_激甘)
	RETURNF DISH_RADICAL_激甘
SIF GETBIT(TCVAR:MASTER:308, DISH_RADICAL_わさび)
	RETURNF DISH_RADICAL_わさび
SIF GETBIT(TCVAR:MASTER:308, DISH_RADICAL_レモン)
	RETURNF DISH_RADICAL_レモン
RETURNF

;薬物の仕込まれた料理である
@DISH_DRAG()
#FUNCTION
SIF GETBIT(TCVAR:MASTER:308, DISH_DRAG_媚薬)
	RETURNF DISH_DRAG_媚薬
SIF GETBIT(TCVAR:MASTER:308, DISH_DRAG_利尿剤)
	RETURNF DISH_DRAG_利尿剤
SIF GETBIT(TCVAR:MASTER:308, DISH_DRAG_惚れ薬)
	RETURNF DISH_DRAG_惚れ薬
SIF GETBIT(TCVAR:MASTER:308, DISH_DRAG_睡眠薬)
	RETURNF DISH_DRAG_睡眠薬
SIF GETBIT(TCVAR:MASTER:308, DISH_DRAG_排卵誘発剤)
	RETURNF DISH_DRAG_排卵誘発剤
RETURNF



;-------------------------------------------------
;ここから料理の味情報の記述
;-------------------------------------------------
;GET_TASTE系は文字列を渡して文字列が返ってくる形式
;理由としては実際に料理を作る場面以外でも使えるようにするため
;戻り値は"HOGE/"で必ず末尾に"/"を入れること（後からの追加に対応できるようにする）
;-------------------------------------------------
;料理の種類
@GET_TASTE_MENUTYPE(ARGS)
#FUNCTIONS
SELECTCASE ARGS
CASE "SNACK"
	RETURNF "軽食/"
CASE "MAINDISH"
	RETURNF "主食/"
CASE "DESSERT"
	RETURNF "デザート/"
CASE "FORBIDDEN"
	RETURNF "謎料理/"
	;"禁断"は食材側で付与してます
CASE "FISH"
	RETURNF "魚料理/"
CASE "FROG"
	RETURNF "カエル料理/"
CASE "FESTIVAL"
	RETURNF "ごちそう/"
ENDSELECT


;メイン食材
@GET_TASTE_INGREDIENT(ARGS)
#FUNCTIONS
SELECTCASE ARGS
CASE "雑穀"
	RETURNF "雑穀/野趣/"
CASE "古米"
	RETURNF "古米/質素/"
CASE "白米"
	RETURNF "白米/上品/"
CASE "カエル"
	RETURNF "野趣/"
CASE "魚"
	RETURNF "魚/質素/"
CASE "うまい魚"
	RETURNF "うまい魚/上品/"
CASE "ローター"
	RETURNF "禁断/"
ENDSELECT


;追加食材＝野菜
;追加食材は呼び出し元で食材名自体を付与
@GET_TASTE_VEGE(ARGS)
#FUNCTIONS
SELECTCASE ARGS
CASE "キャベツ"
	RETURNF "野菜/さっぱり/"
CASE "イチゴ"
	RETURNF "果物/かわいい/紅い/鮮やか/"
CASE "カブ"
	RETURNF "野菜/なめらか/"

CASE "キュウリ"
	RETURNF "野菜/水気/"
CASE "トマト"
	RETURNF "野菜/紅い/鮮やか/"
CASE "スイカ"
	RETURNF "果物/水気/甘い/紅い/"

CASE "ナス"
	RETURNF "野菜/さっぱり/"
CASE "サツマイモ"
	RETURNF "野菜/ボリューム感/"
CASE "ブドウ"
	RETURNF "果物/かわいい/甘い/"

CASE "ユリネ"
	RETURNF "野菜/上品/"
CASE "ニンジン"
	RETURNF "野菜/鮮やか/"
CASE "ネギ"
	RETURNF "野菜/スパイス/"

CASE "山菜"
	RETURNF "野菜/野趣/"
ENDSELECT


;各料理の味
;各料理は呼び出し元で料理名自体を付与
@GET_TASTE_DISH(ARGS)
#FUNCTIONS
SELECTCASE ARGS
;主食
CASE "雑炊"
	RETURNF "和食/お米/さっぱり/"
CASE "卵かけご飯"
	RETURNF "和食/お米/鶏卵/さっぱり/なめらか/シンプル/"
CASE "野菜炒め"
	RETURNF "野菜/シンプル/"
CASE "味噌汁"
	RETURNF "和食/汁物/"
CASE "おひたし"
	RETURNF "和食/野菜/さっぱり/"
CASE "うどん"
	RETURNF "和食/麺類/なめらか/"
CASE "素麺"
	RETURNF "和食/麺類/さっぱり/なめらか/"
CASE "蕎麦"
	RETURNF "和食/麺類/野趣/さっぱり/なめらか/"
CASE "ヲムレツ"
	RETURNF "洋食/鶏卵/ボリューム感/"
CASE "シチュー"
	RETURNF "洋食/汁物/ボリューム感/濃厚/"
CASE "ハンバーグ"
	RETURNF "洋食/肉料理/ボリューム感/濃厚/"
CASE "グラタン"
	RETURNF "洋食/チーズ/ボリューム感/濃厚/"
CASE "カレーライス"
	RETURNF "洋食/お米/ボリューム感/スパイス/濃厚/"
CASE "しゃぶしゃぶ"
	RETURNF "和食/肉料理/さっぱり/"
CASE "ラーメン"
	RETURNF "中華/麺類/なめらか/"
CASE "生姜焼き"
	RETURNF "和食/肉料理/ボリューム感/油/"
CASE "ステーキ"
	RETURNF "洋食/肉料理/野趣/ボリューム感/油/"
CASE "油淋鶏"
	RETURNF "中華/肉料理/鶏肉/ボリューム感/揚げ物/油/サクサク/"
CASE "モッツァレラチーズとトマトのサラダ"
	RETURNF "洋食/野菜/チーズ/トマト/さっぱり/鮮やか/紅い/"
CASE "ドーピングコンソメスープ"
	RETURNF "洋食/謎料理/汁物/禁断/スパイス/"
CASE "義大莉而鳥苗鳥肉"
	RETURNF "中華/肉料理/鶏肉/虫肉/ボリューム感/揚げ物/油/サクサク/鮮やか/"
CASE "ギンギー料理"
	RETURNF "謎料理/ボリューム感/シンプル/"
CASE "天麩羅"
	RETURNF "和食/揚げ物/油/サクサク/"
CASE "やまもりごはん"
	RETURNF "和食/お米/ボリューム感/シンプル/"
CASE "漬物に大盛り飯"
	RETURNF "和食/お米/野菜/質素/さっぱり/ボリューム感/シンプル/"


;甘味
CASE "プリン"
	RETURNF "洋食/上品/鶏卵/ぷるぷる/なめらか/"
CASE "ケーキ"
	RETURNF "洋食/上品/生地/鶏卵/ボリューム感/鮮やか/"
CASE "ゼリー"
	RETURNF "洋食/ぷるぷる/さっぱり/鮮やか/水気/"
CASE "タルト"
	RETURNF "洋食/生地/鶏卵/かわいい/"
CASE "クッキー","マカロン"
	RETURNF "洋食/生地/鶏卵/サクサク/かわいい/"
CASE "パイ"
	RETURNF "洋食/生地/サクサク/"
CASE "羊羹"
	RETURNF "和食/あんこ/ボリューム感/なめらか/"
CASE "大福"
	RETURNF "和食/あんこ/ボリューム感/もちもち/"
CASE "カステラ"
	RETURNF "洋食/上品/生地/鶏卵/"
CASE "団子"
	SIF GROUPMATCH(NAME_MOON(DAY:3), "望月", "十五夜 -中秋の名月- ")
		RETURNF "和食/もちもち/月見/"
	RETURNF "和食/もちもち/"
CASE "フルーツ盛り合わせ"
	RETURNF "果物/さっぱり/鮮やか/シンプル/"
CASE "パウンドケーキ"
	RETURNF "洋食/生地/鶏卵/"
CASE "あんみつ"
	RETURNF "和食/あんこ/水気/なめらか/"
CASE "やしょうま"
	RETURNF "和食/もちもち/シンプル/"
CASE "タピオカミルクティー"
	RETURNF "なめらか/もちもち/かわいい/水気/"


;軽食
CASE "サンドイッチ"
	RETURNF "洋食/生地/"
CASE "おにぎり"
	RETURNF "和食/シンプル/"
CASE "ハンバーガー"
	RETURNF "洋食/野趣/生地/ボリューム感/"
CASE "スコーン"
	RETURNF "洋食/上品/生地/"
CASE "シウマイ"
	RETURNF "中華/肉料理/"
CASE "肉まん"
	RETURNF "中華/生地/肉料理/濃厚/もちもち/"
CASE "フィッシュ＆チップス"
	RETURNF "魚料理/揚げ物/油/"
CASE "タコス"
	RETURNF "洋食/野趣/生地/濃厚/"
CASE "たこ焼き"
	RETURNF "和食/生地/"

;禁断
CASE "揚げローター"
	RETURNF "揚げ物/油/"
CASE "Eマッサージャ風ローターの湯引き"
	RETURNF "さっぱり/"
CASE "クリキャップ用ローターの握り"
	RETURNF "和食/さっぱり/"
CASE "オナホール仕立てのローター煮込み"
	RETURNF "和食/汁物/ボリューム感/濃厚/"

;カエル料理
CASE "カエルのから揚げ"
	RETURNF "肉料理/揚げ物/油/サクサク/"
CASE "カエルカレー"
	RETURNF "洋食/お米/肉料理/ボリューム感/スパイス/濃厚/"
CASE "カエルのソテー"
	RETURNF "洋食/肉料理/油/"
CASE "カエルの塩焼き"
	RETURNF "肉料理/シンプル/"

;魚料理
CASE "スシ"
	RETURNF "和食/上品/さっぱり/鮮やか/"
CASE "焼き魚"
	RETURNF "和食/シンプル/"
CASE "煮魚"
	RETURNF "和食/水気/"
CASE "蒲焼き"
	RETURNF "和食/濃厚/"
CASE "生け作り"
	RETURNF "和食/上品/さっぱり/シンプル/鮮やか/"
CASE "マリネ"
	RETURNF "洋食/さっぱり/鮮やか/"
CASE "ムニエル"
	RETURNF "洋食/サクサク/"
CASE "刺身"
	RETURNF "和食/上品/さっぱり/シンプル/"
CASE "照り焼き"
	RETURNF "和食/濃厚/"
CASE "竜田揚げ"
	RETURNF "和食/揚げ物/油/サクサク/"
CASE "フィッシュカレー"
	RETURNF "洋食/お米/ボリューム感/スパイス/濃厚/"
CASE "スターゲイジーパイ"
	RETURNF "謎料理/洋食/生地/サクサク/"

;ごちそう
CASE "おせち料理"
	RETURNF "和食/上品/ボリューム感/鮮やか/"
CASE "恵方巻"
	RETURNF "和食/お米/ボリューム感/"
CASE "年越し蕎麦"
	RETURNF "和食/麺類/なめらか/"
ENDSELECT



;助手で付与される味覚
@GET_TASTE_ASSIBONUS(C_ID)
#FUNCTIONS
#DIM          C_ID
#DIMS DYNAMIC TEMP_TASTE
IF C_ID == MASTER && FLAG:なりきり
	TEMP_TASTE '= GET_STR(, "キャラデータ", FLAG:なりきり, "料理：助手効果")
ELSE
	TEMP_TASTE '= GET_STR(, "キャラデータ", C_ID, "料理：助手効果")
ENDIF

SELECTCASE GET_TALENTNAME(GETNUM(TALENT, "神霊"), TALENT:C_ID:神霊)
CASE "死神", "閻魔"
	TEMP_TASTE += "冥界風/"
CASEELSE
	SIF TALENT:C_ID:神霊
		TEMP_TASTE += "霊験/"
ENDSELECT

SELECTCASE GET_TALENTNAME(GETNUM(TALENT, "人間"), TALENT:C_ID:人間)
CASE "人間"
CASE "魔法使い"
	TEMP_TASTE += "魔法/"
CASE "巫女"
	TEMP_TASTE += "霊験/"
CASE "天人"
	TEMP_TASTE += "天界風/"
CASE "仙人"
	TEMP_TASTE += "精妙/"
CASE "月人"
	TEMP_TASTE += "月都風/"
ENDSELECT

SIF GET_TALENTNAME(GETNUM(TALENT, "非生物"), TALENT:C_ID:非生物) == "霊体"
	TEMP_TASTE += "不思議/"

SIF TALENT:C_ID:妖精
	TEMP_TASTE += "妖精風/"
SIF TALENT:C_ID:鬼
	TEMP_TASTE += "鬼の隠し味/"
SIF TALENT:C_ID:天狗
	TEMP_TASTE += "天狗流/"
SIF TALENT:C_ID:氷精
	TEMP_TASTE += "氷結/"

SIF TALENT:C_ID:調合知識
	TEMP_TASTE += "ハーブ/"
SIF GET_TALENTNAME(GETNUM(TALENT, "回復速度"), TALENT:C_ID:回復速度) == "回復早い" || TALENT:C_ID:鼓舞
	TEMP_TASTE += "パワフル/"
SIF GET_TALENTNAME(GETNUM(TALENT, "習得速度"), TALENT:C_ID:習得速度) == "習得早い" || GET_TALENTNAME(GETNUM(TALENT, "回復速度"), TALENT:C_ID:回復速度) == "回復遅い"
	TEMP_TASTE += "繊細/"
SIF TALENT:C_ID:音楽知識 >= 1 || TALENT:C_ID:音感 >= 1
	TEMP_TASTE += "芸術的/"
SIF TALENT:C_ID:セフレ || TALENT:C_ID:愛欲
	TEMP_TASTE += "官能的/"
SIF TALENT:C_ID:思慕 || TALENT:C_ID:恋慕
	TEMP_TASTE += "愛情/"
SIF TALENT:C_ID:献身的
	TEMP_TASTE += "気配り/"
SIF GROUPMATCH(ALCOHOL_FACE(C_ID), "ぐでぐで", "酔いどれ")
	TEMP_TASTE += "へべれけ/"
SIF TALENT:C_ID:応答 < 0
	TEMP_TASTE += "マイルド/"
RETURNF TEMP_TASTE

@COOKING_TAKARI(ARG, 確率)
#DIM 確率

;タカらない
IF !CAN_TAKARI(ARG)
	確率 = 0
;来訪中なら100%
ELSEIF AT_HOME(ARG) && !CFLAG:ARG:神社在住
	確率 = 100
;同じ拠点なら二分の一
ELSEIF AT_HOME(ARG) && CFLAG:ARG:神社在住
	確率 = 確率 * 1 / 2
ENDIF

;確率
SIF 確率 < RAND:100
	RETURN

CFLAG:ARG:拠点来訪 = 1
CFLAG:ARG:現在位置 = CFLAG:MASTER:現在位置
TCVAR:ARG:なるべく動かない = 1
PRINTFORMW 料理の気配を嗅ぎつけて%CALLNAME:ARG%がやってきた……

@CAN_TAKARI(ARG)
#FUNCTION
;活動時間-1h外
SIF !BETWEENTIME(CFLAG:ARG:来訪時間, CFLAG:ARG:帰宅時間 - 60)
	RETURNF 0
;MASTERが外
SIF !AT_HOME(MASTER)
	RETURNF 0
;お招き部屋
SIF CFLAG:MASTER:現在位置 == OMANEKIBEYA()
	RETURNF 0
;MASTERと同じ場所
SIF CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置
	RETURNF 0
;寝てる
SIF CFLAG:ARG:睡眠
	RETURNF 0
;仕事中
SIF CFLAG:ARG:行動 == 5
	RETURNF 0
RETURNF 1

;-------------------------------------------------
;アイテム画面
;-------------------------------------------------
@COM490
#DIM 回復前体力１
#DIM 回復前気力１
#DIM 回復前精力１
#DIM 回復後体力１
#DIM 回復後気力１
#DIM 回復後精力１
#DIM 回復前体力２
#DIM 回復前気力２
#DIM 回復前精力２
#DIM 回復後体力２
#DIM 回復後気力２
#DIM 回復後精力２
#DIM 対象
#DIM 全員
#DIM CLIENT_ID
#DIM 虚無への回帰
#DIM 使用アイテム
回復前体力１ = BASE:MASTER:体力
回復前気力１ = BASE:MASTER:気力
回復前精力１ = BASE:MASTER:精力
回復前体力２ = BASE:TARGET:体力
回復前気力２ = BASE:TARGET:気力
回復前精力２ = BASE:TARGET:精力
VARSET LOCAL
VARSET LOCALS
対象 = MASTER
虚無への回帰 = 0
PRINTL 何を使いますか？
$LOOP
DRAWLINE
FOR LOCAL,120, 699
	IF 対象
		IF 使用可能アイテム(LOCAL) == 2 && ITEM:LOCAL
			PRINTFORM [{LOCAL}] %ITEMNAME:LOCAL, 18, LEFT%
			SIF LOCAL > 99	;後々のことを考えて仕込み
				PRINT  
			SIF ITEM:LOCAL < 10
				PRINT  
			PRINTFORM 　{ITEM:LOCAL}個　
			SETCOLOR C_L_GRAY
				PRINTFORML %ITEM_EXPLANATION(LOCAL)%
			RESETCOLOR
		ENDIF
	ELSE
		IF 使用可能アイテム(LOCAL) && ITEM:LOCAL
			PRINTFORM [{LOCAL}] %ITEMNAME:LOCAL, 18, LEFT%
			SIF LOCAL > 99
				PRINT   
			SIF ITEM:LOCAL < 10
				PRINT  
			PRINTFORM 　{ITEM:LOCAL}個　
			SETCOLOR C_L_GRAY
				PRINTFORML %ITEM_EXPLANATION(LOCAL)%
			RESETCOLOR
		ENDIF
	ENDIF
NEXT
PRINTL
DRAWLINE
PRINTFORML [900] コレクションのスキマ自動仕様切り替え（現在\@ FLAG:Cスキマ自動仕様 ? ON# OFF\@）
IF TARGET && SHIRAHU(TARGET)
	SIF 対象
		SETCOLOR C_YELLOW
	PRINTFORMLC [998] %CALLNAME:TARGET%に使う
	RESETCOLOR
ENDIF

PRINTLC [999] やめる
INPUT
使用アイテム = RESULT
IF DRUG(使用アイテム,対象)
	PRINTL 今日はもうやめておこう
	GOTO LOOP
ENDIF
IF ITEM_SWEETS(使用アイテム) && !TIME_PROGRESS(TCVAR:対象:デザート空腹時刻)
	PRINTL まだ食べたくない
	GOTO LOOP
ENDIF
IF RESULT == 900
	FLAG:Cスキマ自動仕様 = !FLAG:Cスキマ自動仕様
	PRINTFORMW 自動仕様を\@ FLAG:Cスキマ自動仕様 ? ON# OFF\@にしました
	GOTO LOOP
ENDIF
SELECTCASE 使用アイテム
	CASE 120
		IF 対象 == MASTER
			PRINTFORML %ITEMNAME:使用アイテム%を食べた
			PRINTL ……チープな味がした
			CALL RECOVER(MASTER,50, "体力", "チョコによる回復")
			TCVAR:対象:デザート空腹時刻 = 1440 * DAY + TIME + 180
			LOCAL:1 ++
		ENDIF
	CASE 121
		IF 対象 == MASTER
			PRINTFORML %ITEMNAME:使用アイテム%を食べた
			CALL RECOVER(MASTER,300, "体力", "チョコによる回復")
			CALL RECOVER(MASTER,300, "気力", "チョコによる回復")
			TCVAR:対象:デザート空腹時刻 = 1440 * DAY + TIME + 180
			LOCAL:1 ++
		ENDIF
	CASE 122
		IF 対象 == MASTER
			PRINTFORML %ITEMNAME:使用アイテム%を食べた
			PRINTFORML うまい！
			CALL RECOVER(MASTER,500, "体力", "チョコによる回復")
			CALL RECOVER(MASTER,500, "気力", "チョコによる回復")
			TCVAR:対象:デザート空腹時刻 = 1440 * DAY + TIME + 180
			LOCAL:1 ++
		ENDIF
	CASE 123
		IF 対象 == MASTER
			PRINTFORML %ITEMNAME:使用アイテム%を食べた
			PRINTFORML 愛を感じる！
			CALL RECOVER(MASTER,1000, "体力", "チョコによる回復")
			CALL RECOVER(MASTER,1000, "気力", "チョコによる回復")
			TCVAR:対象:デザート空腹時刻 = 1440 * DAY + TIME + 180
			LOCAL:1 ++
		ENDIF
	CASE 212
			PRINTFORML %ITEMNAME:使用アイテム%を食べた
			LOCAL:1 ++
			CALL RECOVER(MASTER,100, "体力", "かしわもちによる回復")
			TCVAR:対象:デザート空腹時刻 = 1440 * DAY + TIME + 180
	CASE 213
		SIF TARGET && SHIRAHU(TARGET)
			PRINT 二人並んで
			PRINTDATA
				DATAFORM 『アイス』
				DATAFORM 『冷凍ミカン』
				DATAFORM 『柄が二つ付いたソーダアイス』
				DATAFORM 『二つで一組のたまごアイス』
				DATAFORM 『チューペット』
				DATAFORM 『パピコ』
				DATAFORM 『練乳の詰まったミルクバー』
				DATAFORM 『アズキバー』
				DATAFORM 『スイカバー』
				DATAFORM 『ガツン、とみかん』
				DATAFORM 『ブラックサンダーアイス』
				DATAFORM 『雪見大福』
			ENDDATA
		PRINTFORML を食べた
			CALL RECOVER(MASTER,150, "気力", "アイスによる回復")
			TCVAR:MASTER:デザート空腹時刻 = 1440 * DAY + TIME + 180
			IF  TARGET && SHIRAHU(TARGET)
				CALL RECOVER(TARGET,150, "気力", "アイスによる回復")
				TCVAR:デザート空腹時刻 = 1440 * DAY + TIME + 180
			ENDIF
		LOCAL:1 ++
	CASE 215
		PRINTFORML %ITEMNAME:使用アイテム%を使った
		SETBIT TCVAR:MASTER:特別なお茶, 0
		LOCAL:1 ++
	CASE 352
		PRINTFORML %ITEMNAME:使用アイテム%を食べた
		CALL RECOVER(対象,50, "気力", "イチゴによる回復")
		SIF 対象 == (NO:9)
			CALL RECOVER(対象,50, "気力", "イチゴによる回復")
		LOCAL:1 ++
	CASE 364
		IF FLAG:70 == 1
			PRINTFORML %ITEMNAME:使用アイテム%を食べた
			CALL RECOVER(MASTER,100, "気力", "スイカによる回復")
		ELSE
			PRINTFORML 皆で%ITEMNAME:使用アイテム%を食べた
			FOR 全員, 0, CHARANUM
				SIF CFLAG:全員:現在位置 != CFLAG:MASTER:現在位置
					CONTINUE
				SIF !SHIRAHU(全員)
					CONTINUE
				CALL RECOVER(全員,100, "気力", "スイカによる回復")
			NEXT
		ENDIF
		LOCAL:1 ++
	;CASE 400
	;	PRINTFORML %CALLNAME:MASTER%は%ITEMNAME:使用アイテム%を開いた
	;	CALL COM401
	;	LOCAL:1 ++
	CASE 500
		PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
		PRINTL マズイ！もう一杯！
		CALL RECOVER(対象,100, "体力", "青汁による回復")
		CALL RECOVER(対象,200, "気力", "青汁による回復")
		LOCAL:1 ++
	CASE 501
			PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
			CALL RECOVER(対象,200, "体力", "栄養ドリンクによる回復")
			CALL RECOVER(対象,100, "気力", "栄養ドリンクによる回復")
			TCVAR:対象:お薬 = 1
			LOCAL:1 ++
	CASE 502
			PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
			SIF GETBIT(TALENT:対象:性別, 1)
				CALL RECOVER(対象,300, "精力", "精力剤による回復")
			TCVAR:対象:お薬 = 1
			LOCAL:1 ++
	CASE 503
			PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
			PRINTL ふぁいといっぱつ、まんたーん！
			CALL RECOVER(対象,500, "体力", "ギンギンＺによる回復")
			CALL RECOVER(対象,300, "気力", "ギンギンＺによる回復")
			CFLAG:対象:385 = 60
			TCVAR:対象:お薬 = 1
			LOCAL:1 ++
	;CASE 504
	;		IF FLAG:70
	;			PRINTFORML 時間停止を解除してから使おう
	;			GOTO LOOP
	;		ENDIF
	;		IF CFLAG:MASTER:現在位置 != CFLAG:MASTER:初期位置
	;			PRINTFORML これは自室で飲もう
	;			GOTO LOOP
	;		ENDIF
	;		PRINTFORML ストロング虚無を口にした途端、虚無の力で思考はぼやけ、すべてがどうでもよくなってきた
	;		PRINTFORML %CALLNAME:MASTER%は缶を空けながら一日を無為に過ごした…
	;		虚無への回帰 = 1
	CASE 505	;ギンギラギンＸ
			PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
			PRINTL ２４時間戦えそうな気がする…！
			CALL RECOVER(対象,800, "体力", "ギンギラギンＸによる回復")
			CALL RECOVER(対象,600, "気力", "ギンギラギンＸによる回復")
			CFLAG:対象:眠気覚まし = 180
			CFLAG:対象:徹夜 = 1
			TCVAR:対象:お薬 = 1
			LOCAL:1 ++
	CASE 506
			PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
			PRINTL おまえの×××で天を衝け！
			CALL RECOVER(対象,200, "体力", "精力剤『天元凸破』による回復")
			CALL RECOVER(対象,200, "気力", "精力剤『天元凸破』による回復")
			CALL RECOVER(対象,600, "精力", "精力剤『天元凸破』による回復")
			TCVAR:対象:お薬 = 1
			LOCAL:1 ++
	CASE 507
			PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
			CALL RECOVER(対象,150, "気力", "ポーションによる回復")
			CALL RECOVER(対象,150, "TSP", "ポーションによる回復")
			TCVAR:対象:お薬 = 1
			LOCAL:1 ++
	CASE 508
			PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
			PRINTL 魔力がみなぎってくる…
			CALL RECOVER(対象,300, "気力", "マジックポーションによる回復")
			CALL RECOVER(対象,600, "TSP", "マジックポーションによる回復")
			TCVAR:対象:お薬 = 1
			LOCAL:1 ++
	CASE 509
			PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
			CALL RECOVER(対象,200, "気力", "破眠ポーションによる回復")
			CFLAG:対象:眠気覚まし = 240
			CFLAG:対象:徹夜 = 1
			TCVAR:対象:お薬 = 1
			LOCAL:1 ++
	CASE 510
			PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
			PRINTL 肝機能が高まり、酔いにくくなった
			TFLAG:酔い止め = 1
			LOCAL:1 ++
	CASE 511	;エリクサー
			PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
			PRINTL 疲れが彼方へと吹き飛んだ…！
			CALL RECOVER(対象, MAXBASE:対象:体力, "体力", "エリクサーによる回復")
			CALL RECOVER(対象,9999, "気力", "エリクサーによる回復")
			SIF 対象 == MASTER
				CALL RECOVER(対象,9999, "TSP", "エリクサーによる回復")
			CALL RECOVER(対象,9999, "精力", "エリクサーによる回復")
			CFLAG:対象:眠気覚まし = 300
			TCVAR:対象:お薬 = 1
			LOCAL:1 ++
	CASE 512	;仙丹
			PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
			PRINTL 尋常でない力がみなぎる……！
			CALL BUFF_BASE(対象, BASE_体力, MAX(MAXBASE:対象:体力 / 5, 500))
			CALL BUFF_BASE(対象, BASE_気力, MAX(MAXBASE:対象:気力 / 5, 500))
			CALL BUFF_BASE(対象, BASE_精力, MAX(MAXBASE:対象:精力 / 5, 500))
			LOCAL:1 ++
	CASE 520
			IF !TFLAG:キスマーク
				PRINTFORML 今使っても意味がない
				GOTO LOOP
			ELSE
				PRINTFORML %CALLNAME:MASTER%は%ITEMNAME:使用アイテム%を巻いてキスマークを隠した
				TFLAG:包帯 = 1
				LOCAL:1 ++
			ENDIF
	CASE 521
			IF FLAG:70
				PRINTFORML 時間停止を解除してから使おう
				GOTO LOOP
			ENDIF
			PRINTFORML %CALLNAME:MASTER%は易書を開いた
			PRINTFORML 易者が出てきた
			PRINTFORMW 
			IF CFLAG:1:現在位置 == CFLAG:MASTER:現在位置
				PRINTFORML すかさず霊夢は易者の頭目掛けてお祓い棒を振り下ろした
				PRINTFORML 哀れ易者は２つに割られてしまった
				FOR LOCAL,1,CHARANUM
					IF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
						BASE:LOCAL:理性 = MAXBASE:LOCAL:理性
						BASE:LOCAL:ムード = 0
					ENDIF
				NEXT
				LOCAL:1 ++
			ELSE
				PRINTFORML 「占術を通じて進むべき道を見てやろう」
				$EKISYA
				CALL ASK_M("今日の運勢", !GETBIT(FLAG:999, 1), "困っている人を探す", 1, "爪切り", BASE:MASTER:TSP >= 20, "ミラダの石の解呪(100カリスマ)", MONEY:2 >= 100, "サムズアップ", 1, "供物にする", 1)
				SELECTCASE RESULT
					CASE 0
						SELECTCASE RAND:10
							CASE 0
								TFLAG:幸運補正 = 3
								PRINTFORML 「絶好調だ、くじでも引きに行ったらどうだ？」
							CASE 1 TO 3
								TFLAG:幸運補正 = 1
								PRINTFORML 「良好だ、溜まっていた作業があればやるといい」
							CASE 4,5
								PRINTFORML 「よくもなし悪くもなし」
							CASE 6 TO 8
								TFLAG:幸運補正 = -1
								PRINTFORML 「低調だ、無理はするな」
							CASE 9
								TFLAG:幸運補正 = -3
								PRINTFORML 「……てんで駄目だな、大人しくしていろ」
						ENDSELECT
					CASE 1
						CLIENT_ID = SEARCH_UNIQUE_COM_人助け()
						IF !CLIENT_ID
							PRINTFORML 「うーむ、特に何も見えんな」
							PRINTFORML 「強いて言えば私だ、巫女に狙われている」
							PRINTFORML それはどうにもならなさそうだ
						ELSE
							PRINTFORML 「%CALLNAME:CLIENT_ID%が手助けを求めているようだぞ」
						ENDIF
					CASE 2
						PRINTFORMW %CALLNAME:MASTER%は易者を見ていると苛ついてきた
						PRINTFORMW ……なんだその爪は。
						PRINTFORML 人間やめましたアピールなのか知らないが、いくらなんでも伸ばしすぎだ。
						PRINTFORML 日常生活で不便じゃないのか？他人をうっかり引っ掻いちゃったらどうしようとは思わないのか？
						PRINTFORML 自分はいつデリケートな部分に触ってもいいように毎日チェックしているぞ。
						PRINTFORML それ以前に何かに引っ掛けて生爪剥がすことになったら痛いじゃないか。
						PRINTFORMW 第一汚れが溜まって不潔だ。
						PRINTFORMW 我慢できなくなった%CALLNAME:MASTER%は時間を止めて易者の爪を丁寧に切り揃えた
						PRINTFORMW %CALLNAME:MASTER%は大きな達成感を味わった……
						PRINTFORML 一方突然爪が短くなった易者は困惑している……
						CALL RECOVER_PERMIL(対象,40,"気力")
						BASE:MASTER:TSP -= 20
					CASE 3
						PRINTFORML 誰のを解呪する？
						FOR LOCAL,1,CHARANUM
							SIF CFLAG:LOCAL:体目当て
								PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
						NEXT
						PRINTFORML 
						DRAWLINE
						PRINTFORML [999] やめる
						INPUT
						IF RESULT == 999
							GOTO EKISYA
						ELSEIF CFLAG:RESULT:体目当て
							PRINTFORML 「こういう道具はよくないと思うぞ」
							PRINTFORMW 易者が何やら複雑な儀式を執り行うと、ミラダの石は徐々に光を失い崩れ去った
							TIME += 25
							CFLAG:RESULT:体目当て = 0
							MONEY:2 -= 100
						ENDIF
					CASE 4
						PRINTFORML %CALLNAME:MASTER%は易者と視線を交わし無言でサムズアップをした
						PRINTFORML 易者はしばし躊躇った後、力強いサムズアップを返してきた
						PRINTFORMW ふたりは友情を育んだ
						GOTO EKISYA
					CASE 5
						PRINTFORML %CALLNAME:MASTER%は時間を止め易者の服を脱がせ、その下着を神への供物とした
						CALL BUFF_BASE(MASTER,BASE_TSP,100,1)
						PRINTFORMW 知らぬ間に下着を脱がされた易者は落ち着かなさそうだ
				ENDSELECT
				PRINTFORMW 
				DRAWLINE
				$EKISYA_DIVIDE
				PRINTFORML 霊夢が現れ、易者の頭目掛けてお祓い棒を振り下ろした
				PRINTFORML 哀れ易者は２つに割られてしまった
				PRINTFORMW 霊夢は去っていった
				FOR LOCAL,1,CHARANUM
					IF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
						BASE:LOCAL:理性 = MAXBASE:LOCAL:理性
						BASE:LOCAL:ムード = 0
					ENDIF
				NEXT
				LOCAL:1 ++
			ENDIF
	CASE 530
		CALL SET_MIRADA
		IF RESULT == -1
			RETURN -1
		ELSE
			LOCAL:1 ++
		ENDIF
	CASE 550
		IF !TIME_PROGRESS(TCVAR:対象:空腹時刻)
			PRINTFORML %CALLNAME:対象%はまだ空腹になってない
			GOTO LOOP
		ELSE
			PRINTFORML %ITEMNAME:使用アイテム%を食べた
			CALL 満腹度上昇(対象,"軽食")
			CALL RECOVER_PERMIL(対象,40,"体力")
			CALL RECOVER_PERMIL(対象,80,"気力")
			CALL RECOVER_PERMIL(対象,50,"精力")
			LOCAL:1 ++
		ENDIF
	CASE 626	;キイチゴ
		PRINTFORML %ITEMNAME:使用アイテム%を食べた
		CALL RECOVER(対象,50, "気力", "キイチゴによる回復")
		LOCAL:1 ++
	CASE 628	;アケビ
		PRINTFORML %ITEMNAME:使用アイテム%を食べた
		CALL RECOVER(対象,80, "体力", "アケビによる回復")
		CALL RECOVER(対象,80, "気力", "アケビによる回復")
		LOCAL:1 ++
	CASE 630	;仙桃
		PRINTFORML %ITEMNAME:使用アイテム%を食べた
		PRINTL 力がみなぎる……！
		CALL BUFF_BASE(対象, BASE_体力, MAX(MAXBASE:対象:体力 / 10, 500))
		CALL BUFF_BASE(対象, BASE_気力, MAX(MAXBASE:対象:気力 / 10, 500))
		SIF GETBIT(TALENT:対象:性別, 1)
			CALL BUFF_BASE(対象, BASE_精力, MAX(MAXBASE:対象:精力 / 10, 500))
		LOCAL:1 ++
	CASE 643	;即席ラーメン
		IF !KITCHEN(CFLAG:MASTER:現在位置) &&  CFLAG:MASTER:現在位置 != 29 &&  CFLAG:MASTER:現在位置 != 42 &&  CFLAG:MASTER:現在位置 != 長屋街
			PRINTFORML %ITEMNAME:使用アイテム%の容器から麺を取り出しそのままかじった
			PRINTFORML イマイチ…
			CALL RECOVER(対象,100, "体力", "即席ラーメンによる回復")
			CALL RECOVER(対象,100, "気力", "即席ラーメンによる回復")
		ELSE
			PRINTFORML %ITEMNAME:使用アイテム%を調理して食べた
			PRINTFORML ふぉーゆーっ！
			CALL RECOVER(対象,600, "体力", "即席ラーメンによる回復")
			CALL RECOVER(対象,500, "気力", "即席ラーメンによる回復")
			CALL 満腹度上昇(対象,"軽食")
			EXP:対象:料理経験 += 1
		ENDIF
		LOCAL:1 ++
	CASE 644	;瓶コーラ
		PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
		PRINTFORML ふぅ…
		CALL RECOVER(対象,700, "気力", "瓶コーラによる回復")
		LOCAL:1 ++
	CASE 645	;缶詰保存食
		SELECTCASE RAND:30
		CASE 0 TO 5
			PRINTFORML やきとりの缶詰を開封し食べた
			CALL RECOVER(対象,750, "体力", "缶詰保存食による回復")
			CALL RECOVER(対象,250, "気力", "缶詰保存食による回復")
			CALL 満腹度上昇(対象,"軽食")
		CASE 6 TO 11
			PRINTFORML さばの味噌煮の缶詰を開封し食べた
			CALL RECOVER(対象,400, "体力", "缶詰保存食による回復")
			CALL RECOVER(対象,300, "気力", "缶詰保存食による回復")
			CALL 満腹度上昇(対象,"軽食")
			EXP:対象:学習経験 += 5
		CASE 12 TO 15
			PRINTFORML パンの缶詰を開封し食べた
			CALL RECOVER(対象,600, "体力", "缶詰保存食による回復")
			CALL RECOVER(対象,600, "気力", "缶詰保存食による回復")
			CALL 満腹度上昇(対象,"主食")
		CASE 16 TO 19
			PRINTFORML 桃の缶詰を開封し食べた
			CALL RECOVER(対象,100, "体力", "缶詰保存食による回復")
			CALL RECOVER(対象,1000, "気力", "缶詰保存食による回復")
			CALL 満腹度上昇(対象,"デザート")
		CASE 20 TO 23
			PRINTFORML シチューの缶詰を開封し食べた
			CALL RECOVER(対象,1000, "体力", "缶詰保存食による回復")
			CALL RECOVER(対象,500, "気力", "缶詰保存食による回復")
			CALL 満腹度上昇(対象,"主食")
		CASE 24
			PRINTFORML ローターの缶詰を開封し食べた
			CALL RECOVER(対象,200, "体力", "缶詰保存食による回復")
			CALL RECOVER(対象,300, "気力", "缶詰保存食による回復")
			CALL RECOVER(対象,1000, "精力", "缶詰保存食による回復")
			CALL 満腹度上昇(対象,"軽食")
		CASE 25 
			PRINTFORML ニシンの缶詰を開封し食べた
			PRINTFORML 周囲におぞましい臭気が漂う！！
			CALL RECOVER(対象,600, "体力", "缶詰保存食による回復")
			CALL RECOVER(対象,-1000, "気力", "缶詰保存食による回復")
			CALL 満腹度上昇(対象,"軽食")
			EXP:対象:掃除経験 += 20
		CASE 26
			PRINTFORML 見ているだけで幸せな気分になれる紫色の缶の開封を試みた
			PRINTFORMW ちゅどーん
			PRINTFORML 幸福な缶は%CALLNAME:対象%を巻き込み爆発した
			CALL RECOVER(対象,-5000, "体力",)
		CASE 27
			PRINTFORML 未知の言語が記された緑色の缶を開封し謎の肉を食べた
			SETCOLOR 0 , 255, 0
			PRINTFORMW 本当に食べてしまったのか？
			RESETCOLOR 
			PRINTFORML 腹立たしい幻聴とともに%CALLNAME:対象%の意識は朦朧とした
			CALL RECOVER(対象,100, "体力", "缶詰保存食による回復")
			CALL RECOVER(対象,-5000, "気力",)
			CALL 満腹度上昇(対象,"軽食")
		CASE 28
			PRINTFORML 500と記された分厚い缶を開封した
			PRINTFORMW 入っていた一粒の赤い錠剤を%CALLNAME:対象%は摂取した
			CALL RECOVER(対象,9999, "体力", "缶詰保存食による回復")
			CALL RECOVER(対象,9999,"気力", "缶詰保存食による回復")
			CALL RECOVER(対象,9999, "精力", "缶詰保存食による回復")
			CALL RECOVER(対象,9999, "TSP", "缶詰保存食による回復")
		CASE 29
			PRINTFORML 蠢く触手で覆われた生暖かい缶を開封した
			PRINTFORMW ぎっしりと詰まった輝く肉塊を%CALLNAME:対象%は食した
			CALL RECOVER(対象,5000, "TSP", "缶詰保存食による回復")
		SIF GETBIT(TALENT:対象:性別, 1)
			PRINTFORML %CALLNAME:対象%の性器が真っ赤に燃える！
			MAXBASE:対象:精力 += 50
			CALL 満腹度上昇(対象,"軽食")
		ENDSELECT
			LOCAL:1 ++
	CASE 646
	CASE 998
		IF 対象
			対象 = MASTER
		ELSEIF SHIRAHU(TARGET) && TARGET
			対象 = TARGET
		ENDIF
		GOTO LOOP
	CASE 999
		RETURN -1
	CASEELSE
		GOTO LOOP
ENDSELECT
IF LOCAL:1
	IF !FLAG:70
		TIME += 5
	ELSE
		BASE:MASTER:TSP -= 5
	ENDIF
	ITEM:使用アイテム -= 1
	回復後体力１ = BASE:対象:体力
	回復後気力１ = BASE:対象:気力
	回復後精力１ = BASE:対象:精力
	回復後体力２ = BASE:対象:体力
	回復後気力２ = BASE:対象:気力
	回復後精力２ = BASE:対象:精力
	;@RECOVERとの二重表記を避けるために、100未満を回復量にとるもののみ、以下の表記を行う
	IF GROUPMATCH(使用アイテム, 120, 352, 626, 628)
		SETCOLOR C_GREEN
		IF !対象
			SIF ABS(回復後体力１ - 回復前体力１)
				PRINTFORML 体力＋{ABS(回復後体力１ - 回復前体力１)}（%CALLNAME:対象%）
			SIF ABS(回復後気力１ - 回復前気力１)
				PRINTFORML 気力＋{ABS(回復後気力１ - 回復前気力１)}（%CALLNAME:対象%）
			SIF ABS(回復後精力１ - 回復前精力１)
				PRINTFORML 精力＋{ABS(回復後精力１ - 回復前精力１)}（%CALLNAME:対象%）
		ELSE
			SIF ABS(回復後体力２ - 回復前体力２)
				PRINTFORML 体力＋{ABS(回復後体力２ - 回復前体力２)}（%CALLNAME:対象%）
			SIF ABS(回復後気力２ - 回復前気力２)
				PRINTFORML 気力＋{ABS(回復後気力２ - 回復前気力２)}（%CALLNAME:対象%）
			SIF ABS(回復後精力２ - 回復前精力２)
				PRINTFORML 精力＋{ABS(回復後精力２ - 回復前精力２)}（%CALLNAME:対象%）
		ENDIF
		RESETCOLOR
	ENDIF
	SIF 虚無への回帰
		CALL OYASUMI
	RETURN 0
ENDIF


@SET_MIRADA
PRINTFORML 対象を選んでください。
PRINTFORML 黄色は既に有効、ピンクは既に陥落素質がついているキャラです。
FOR LOCAL,1,CHARANUM
	;SIF TALENT:LOCAL:行きずり
	;	CONTINUE
	SIF CFLAG:LOCAL:出禁
		CONTINUE
	SIF CFLAG:LOCAL:体目当て
		SETCOLOR C_YELLOW
	SIF TALENT:LOCAL:セフレ || TALENT:LOCAL:恋慕
		SETCOLOR C_PINK
	PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
	RESETCOLOR
NEXT
PRINTFORML 
DRAWLINE
PRINTFORML [999] やめる
INPUT
SIF !INRANGE(RESULT,1,CHARANUM)
	RETURN -1
SIF CFLAG:RESULT:体目当て
	RETURN -1
SIF TALENT:RESULT:セフレ || TALENT:RESULT:恋慕
	RETURN -1
PRINTFORMW %CALLNAME:RESULT%のことを思い浮かべながらミラダの石を強く握ると、石はうっすらと光を帯びた
CFLAG:RESULT:体目当て = 1
RETURN 1

@使用可能アイテム(ARG)
;1だとMASTERのみ、2だとあなた以外にも使える
#FUNCTION
SELECTCASE ARG
	CASE 120 TO 123, 215, 364, 500, 510, 520, 521, 530
		RETURNF 1
	CASE 212, 213, 352, 501 TO 503, 505 TO 509, 511, 512, 550, 626, 628, 630, 643 TO 645
		RETURNF 2
	CASEELSE
		RETURNF 0
ENDSELECT

@DRUG(ARG,ARG:1)
#FUNCTION
SIF GROUPMATCH(ARG, 501, 502, 503, 505, 506, 507, 508, 509, 511) && TCVAR:(ARG:1):お薬
	RETURNF 1

@ITEM_SWEETS(ARG)
#FUNCTION
SIF GROUPMATCH(ARG, 120, 121, 123, 124, 212, 213)
	RETURNF 1


;アイテム
@COM_ABLE490
;移動実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(490)
	RETURN RESULT
SIF CFLAG:MASTER:うふふ
	RETURN 0
RETURN 1

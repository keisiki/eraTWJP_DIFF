;釣りをする
@COM621
#DIM 釣れたもの
#DIM 釣りパワー
#DIM 釣りパワーM
#DIM 釣りパワーT
#DIM 釣れない補正
#DIM 釣り運
#DIM CHARA
#DIM 相方が釣った
相方が釣った = 0
;参加者判定
IF FLAG:デート相手
	CHARA = FLAG:デート相手
ELSEIF TARGET && ABL:TARGET:親密 >= 5 - ABL:TARGET:釣り技能
	CHARA = TARGET
ELSE
	CHARA = 0
ENDIF
;かわいそう；；
IF CHARA == 93 || TARGET == 93
	PRINTFORMW 彼女の前で釣りをするのは酷なものだ…
	RETURN -1
ENDIF


;日替わりで-50〜+50,アイテムやお祈り等で補正をかけることも可
釣り運 = FLAG:日替わりイベント / 10 - 50
;釣りの醍醐味は待ち時間だと聞いた
FOR LOCAL, 0, 5
	REUSELASTLINE  \@ CHARA ? %CALLNAME:CHARA%と一緒に #  \@釣り中.%"." * LOCAL%
	TWAIT 150, 1
NEXT
;釣りパワー設定
釣りパワーM = 10 + ABL:MASTER:釣り技能 * 15 + 釣り運 + 釣れない補正
SIF CHARA
	釣りパワーT = 10 + ABL:CHARA:釣り技能 * 15 + 釣れない補正

;（釣りパワー）％で釣れる
IF RAND:100 < 釣りパワーM
	釣りパワー = 釣りパワーM
	$FISH
	;釣れたら補正リセット
	釣れない補正 = 0
	;釣りパワーで釣れるものが変わる
	SELECTCASE MIN(RAND:釣りパワー, 100)
	CASE IS < 5
		;木材
		釣れたもの = 610
	CASE IS < 10
		;守矢の守札
		釣れたもの = 131
	CASE IS < 20
		;ウグイ
		釣れたもの = 1
	CASE IS < 30
		;アマゴ
		釣れたもの = 2
	CASE IS < 40
		;ヤマメちゃん
		釣れたもの = 3
	CASE IS < 50
		;イワナ
		釣れたもの = 4
	CASE IS < 60
		;カエル
		釣れたもの = 706
	CASE IS < 70
		IF DAY:2 == 3
			;サケ,秋
			釣れたもの = 11
		ELSEIF DAY:2 == 4
			;ワカサギ,冬
			釣れたもの = 12
		ELSE
			;ニジマス
			釣れたもの = 13
		ENDIF
	CASE IS < 80
		;ヤツメウナギ
		釣れたもの = 14
	CASE IS < 90
		;カムルチー
		釣れたもの = 15
	CASE IS < 95
		;ローター
		釣れたもの = 21
	CASEELSE
		IF DAY:2 == 4
			;氷の鱗,冬
			釣れたもの = 609
		ELSE
			;龍の爪
			釣れたもの = 606
		ENDIF
		;ボーナス
		IF 相方が釣った
			EXP:CHARA:釣り経験 ++
		ELSE
			EXP:MASTER:釣り経験 ++
		ENDIF
	ENDSELECT
	;アイテムに追加
	SELECTCASE 釣れたもの
	CASE IS < 10
		ITEM:魚 ++
	CASE IS < 20
		ITEM:うまい魚 ++
	CASE 21
		ITEM:ローター ++
	CASEELSE
		ITEM:釣れたもの ++
	ENDSELECT
;CHARAが釣った場合
ELSEIF CHARA && RAND:100 < 釣りパワーT
	釣りパワー = 釣りパワーT
	相方が釣った = 1
	GOTO FISH
;釣れなかったら補正加算,釣れるまで蓄積していく
ELSE
	釣れない補正 += 10
	釣れたもの = 0
ENDIF

;基本の経験
EXP:MASTER:釣り経験 ++
SIF CHARA
	EXP:CHARA:釣り経験 ++
;釣った人の追加経験
IF 釣れたもの
	IF 相方が釣った
		EXP:CHARA:釣り経験 ++
	ELSE
		EXP:MASTER:釣り経験 ++
	ENDIF
ENDIF
;デート経験
IF CHK_DATENOW(CFLAG:CHARA:デート中) && CHARA > 0
	EXP:MASTER:デート経験 ++
	EXP:CHARA:デート経験 ++
ENDIF

;あなたが釣ったら体力減少
SIF 釣れたもの && !相方が釣った
	DOWNBASE:MASTER:体力 = 50
DOWNBASE:MASTER:気力 = 100

;参加者のソース,適当に設定してます,同一コマンド制限を受けないので控えめに,ただし釣れないと毎回半減
IF CHARA > 0
	;固定で獲得するソース
	SOURCE:CHARA:歓楽 = 400
	SOURCE:CHARA:情愛 = 50
	
	;ABL:従順をみる
	IF ABL:従順 <= 1
		SOURCE:CHARA:歓楽 += (ABL:従順 * 40)
		SOURCE:CHARA:情愛 += (ABL:親密 * 20)
	ELSEIF ABL:従順 <= 3
		SOURCE:CHARA:歓楽 += 200 + (ABL:従順 * 40)
		SOURCE:CHARA:情愛 += 50 + (ABL:親密 * 30)
	ELSEIF ABL:従順 <= 5
		SOURCE:CHARA:歓楽 += 500 + (ABL:従順 * 40)
		SOURCE:CHARA:情愛 += 200 + (ABL:親密 * 30)
	ELSEIF ABL:従順 <= 8
		SOURCE:CHARA:歓楽 += 750 + (ABL:従順 * 60)
		SOURCE:CHARA:情愛 += 300 + (ABL:親密 * 40)
	ELSEIF ABL:従順 <= 11
		SOURCE:CHARA:歓楽 += 1000 + (ABL:従順 * 60)
		SOURCE:CHARA:情愛 += 600 + (ABL:親密 * 40)
	ELSE
		SOURCE:CHARA:歓楽 += 1600 + (ABL:従順 * 30)
		SOURCE:CHARA:情愛 += 1000 + (ABL:親密 * 20)
	ENDIF
	SIF CHARA != FLAG:デート相手 
		SOURCE:CHARA:情愛 = 0
	
	;好感度はみない
	
	
	SOURCE:CHARA:受動 = 200 + 100 * ABL:従順
	SOURCE:CHARA:征服 = 200 + 100 * ABL:サドっ気
	
	;釣果をみる
	SELECTCASE 釣れたもの
	;釣れない
	CASE 0
		TIMES SOURCE:CHARA:歓楽 , 0.50
		TIMES SOURCE:CHARA:受動 , 0.50
		TIMES SOURCE:CHARA:征服 , 0.50
	;木材, 守矢の守札
	CASE 610, 131
		TIMES SOURCE:CHARA:歓楽 , 0.10
		TIMES SOURCE:CHARA:受動 , 0.50
		TIMES SOURCE:CHARA:征服 , 0.50
		SOURCE:CHARA:達成 = 20
	;ウグイ, アマゴ, ヤマメちゃん, イワナ
	CASE 1, 2, 3, 4
		TIMES SOURCE:CHARA:歓楽 , 1.00
		TIMES SOURCE:CHARA:受動 , 1.00
		TIMES SOURCE:CHARA:征服 , 1.00
		SOURCE:CHARA:達成 = 100
	;カエル
	CASE 706
		;チルノ
		IF CHARA == 14
			TIMES SOURCE:CHARA:歓楽 , 2.00
			TIMES SOURCE:CHARA:受動 , 2.00
			TIMES SOURCE:CHARA:征服 , 2.00
		;諏訪子
		ELSEIF CHARA == 33
			TIMES SOURCE:CHARA:歓楽 , 0.50
			TIMES SOURCE:CHARA:受動 , 0.50
			TIMES SOURCE:CHARA:征服 , 0.50
		ELSE
			TIMES SOURCE:CHARA:歓楽 , 1.25
			TIMES SOURCE:CHARA:受動 , 1.00
			TIMES SOURCE:CHARA:征服 , 1.00
		ENDIF
		SOURCE:CHARA:達成 = 150
	;サケ, ワカサギ, ニジマス, カムルチー
	CASE 11, 12, 13, 15
		TIMES SOURCE:CHARA:歓楽 , 1.50
		TIMES SOURCE:CHARA:受動 , 1.50
		TIMES SOURCE:CHARA:征服 , 1.50
		SOURCE:CHARA:達成 = 200
	;ヤツメウナギ
	CASE 14
		;みすちー
		IF CHARA == 28
			TIMES SOURCE:CHARA:歓楽 , 2.00
			TIMES SOURCE:CHARA:受動 , 2.00
			TIMES SOURCE:CHARA:征服 , 2.00
		ELSE
			TIMES SOURCE:CHARA:歓楽 , 1.50
			TIMES SOURCE:CHARA:受動 , 1.50
			TIMES SOURCE:CHARA:征服 , 1.50
		ENDIF
		SOURCE:CHARA:達成 = 250
	;ローター
	CASE 21
		SOURCE:CHARA:欲情 = 200
	;氷の鱗, 龍の爪
	CASE 609, 606
		TIMES SOURCE:CHARA:歓楽 , 2.00
		TIMES SOURCE:CHARA:受動 , 2.00
		TIMES SOURCE:CHARA:征服 , 2.00
		SOURCE:CHARA:達成 = 300
	ENDSELECT
ENDIF

;時間経過
TIME += 20

;フラグ引き渡し
TFLAG:SELECTCOMの分岐 = 釣れたもの
TFLAG:SOURCEなどに影響しないSELECTCOMの分岐 = CHARA + (相方が釣った * 1000)

RETURN 1

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;釣りをする
@COM_ABLE621
;一括管理
SIF GLOBAL_COMABLE(621)
	RETURN RESULT
;釣り竿がない
SIF !ITEM:釣り竿
	RETURN 0
;拠点ではできない（仮）
SIF !FISHING_SPOT(CFLAG:MASTER:現在位置)
	RETURN 0
;デート中はデート相手とだけ
SIF FLAG:デート相手 && TARGET != FLAG:デート相手
	RETURN 0
;睡眠中
SIF CFLAG:MASTER:睡眠
	RETURN 0
;時間停止中
SIF FLAG:70
	RETURN 0
RETURN 1
;FileName_SHOP.ERB -------------------------------- Rev1.00
;起床前メニュー画面表示
;CALL		SYSTEM
;ARG		VOID
;RETURN		VOID
;COMMENT	
;----------------------------------------------------------
@SHOW_SHOP
;起床前画面表示中フラグ
TFLAG:100 = 0
;---------------------
;起床時間自動補正、最低睡眠時間は4時間
LOCAL:1 = CFLAG:MASTER:就寝時間 - DAY * 1440
IF (TIME:3 >= LOCAL:1 && (TIME:3 - LOCAL:1) < 240) || (TIME:3 < 240 && (1440 + TIME:3 - LOCAL:1) < 240)
	PRINTL 睡眠時間が短すぎます。最低でも4時間以上寝る必要があります
	CALL 最低睡眠時間設定()
	PRINTFORMW 起床時間を（%時刻表示(TIME:3)%）に設定しました
ENDIF
;---------------------
DRAWLINE
PRINTFORML {DAY}日目(%GET_DAY()%)　所持金:\\%TOSTR(MONEY,"#,###")%
PRINTL

CALL BASE_BAR(MASTER, "体力", 28, "緑")
PRINTFORM 　　
CALL BASE_BAR(MASTER, "気力", 28, "青", "「改行」")
CALL BASE_BAR(MASTER, "TSP", 28, "水色")
PRINTFORM 　　
SIF TALENT:MASTER:2 & 2
	CALL BASE_BAR(MASTER, "精力", 28, "黄")
PRINTFORML 

;PRINT 体力
;CALL PRINT_COLORBAR, BASE:MASTER:0,MAXBASE:MASTER:0,28, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("赤"), RESULT:1
;PRINTFORML  ({BASE:MASTER:体力,5}/{MAXBASE:MASTER:体力,5})  
;PRINT 気力
;CALL PRINT_COLORBAR, BASE:MASTER:1,MAXBASE:MASTER:1,28, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("緑"), RESULT:1
;PRINTFORML  ({BASE:MASTER:1,5}/{MAXBASE:MASTER:1,5})  

PRINTFORM 自宅位置：%GET_MAPNAME(MAIN_MAP)%
IF FLAG:気絶中断
	PRINTFORML 気絶中	
ELSE
	PRINTFORML 
ENDIF
;PRINTFORML 起床時間：%時刻表示(TIME:3)%
PRINTBUTTON "起床時間", 2400
PRINTFORML ：%時刻表示(TIME:3)%
PRINTFORML 
CALL TW_TIPS
PRINTFORML 

SETCOLOR 103,228,126
PRINTL 【メインメニュー】
RESETCOLOR
PRINTLC [100] - 目を覚ます
PRINTLC [102] - 住環境設定
SIF CFLAG:MASTER:現在位置 != OMANEKIBEYA()
	PRINTLC [110] - 自宅引っ越し
PRINTL
PRINTL

PRINTLC [101] - 能力表示
PRINTLC [103] - スキマ通信販売
PRINTL
PRINTLC [105] - 実績解除
SIF FLAG:700
	PRINTLC [106] - コレクション
PRINTLC [108] - 手に入れた楽譜
PRINTL
PRINTL

SETCOLOR 103,228,126
PRINTL 【システムメニュー】
RESETCOLOR
PRINTLC [107] - 依頼リセット
PRINTLC [700] - 衣装リセット
PRINTL
PRINTLC [400] - OPTION
PRINTLC [410] - 採集リスト

SIF SAVESTR:本体バージョン != eraTW_Version
	SETCOLOR C_RED
PRINTLC [500] - UPDATE
RESETCOLOR

PRINTL
PRINTL

PRINTLC [200] - SAVE
PRINTLC [300] - LOAD
PRINTL

SIF MONEY:2
	PRINTFORML [600] - カリスマ売却

DRAWLINE

;FileName_SHOP.ERB -------------------------------- Rev1.00
;起床前メニューRESULT処理
;CALL		SYSTEM
;ARG		VOID
;RETURN		VOID
;COMMENT	
;----------------------------------------------------------
@USERSHOP
;起床
IF RESULT == 100
	BEGIN TRAIN
	RETURN 1
ELSEIF RESULT == 410
	CALL SHOW_GATHERING_LIST
;能力表示
ELSEIF RESULT == 101
	DEBUGCLEAR
	CALL SHOW_CHARA_LIST("存在するキャラリスト", "体力気力", "「情報表示」「条件変更」")
;	CALL SHOW_CHARADATA
;コレクション
ELSEIF RESULT == 106 && FLAG:700 && !FLAG:気絶中断
	CALL SHOW_COLLECTION
;通信販売
ELSEIF RESULT == 103 && !FLAG:気絶中断
	CALL ITEM_BUY
;設定
ELSEIF RESULT == 102 && !FLAG:気絶中断
	CALL SCHEDULE
ELSEIF RESULT == 105 && !FLAG:気絶中断
	CALL BONUS
ELSEIF RESULT == 107 && !FLAG:気絶中断
	CALL IRAI_RESET
ELSEIF RESULT == 108
	CALL SHOW_COLLECTION_SCORE
ELSEIF RESULT == 110 && CFLAG:MASTER:現在位置 != OMANEKIBEYA()&& !FLAG:気絶中断
	CALL SET_MAINHOME
;SAVE
ELSEIF RESULT == 200
	SAVEGAME
;LOAD
ELSEIF RESULT == 300
	LOADGAME
;OPTION
ELSEIF RESULT == 400
	CALL OPTION
ELSEIF RESULT == 500
	CALL UPDATE
ELSEIF RESULT == 600
	CALL CM_BALANCE
ELSEIF RESULT == 700
	CALL RESET_ALL_CLOTHES
ELSEIF RESULT == 2400
	CALL 起床時間設定()
ENDIF

@TW_TIPS
SETCOLOR C_YELLOW
PRINTPLAIN TIPS:
PRINTDATA
	DATAFORM 時間停止能力は使うほど成長する。
	DATAFORM 落とし物を見つけたければ探し物が得意なキャラに頼め。
	DATAFORM 酒は酒屋で買い取ってもらえる。
	DATAFORM おみくじや占いの結果によっては一部の判定に補正がかかる。くじ引きにも…
	DATAFORM 雪が降ると作物の育ちが悪くなる。
	DATAFORM カリスマは紅魔館のカジノなどで使える通貨。
	DATAFORM おおむね現金よりカリスマ払いの方が割安。
	DATAFORM 一部のキャラは固有のコマンドを持つ。
	DATAFORM 戦闘能力や教養が低いと好感度が上がりづらくなる場合がある。
	DATAFORM だいたいのことは神頼みでどうにかなる。
	DATAFORM 人里広場ではランダムキャラを引っかけることができる。
	DATAFORM ランダムキャラは初日のみ能力がモリモリ上がる。
	DATAFORM ランダムキャラを忘却した場合二度と再登場しないが、妊娠判定が行われるのは忘却した場合のみ。
	DATAFORM ランダムキャラが再登場させる場合、能力上昇補正を失い妊娠しなくなる。
	DATAFORM ランダムキャラを二人以上同時に出現させることはできない。
	DATAFORM ランダムキャラの情報はdatフォルダに保存される。
	DATAFORM 出会ったマップでなければランダムキャラを再登場させられない。
	DATAFORM 禁欲が辛くなったら素直に出すか心の平安を願え。
	DATAFORM 昼夜逆転生活しているキャラもいる。お近づきになりたいなら起床時刻をずらそう。
	DATAFORM コマンドが表示されない？フィルタがかかっているのかも。
	DATAFORM 甲斐性無しだと様々なコマンドが使用できなくなるが押し倒されやすくなる。
	DATAFORM キャラの仕事は手伝うのに条件があったりそもそも手伝えないものもある。
	DATAFORM 料理には食材が必要。
	DATAFORM 酒虫の瓶に水を入れると酒ができる。
	DATAFORM ローターは家庭菜園で作れる。
	DATAFORM 汚れ耐性が高いと掃除が雑になる。
	DATAFORM カジノでたくさん遊ぶと会員ランクが上がる。
	DATAFORM 時姦刻印は同レベルの快楽・不埒刻印に準じる効果を持つ。お堅いキャラには身体から仲良くなるのも手。
	DATAFORM コレクションのスキマなどは現実の日付が変わると補充される。所持数上限があるので積極的に使おう。
	DATAFORM あなたの能力や特定の人物との親交など、条件を満たさないと入れない場所がある。
	DATAFORM 勉強や訓練は誰かと一緒にやった方が効率的。
	DATAFORM 焦らしモードは部位快楽が入りにくくなる代わりに絶頂を抑制する。多重絶頂の一助に。
	DATAFORM 鈴奈庵で本を借りると対応技能の経験が入りやすくなる。
	DATAFORM ミラダの石は欲望を上げやすく、押し倒しを通りやすくするが、親密と信頼は上がりにくくなる。
	DATAFORM 1年以上プレイするとオプションのバイツァ・ダストから一部能力を引き継いで周回できる。
	DATAFORM 同時絶頂すると好感度上昇値や絶頂・被射精によるソース追加にボーナスが入る。
	DATAFORM 3日禁欲すると【濃厚精液】になり射精が強化されるが、射精した次の日には【濃厚精液】を失う。
	DATAFORM 禁欲が続くとストレスになったり夢精してしまう。
	DATAFORM 料理評価が80点以上なら機嫌がよく、35点以下なら悪くなる。
	DATAFORM 付喪神は欲情が上がりづらく、妊娠確率が低めになっているがプレイヤーの道具使用経験で緩和される。
	DATAFORM 信頼度が低すぎると好感度が上がりづらくなる。デート中ならこの制約は消える。
	DATAFORM 育児中は欲求不満度にキャップがかかる。子供が寝るか通学中なら…
	DATAFORM 調合セットを所持していなくても、調合可能な場所がいくつかある。
	DATAFORM 調合成功率には調合Lvの他に教養も影響する。
	DATAFORM 特定の季節でしか採集できない素材がある。
	DATAFORM 高級素材を採集したいなら採集レベルを上げよう。
	DATAFORM 仕事中の人の前で演奏するのはやめよう。
	DATAFORM 宴会中に演奏するとおひねりが貰えるかもしれないが、腕が足りないと酷い目に遭う。
	DATAFORM 願掛けで縁結びを願うと、翌日そのキャラが来訪してくる。
ENDDATA
PRINTFORML 
;簡易ブラックジャック
;スキルとか使わない完全に運ゲーです, 時間停止でホールカード覗いたりできてもいいかもね
;勝率はルールが本家と違うんでどうなんでしょう……。テストプレイではゲーム内で195分やって-250とかでした（雑魚）
;カードの追加が常に１回しかできないのでダブルのデメリットがないのはプレイヤーに有利な気がする
;P_HAND   ×広域変数化、プレイヤーの手札, 7まであるけど現状3までしか使いません
;D_HAND   ×広域変数化、ディーラーの手札, 上に同じく
;DECK     ×広域変数化、52枚のカードをランダムに並べてます, でも使うのは上から6枚だけ, 下のカードを持ってきたりとイカサマに応用できるかも

;カジノ作者（morph）による改造
;・HITの可能回数を増やした
;・カード関連変数の広域変数化＆処理を独立させて汎用化
;　（判定関連を別関数に任せられるのでこの方が便利、ポーカー実装されたし）
;・カウンティングを可能に、プレイヤースキルが影響するよ！
;・仕様上はジョーカーも混ぜられるようになったけど処理側で対応してない（計算めんどいのよ）
@CASINO_BLACKJACK
#DIM P_BET        ;賭け金
#DIM ORI_BET      ;最初の賭け金, ダブルを選択するとP_BETが2倍になり、続行すると次のゲームでも2倍になったままなので保存用です
#DIM INS          ;インシュアランスの保険金, 賭けてると再度インシュアランスできない
#DIM LPCOUNT      ;ループカウント
#DIM TOTAL, 3     ;
                  ;TOTAL:1  賭け金合計, カモは最後にまとめて精算しました
                  ;TOTAL:2  配当合計
#DIM P_WIN        ;プレイヤーの勝利判定用
#DIM CONST DL = 0 ;定数、ディーラーのカードプレイヤー番号
#DIM CONST PL = 1 ;定数、プレイヤーのカードプレイヤー番号
;参加プレイヤーはふたり
NUM_CARD_PL = 2

PRINTFORML 
IF MONEY:2 <= 0
	PRINTFORML メイド妖精
	PRINTFORML 　お客様、当カジノ店内では専用通貨“カリスマ”のみを使用してお遊びいただいております
	PRINTFORMW 　お手数ですがカリスマの購入をお願いいたします
	RETURN
ELSE
	PRINTFORML メイド妖精
	PRINTFORML 　いらっしゃいませ
	PRINTFORML 　こちらはブラックジャックコーナーとなっております
	PRINTFORML 　小細工やイカサマは一切無用、純粋にカードの腕前がものをいうゲームでございます
	PRINTFORMW 　まずはゲームの賭け金をご提示下さい
	PRINTFORML 
ENDIF
CALL CASINO_INPUTLOOP("カード賭け金")
P_BET = RESULT
ORI_BET = RESULT
VARSET TOTAL
IF P_BET
	CALL CASINO_MONEY("カリスマ", -P_BET)
	TOTAL:1 += P_BET
	CALL PLAY_CARDS("SHUFFLE")
	PRINTFORML 
	PRINTFORMW メイド妖精は新品のデッキを取り出してシャッフルした
ENDIF

WHILE P_BET
	IF NEW_CARD > 44
		CALL PLAY_CARDS("SHUFFLE")
		PRINTFORML 
		PRINTFORML メイド妖精
		PRINTFORMW 　カードの残りが少なくなってきましたのでシャッフルします
	ENDIF
	TIME += 5
	;手札をリセットして2枚ずつ引きます,INSもリセット
	CALLF CARD_PLAYER("CLEAR_BJ_ALL")
	CALL PLAY_CARDS("RESET_HAND")
	CALL PLAY_CARDS("HIT", DL)
	CALL PLAY_CARDS("HIT", PL)
	CALL PLAY_CARDS("HIT", DL)
	CALL PLAY_CARDS("HIT", PL)
	CALLF CARD_PLAYER("RENEW_BJ_POINT_ALL")
	INS = 0
	P_WIN = 0
	;初期盤面, お互い手札は２枚, ホールカード１枚
	DO
		CALL CASINO_DISP("BJ_CLOSE")
		SIF NEW_CARD < NUM_CARD
			PRINTL [1] - ヒット
		PRINTL [2] - スタンド
		SIF NEW_CARD < NUM_CARD && NUM_HAND:PL == 2 && MONEY:2 >= P_BET
			PRINTL [3] - ダブル
		IF NUM_HAND:PL == 2 && CARD_CHECK("RANK", CARD_HAND:DL:0) == 1
			IF CARD_PLAYER("BJ_BJ", PL)
				PRINTL [4] - イーブンマネー
			ELSEIF !INS && MONEY:2 >= P_BET / 2
				PRINTL [4] - インシュアランス
			ENDIF
		ENDIF
		PRINTL [0] - サレンダー
		INPUT
		DRAWLINEFORM ━
		PRINTFORML メイド妖精
		SELECTCASE RESULT
		CASE 1
		;ヒット枚数を無制限に変更
			SIF NEW_CARD >= NUM_CARD
				GOTO SURRENDER
			PRINTFORMW 　ではカードをどうぞ
			CALL PLAY_CARDS("HIT_BJ", PL)
			SIF CARD_PLAYER("GET_BJ_POINT", PL) >= 21
				BREAK
			CONTINUE
		CASE 2
		;スタンド, 即勝負します
		CASE 3
		;ダブル, 賭け金を２倍にしてカード１枚追加, 賭け金を用意できるときのみ
			SIF MONEY:2 < P_BET || NUM_HAND:PL > 2 || NEW_CARD >= NUM_CARD
				GOTO SURRENDER
			PRINTFORML 　かしこまりました
			PRINTFORML 　賭け金は{P_BET * 2}カリスマとなります
			PRINTFORMW 　ではカードをどうぞ
			CALL PLAY_CARDS("HIT_BJ", PL)
			TOTAL:1 += P_BET
			CALL CASINO_MONEY("カリスマ", -P_BET)
			P_BET = P_BET * 2
		CASE 4
			IF NUM_HAND:PL == 2 && CARD_CHECK("RANK", CARD_HAND:DL:0) == 1
				IF CARD_PLAYER("GET_BJ_BJ", PL)
				;イーブンマネー, 引き分けが怖いので２倍の配当で我慢する, 気になるのでホールカードを見せてくれる
				;表のカードがＡであなたの手札が２１点の時のみ
					PRINTFORML 　かしこまりました
					PRINTFORML 　こちらが配当の{P_BET * 2}カリスマとなります
					PRINTFORMW 　どうぞお受取りください
					TOTAL:2 += P_BET * 2
					CALL CASINO_MONEY("カリスマ", P_BET * 2)
					PRINTFORMW 　ちなみにホールカードは……
					CALL CARD_DISP("CARD", CARD_HAND:DL:1)
					PRINTW
					IF CARD_PLAYER("GET_BJ_BJ", DL)
						PRINTFORMW ふふふ、賢明な判断でしたねぇ
					ELSE
						PRINTFORMW あらあら、残念でしたねぇ
					ENDIF
					GOTO CHECK_NEXTGAME
				ELSEIF !INS && MONEY:2 >= P_BET / 2
				;インシュアランス, ブラックジャックが怖いので保険金を追加BETする
				;表のカードがＡでINSをしておらず保険金分のカリスマが用意できるときのみ
				;その後再度選択肢を選ぶ
					PRINTFORML 　かしこまりました
					PRINTFORMW 　賭け金の半分、{P_BET / 2}カリスマを保険金として追加します
					INS = P_BET / 2
					TOTAL:1 += P_BET / 2
					CALL CASINO_MONEY("カリスマ", -P_BET / 2)
					CONTINUE
				ELSE
					GOTO SURRENDER
				ENDIF
			ELSE
				GOTO SURRENDER
			ENDIF
		CASEELSE
		;手入力したりするとサレンダー, ゲームを降りて半額返してもらう
			$SURRENDER
			PRINTFORML 　あら、降りるんですか
			PRINTFORMW 　それでは配当の{P_BET / 2}カリスマをお受取りください
			TOTAL:2 += P_BET / 2
			CALL CASINO_MONEY("カリスマ", P_BET / 2)
			GOTO CHECK_NEXTGAME
		ENDSELECT
		BREAK
	LOOP 1
	
	IF NUM_HAND:PL > 2
		CALL CASINO_DISP("BJ_CLOSE")
		IF CARD_PLAYER("GET_BJ_BUST", PL)
			PRINTW 
			PRINTFORML メイド妖精
			PRINTFORML 　21を超えてしまいましたね
			GOTO YOULOSE
		ENDIF
	ENDIF

	;ホールカードを公開して盤面を見る
	PRINTW 
	PRINTFORML メイド妖精
	PRINTFORMW 　ではカードをめくります
	PRINTFORML 
	WHILE CARD_PLAYER("GET_BJ_POINT", DL) < 17
		IF NEW_CARD >= 52
			PRINTFORML メイド妖精
			PRINTFORMW 　もうカードがありませんね
			BREAK
		ENDIF
		CALL CASINO_DISP("BJ_OPEN")
		PRINTFORML メイド妖精
		PRINTFORMW 　17点未満なのでカードを追加します
		CALL PLAY_CARDS("HIT_BJ", DL)
	WEND

	CALL CASINO_DISP("BJ_OPEN")
	;結果の判定
	IF CARD_PLAYER("GET_BJ_BJ", DL)
	;ディーラーのBJ
		PRINTFORML メイド妖精
		PRINTFORML 　ブラックジャックでした
		IF INS
		;INSしてたらセーフ
			PRINTFORMW 　インシュアランスを賭けていたので配当の{P_BET + INS}カリスマをお受取りください
			TOTAL:2 += P_BET + INS
			CALL CASINO_MONEY("カリスマ", P_BET + INS)
		ELSEIF CARD_PLAYER("GET_BJ_BJ", PL)
			PRINTFORML 　ブラックジャック同士なので引き分けです
			PRINTFORMW 　配当の{P_BET}カリスマをお受取りください
			TOTAL:2 += P_BET
			CALL CASINO_MONEY("カリスマ", P_BET)
		ELSEIF CARD_PLAYER("GET_BJ_POINT", PL) == 21
		;２１でもBJには勝てない
			PRINTFORML 　こちらはブラックジャックなのであなたの負けです
			PRINTFORMW 　賭け金は全てこちらがいただきます
		ELSE
			GOTO YOULOSE
		ENDIF
	ELSEIF CARD_PLAYER("GET_BJ_BUST", DL)
	;ディーラーBUST（ここまで来た時点であなたはBUSTしていない）
		PRINTFORML メイド妖精
		PRINTFORML 　21を超えてしまいましたのであなたの勝ちです
		P_WIN = 1
	ELSE
	;普通の結果
		PRINTFORML メイド妖精
		IF CARD_PLAYER("GET_BJ_BJ", PL)
		;21POINTだがPL側のみBJが成立した
			PRINTFORML 　そちらはブラックジャックなのであなたの勝ちです
			P_WIN = 1
		ELSEIF CARD_PLAYER("GET_BJ_POINT", PL) > CARD_PLAYER("GET_BJ_POINT", DL)
			PRINTFORML 　あなたの勝ちです
			P_WIN = 1
		ELSEIF CARD_PLAYER("GET_BJ_POINT", PL) == CARD_PLAYER("GET_BJ_POINT", DL)
		;その他の引き分け
			PRINTFORML 　引き分けです
			PRINTFORMW 　配当の{P_BET}カリスマをお受取りください
			TOTAL:2 += P_BET
			CALL CASINO_MONEY("カリスマ", P_BET)
		ELSE
		;負けはここ
			$YOULOSE
			PRINTFORML 　あなたの負けです
			PRINTFORMW 　賭け金は全てこちらがいただきます
		ENDIF
	ENDIF

	IF P_WIN
		IF CARD_PLAYER("GET_BJ_BJ", PL)
			PRINTFORML 　ブラックジャックでしたので配当は{P_BET * 5 / 2}カリスマになります
			PRINTFORMW 　どうぞお受取りください
			TOTAL:2 += P_BET * 5 / 2
			CALL CASINO_MONEY("カリスマ", P_BET * 5 / 2)
		ELSE
			PRINTFORML 　配当は{P_BET * 2}カリスマになります
			PRINTFORMW 　どうぞお受取りください
			TOTAL:2 += P_BET * 2
			CALL CASINO_MONEY("カリスマ", P_BET * 2)
		ENDIF
	ENDIF
	
	;ゲームの続行判定
	$CHECK_NEXTGAME
	IF NEMUKE() >= 700
		P_BET = 0
	ELSEIF MONEY:2 <= 0
		P_BET = 0
	ELSEIF 500000 - FLAG:カリスマ流通 - P_BET * 25 / 10 < 10000
	;ここだけBREAK
		BREAK
	ELSE
		PRINTFORML 　さて、勝負を続けますか？
		PRINTFORML 
		CALL CASINO_INPUTLOOP("カード続行")
		SELECTCASE RESULT
		CASE 0
			P_BET = 0
		CASE 1
			IF MONEY:2 < P_BET
				PRINTFORML メイド妖精
				PRINTFORML 　お手持ちのカリスマが足りないようですわ
				PRINTFORML 　賭け金を再設定なさって下さい
				PRINTFORML 
				CALL CASINO_INPUTLOOP("カード賭け金")
				P_BET = RESULT
				ORI_BET = RESULT
				SIF RESULT
					PRINTFORML 
			ELSE
				P_BET = ORI_BET
			ENDIF
		CASE 2
			PRINTFORML メイド妖精
			PRINTFORML 　それでは賭け金をご入力下さい
			PRINTFORML 
			CALL CASINO_INPUTLOOP("カード賭け金")
			P_BET = RESULT
			ORI_BET = RESULT
			SIF RESULT
				PRINTFORML 
		ENDSELECT
	ENDIF

	IF P_BET
		TOTAL:1 += P_BET
		CALL CASINO_MONEY("カリスマ", -P_BET)
		PRINTFORML メイド妖精
		PRINTFORML 　はい、賭け金{P_BET}カリスマ確かに頂戴いたしました
		PRINTFORMW 　それでは引き続きよろしくお願いいたします
	ENDIF
WEND

PRINTFORML 
IF NEMUKE() >= 700
	PRINTFORML メイド妖精
	PRINTFORML 　あの、お客様？
	PRINTFORML 　随分とお疲れのご様子ですよ
	PRINTFORML 　今日はもうゲームを終えたほうが良いのではないかしら
	PRINTFORMW 　またのお越しをお待ちしておりますわ
ELSEIF MONEY:2 <= 0
	PRINTFORML メイド妖精
	PRINTFORML 　お手持ちのカリスマが尽きてしまったようですわね
	PRINTFORMW 　またのお越しをお待ちしておりますわ
ELSEIF P_BET
;払い戻しに店が耐えられない時
	PRINTFORML メイド妖精
	PRINTFORML 　申し訳ありませんがそろそろ閉店のようです
	PRINTFORMW 　またのお越しをお待ちしておりますわ
ELSE
	PRINTFORML メイド妖精
	PRINTFORML 　かしこまりました
	PRINTFORMW 　またのお越しをお待ちしておりますわ
ENDIF
CALLF CARD_PLAYER("CLEAR_BJ_ALL")

IF TOTAL:1
	CALL CASINO_MONEY("カモ", TOTAL:1)
	CALL CASINO_DISP("RESULT", TOTAL:1, TOTAL:2)
ENDIF
VARSET TOTAL

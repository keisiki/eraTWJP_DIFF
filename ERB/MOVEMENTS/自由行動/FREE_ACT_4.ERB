;掃除を手伝う
@EXIST_FreeAct4
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
@FreeAct4(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
#DIM 経過時間
経過時間 = Activity_Duration:TARGET
SELECTCASE O_DATA
CASE "名称"
	CALLF MAKE_STR(V_NAME, "掃除を手伝う")
CASE "CHAIN"
;他キャラに連鎖する場合コメントアウト外すこと
	CALLF MAKE_INT(V_NAME, 1)
CASE "経過時間"
;自由行動に付き合うコマンドでの経過時間
;未入力だと最大30分
	;CALLF MAKE_INT(V_NAME, 経過時間)
ENDSELECT

@自由行動内容_4(CHARA, ARGS, 時間係数, CHAIN)
#DIM CHARA
#DIM CHAIN
#DIM 現在地
#DIM メッセージ表示
#DIM 時間係数
#DIM 経過時間
現在地 = CFLAG:CHARA:現在位置
メッセージ表示 = 現在地 == CFLAG:MASTER:現在位置 ? 1 # 0
SELECTCASE ARGS
	CASE "発生"
		;個室にいる時
		SIF !BEDROOM(現在地)
			RETURN 0
		SIF BASE:CHARA:気力 <= 500
			RETURN 0
		;汚臭耐性（掃除係）によって積極性が変わる
		IF TALENT:CHARA:汚臭耐性 < 0 || TALENT:CHARA:掃除係
			SIF YOGORE:(現在地) < 2000
				RETURN 0
		ELSEIF TALENT:CHARA:汚臭耐性 == 0
			SIF YOGORE:(現在地) < 3500
				RETURN 0
		ELSEIF TALENT:CHARA:汚臭耐性 > 0
			SIF YOGORE:(現在地) < 5000
				RETURN 0
		ENDIF
		SIF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%は\@ CHAIN ? %CALLNAME:CHAIN%を手伝い始めた。# 汚れが気になり掃除を始めた。\@
		RETURN 60 + RAND:30
	CASE "行動中"
		;実際に掃除する
		YOGORE:(現在地) = MAX(YOGORE:(現在地) -100 * TIME_PROGRESS(TIME:1) / 5, 0)
		IF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%は%TEXTR("ゴミをまとめている/整理整頓をしている/埃を払っている/テキパキと部屋の掃除をしている")%。
		ENDIF
	CASE "完了"
		;清掃経験が入る
		EXP:CHARA:清掃経験 += 1
		SIF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%は掃除を終えたようだ。
	CASE "中断"
		;中断されても清掃経験は入る
		EXP:CHARA:清掃経験 += 1
	CASE "付き合う"
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%%TEXTR("の集めたゴミを片付けたり/がはたいた場所を雑巾で拭き取ったり/の手の届かない場所の掃除をしたり")%して手伝った
	CASE "ソース"
		LOCAL = YOGORE:(CFLAG:MASTER:現在位置)
		YOGORE:(CFLAG:MASTER:現在位置) = MAX(YOGORE:(CFLAG:MASTER:現在位置) -100 * 経過時間 / 5, 0)
		EX:MASTER:今日の掃除回数 += LOCAL - YOGORE:(CFLAG:MASTER:現在位置)
		DOWNBASE:MASTER:体力 += 20 * 時間係数
		DOWNBASE:MASTER:気力 += 40 * 時間係数
		DOWNBASE:TARGET:体力 += 10 * 時間係数
		DOWNBASE:TARGET:気力 += 20 * 時間係数
		SOURCE:歓楽 = (100 + ((ABL:従順 + ABL:MASTER:清掃技能) * 50)) * 時間係数
		TFLAG:98 = 2
		EXP:MASTER:清掃経験 ++
ENDSELECT
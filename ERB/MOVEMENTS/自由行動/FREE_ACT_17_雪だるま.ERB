@EXIST_FreeAct17
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
@FreeAct17(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
#DIM 経過時間
経過時間 = Activity_Duration:TARGET
SELECTCASE O_DATA
;自由行動に付き合うコマンドの名称
CASE "名称"
	CALLF MAKE_STR(V_NAME, "一緒に雪作り")
CASE "CHAIN"
;他キャラに連鎖しない場合コメントアウトすること
	CALLF MAKE_INT(V_NAME, 1)
CASE "経過時間"
;自由行動に付き合うコマンドでの経過時間
;未入力だと最大30分
;	CALLF MAKE_INT(V_NAME, 0)
ENDSELECT

@自由行動内容_17(CHARA, ARGS, 時間係数, CHAIN)
#DIM CHARA
#DIM CHAIN
#DIM 現在地
#DIM メッセージ表示
#DIM 時間係数
#DIM 経過時間
現在地 = CFLAG:CHARA:現在位置
メッセージ表示 = 現在地 == CFLAG:MASTER:現在位置 ? 1 # 0
IF GROUPMATCH(CHARA, [[瓔花]])
	LOCALS = 超多段雪だるま
	LOCAL = 90
ELSEIF GROUPMATCH(CHARA, [[幽香]])
	LOCALS = アイスフラワー
	LOCAL = 40
ELSEIF GROUPMATCH(CHARA, [[ぬえ]])
	LOCALS = 雪宇宙人
ELSEIF GROUPMATCH(CHARA, [[マミゾウ]])
	LOCALS = 雪だぬき
ELSEIF GROUPMATCH(CHARA, [[ミケ]])
	LOCALS = 雪まねきねこ
ELSEIF GROUPMATCH(CHARA, [[魅須丸]])
	LOCALS = 陰陽雪玉
ELSEIF GROUPMATCH(CHARA, [[磨弓]], [[袿姫]])
	LOCALS = 雪はにわ
	LOCAL = 40
ELSEIF GROUPMATCH(CHARA, [[里香]])
	LOCALS = 雪戦車
	LOCAL = 60
;ウサミミ
ELSEIF TALENT:CHARA:動物耳 == 2
	LOCALS = 雪うさぎ
;月人
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "人間")) == "月人"
	LOCALS = 雪うさぎ
;教養3以上
ELSEIF CSVABL(CHARA, GETNUM(ABL, "教養")) >= 3
	LOCALS = 雪うさぎ
ELSE
	LOCALS = 雪だるま
ENDIF
SIF !LOCAL
	LOCAL = 30
SELECTCASE ARGS
	CASE "発生"
		;冬の屋外で訓練できる場所
		SIF !OUTROOF(現在地)
			RETURN 0
		SIF !戦闘訓練可(現在地)
			RETURN 0
		SIF DAY:2 != 4
			RETURN 0
		SIF BASE:CHARA:気力 <= 500
			RETURN 0
		;積もってないとダメ
		SIF YOGORE:現在地 < 500
				RETURN 0
		;吹雪,猛吹雪中はダメ
		SIF GROUPMATCH(天候, 10)
			RETURN 0
		SIF FLAG:異常気象 == 10
			RETURN 0
		SIF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%は\@ CHAIN ? %CALLNAME:CHAIN%を手伝い始めた。# %LOCALS%を作り始めた。\@
		RETURN LOCAL + RAND:30
	CASE "行動中"
		;ちょっとだけ掃除される
		YOGORE:(現在地) = MAX(YOGORE:(現在地) -50 * TIME_PROGRESS(TIME:1) / 5, 0)
		IF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%は%TEXTR("雪を集めている/雪を固めている/形を整えている")%。
		ENDIF
	CASE "完了"
		SIF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%は%LOCALS%を完成させて満足したようだ。
	CASE "中断"
		;中断メッセージはうふふ突入時等でも表示されてしまうので表示しない
	CASE "付き合う"
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の%LOCALS%作りを手伝った
	CASE "ソース"
		;遊びに付き合うと一緒
		DOWNBASE:MASTER:体力 += 40 * 時間係数
		DOWNBASE:MASTER:気力 += 40 * 時間係数
		DOWNBASE:TARGET:体力 += 20 * 時間係数
		DOWNBASE:TARGET:気力 += 20 * 時間係数
		SOURCE:歓楽 = (150 + ((ABL:親密 + 好感度係数(TARGET)) * 50)) * 時間係数
		SOURCE:受動 = (100 + ((ABL:従順 + 好感度係数(TARGET)) * 50)) * 時間係数
		SOURCE:征服 = (100 + (ABL:サドっ気 * 100)) * 時間係数
		TFLAG:98 = 1
ENDSELECT

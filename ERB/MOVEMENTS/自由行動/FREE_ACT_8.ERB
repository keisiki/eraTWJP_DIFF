@EXIST_FreeAct8
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
@FreeAct8(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
#DIM 経過時間
経過時間 = Activity_Duration:TARGET
SELECTCASE O_DATA
;自由行動に付き合うコマンドの名称
CASE "名称"
	IF TALENT:ARG:音楽知識 == 1
		CALLF MAKE_STR(V_NAME, "セッションする")
	ELSE
		CALLF MAKE_STR(V_NAME, "伴奏する")
	ENDIF
CASE "CHAIN"
;他キャラに連鎖しない場合コメントアウトすること
	CALLF MAKE_INT(V_NAME, 1)
CASE "経過時間"
;自由行動に付き合うコマンドでの経過時間
;0だと30分か残り時間すべての小さい方
	CALLF MAKE_INT(V_NAME, 0)
ENDSELECT

@自由行動内容_8(CHARA, ARGS, 時間係数, CHAIN)
#DIM CHARA
#DIM CHAIN
#DIM 現在地
#DIM メッセージ表示
#DIM 時間係数
#DIM 経過時間
現在地 = CFLAG:CHARA:現在位置
メッセージ表示 = 現在地 == CFLAG:MASTER:現在位置 ? 1 # 0
SELECTCASE ARGS
	CASE "発生"
		;［音楽知識］持ちのキャラが訓練できる場所にいる時
		SIF !TALENT:CHARA:音楽知識
			RETURN 0
		SIF !戦闘訓練可(現在地)
			RETURN 0
		SIF BASE:CHARA:気力 <= MAXBASE:CHARA:気力 / 2
			RETURN 0
		IF メッセージ表示
			IF CHAIN
				SELECTCASE TALENT:CHAIN:音楽知識
				CASE 1
					SELECTCASE TALENT:CHARA:音楽知識
					CASE 1
						PRINTFORMW %CALLNAME:CHARA%は%CALLNAME:CHAIN%とのセッションを始めた。
					CASE 2
						PRINTFORMW %CALLNAME:CHARA%は%CALLNAME:CHAIN%の演奏に合わせて歌い始めた。
					CASE 3
						PRINTFORMW %CALLNAME:CHARA%は%CALLNAME:CHAIN%の演奏に合わせて踊り始めた。
					ENDSELECT
				CASE 2
					SELECTCASE TALENT:CHARA:音楽知識
					CASE 1
						PRINTFORMW %CALLNAME:CHARA%は%CALLNAME:CHAIN%の歌に合わせて演奏を始めた。
					CASE 2
						PRINTFORMW %CALLNAME:CHARA%は%CALLNAME:CHAIN%とのコーラスを始めた。
					CASE 3
						PRINTFORMW %CALLNAME:CHARA%は%CALLNAME:CHAIN%の歌に合わせて踊り始めた。
					ENDSELECT
				CASE 3
					SELECTCASE TALENT:CHARA:音楽知識
					CASE 1
						PRINTFORMW %CALLNAME:CHARA%は%CALLNAME:CHAIN%の舞踊に合わせて演奏を始めた。
					CASE 2
						PRINTFORMW %CALLNAME:CHARA%は%CALLNAME:CHAIN%の舞踊に合わせて歌い始めた。
					CASE 3
						PRINTFORMW %CALLNAME:CHARA%は%CALLNAME:CHAIN%と一緒に踊り始めた。
					ENDSELECT
				ENDSELECT
			ELSE
				SELECTCASE TALENT:CHARA:音楽知識
				CASE 1
					PRINTFORMW %CALLNAME:CHARA%はおもむろに演奏を始めた。
				CASE 2
					PRINTFORMW %CALLNAME:CHARA%はおもむろに歌い始めた。
				CASE 3
					PRINTFORMW %CALLNAME:CHARA%はおもむろに舞踊を始めた。
				ENDSELECT
			ENDIF
		ENDIF
		RETURN 60 + RAND:30
	CASE "行動中"
		IF メッセージ表示
			SELECTCASE TALENT:CHARA:音楽知識
			CASE 1
				PRINTFORMW %CALLNAME:CHARA%は小気味よく演奏している。
			CASE 2
				PRINTFORMW %CALLNAME:CHARA%は気ままに歌っている。
			CASE 3
				PRINTFORMW %CALLNAME:CHARA%は華麗に踊っている。
			ENDSELECT
		ENDIF
	CASE "完了"
		SELECTCASE TALENT:CHARA:音楽知識
		CASE 1
			EXP:CHARA:演奏経験 += 1
			SIF メッセージ表示
				PRINTFORMW %CALLNAME:CHARA%は演奏を終えて一息ついた。
		CASE 2
			EXP:CHARA:歌唱経験 += 1
			SIF メッセージ表示
				PRINTFORMW %CALLNAME:CHARA%は歌い終えて一息ついた。
		CASE 3
			EXP:CHARA:舞踏経験 += 1
			SIF メッセージ表示
				PRINTFORMW %CALLNAME:CHARA%は舞踊を終えて一息ついた。
		ENDSELECT
	CASE "中断"
		SELECTCASE TALENT:CHARA:音楽知識
		CASE 1
			EXP:CHARA:演奏経験 += 1
		CASE 2
			EXP:CHARA:歌唱経験 += 1
		CASE 3
			EXP:CHARA:舞踏経験 += 1
		ENDSELECT
	CASE "行動中"
		;行動中の処理、地の文もここで
		IF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%は%TEXTR("呼吸している/瞬きしている/変顔している")%。
		ENDIF
	CASE "完了"
		;行動時間を使い切ったときの処理、地の文もここで
		SIF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA%は活動を終えたようだ。
	CASE "中断"
		;中断したときの処理、地の文もここで
	CASE "付き合う"
		IF TFLAG:193 == -2
			SELECTCASE TALENT:TARGET:音楽知識
			CASE 1
				PRINTFORML %CALLNAME:MASTER%は使える楽器がないので%CALLNAME:TARGET%の演奏をぼーっと聞いていた…
			CASE 2
				PRINTFORML %CALLNAME:MASTER%は使える楽器がないので%CALLNAME:TARGET%の歌をぼーっと聞いていた…
			CASE 3
				PRINTFORML %CALLNAME:MASTER%は使える楽器がないので%CALLNAME:TARGET%の舞をぼーっと見ていた…
			ENDSELECT
		ELSE
			SELECTCASE TALENT:TARGET:音楽知識
			CASE 1
				PRINTFORML %CALLNAME:MASTER%は%CALLNAME:TARGET%と並んでセッションした
				IF TFLAG:193 > 0
					PRINTFORML %CALLNAME:TARGET%はノリノリで演奏している…
				ELSEIF TFLAG:193 < 0
					PRINTFORML %CALLNAME:TARGET%は渋い顔をしている…
				ELSE
					PRINTFORML %CALLNAME:TARGET%は落ち着いて演奏している…
				ENDIF
			CASE 2
				PRINTFORML %CALLNAME:MASTER%は%CALLNAME:TARGET%の歌に合わせて伴奏した
				IF TFLAG:193 > 0
					PRINTFORML %CALLNAME:TARGET%はノリノリで歌っている…
				ELSEIF TFLAG:193 < 0
					PRINTFORML %CALLNAME:TARGET%は渋い顔をしている…
				ELSE
					PRINTFORML %CALLNAME:TARGET%は落ち着いて歌っている…
				ENDIF
			CASE 3
				PRINTFORML %CALLNAME:MASTER%は%CALLNAME:TARGET%の舞に合わせて伴奏した
				IF TFLAG:193 > 0
					PRINTFORML %CALLNAME:TARGET%はノリノリで踊っている…
				ELSEIF TFLAG:193 < 0
					PRINTFORML %CALLNAME:TARGET%は渋い顔をしている…
				ELSE
					PRINTFORML %CALLNAME:TARGET%は落ち着いて踊っている…
				ENDIF
			ENDSELECT
		ENDIF
	CASE "ソース"
		IF !GROUPMATCH(1, MUSIC_ABLE(30), MUSIC_ABLE(31), MUSIC_ABLE(32), MUSIC_ABLE(33), MUSIC_ABLE(34), MUSIC_ABLE(35))
			;楽器が使えないと見てるだけ
			TFLAG:193 = -2
			DOWNBASE:MASTER:気力 += 20 * 時間係数
			DOWNBASE:TARGET:体力 += 10 * 時間係数
			DOWNBASE:TARGET:気力 += 20 * 時間係数
			SOURCE:歓楽 = (50 + ((ABL:親密 + ABL:MASTER:音楽技能) * 10)) * 時間係数
		ELSE
			DOWNBASE:MASTER:体力 += 40 * 時間係数
			DOWNBASE:MASTER:気力 += 40 * 時間係数
			DOWNBASE:TARGET:体力 += 20 * 時間係数
			DOWNBASE:TARGET:気力 += 20 * 時間係数
			SELECTCASE RAND:(10 + ABL:MASTER:音楽技能)
			CASE 0
				;失敗
				TFLAG:193 = -1
				SOURCE:歓楽 = (100 + ((ABL:親密 + ABL:MASTER:音楽技能) * 20)) * 時間係数
			CASE 1 TO 9
				;成功
				SOURCE:歓楽 = (150 + ((ABL:親密 + ABL:MASTER:音楽技能) * 50)) * 時間係数
				TFLAG:98 = 1
			CASEELSE
				;音楽技能があるとその確率で大成功
				TFLAG:193 = 1
				SOURCE:歓楽 = (200 + ((ABL:親密 + ABL:MASTER:音楽技能) * 80)) * 時間係数
				TFLAG:98 = 2
			ENDSELECT
			;その場にいる他のキャラにもソースが入る
			FOR LOCAL, 1, GET_TARGETNUM() + 1
				SIF TARGET:LOCAL == TARGET
					CONTINUE
				SIF !SHIRAHU(TARGET:LOCAL)
					CONTINUE
				SOURCE:(TARGET:LOCAL):歓楽 = ((150 + (50 * TFLAG:193)) + ((ABL:(TARGET:LOCAL):親密 + ABL:MASTER:音楽技能) * (50 + (30 * TFLAG:193)))) * 時間係数
			NEXT
			EXP:MASTER:演奏経験 ++
		ENDIF
ENDSELECT
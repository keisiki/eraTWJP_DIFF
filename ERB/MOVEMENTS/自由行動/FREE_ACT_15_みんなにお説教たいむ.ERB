@EXIST_FreeAct15
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
@FreeAct15(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
#DIM 経過時間
経過時間 = Activity_Duration:TARGET
SELECTCASE O_DATA
;自由行動に付き合うコマンドの名称
CASE "名称"
	CALLF MAKE_STR(V_NAME, "様子をうかがう")
CASE "CHAIN"
;他キャラに連鎖しない場合コメントアウトすること
	CALLF MAKE_INT(V_NAME, 1)
CASE "経過時間"
;自由行動に付き合うコマンドでの経過時間
;0だと30分か残り時間すべての小さい方
	CALLF MAKE_INT(V_NAME, 0)
ENDSELECT

@自由行動内容_15(CHARA, ARGS, 時間係数, CHAIN)
#DIM CHARA
#DIM CHAIN
#DIM 現在地
#DIM メッセージ表示
#DIM 時間係数
#DIM 経過時間
現在地 = CFLAG:CHARA:現在位置
メッセージ表示 = 現在地 == CFLAG:MASTER:現在位置 ? 1 # 0
SELECTCASE ARGS
	CASE "発生"
		;屋外で発生
		SIF INROOM(現在地)
			RETURN 0
		;仕事予定の邪魔をしない
		SIF !CHARA_HOLIDAY(CHARA)
			RETURN 0
		;映姫様は常に起点
		SIF CHARA == 30
			GOTO START
		;小町/久侘歌以外の神霊と蓬莱人は対象外
		SIF CHECK_CHARA(CHARA, "蓬莱人") || (TALENT:CHARA:神霊 && !GROUPMATCH(CHARA, 76, 129))
			RETURN 0
		;映姫様がすでに説教中なら発生
		SIF Activity_Type:30 != 15 || 現在地 != CFLAG:30:現在位置 || !Activity_Duration:30
			RETURN 0
		$START
		IF メッセージ表示
			PRINTFORM %CALLNAME:CHARA%
			IF CHARA == 30 
				PRINTFORMW は%FA15_TARGET(0)%に説教を始めた。
			ELSE
				PRINTFORMW も\@ CHAIN ? 巻き添えで説教され始めた # 映姫の説教に捕まった \@…
			ENDIF
		ELSEIF CHARA == 30
			CALLF FA15_TARGET(0)
		ENDIF
		;自由行動にかける時間をRETURNする。小町のみ最低保障2時間
		IF CHARA == 30
			RETURN 60 + RAND:30
		ELSE
			LOCAL = 30 + RAND:60 + ((CHARA == 76) * 90)
			Activity_Duration:30 = MAX(Activity_Duration:30, LOCAL)
			RETURN LOCAL
		ENDIF
	CASE "行動中"
		;行動中の処理、地の文もここで。
		IF メッセージ表示
			IF CHARA == 30
				PRINTFORMW %CALLNAME:CHARA%は%FA15_TARGET(1)%に%TEXTR("懇/延/長/")%々と説教している…
			ELSEIF 現在地 != CFLAG:30:現在位置 || Activity_Type:30 != 15
				CALL FA15_RESET(CHARA)
			ELSE
				PRINTFORM %CALLNAME:CHARA%は映姫の説教
				SELECTCASE CFLAG:CHARA:性格傾向
					;気弱系
					CASE 1
						PRINTFORMW %TEXTR("で涙目になっ/を聞いてうなだれ")%ている…
					;強気系
					CASE 2
						PRINTFORMW %TEXTR("にこっそり舌を出し/を適当に聞き流し")%ている…
					;素直系
					CASE 3
						PRINTFORMW %TEXTR("を聞いてしゅんとし/にいちいち頷い")%ている…
					;真面目系
					CASE 4
						PRINTFORMW を%TEXTR("聞いて反省して/真剣に聞いて")%いる…
					;その他
					CASEELSE
						PRINTFORMW を%TEXTR("おとなしく受け/じっと聞い")%ている…
				ENDSELECT
			ENDIF
		ELSEIF CHARA != 30 && 現在地 != CFLAG:30:現在位置
			CALL FA15_RESET(CHARA)
		ENDIF
	CASE "完了"
		;行動時間を使い切ったときの処理、地の文もここで
		IF メッセージ表示
			IF CHARA == 30
				PRINTFORMW %CALLNAME:CHARA%はようやく説教を終えたようだ。
			ELSE
				PRINTDATAW
					DATAFORM やっと説教が終わって%CALLNAME:CHARA%は\@ CFLAG:CHARA:性格傾向 == 2 ? げんなり # ほっと \@している。
					DATAFORM %CALLNAME:CHARA%は説教が終わって\@ CFLAG:CHARA:性格傾向 == 1 ? も涙目のままだ # 小さく溜息をついた \@。
					DATAFORM 説教が終わって%CALLNAME:CHARA%は\@ CFLAG:CHARA:性格傾向 == 4 ? 律儀にお礼を言っている # ばつの悪そうな顔をした \@。
				ENDDATA
			ENDIF
		ENDIF
	CASE "中断"
		;中断メッセージはうふふ突入時等でも表示されてしまうので表示しない
		;説教する側がいなくなった場合の処理
		IF CHARA == 30
			FOR COUNT, 1, CHARANUM
				SIF Activity_Type:COUNT != 15
					CONTINUE
				CALL FA15_RESET(COUNT)
			NEXT
		ENDIF
	CASE "付き合う"
		;自由行動に付き合ったとき用メッセージ
		PRINTFORML %CALLNAME:PLAYER%はなんとなく説教を聞いてみた。
		IF TFLAG:193
			PRINTFORML 映姫に見つかって説教されてしまった…
		ELSE
			LOCALS = %\@ CHARA == 30 ? %FA15_TARGET(1)% # %CALLNAME:CHARA% \@%
			PRINTDATAL
				DATAFORM ……いつ終わるのか見当もつかない。
				DATAFORM ……自分ではなくて本当に良かった。
				DATAFORM ……運の悪い%LOCALS%にちょっと同情してしまった。
			ENDDATA
		ENDIF
	CASE "ソース"
		;付き合うコマンド選択時の入手ソース、時間係数は付き合った時間の1/10
		;ソースの基準よくわかんないので適当
		DOWNBASE:MASTER:気力 += 20 * 時間係数
		;15%でいっしょに説教されて入手ソース増/MASTER消費気力激増
		IF !RAND:6
			TFLAG:193 = 1
			SOURCE:30:歓楽 = (100 + ((ABL:30:親密 + 好感度係数(TARGET)) * 40)) * 時間係数
			SOURCE:30:征服 = (60 + (ABL:30:サドっ気 * 60)) * 時間係数
			IF TARGET != 30
				SOURCE:TARGET:受動 = (30 + ((ABL:従順 + 好感度係数(TARGET)) * 50)) * 時間係数
				SOURCE:TARGET:屈従 = (30 + ((ABL:従順 + 好感度係数(TARGET)) * 50)) * 時間係数
				DOWNBASE:TARGET:気力 += 10 * 時間係数
			ENDIF
			DOWNBASE:MASTER:気力 += 150 * 時間係数
		ELSE
			SOURCE:歓楽 = (50 + ((ABL:親密 + 好感度係数(TARGET)) * 30)) * 時間係数
			SOURCE:征服 = (20 + (ABL:サドっ気 * 50)) * 時間係数
			IF TARGET != 30
				SOURCE:TARGET:受動 = (20 + ((ABL:従順 + 好感度係数(TARGET)) * 20)) * 時間係数
				SOURCE:TARGET:屈従 = (20 + ((ABL:従順 + 好感度係数(TARGET)) * 20)) * 時間係数
				DOWNBASE:TARGET:気力 += 10 * 時間係数
			ENDIF
		ENDIF
ENDSELECT
;説教相手を表示する式中関数
@FA15_TARGET(ARG)
#FUNCTIONS
#DIM 累計
#DIMS モブ
#DIMS ネームド
;説教開始は仕様上常にモブキャラ
IF !ARG
	SELECTCASE GET_MAPID(CFLAG:30:現在位置)
		CASE 0
			モブ = 妖怪
		CASE 1
			モブ = 参拝客
		CASE 2
			モブ = %TEXTR("町娘/村人/")%
		CASE 3
			モブ = %TEXTR("メイド妖精/ホフゴブリン/")%
		CASE 4
			モブ = イナバ
		CASE 5
			モブ = 化け%TEXTR("猫/狸/狐/")%
		CASE 6
			モブ = %TEXTR("ゾンビフェアリー/死神/")%
		CASE 7
			モブ = 河童
		CASE 8
			モブ = %TEXTR("参拝客/天狗/山童/")%
			SIF INRANGE(CFLAG:30:現在位置, 830, 850)
				モブ = 天人
		CASE 9
			モブ = 鬼
		CASE 10
			モブ = %TEXTR("玉兎/月人/")%
	ENDSELECT
	LOCALS = %TEXTR("通りすがりの/目が合った/その辺にいた/運悪く逃げ遅れた")%%モブ%
	RETURNF LOCALS
;映姫様と同じ場所にいて自由行動=15且つ行動時間中のキャラが説教され中
ELSE
	ネームド =
	累計 = 0
	FOR COUNT, 1, CHARANUM
		SIF COUNT == 30
			CONTINUE
		SIF CFLAG:30:現在位置 != CFLAG:COUNT:現在位置
			CONTINUE
		SIF Activity_Type:COUNT != 15 || !Activity_Duration:COUNT
			CONTINUE
		ネームド += @"%\@ 累計 ? と # \@%%CALLNAME:COUNT%"
		累計 ++
	NEXT
	SIF 累計
		RETURNF ネームド
	RETURNF モブ
ENDIF
;集合等での強制中断処理
@FA15_RESET(ARG)
Activity_Type:ARG = 0
Activity_Duration:ARG = 0
RETURN

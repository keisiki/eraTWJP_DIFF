;FileName_SLEEP.ERB ----------------------------- Rev1.00
;キャラ睡眠処理
;CALL		USER
;ARG		0:キャラNO, 1:MESSAGE出力要否
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CHARA_SLEEP, ARG ,ARG:1
#DIM 先客
SIF TALENT:ARG:妄想の産物
	RETURN 0
SIF TCVAR:ARG:泥酔
	RETURN 0

VARSET LOCAL
IF CAN_SEE(ARG) && !CFLAG:MASTER:睡眠 && !CFLAG:ARG:ブチギレ
	IF (IN_HOME(CFLAG:MASTER:現在位置) && CFLAG:ARG:現在位置 == CFLAG:ARG:初期位置 && CFLAG:ARG:現在位置 != MAXROOM) || CFLAG:MASTER:現在位置 == OMANEKIBEYA()
		;寝てる場合
		IF CFLAG:ARG:睡眠
			IF TALENT:ARG:恋慕 && CFLAG:ARG:添い寝中 && ARG:1
				PRINTDATA
					DATAFORM %CALLNAME:ARG%は幸せそうに微笑んでいる
					DATAFORM %CALLNAME:ARG%は小さく%CALLNAME:MASTER%の名前を呟いた
					DATAFORM %CALLNAME:ARG%はすやすやと眠っている
					DATAFORM %CALLNAME:ARG%は無意識に%CALLNAME:MASTER%に抱きついた
				ENDDATA
				PRINTW
			ELSE
				SIF ARG:1
					PRINTFORMW %CALLNAME:ARG%は眠っている
			ENDIF
			LOCAL = 3
		ELSEIF TALENT:ARG:恋慕 && CFLAG:ARG:添い寝中
			IF ARG:1
				PRINTDATA
					DATAFORM %CALLNAME:ARG%は幸せそうに微笑んでいる
					DATAFORM %CALLNAME:ARG%は小さくあくびをした
					DATAFORM %CALLNAME:ARG%は頬を染め%CALLNAME:MASTER%を見つめている
					DATAFORM %CALLNAME:ARG%はそっと%CALLNAME:MASTER%に寄り添った
				ENDDATA
			ENDIF
			LOCAL = 2
			PRINTW
		ELSEIF CFLAG:ARG:現在位置 == CFLAG:ARG:前ターン位置
			IF ARG:1
				IF CFLAG:ARG:ディレイ
					PRINTFORMW %CALLNAME:ARG%は眠そうにしている
				ELSE
					PRINTFORMW %CALLNAME:ARG%は眠気に負け、眠りについた…
					IF CFLAG:ARG:うふふ
						LOCAL:3 = FINDCHARA(CFLAG:うふふ, CFLAG:ARG:うふふ, 1)
						WHILE INRANGE(LOCAL:3, 1, CHARANUM - 1)
							SIF LOCAL:3 != ARG
								CALL ENDUFUFU(LOCAL:3)
							SIF LOCAL:3 == CHARANUM - 1
								BREAK
							LOCAL:3 = FINDCHARA(CFLAG:うふふ, CFLAG:ARG:うふふ, LOCAL:3 + 1)
						WEND
						CALL ENDUFUFU(ARG)
						TFLAG:106 = 0
						CFLAG:MASTER:うふふ = 0
					ENDIF
				ENDIF
			ENDIF
			SIF !CFLAG:ARG:ディレイ
				LOCAL = 9
		ELSE
			SIF ARG:1
				PRINTFORMW %CALLNAME:ARG%は眠そうにしている
			CALL 寝室から追い出し(ARG)
			LOCAL = 2
		ENDIF
		;隠密中セット
		SIF CFLAG:MASTER:現在位置 != CFLAG:MASTER:前ターン位置
			CALL SET_SNEAK(1,ARG,1)
	;寝てない場合
	ELSEIF 睡眠時間(ARG) && CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置 && CFLAG:ARG:現在位置 == CFLAG:ARG:前ターン位置 && !TCVAR:ARG:休憩中
		IF CFLAG:ARG:神社在住
			CALL SLEEP_RESIDENT(ARG)
		ELSE
			CALL SLEEP_VISITOR(ARG)
		ENDIF
		LOCAL = RESULT
		IF CFLAG:ARG:うふふ
			LOCAL:3 = FINDCHARA(CFLAG:うふふ, CFLAG:ARG:うふふ, 1)
			WHILE INRANGE(LOCAL:3, 1, CHARANUM - 1)
				SIF LOCAL:3 != ARG
					CALL ENDUFUFU(LOCAL:3)
				SIF LOCAL:3 == CHARANUM - 1
					BREAK
				LOCAL:3 = FINDCHARA(CFLAG:うふふ, CFLAG:ARG:うふふ, LOCAL:3 + 1)
			WEND
			CALL ENDUFUFU(ARG)
			TFLAG:106 = 0
			CFLAG:MASTER:うふふ = 0
		ENDIF
		LOCAL:6 ++
	;相手が自室に帰っていく
	ELSE
		LOCAL = 1
	ENDIF
	;おやすみ口上or帰宅口上
	IF (CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置 || (睡眠時間(ARG) && CFLAG:ARG:現在位置 == CFLAG:ARG:神社在住)) && !LOCAL:6
		;CALL KOJO_EVENT(7, ARG, LOCAL)
		IF CFLAG:ARG:神社在住 || CFLAG:MASTER:現在位置 == OMANEKIBEYA() || TCVAR:ARG:休憩中
			CALL KOJO_MESSAGE_SEND("EVENT",3,ARG,LOCAL,0)
		ELSE
			LOCAL:2 = 1
			SIF CHK_DATENOW(CFLAG:デート中)
				LOCAL:2 = 2
			CALL KOJO_MESSAGE_SEND("EVENT",20,ARG,LOCAL:2,0)
		ENDIF
	ENDIF
ENDIF
;宴会中に寝たキャラは宴会フラグが外れる
IF GROUPMATCH(CFLAG:ARG:職種, 48, 49, 50, 51)
	CFLAG:ARG:宴会参加 = 0
	CFLAG:ARG:職種 = 0
	CFLAG:ARG:行動 = 0
	BASE:ARG:仕事量 = 0
ENDIF
SIF BASE:ARG:仕事量 <= 0 && CFLAG:ARG:行動 == 5
	CALL CHARA_JOBEND(ARG)
;仕事中にぶっ倒れたらお仕事終了
IF CFLAG:ARG:衰弱 && CFLAG:ARG:行動 == 5
	CFLAG:ARG:行動 = 0
	BASE:ARG:仕事量 = 0
ENDIF


;自室で寝るべく戻ってくる
IF CFLAG:ARG:現在位置 != CFLAG:ARG:初期位置 && !CFLAG:ARG:睡眠 && !CFLAG:ARG:出禁
	IF CFLAG:MASTER:現在位置 == CFLAG:ARG:初期位置
		PRINTFORML %CALLNAME:ARG%が部屋に戻ってきた
		IF TALENT:ARG:恋慕 || (CFLAG:ARG:初期位置 == 15 && CAN_ENTRY_REIMUROOM())
			LOCAL:4 = 0
			LOCAL:5 = 0
			IF CFLAG:MASTER:うふふ
				LOCAL:3 = FINDCHARA(CFLAG:うふふ, CFLAG:MASTER:うふふ, 1)
				WHILE INRANGE(LOCAL:3, 1, CHARANUM - 1)
					CALL ENDUFUFU(LOCAL:3)
					LOCAL:4 ++
					SIF LOCAL:3 == CHARANUM - 1
						BREAK
					LOCAL:3 = FINDCHARA(CFLAG:うふふ, CFLAG:MASTER:うふふ, LOCAL:3 + 1)
				WEND
				TFLAG:106 = 0
				CFLAG:MASTER:うふふ = 0
				IF !RAND:2
					LOCAL:3 = FINDCHARA(CFLAG:現在位置, CFLAG:MASTER:現在位置,1)
					SIF LOCAL:3 == ARG
						LOCAL:3 = FINDLASTCHARA(CFLAG:現在位置, CFLAG:MASTER:現在位置,1)
				ELSE
					LOCAL:3 = FINDLASTCHARA(CFLAG:現在位置, CFLAG:MASTER:現在位置,1)
					SIF LOCAL:3 == ARG
						LOCAL:3 = FINDCHARA(CFLAG:現在位置, CFLAG:MASTER:現在位置,1)
				ENDIF
				PRINTFORML 唇をとがらせる%CALLNAME:ARG%に、%CALLNAME:MASTER%は渋々%CALLNAME:(LOCAL:3)%\@ LOCAL:4 > 1 ? 達 #  \@から離れた
				LOCAL:5 = 2
			ENDIF
			PRINTL 
			CALL 寝起き着替え(ARG)
			;PRINTFORMW %CALLNAME:ARG%は着替えると布団に潜った…
			CALL KOJO_MESSAGE_SEND("EVENT",3,ARG,7,LOCAL:5)
		ELSE
			PRINTFORMW 眠そうな%CALLNAME:ARG%に追い出された…
			CALL KOJO_MESSAGE_SEND("EVENT",3,ARG,7,1)
			;忍び込みOFF
			CALL SET_SNEAK(0,LOCAL,2)
			;イタズラOFF
			CALL SET_PRANK(0,LOCAL,2)
			FOR LOCAL:1,0,CHARANUM
				CALL ENDUFUFU(LOCAL:1)
			NEXT
			TFLAG:106 = 0
			;移動可能な場所へ移動
			CALL GETOUT(MASTER)
			;遠距離移動フラグOFF
			TFLAG:195 = 0
		ENDIF
	ENDIF
	;寝間着にお着替え

	CALL CTRL_CLOTHES_SET(ARG, "現在衣装の変更_パジャマ")
	CALL 下着確認リセット(ARG)
	CALL CLOTHES_SETTING_TRAIN(ARG)
ENDIF

;自室でねる
IF !TCVAR:ARG:休憩中
	CFLAG:ARG:現在位置 = CFLAG:ARG:初期位置
	DEBUGPRINTFORML %CALLNAME:ARG%は{TIME}に{CFLAG:ARG:初期位置}で寝たよ
ENDIF
SIF LOCAL == 5 && CFLAG:ARG:初期位置 == 54 && TALENT:MASTER:100 >= -4
	CFLAG:MASTER:現在位置 = 9
SIF LOCAL == 5
	CFLAG:MASTER:現在位置 = CFLAG:ARG:初期位置
;施錠
SIF GET_MAPID(CFLAG:ARG:初期位置) == MAIN_MAP && CFLAG:MASTER:初期位置 != CFLAG:ARG:初期位置
	LOCK:(CFLAG:ARG:初期位置) = 1
CFLAG:ARG:同行 = 0
CALL SET_SLEEP(1,ARG,0)
SIF CFLAG:ARG:睡眠
	CALL SET_DERAY(99,ARG)
;FileName_SLEEP.ERB ----------------------------- Rev1.00
;キャラ起床処理
;CALL		USER
;ARG		0:キャラNO, 1:施錠解除有無, 2:睡眠姦からの起床かどうか
;起床場所	数値1なら同棲、数値0なら非同棲
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CHARA_AWAKE, ARG,ARG:1,ARG:2
#DIM 起床場所
起床場所 = 0
IF CFLAG:ARG:睡眠 && CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置 && CFLAG:ARG:起床時間 <= TIME
	SIF ARG > 0 && !CFLAG:MASTER:睡眠 && !ARG:2
		PRINTFORMW %CALLNAME:ARG%が目を覚ました
	TCVAR:ARG:睡眠深度 = 0
	;睡眠姦からの起床ならスルー
	IF !ARG:2
		IF !IS_LOVER(ARG)
			PRINTFORML %CALLNAME:MASTER%は、%CALLNAME:ARG%の着替えが済むまでの間、一旦、外に追い出された
			;おはよう口上
			IF !CFLAG:ARG:面識
				CALL KOJO_MESSAGE_SEND("ENCOUNTER",0,ARG,2,0)
			ELSEIF !ARG:1 && !CFLAG:MASTER:睡眠
				CALL KOJO_MESSAGE_SEND("EVENT",2,ARG,4,0)
			ENDIF
		ELSE
			SIF CFLAG:MASTER:初期位置 == CFLAG:ARG:初期位置
				起床場所 = 1
			;おはよう口上
			IF !CFLAG:ARG:面識
				CALL KOJO_MESSAGE_SEND("ENCOUNTER",0,ARG,2,0)
			ELSEIF !ARG:1 && !CFLAG:MASTER:睡眠
				CALL KOJO_MESSAGE_SEND("EVENT",2,ARG,4,起床場所)
			ENDIF
			IF GETBIT(CFLAG:ARG:着替え実行, 1) && !CFLAG:MASTER:睡眠
				CALL 寝起き着替え(ARG)
			ENDIF
		ENDIF
		CALL CTRL_CLOTHES_SET(ARG, "現在衣装の変更_普段着")
		CALL 下着確認リセット(ARG)
		CALL CLOTHES_SETTING_TRAIN(ARG)
	ENDIF
ENDIF

;ディレイSET
IF CFLAG:ARG:睡眠
	CALL SET_DERAY(2,ARG)
ENDIF

CALL SET_SLEEP(0,ARG,0)

;各フラグOFF
CALL SET_PRANK(0,ARG,2)
CALL SET_SNEAK(0,ARG,2)
;CALL SET_TOGETHER(0,ARG,0)
;施錠
SIF ARG:1 == 0 && GET_MAPID(CFLAG:ARG:初期位置) == MAIN_MAP
	LOCK:(CFLAG:ARG:初期位置) = 0
CFLAG:ARG:休日移動済 = 0


@SLEEP_RESIDENT(ARG)
IF CHK_DATENOW(CFLAG:ARG:デート中)
	PRINTFORML %CALLNAME:ARG%が眠そうなので帰宅します
	CALL SET_DATE(99,ARG)
	CALL 帰宅の時間経過
	CFLAG:ARG:現在位置 = CFLAG:ARG:神社在住
	CFLAG:MASTER:現在位置 = CFLAG:ARG:神社在住
	TCVAR:ARG:休憩中 = 1
	IF TALENT:ARG:恋慕
		CFLAG:MASTER:現在位置 = CFLAG:ARG:神社在住
	ELSE
		CFLAG:MASTER:現在位置 = CFLAG:MASTER:初期位置
	ENDIF
ELSEIF (TALENT:ARG:恋慕 && AT_HOME(MASTER) && CFLAG:ARG:衰弱) && CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置
	PRINTFORM %CALLNAME:ARG%を
	PRINTFORMS NAME_FROM_PLACE(CFLAG:ARG:現在位置)
	PRINT から
	PRINTFORMS NAME_FROM_PLACE(CFLAG:ARG:初期位置)
	PRINTW へ運びました
	CFLAG:ARG:現在位置 = CFLAG:ARG:神社在住
	CFLAG:MASTER:現在位置 = CFLAG:ARG:神社在住
	TCVAR:ARG:休憩中 = 1
	RESULT = 5
	RETURN RESULT
ELSEIF CFLAG:ARG:現在位置 != CFLAG:ARG:初期位置
	PRINTFORM %CALLNAME:ARG%は
	PRINTFORMS NAME_FROM_PLACE(CFLAG:ARG:現在位置)
	PRINT から
	PRINTFORMS NAME_FROM_PLACE(CFLAG:ARG:初期位置)
	PRINTW に戻りました
	CFLAG:ARG:現在位置 = CFLAG:ARG:神社在住
	;MASTERついてく必要ないよね？
	;CFLAG:MASTER:現在位置 = CFLAG:ARG:神社在住
	TCVAR:ARG:休憩中 = 1
	RESULT = 1
	RETURN RESULT
ENDIF

@SLEEP_VISITOR(ARG)
IF CHK_DATENOW(CFLAG:ARG:デート中) && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
	CALL KOJO_MESSAGE_SEND("EVENT",20,ARG,2,0)
	PRINTFORMW %CALLNAME:ARG%は\@ CFLAG:ARG:衰弱 ? 疲れた # 眠くなった\@と言って帰っていきました
	CFLAG:MASTER:同行 = 0
	CFLAG:ARG:同行 = 0
	CFLAG:ARG:デート中 = MAIN_MAP
	FLAG:デート相手 = 0
	TFLAG:デート前好感度 = 0
	;日常ON
	TFLAG:102 = 1
	;挿入解除
	TFLAG:106 = 0
ELSEIF TALENT:ARG:恋慕 && AT_HOME(MASTER) && CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置
	SIF 睡眠時間(ARG) && !CFLAG:ARG:衰弱
		GOTO 神社から帰る
	PRINTFORML %CALLNAME:ARG%は疲れ切っている…
	CALL ASK_YN("参道まで送るよ","ちょっと休んでいきなよ")
	IF RESULT == 0
		CFLAG:MASTER:現在位置 = GET_ENTRANCE(MAIN_MAP)
		;CALL KOJO_MESSAGE_SEND("EVENT",20,ARG,1,0)
		GOTO 神社から帰る
	ELSE
		PRINTFORM %CALLNAME:ARG%を
		PRINTFORMW %NAME_FROM_PLACE(デフォルト初期位置)%へと運びました
		CFLAG:ARG:同行 = 0
		FLAG:デート相手 = 0
		TFLAG:デート前好感度 = 0
		CFLAG:ARG:デート中 = MAIN_MAP
		CFLAG:ARG:現在位置 = デフォルト初期位置
		TCVAR:ARG:休憩中 = 1
		CFLAG:MASTER:現在位置 = デフォルト初期位置
		SIF デフォルト初期位置 == 小人の虫かご && TALENT:MASTER:体型 > -5
			CFLAG:ARG:現在位置 = 縁側
		RESULT = 8
		RETURN RESULT
	ENDIF
ELSEIF CFLAG:MASTER:現在位置 == OMANEKIBEYA()
	IF CFLAG:MASTER:イタズラ == 2
		PRINTFORMW %CALLNAME:ARG%に無茶をしすぎたようだ、目を覚ます前に撤収しよう
	ELSE
		PRINTFORMW %CALLNAME:ARG%は疲れたと言って、横になりました
	ENDIF
	TCVAR:ARG:休憩中 = 1
	RESULT = 5
	RETURN RESULT
ELSE
	$神社から帰る
	CALL KOJO_MESSAGE_SEND("EVENT",20,ARG,1,0)
	PRINTFORMW 疲れきった%CALLNAME:ARG%は帰って行きました
	IF CHK_DATENOW(CFLAG:ARG:デート中)
		CFLAG:ARG:同行 = 0
		FLAG:デート相手 = 0
		TFLAG:デート前好感度 = 0
		CFLAG:ARG:デート中 = MAIN_MAP
		CFLAG:ARG:拠点来訪 = 0
	ENDIF
	CFLAG:ARG:現在位置 = CFLAG:ARG:初期位置
ENDIF

@CAN_ENTRY_REIMUROOM(ARG)
#FUNCTION
SIF TALENT:1:恋慕 || TALENT:57:恋慕 || TALENT:71:恋慕 || TALENT:1:愛欲 || TALENT:57:愛欲 || TALENT:71:愛欲
	RETURNF 1
RETURNF 0
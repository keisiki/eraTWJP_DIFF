;--------------------------------------------------
;地点名変更関数
;--------------------------------------------------
@MAP_PLACENAME_6(ARG)
#FUNCTIONS
IF GET_MAPID(ARG) == MAIN_MAP
	SELECTCASE ARG
		CASE P618空き部屋, P625空室, P634空部屋
			IF CFLAG:MASTER:初期位置 == ARG
				RETURNF CALLNAME:MASTER + "私室"
			ELSE
				RETURNF "空き部屋"
			ENDIF
		CASE SUMIKOMI_ROOM
			;※このマップでは未実装
			IF FLAG:住み込みキャラ
				RETURNF CALLNAME:(FLAG:住み込みキャラ) + "の部屋"
			ELSE
				RETURNF "空き部屋"
			ENDIF
		CASE MINROOM() TO (MAXROOM - 1)
			RETURNF STR:(ARG + 8000)
		CASE OMANEKIBEYA()
			RETURNF OMANEKI_PLACE()
		CASE SUKIMA()
			RETURNF "スキマ空間"
		CASE MAXROOM
			RETURNF "道の向こう"
		CASEELSE
			RETURNF "不明"
	ENDSELECT
ELSE
	RETURNF "冥界の外"
ENDIF

;--------------------------------------------------
;場所ARGがどのエリアにあるか
;表示するマップの番号と連動（TFLAG:マップ切り替え）
;ARG     場所ID
;--------------------------------------------------
@MAP_AREASELECT_6(ARG)
#FUNCTION
SELECTCASE ARG
	;是非曲直庁
	CASE P610是非曲直庁ホール TO P618空き部屋
		RETURNF 2
	;白玉楼
	CASE P620冥界の門 TO P629西行妖
		RETURNF 3
	;八雲邸
	CASE P630八雲邸玄関 TO P639八雲邸居間
		RETURNF 4
	;畜生界
	CASE P640異界の関所 TO P655尤魔宅の風呂
		RETURNF 5
	;彼岸
	CASEELSE
		RETURNF 1
ENDSELECT

;--------------------------------------------------
;内部構造がある場所の内部の判定
;TFLAG:マップ切り替え  内部察知が必要なマップ番号
;                P_ID  表示ボタンの場所ID
;                Q_ID  判定する内部の場所ID,必ずP_IDと一致した数値も含める
;--------------------------------------------------
@GETMAP_BUILDING_6(P_ID, Q_ID)
#FUNCTION
#DIM P_ID
#DIM Q_ID
IF TFLAG:マップ切り替え == 1
	SELECTCASE P_ID
		;是非曲直庁
		CASE P610是非曲直庁ホール
			SELECTCASE Q_ID
			CASE P610是非曲直庁ホール TO P618空き部屋
				RETURNF 1
			ENDSELECT
		;冥界
		CASE P620冥界の門
			SELECTCASE Q_ID
			CASE P620冥界の門 TO P629西行妖
				RETURNF 1
			ENDSELECT
		;八雲邸
		CASE P630八雲邸玄関
			SELECTCASE Q_ID
			CASE P630八雲邸玄関 TO P639八雲邸居間
				RETURNF 1
			ENDSELECT
		;畜生界
		CASE P640異界の関所
			SELECTCASE Q_ID
			CASE P640異界の関所 TO P655尤魔宅の風呂
				RETURNF 1
			ENDSELECT
	ENDSELECT
ENDIF
RETURNF 0

;--------------------------------------------------
;移動制限設定
;ARG    場所ID
;CHARA  移動するキャラID
;RETURNF 1=移動できる,0=移動できない
;TFLAG:移動不能メッセージ   ↓の@MAP_CANNOT_WORD用の設定
;--------------------------------------------------
@MAP_CAN_MOVE_6(ARG, CHARA)
#FUNCTION
#DIM CHARA
#DIM 家主
SELECTCASE ARG
	;各組長の部屋
	CASE P646八千慧宅の風呂 TO P655尤魔宅の風呂
		IF GROUPMATCH(ARG, P646八千慧宅の風呂, P647八千慧宅のリビング, P648八千慧宅の寝室)
			家主 = [[八千慧]]
			LOCAL:1 = P646八千慧宅の風呂
			LOCAL:2 = P647八千慧宅のリビング
			LOCAL:3 = P648八千慧宅の寝室
		ELSEIF GROUPMATCH(ARG, P650早鬼宅のリビング, P651早鬼宅の寝室, P652早鬼宅の風呂)
			家主 = [[早鬼]]
			LOCAL:1 = P650早鬼宅のリビング
			LOCAL:2 = P651早鬼宅の寝室
			LOCAL:3 = P652早鬼宅の風呂
		ELSEIF GROUPMATCH(ARG, P653尤魔宅のリビング, P654尤魔宅の寝室, P655尤魔宅の風呂)
			家主 = [[尤魔]]
			LOCAL:1 = P653尤魔宅のリビング
			LOCAL:2 = P654尤魔宅の寝室
			LOCAL:3 = P655尤魔宅の風呂
		ENDIF
		;除外
		SIF CHARA == 家主
			RETURNF 1
		;中からは動ける
		SIF INRANGE(CFLAG:CHARA:現在位置, LOCAL:1, LOCAL:3)
			RETURNF 1
		;MAIN_MAP == 6
		SIF INRANGE(CFLAG:CHARA:初期位置, LOCAL:1, LOCAL:3)
			RETURNF 1
		;あなた
		IF CHARA == MASTER
			;同行か恋慕なら入れる（家主睡眠中も可
			IF CFLAG:家主:同行 || TALENT:家主:恋慕
				RETURNF 1
			;家主がいて起きてる
			ELSEIF INRANGE(CFLAG:家主:現在位置, LOCAL:1, LOCAL:3) && SHIRAHU(家主)
				RETURNF 1
			;家主以外の誰かがいる,この人物は寝てたりしても良い
			ELSEIF IN_ROOM_NUMBER(LOCAL:1) || IN_ROOM_NUMBER(LOCAL:2) || IN_ROOM_NUMBER(LOCAL:3)
				RETURNF 1
			ELSE
				TFLAG:移動不能メッセージ = 999
				RETURNF 0
			ENDIF
		;組長
		ELSEIF GROUPMATCH(CHARA, [[八千慧]], [[早鬼]], [[尤魔]])
			;家主以外の誰かがいる,この人物は寝てたりしても良い
			IF IN_ROOM_NUMBER(LOCAL:1) || IN_ROOM_NUMBER(LOCAL:2) || IN_ROOM_NUMBER(LOCAL:3)
				RETURNF 1
			ELSE
				RETURNF 0
			ENDIF
		;それ以外は入れない
		ELSE
			RETURNF 0
		ENDIF
	;誰かが住んでないと入れない
	CASE P618空き部屋, P625空室, P634空部屋
		IF !PRIVATEROOM:(ARG % 100) && CFLAG:MASTER:初期位置 != ARG
			TFLAG:移動不能メッセージ = 999
			RETURNF 0
		ENDIF
ENDSELECT
RETURNF 1

;--------------------------------------------------
;移動不能時に表示されるメッセージ
;--------------------------------------------------
@MAP_CANNOT_WORD_6(ARG)
RETURN
SELECTCASE TFLAG:移動不能メッセージ
	CASE 1
		PRINTFORMW ※ここにメッセージを入れる。行数+1のCLEARLINEをする
		CLEARLINE 2
ENDSELECT
REUSELASTLINE

;-----------------------------------------
;拠点専用の情景描写
;WAITはかけないように
;-----------------------------------------
@MAP_UNIQUEMESSAGE_6
SELECTCASE CFLAG:MASTER:現在位置
	CASE P604彼岸
		PRINTFORML 三途の川では時折渡し船が行き来している
	CASE P610是非曲直庁ホール
		PRINTFORML 是非曲直庁のホールだ
		PRINTFORML 造りは立派だが年季が入っており、なんとなく金がなさそうな感じがする
ENDSELECT

;-----------------------------------------
;拠点の場所IDをお出かけマップ上の場所IDに置換する関数
;ARG      対象の場所ID（拠点マップ）
;RETURNF  差し替え場所ID（お出かけマップ）
;-----------------------------------------
@MAP_SEARCH_REPLACEMENT_6(ARG)
#FUNCTION
SELECTCASE ARG
	CASE P601中有の道
		RETURNF 中有の道
	CASE P630八雲邸玄関 TO P639八雲邸居間
		RETURNF マヨヒガ
	CASE P604彼岸, P610是非曲直庁ホール TO P618空き部屋
		RETURNF 彼岸
	CASE P620冥界の門, P621庭, P629西行妖
		RETURNF 白玉楼庭
	CASE P620冥界の門 TO P628居間
		RETURNF 白玉楼
	CASE P642霊長園 TO P645磨弓の部屋
		RETURNF 霊長園
	CASE P640異界の関所 TO P655尤魔宅の風呂
		RETURNF 畜生界
	CASE P605地獄, P606日狭美の家, P607残無の家
		RETURNF 地獄
	CASEELSE
		RETURNF 三途の川
ENDSELECT
RETURNF 0

;--------------------------------------------------
;マップの紹介文
;--------------------------------------------------
@マップ紹介_6
;とりあえず5行
PRINTFORML 冥界・彼岸といった死者の世界に加え八雲一家の住むマヨイガを含むマップ。
PRINTFORML 原作では訪問にも苦労する場所ばかりの為、他拠点へのアクセスはあまり良くない。
PRINTFORML だがeraのあなたなら教養さえあれば三途の川も渡り放題、問題無く住むこともできる。
PRINTFORML 手伝える仕事が少ないことを除けば住み心地もこの世とあまり変わらないようだ。
PRINTFORML なお三途の川では積み石コンテストが行われ、参加者たちが日夜しのぎを削っている。

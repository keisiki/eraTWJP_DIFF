;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;地点の属性設定
;場所_ワンルーム   家主が不在のとき鍵がかかる,布団・屋内・厨房・勉強・休憩・飲食・プライベートが自動で付与
;場所_布団         休憩が自動で付与
;場所_洋室         屋内だけど靴を履いたまま,屋内が自動で付与
;場所_屋内         
;場所_屋根         屋外だけど屋根がある場所,屋内でも屋根でも無い場合は野外判定（旧青天井）
;場所_広い         
;場所_月見         屋内や屋根があっても月が見える場所
;場所_休憩         
;場所_風呂         
;場所_トイレ       
;場所_厨房         つまみ食いが自動で付与
;場所_訓練         
;場所_勉強         
;場所_天の声       
;場所_菜園         
;場所_水汲み       
;場所_身体測定     
;場所_物干し       風景描写用
;場所_飲食         自由行動用
;場所_つまみ食い   自由行動用
;場所_留まらない   あまりキャラが留まらない（移動しやすい）
;場所_過疎         あまりキャラが立ち入らない（移動されにくい）
;場所_プライベート 節度のあるキャラは入ってこない,主に私室と風呂・トイレ
;場所_炬燵         自由行動用
;場所_公共の場     押し倒してこない
;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
@ROOMSETTING_0
#DIM ROOM
VARSET ROOMDATA
MAXROOM = 57
ENTRANCE = P22参道
SUMIKOMI_ROOM = P16ボロ小屋
避難先 = P14トイレ
SIF GET_MAPID(拠点_酒虫) != MAIN_MAP || 拠点_酒虫 == 0
	拠点_酒虫 = P8台所
MAP_PAGES = 4
MAPNAME:1 = 博麗神社
MAPNAME:2 = 夢幻遺跡
MAPNAME:3 = 三妖精の家
MAPNAME:4 = 神社周辺

CFLAG:[[紫]]:(450 + MAIN_MAP) = P9居間
CFLAG:[[早苗]]:(450 + MAIN_MAP) = P21守矢神社分社
CFLAG:[[神奈子]]:(450 + MAIN_MAP) = P21守矢神社分社
CFLAG:[[諏訪子]]:(450 + MAIN_MAP) = P21守矢神社分社

FOR LOCAL, MINROOM(), MAXROOM
	ROOM = LOCAL % 100
	SELECTCASE LOCAL
	CASE P1鳥居
	CASE P2境内
		ROOMDATA:ROOM |= 場所_広い
		ROOMDATA:ROOM |= 場所_訓練
	CASE P3賽銭箱
		ROOMDATA:ROOM |= 場所_天の声
		ROOMDATA:ROOM |= 場所_過疎
	CASE P4本堂
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_過疎
	CASE P5手水舎
		ROOMDATA:ROOM |= 場所_水汲み
		ROOMDATA:ROOM |= 場所_留まらない
	CASE P6物置
		ROOMDATA:ROOM |= 場所_屋根
		ROOMDATA:ROOM |= 場所_過疎
	CASE P7土間
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_留まらない
	CASE P8台所
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_厨房
	CASE P9居間
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_勉強
		ROOMDATA:ROOM |= 場所_休憩
		ROOMDATA:ROOM |= 場所_飲食
		ROOMDATA:ROOM |= 場所_炬燵
	CASE P10縁側
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_休憩
		ROOMDATA:ROOM |= 場所_飲食
		ROOMDATA:ROOM |= 場所_月見
		ROOMDATA:ROOM |= 場所_物干し
	CASE P11庭
		ROOMDATA:ROOM |= 場所_広い
		ROOMDATA:ROOM |= 場所_訓練
		ROOMDATA:ROOM |= 場所_菜園
	CASE P12脱衣所
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_身体測定
		ROOMDATA:ROOM |= 場所_留まらない
		ROOMDATA:ROOM |= 場所_プライベート
	CASE P13風呂
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_風呂
		ROOMDATA:ROOM |= 場所_プライベート
	CASE P14トイレ
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_プライベート
	CASE P15霊夢私室, P54小人の虫かご, P55和室, P56あうんの棲家
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_布団
		ROOMDATA:ROOM |= 場所_プライベート
	;人が入るとワンルームが付与
	CASE P16ボロ小屋
		ROOMDATA:ROOM |= 場所_過疎
	CASE P17小さい納戸, P18押入れ, P19大きい納戸, P53納戸
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_布団
		ROOMDATA:ROOM |= 場所_プライベート
		ROOMDATA:ROOM |= 場所_過疎
	CASE P52離れ
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_布団
		ROOMDATA:ROOM |= 場所_勉強
		ROOMDATA:ROOM |= 場所_プライベート

	CASE P20本殿の裏
		ROOMDATA:ROOM |= 場所_広い
		ROOMDATA:ROOM |= 場所_訓練
	CASE P21守矢神社分社
		ROOMDATA:ROOM |= 場所_過疎
	CASE P22参道, P25鎮守の森
		ROOMDATA:ROOM |= 場所_広い
	CASE P23温泉・脱衣所
		ROOMDATA:ROOM |= 場所_屋内
		ROOMDATA:ROOM |= 場所_身体測定
		ROOMDATA:ROOM |= 場所_留まらない
	CASE P24温泉
		ROOMDATA:ROOM |= 場所_風呂

	CASE P27三妖精の家・広間
		ROOMDATA:ROOM |= 場所_洋室
		ROOMDATA:ROOM |= 場所_広い
		ROOMDATA:ROOM |= 場所_休憩
		ROOMDATA:ROOM |= 場所_飲食
		ROOMDATA:ROOM |= 場所_炬燵
	CASE P29三妖精の家・台所
		ROOMDATA:ROOM |= 場所_洋室
		ROOMDATA:ROOM |= 場所_厨房
	CASE P30三妖精の家・風呂
		ROOMDATA:ROOM |= 場所_洋室
		ROOMDATA:ROOM |= 場所_風呂
		ROOMDATA:ROOM |= 場所_プライベート
	CASE P31三妖精の家・二階廊下
		ROOMDATA:ROOM |= 場所_洋室
		ROOMDATA:ROOM |= 場所_留まらない
	CASE P32サニー私室, P33ルナ私室, P34スター私室
		ROOMDATA:ROOM |= 場所_洋室
		ROOMDATA:ROOM |= 場所_布団
		ROOMDATA:ROOM |= 場所_月見
		ROOMDATA:ROOM |= 場所_プライベート
	CASE P35三妖精の家・地下室
		ROOMDATA:ROOM |= 場所_洋室
		ROOMDATA:ROOM |= 場所_過疎

	CASE P36夢幻遺跡前
		ROOMDATA:ROOM |= 場所_留まらない
	CASE P37夢幻遺跡・一階通路, P46夢幻遺跡・二階通路
		ROOMDATA:ROOM |= 場所_洋室
		ROOMDATA:ROOM |= 場所_留まらない
	CASE P38夢幻遺跡・洗面所
		ROOMDATA:ROOM |= 場所_洋室
		ROOMDATA:ROOM |= 場所_身体測定
	CASE P39夢幻遺跡・浴室
		ROOMDATA:ROOM |= 場所_洋室
		ROOMDATA:ROOM |= 場所_風呂
		ROOMDATA:ROOM |= 場所_プライベート
	CASE P40夢幻遺跡・トイレ
		ROOMDATA:ROOM |= 場所_洋室
		ROOMDATA:ROOM |= 場所_トイレ
		ROOMDATA:ROOM |= 場所_プライベート
	CASE P41夢幻遺跡・食堂兼談話室
		ROOMDATA:ROOM |= 場所_洋室
		ROOMDATA:ROOM |= 場所_飲食
	CASE P42夢幻遺跡・調理室
		ROOMDATA:ROOM |= 場所_洋室
		ROOMDATA:ROOM |= 場所_厨房
	CASE P43夢美私室, P44ちゆり私室
		ROOMDATA:ROOM |= 場所_洋室
		ROOMDATA:ROOM |= 場所_布団
		ROOMDATA:ROOM |= 場所_勉強
		ROOMDATA:ROOM |= 場所_プライベート
		ROOMDATA:ROOM |= 場所_炬燵
	CASE P47夢幻遺跡・艦橋
	CASE P48夢幻遺跡・第一資料室, P50夢幻遺跡・第二資料室, P49夢幻遺跡・研究室
		ROOMDATA:ROOM |= 場所_洋室
		ROOMDATA:ROOM |= 場所_勉強
	CASE P51夢幻遺跡・倉庫
		ROOMDATA:ROOM |= 場所_洋室
		ROOMDATA:ROOM |= 場所_過疎
	ENDSELECT
NEXT

;-----------------------------------------
;デフォルトの初期位置と大家の設定
;-----------------------------------------
@DEFAULT_OWNER_0
デフォルト初期位置 = P53納戸
拠点_大家 = [[霊夢]]

;-----------------------------------------
;お出かけマップの設定
;ARG   対象の地点
;ARGS  判定文字引数
;-----------------------------------------
@ODEKAKEMAP_SETTING_0(ARG, ARGS)
#FUNCTION
;＼1 2 3 4 5 6 7 8 9 
;01　　　　　　　　　
;02　　　　　　　　　
;03　　　　　　　　　
;04　　　　　　　　　
;05　　　４　　　　　
;06５　　　　　　　　
;07　　　３　　　　　
;08　　　２　　　　　
;09　　　１　　　６　
SELECTCASE ARG
CASE 桜並木の鳥居
	SIF ARGS == "座標"
		RETURNF 49
	SIF GROUPMATCH(ARGS, "お弁当")
		RETURNF 1
CASE 神社境内
	SIF ARGS == "座標"
		RETURNF 48
	SIF GROUPMATCH(ARGS, "広い", "訓練")
		RETURNF 1
CASE 神社本堂
	SIF ARGS == "座標"
		RETURNF 47
	SIF GROUPMATCH(ARGS, "屋内")
		RETURNF 1
CASE 神社縁側
	SIF ARGS == "座標"
		RETURNF 45
	SIF GROUPMATCH(ARGS, "屋内", "月見", "休憩")
		RETURNF 1
CASE 妖精の大樹
	SIF ARGS == "座標"
		RETURNF 16
	SIF GROUPMATCH(ARGS, "屋内", "洋室", "月見")
		RETURNF 1
CASE 夢幻遺跡
	SIF ARGS == "座標"
		RETURNF 88
	SIF GROUPMATCH(ARGS, "屋内", "洋室", "勉強")
		RETURNF 1
ENDSELECT
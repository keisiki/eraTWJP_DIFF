;--------------------------------------------------
;	マップ内移動処理拡張パッチ付属
;	博麗神社MAP用関数
;--------------------------------------------------

@MAP_PLACENAME_0(ARG)
#FUNCTIONS
#DIM PLACEID
#DIMS HAKUNAME,25 = "鳥居","境内","賽銭箱","本堂","手水舎","物置","土間","台所","居間","縁側","庭","脱衣所","風呂","トイレ","霊夢私室","ボロ小屋","小さい納戸","押入れ","大きい納戸","本殿の裏","守矢神社分社","参道","温泉・脱衣所","温泉","鎮守の森"
#DIMS FAIRYNAME, 11 = "三妖精の家・入口","三妖精の家・広間","三妖精の家・階段","三妖精の家・台所","三妖精の家・風呂","三妖精の家・二階廊下","サニー私室","ルナ私室","スター私室","三妖精の家・地下室"
#DIMS MUGENNAME, 16 = "夢幻遺跡前","夢幻遺跡・一階通路","夢幻遺跡・洗面所","夢幻遺跡・浴室","夢幻遺跡・トイレ","夢幻遺跡・食堂兼談話室","夢幻遺跡・調理室","夢美私室","ちゆり私室","夢幻遺跡・階段","夢幻遺跡・二階通路","夢幻遺跡・艦橋","夢幻遺跡・第一資料室","夢幻遺跡・研究室","夢幻遺跡・第二資料室","夢幻遺跡・倉庫"
#DIMS NEWHAKU, 4 = "離れ","納戸","小人の虫かご","和室"
SIF FLAG:住み込みキャラ
	HAKUNAME:15 = %CALLNAME:(FLAG:住み込みキャラ)%の小屋
SIF FLAG:16
	NEWHAKU:3 = %CALLNAME:(FLAG:16)%私室
IF GET_MAPID(ARG) == MAIN_MAP
	PLACEID = ARG % 100
	SELECTCASE PLACEID
		CASE 1 TO 25
			RETURNF HAKUNAME:(PLACEID - 1)
		CASE 26 TO 35
			RETURNF FAIRYNAME:(PLACEID - 26)
		CASE 36 TO 51
			RETURNF MUGENNAME:(PLACEID - 36)
		CASE 52
			RETURNF "離れ"
		CASE 53 TO 55
			RETURNF NEWHAKU:(PLACEID - 52)
		CASE 98
			RETURNF OMANEKI_PLACE()
		CASE 99
			RETURNF "スキマ空間"
		CASEELSE
			RETURNF "参道の向こう"
	ENDSELECT
ELSE
	RETURNF "不明"
ENDIF

@MAP_VIEWING_0(AA,COLOR)
#DIMS REF AA, 0
#DIMS REF COLOR, 0
IF !TFLAG:マップ切り替え
	SELECTCASE CFLAG:MASTER:現在位置
		CASE 26 TO 35
			TFLAG:マップ切り替え = 3
		CASE 36 TO 51
			TFLAG:マップ切り替え = 2
		CASE 22 TO 25
			TFLAG:マップ切り替え = 4
		CASEELSE
			TFLAG:マップ切り替え = 1
	ENDSELECT
ENDIF
CALL DRAW_INFORMATIONLINE(@"%MAPNAME:(TFLAG:マップ切り替え)%")
PRINTFORML 
IF RECOLOREDMAPS
TRYCCALLFORM COLOREDMAP_AA_HAKUREI_{TFLAG:マップ切り替え}(AA,COLOR)
CATCH
RECOLOREDMAPS = 0
CALL DRAW_MAP
RECOLOREDMAPS = 1
RETURN
ENDCATCH
ELSE
TRYCALLFORM MAP_AA_HAKUREI_{TFLAG:マップ切り替え}(AA)
ENDIF

@MAP_CAN_MOVE_0(ARG, ARG:1)
#FUNCTION
TFLAG:移動不能メッセージ = 0
SELECTCASE ARG
	CASE IS >= 53
		;虫かご
		IF ARG == 54
			;小人じゃないと入れない
			IF TALENT:(ARG:1):体型 > -5
				TFLAG:移動不能メッセージ = 4
				RETURNF 0
			ENDIF
		ENDIF
	;三月精ハウス
	CASE 26 TO 35
		;妖精なら入れる
		SIF TALENT:(ARG:1):妖精
			RETURNF 1
		;中からは動ける
		SIF INRANGE(CFLAG:(ARG:1):現在位置,26,35)
			RETURNF 1
		;あなたは恋慕か教養LV1以上で入れる
		IF ARG:1 == MASTER
			IF TALENT:[[サニー]]:恋慕 || TALENT:[[ルナ]]:恋慕 || TALENT:[[スター]]:恋慕 || ABL:(ARG:1):教養 >= 1
				RETURNF 1
			ELSEIF INRANGE(CFLAG:MASTER:初期位置,26,35)
				RETURNF 1
			ELSE
				TFLAG:移動不能メッセージ = 2
				RETURNF 0
			ENDIF
		;それ以外は入れない
		ELSE
			RETURNF 0
		ENDIF
	;夢幻遺跡・一階通路,ここだけ教養LV3が必要（あなた以外）
	CASE 37
		;中からは出れる
		SIF INRANGE(CFLAG:(ARG:1):現在位置,37,51)
			RETURNF 1
		IF ABL:(ARG:1):教養 <= 2 && ARG:1 != MASTER
			RETURNF 0
		ELSE
			RETURNF 1
		ENDIF
	CASEELSE
		;施錠中は動けない
		IF FLAG:納戸施錠
			TFLAG:移動不能メッセージ = 1
			RETURNF 0
		ENDIF
		RETURNF 1
ENDSELECT
RETURNF 1

@MAP_CANNOT_WORD_0(ARG)
SELECTCASE TFLAG:移動不能メッセージ
	CASE 4
		PRINTFORMW とても%CALLNAME:MASTER%には入れない
	CASE 3
		PRINTW まだ行けない
	CASE 2
		PRINTFORMW 未熟な%CALLNAME:MASTER%には見つけることができない…
	CASE 1
		PRINTFORMW 鍵を開けないと
	CASEELSE
		RETURN
ENDSELECT
CLEARLINE 2
REUSELASTLINE 
RETURN

@マップ紹介_0
;とりあえず5行
PRINTFORML eratohoTWにおける元祖マップ。様々な設定を盛り込んでいる為、神社は大所帯となっている。
PRINTFORML 神社の他にも三月精の住む大樹と夢美＆ちゆりの住む夢幻遺跡ある。
PRINTFORML 基本的な施設は一通り揃っており、仕事で来訪するキャラも多い。
PRINTFORML なお、三月精の家・夢幻遺跡はいずれも教養が一定以上ないと入ることができない。
PRINTFORML また守矢三神のいずれかと同行中なら分社から守矢神社へワープできる。
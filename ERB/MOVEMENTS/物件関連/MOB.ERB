;-------------------------------------------------
;モブのいるマップを返す
;ARG=MAPID
;-------------------------------------------------
@GET_EXISTMOB(ARG)
#FUNCTION
SIF GROUPMATCH(ARG, 3, 4, 9, 10)
	RETURNF 1

;-------------------------------------------------
;お出かけ時のモブ判定
;ARG=場所ID
;-------------------------------------------------
@DATE_HITOGOMI(ARG)
#FUNCTION
SIF !ARG
	ARG = CFLAG:MASTER:現在位置
IF IN_HOME(ARG)
	RETURNF 0
ELSE
	SIF ODEKAKEMAP_SETTING(ARG, "モブ")
		RETURNF 1
ENDIF
RETURNF 0

;-------------------------------------------------
;MASTERが在宅中かつモブと同じ位置にいるならMAIN_MAPを返す
;-------------------------------------------------
@WITH_MOB()
#FUNCTION
SIF AT_HOME(MASTER) && ROOMDATA:(CFLAG:MASTER:現在位置 % 100) & 場所_モブ
	RETURNF MAIN_MAP

;-------------------------------------------------
;モブの与える汚れ値（掃除値）
;ARG=MAPID
;-------------------------------------------------
@MOB_YOGORE(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 3
		RETURNF -3
	CASE 4
		SIF !GROUPMATCH(TIME:2,1,6,7)
			RETURNF 1
	CASE 9
		SIF !GROUPMATCH(TIME:2,1,6,7)
			RETURNF 2
	CASE 10
		;玉兎はプラスマイナスゼロ
ENDSELECT

;-------------------------------------------------
;モブの基本名
;ARG=MAPID
;-------------------------------------------------
@MOB_NAME(ARG)
#FUNCTIONS
SIF !ARG
	ARG = MAIN_MAP
SELECTCASE ARG
CASE 3
	LOCALS = ホフゴブリン
CASE 4
	LOCALS = イナバ
CASE 9
	LOCALS = %ANIMALNAME()%
CASE 10
	LOCALS = 玉兎
ENDSELECT
RETURNF LOCALS

;-------------------------------------------------
;お出かけ中のモブ名
;ARG=場所ID
;-------------------------------------------------
@DATE_HITOGOMI_SYUBETU(ARG)
#FUNCTIONS
VARSET LOCALS
SIF !ARG
	ARG = CFLAG:PLAYER:現在位置
SELECTCASE GET_MAPID(ARG)
	;モブの種類
	;月
	CASE 10
		IF ARG == 月の都
			LOCALS += "月人"
		ELSE
			LOCALS += "玉兎"
		ENDIF
	;地底（街道と温泉では鬼も出てくる）
	CASE 9
		IF ARG != 地霊殿 && !RAND:3
			LOCALS += "鬼"
		ELSE
			LOCALS += "妖怪"
		ENDIF
	;妖怪の山山頂（守矢神社境内のみ希に人間）
	CASE 8
		IF ARG == 守矢神社境内 && !RAND:5
			LOCALS += "人間"
		ELSE
			LOCALS += "妖怪"
		ENDIF
	;妖怪の山山麓
	CASE 7
	;三途の川～冥界（死神、妖怪、希に人間）
	CASE 6
		IF !RAND:5
			LOCALS += "人間"
		ELSEIF !RAND:3
			LOCALS += "妖怪"
		ELSE
			LOCALS += "死神"
		ENDIF
	;魔法の森
	CASE 5
	;迷いの竹林（妖怪ウサギ、希に人間(患者)）
	CASE 4
		IF RAND:5
			LOCALS += "妖怪兎"
		ELSE
			LOCALS += "人間"
		ENDIF
	;紅魔館（お出かけ中のモブは全員少女なのでホフさんはお休み）
	CASE 3
		LOCALS += "妖精メイド"
	;人間の里（基本人間、希に妖怪）
	CASE 2
		IF RAND:5
			LOCALS += "人間"
		ELSE
			LOCALS += "妖怪"
		ENDIF
	;命蓮寺（人間と妖怪）
	CASE 1
		IF !RAND:2
			LOCALS += "人間"
		ELSE
			LOCALS += "妖怪"
		ENDIF
	;博麗神社
	CASE 0
ENDSELECT
LOCALS += "の少女"
RETURNF LOCALS

;-------------------------------------------------
;まかないイベント処理
;-------------------------------------------------
@MAKANAI
#DIM 気力回復量
#DIM 体力回復量
VARSET LOCAL
IF DISH_NAME == "" && !TFLAG:まかない && TIME:2 == 3
	;食事できない人がいるかをチェック
	FOR LOCAL, 0, CHARANUM
		SIF TCVAR:LOCAL:空腹時刻 - TIME:1 > 0 && CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
			LOCAL:1 ++
	NEXT
	IF !LOCAL:1 && !RAND:5
		TFLAG:まかない = 1
		PRINTFORML %MOB_NAME()%からまかないを勧められた
		PRINTFORML 頂きますか？
		CALL ASK_YN
		IF RESULT
			PRINTFORML %CALLNAME:MASTER%は丁重に断った
		ELSE
			SELECTCASE RAND:5
				CASE 0, 1
					PRINTFORML まあまあの味だ
					体力回復量 = 100
					気力回復量 = 300
				CASE 2, 3
					PRINTFORML 結構イケる
					体力回復量 = 150
					気力回復量 = 400
				CASE 4
					PRINTFORML これは旨い！
					体力回復量 = 300
					気力回復量 = 500
			ENDSELECT
			PRINTFORMW %CALLNAME:MASTER%\@GET_TARGETNUM() ? たち# \@は小腹を満たした
			TIME += 15
			FOR LOCAL, 0, CHARANUM
				IF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
					CALL RECOVER(LOCAL, 体力回復量, "体力")
					CALL RECOVER(LOCAL, 気力回復量, "気力")
					CALL 満腹度上昇(LOCAL, "軽食")
				ENDIF
			NEXT
		ENDIF
	ENDIF
ENDIF

;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;紅魔館のモブ設定（ホフゴブリン）
;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;-------------------------------------------------
;モブのいる場所
;-------------------------------------------------
@MOB_PLACE_3
#DIM ROOM
FOR LOCAL, MINROOM(), MAXROOM
	ROOM = LOCAL % 100
	ROOMDATA:ROOM &= ~場所_モブ
	SELECTCASE TIME:2
		CASE 2
			SIF GROUPMATCH(LOCAL, P302庭, P306裏庭, P307東テラス, P308西テラス)
				ROOMDATA:ROOM |= 場所_モブ
		CASE 3
			SIF GROUPMATCH(LOCAL, P313厨房, P314食堂, P315応接間)
				ROOMDATA:ROOM |= 場所_モブ
		CASE 4
			SIF GROUPMATCH(LOCAL, P309広間, P319二階廊下)
				ROOMDATA:ROOM |= 場所_モブ
		CASE 5
			SIF GROUPMATCH(LOCAL, P324大バルコニー, P327小バルコニー)
				ROOMDATA:ROOM |= 場所_モブ
		CASE 6
			SIF GROUPMATCH(LOCAL, P306裏庭, P328地下廊下, P329倉庫, P330使用人部屋)
				ROOMDATA:ROOM |= 場所_モブ
	ENDSELECT
NEXT
;-------------------------------------------------
;モブのメッセージ
;-------------------------------------------------
@MOB_MESSAGE_3
IF FLAG:時間停止
	PRINTFORML ホフゴブリンが停止している…
ELSEIF CFLAG:MASTER:現在位置 == P329倉庫
	PRINTFORML ホフゴブリンが猫の世話をしている…
ELSE
	PRINTFORML ホフゴブリンが働いている…
ENDIF
;-------------------------------------------------
;モブのイベント
;-------------------------------------------------
@MOB_TURNENDEVENT_3
IF !FLAG:時間停止
	IF CFLAG:MASTER:うふふ
		PRINTFORML ホフゴブリンからここは公共の空間なのでそういった行為はご遠慮くださいと言われた
		PRINTFORML %CALLNAME:MASTER%はバツの悪い思いをした
		BASE:TARGET:ムード = 0
		CALL ENDUFUFU_ALL
	ELSEIF CFLAG:MASTER:現在位置 == P313厨房
		CALL MAKANAI
	ENDIF
ENDIF

;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;永遠亭のモブ設定（イナバ）
;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;-------------------------------------------------
;モブのいる場所
;-------------------------------------------------
@MOB_PLACE_4
#DIM ROOM
FOR LOCAL, MINROOM(), MAXROOM
	ROOM = LOCAL % 100
	ROOMDATA:ROOM &= ~場所_モブ
	SELECTCASE TIME:2
		CASE 1 TO 7
			SIF GROUPMATCH(LOCAL, P407廊下, P408中廊下, P409縁側, P421庭, P422裏庭, P431兎の洞穴)
				ROOMDATA:ROOM |= 場所_モブ
	ENDSELECT
NEXT
;-------------------------------------------------
;モブのメッセージ
;-------------------------------------------------
@MOB_MESSAGE_4
IF FLAG:時間停止
	PRINTFORML イナバ達が停止している
ELSEIF GROUPMATCH(TIME:2, 1, 6, 7)
	PRINTFORML イナバ達が眠っている…
ELSEIF !DAY:3 % 10
	IF !TALENT:MASTER:性別 & 2
		PRINTFORML 発情中のイナバ達は悶々としている……
	ELSE
		PRINTFORML 発情中のイナバ達から強烈な視線を感じる……
	ENDIF
ELSE
	SELECTCASE RAND:5
		CASE 0
			PRINTFORML イナバ達がうろついている
		CASE 1
			PRINTFORML イナバ達が遊んでいる
		CASE 2
			PRINTFORML イナバ達が話し合っている
		CASE 3
			PRINTFORML イナバとイナバがごっつんこしている
		CASE 4
			PRINTFORML イナバ達が積み重なっている
	ENDSELECT
ENDIF
;-------------------------------------------------
;モブのイベント
;-------------------------------------------------
@MOB_TURNENDEVENT_4
IF !FLAG:時間停止
	IF CFLAG:MASTER:うふふ
		PRINTFORML %CALLNAME:MASTER%たちを見て周りのイナバたちがきゃあきゃあと騒ぎ出し、さらにそれを聞きつけた野次馬が集まり始めた
		PRINTFORML とても事に及ぶ雰囲気ではない……
		BASE:TARGET:ムード = 0
		CALL ENDUFUFU_ALL
	ENDIF
ENDIF
;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;地霊殿のモブ設定（ペット＆怨霊＆鬼）
;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;-------------------------------------------------
;モブのいる場所
;-------------------------------------------------
@MOB_PLACE_9
#DIM ROOM
FOR LOCAL, MINROOM(), MAXROOM
	ROOM = LOCAL % 100
	ROOMDATA:ROOM &= ~場所_モブ
	SIF GROUPMATCH(LOCAL, P904大広間, P905一階廊下, P913食堂, P914二階廊下, P915バルコニー, P923裏庭, P926中庭)
		ROOMDATA:ROOM |= 場所_モブ
	SIF GROUPMATCH(LOCAL, P931大通り, P932裏通り, P933酒場, P938鬼の溜まり場)
		ROOMDATA:ROOM |= 場所_モブ
	SIF GROUPMATCH(LOCAL, P928灼熱地獄跡, P929地獄の人工太陽, P930旧血の池地獄)
		ROOMDATA:ROOM |= 場所_モブ
NEXT
;-------------------------------------------------
;モブのメッセージ
;-------------------------------------------------
@MOB_MESSAGE_9
IF INRANGE(CFLAG:MASTER:現在位置, P928灼熱地獄跡, P930旧血の池地獄)
	IF FLAG:時間停止
		PRINTL 怨霊達が停止している
	ELSE
		IF CFLAG:うふふ
			PRINTL 怨霊が居心地悪そうにしている…
		ELSEIF !RAND:2
			PRINTL 怨霊がさまよっている
		ELSE
			PRINTL 怨霊が漂っている
		ENDIF
	ENDIF
ELSEIF INRANGE(CFLAG:MASTER:現在位置, P931大通り, P939路地裏)
	IF FLAG:時間停止
		PRINTL 鬼達が停止している
	ELSEIF GROUPMATCH(TIME:2, 1, 6, 7)
		PRINTL 鬼達が眠っている…
	ELSE
		SELECTCASE RAND:6
			CASE 0
				PRINTL 鬼達が酒盛りしている
			CASE 1
				PRINTL 鬼達がたむろしている
			CASE 2
				PRINTL 鬼達が喧嘩をしている
			CASE 3
				PRINTL 鬼達がうろついている
			CASE 4
				PRINTL 鬼達が騒いでいる
			CASE 5
				PRINTL 鬼が寝そべっている…邪魔くさい
		ENDSELECT
	ENDIF
ELSE
	IF FLAG:時間停止
		PRINTFORML %ANIMALNAME()%が停止している
	ELSEIF GROUPMATCH(TIME:2, 1, 6, 7)
		PRINTFORML %ANIMALNAME()%が眠っている…
	ELSEIF CFLAG:MASTER:現在位置 == P923裏庭
		IF !RAND:2
			PRINTFORML %ANIMALNAME()%が水浴びをしている
		ELSE
			PRINTFORML %ANIMALNAME()%が泳いでいる
		ENDIF
	ELSE
		IF !RAND:2
			PRINTFORML %ANIMALNAME()%がうろついている
		ELSE
			PRINTFORML %ANIMALNAME()%が寝そべっている…邪魔くさい
		ENDIF
	ENDIF
ENDIF
;-------------------------------------------------
;ペットの名前
;-------------------------------------------------
@ANIMALNAME()
#FUNCTIONS
SELECTCASE TIME:1 % 25
	;小型哺乳類
	CASE 0
		LOCALS = イヌ
	CASE 1
		LOCALS = ネコ
	CASE 2
		LOCALS = コアラ
	CASE 3
		LOCALS = ゴリラ
	CASE 4
		LOCALS = ジャージーデビル
	CASE 5
		LOCALS = アルマジロ
	CASE 6
		LOCALS = うりぼう
	CASE 7
		LOCALS = カピバラ
	;大型哺乳類
	CASE 8
		LOCALS = アリクイ
	CASE 9
		LOCALS = 馬
	CASE 10
		LOCALS = 牛
	CASE 11
		LOCALS = 羊
	CASE 12
		LOCALS = アルパカ
	CASE 13
		LOCALS = アザラシ
	;超大型哺乳類
	CASE 14
		LOCALS = ゾウ
	CASE 15
		LOCALS = キリン
	;鳥類
	CASE 16
		LOCALS = ひよこ
	CASE 17
		LOCALS = ペンギン
	CASE 18
		LOCALS = ハシビロコウ
	CASE 19
	;LOCALS = ゾンビフェアリー
		LOCALS = 孔雀
	CASE 20
		LOCALS = ダチョウ
	;爬虫類
	CASE 21
		LOCALS = ワニ
	CASE 22
		LOCALS = コモドドラゴン
	CASE 23
		LOCALS = ビルマニシキヘビ
	CASE 24
		LOCALS = ゾウガメ
ENDSELECT
RETURNF LOCALS
;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;月都のモブ設定（玉兎）
;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
@MOB_PLACE_10
#DIM ROOM
FOR LOCAL, MINROOM(), MAXROOM
	ROOM = LOCAL % 100
	ROOMDATA:ROOM &= ~場所_モブ
	SELECTCASE TIME:2
		CASE 2
			SIF GROUPMATCH(LOCAL, P1003玉兎街, P1004広場, P1014桃林, P1020綿月亭・門, P1023門番詰め所, P1025玉兎詰め所)
				ROOMDATA:ROOM |= 場所_モブ
		CASE 3
			SIF GROUPMATCH(LOCAL, P1003玉兎街, P1004広場, P1020綿月亭・門, P1024玉兎食堂)
				ROOMDATA:ROOM |= 場所_モブ
		CASE 4
			SIF GROUPMATCH(LOCAL, P1003玉兎街, P1004広場, P1014桃林, P1020綿月亭・門, P1023門番詰め所, P1025玉兎詰め所)
				ROOMDATA:ROOM |= 場所_モブ
		CASE 5
			SIF GROUPMATCH(LOCAL, P1003玉兎街, P1004広場, P1020綿月亭・門, P1024玉兎食堂)
				ROOMDATA:ROOM |= 場所_モブ
		CASE 6
			SIF GROUPMATCH(LOCAL, P1003玉兎街, P1004広場, P1020綿月亭・門, P1023門番詰め所, P1024玉兎食堂, P1025玉兎詰め所)
				ROOMDATA:ROOM |= 場所_モブ
		CASEELSE
			SIF GROUPMATCH(LOCAL, P1020綿月亭・門)
				ROOMDATA:ROOM |= 場所_モブ
	ENDSELECT
NEXT
;-------------------------------------------------
;モブのメッセージ
;-------------------------------------------------
@MOB_MESSAGE_10
IF FLAG:時間停止
	IF GROUPMATCH(CFLAG:MASTER:現在位置, P1020綿月亭・門, P1023門番詰め所)
		PRINTFORML 門番達が停止している
	ELSE
		PRINTFORML 玉兎達が停止している
	ENDIF
ELSE
	SELECTCASE CFLAG:MASTER:現在位置
	CASE P1003玉兎街, P1004広場
		PRINTFORML 玉兎達で賑わっている
	CASE P1014桃林
		PRINTFORML 玉兎達が訓練している
	CASE P1020綿月亭・門
		PRINTFORML 門番が見張っている
	CASE P1023門番詰め所
		PRINTFORML 門番達が休息している
	CASE P1024玉兎食堂
		PRINTFORML 玉兎達が食事をしている
	CASE P1025玉兎詰め所
		PRINTFORML 玉兎達が休息している
	ENDSELECT
ENDIF
;-------------------------------------------------
;モブのイベント
;-------------------------------------------------
@MOB_TURNENDEVENT_10
IF !FLAG:時間停止
	IF CFLAG:MASTER:うふふ
		IF GROUPMATCH(CFLAG:MASTER:現在位置, P1020綿月亭・門, P1023門番詰め所)
			PRINTFORML 門番が何事かと騒ぎ出し駆けつけてきた
			PRINTFORML とても事に及ぶ雰囲気ではない……
		ELSE
			PRINTFORML %CALLNAME:MASTER%たちを見て周りの玉兎たちがきゃあきゃあと騒ぎ出し、さらにそれを聞きつけた野次馬が集まり始めた
			PRINTFORML とても事に及ぶ雰囲気ではない……
		ENDIF
		BASE:TARGET:ムード = 0
		CALL ENDUFUFU_ALL
	ENDIF
ENDIF
@GET_EXISTMOB(ARG)
#FUNCTION
SIF GROUPMATCH(ARG, 3,4,9,10)
	RETURNF 1

;MASTERが在宅中かつモブと同じ位置にいるならMAIN_MAPを返す
@WITH_MOB()
#FUNCTION
SIF AT_HOME(MASTER) && ROOMDATA:(CFLAG:MASTER:現在位置 % 100) & 場所_モブ
	RETURNF MAIN_MAP

@MOB_YOGORE(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 3
		RETURNF -3
	CASE 4
		SIF !GROUPMATCH(TIME:2,1,6,7)
			RETURNF 1
	CASE 9
		SIF !GROUPMATCH(TIME:2,1,6,7)
			RETURNF 2
	CASE 10
ENDSELECT

@MOB_PLACE_3
FOR LOCAL,1,100
	CLEARBIT ROOMDATA:LOCAL,20
	SELECTCASE TIME:2
		CASE 2
			SIF GROUPMATCH(LOCAL,2,6,7,8,9,27)
				ROOMDATA:LOCAL |= 場所_モブ
		CASE 3
			SIF GROUPMATCH(LOCAL,13,14,15)
				ROOMDATA:LOCAL |= 場所_モブ
		CASE 4
			SIF GROUPMATCH(LOCAL,17,18,19)
				ROOMDATA:LOCAL |= 場所_モブ
		CASE 5
			SIF GROUPMATCH(LOCAL,24,25)
				ROOMDATA:LOCAL |= 場所_モブ
		CASE 6
			SIF GROUPMATCH(LOCAL,6,28,30)
				ROOMDATA:LOCAL |= 場所_モブ
			SIF LOCAL == 29 && CFLAG:50:現在位置 != 329
				ROOMDATA:LOCAL |= 場所_モブ
	ENDSELECT
NEXT

@MOB_MESSAGE_3
IF FLAG:70
	PRINTFORML ホフゴブリンが停止している…
ELSEIF CFLAG:MASTER:現在位置 == 329
	PRINTFORML ホフゴブリンが猫の世話をしている…
ELSE
	PRINTFORML ホフゴブリンが働いている…
ENDIF

@MOB_TURNENDEVENT_3
IF !FLAG:70
	IF CFLAG:MASTER:うふふ
		PRINTFORML ホフゴブリンからここは公共の空間なのでそういった行為はご遠慮くださいと言われた
		PRINTFORML %CALLNAME:MASTER%はバツの悪い思いをした
		BASE:ムード = 0
		FOR LOCAL,0,CHARANUM
			CALL ENDUFUFU(LOCAL)
		NEXT
		TFLAG:106 = 0
		;MASTERとの接触位置をリセット
		CALL TOUCH_SET(0,0,0,1)
		;はだけフラグをオフ
		TEQUIP:2 = 0
		TEQUIP:上半身はだけ状態 = 0
		;キャラ全員の服再セッティング
		FOR LOCAL,0,CHARANUM
			CALL CLOTHES_SETTING_TRAIN(LOCAL)
			;上着脱衣済みフラグをオフ
			TEQUIP:LOCAL:5 = 0
		NEXT
	ELSEIF CFLAG:MASTER:現在位置 == 313
		CALL MAKANAI
	ENDIF
ENDIF

@MOB_PLACE_4
FOR LOCAL,1,100
	CLEARBIT ROOMDATA:LOCAL,20
	SELECTCASE TIME:2
		CASE 1 TO 7
			SIF GROUPMATCH(LOCAL,7,8,9,21,22,31)
				ROOMDATA:LOCAL |= 場所_モブ
	ENDSELECT
NEXT

@MOB_TURNENDEVENT_4
IF !FLAG:70
	IF CFLAG:MASTER:うふふ
		PRINTFORML %CALLNAME:MASTER%たちを見て周りのイナバたちがきゃあきゃあと騒ぎ出し、さらにそれを聞きつけた野次馬が集まり始めた
		PRINTFORML とても事に及ぶ雰囲気ではない……
		BASE:ムード = 0
		FOR LOCAL,0,CHARANUM
			CALL ENDUFUFU(LOCAL)
		NEXT
		TFLAG:106 = 0
		;MASTERとの接触位置をリセット
		CALL TOUCH_SET(0,0,0,1)
		;はだけフラグをオフ
		TEQUIP:2 = 0
		;キャラ全員の服再セッティング
		FOR LOCAL,0,CHARANUM
			CALL CLOTHES_SETTING_TRAIN(LOCAL)
			;上着脱衣済みフラグをオフ
			TEQUIP:LOCAL:5 = 0
		NEXT
	ELSEIF CFLAG:MASTER:現在位置 == 313
		CALL MAKANAI
	ENDIF
ENDIF

@MOB_PLACE_9
FOR LOCAL,1,100
	CLEARBIT ROOMDATA:LOCAL,20
	IF GROUPMATCH(TIME:2,1,6,7)
		SIF GROUPMATCH(LOCAL,2,3,4,5,13,14,15,23,26,28,29,30,33,38)
			ROOMDATA:LOCAL |= 場所_モブ
	ELSE
		SIF GROUPMATCH(LOCAL,2,3,4,5,13,14,15,23,24,26,28,29,30,31,32,33,38)
			ROOMDATA:LOCAL |= 場所_モブ
	ENDIF
NEXT

@MOB_PLACE_10
FOR LOCAL,1,100
	CLEARBIT ROOMDATA:LOCAL,20
	SELECTCASE TIME:2
		CASE 2
			SIF GROUPMATCH(LOCAL,3, 4, 14, 20, 23, 25)
				ROOMDATA:LOCAL |= 場所_モブ
		CASE 3
			SIF GROUPMATCH(LOCAL,3, 4, 20, 24)
				ROOMDATA:LOCAL |= 場所_モブ
		CASE 4
			SIF GROUPMATCH(LOCAL,3, 4, 14, 20, 23, 25)
				ROOMDATA:LOCAL |= 場所_モブ
		CASE 5
			SIF GROUPMATCH(LOCAL,3, 4, 20, 24)
				ROOMDATA:LOCAL |= 場所_モブ
		CASE 6
			SIF GROUPMATCH(LOCAL,3, 4, 20, 23, 24, 25)
				ROOMDATA:LOCAL |= 場所_モブ
		CASEELSE
			SIF GROUPMATCH(LOCAL,20)
				ROOMDATA:LOCAL |= 場所_モブ
	ENDSELECT
NEXT

@MOB_TURNENDEVENT_10
IF !FLAG:70
	IF CFLAG:MASTER:うふふ
		IF GROUPMATCH(CFLAG:MASTER:現在位置,20, 23)
			PRINTFORML 門番が何事かと騒ぎ出し駆けつけてきた
			PRINTFORML とても事に及ぶ雰囲気ではない……
		ELSE
			PRINTFORML %CALLNAME:MASTER%たちを見て周りの玉兎たちがきゃあきゃあと騒ぎ出し、さらにそれを聞きつけた野次馬が集まり始めた
			PRINTFORML とても事に及ぶ雰囲気ではない……
		ENDIF
		BASE:ムード = 0
		FOR LOCAL,0,CHARANUM
			CALL ENDUFUFU(LOCAL)
		NEXT
		TFLAG:106 = 0
		;MASTERとの接触位置をリセット
		CALL TOUCH_SET(0,0,0,1)
		;はだけフラグをオフ
		TEQUIP:2 = 0
		TEQUIP:上半身はだけ状態 = 0
		;キャラ全員の服再セッティング
		FOR LOCAL,0,CHARANUM
			CALL CLOTHES_SETTING_TRAIN(LOCAL)
			;上着脱衣済みフラグをオフ
			TEQUIP:LOCAL:5 = 0
		NEXT
	ENDIF
ENDIF

@ANIMALNAME()
#FUNCTIONS
SELECTCASE TIME:1 % 25
	;小型哺乳類
	CASE 0
		LOCALS = イヌ
	CASE 1
		LOCALS = ネコ
	CASE 2
		LOCALS = コアラ
	CASE 3
		LOCALS = ゴリラ
	CASE 4
		LOCALS = ジャージーデビル
	CASE 5
		LOCALS = アルマジロ
	CASE 6
		LOCALS = うりぼう
	CASE 7
		LOCALS = カピバラ
	;大型哺乳類
	CASE 8
		LOCALS = アリクイ
	CASE 9
		LOCALS = 馬
	CASE 10
		LOCALS = 牛
	CASE 11
		LOCALS = 羊
	CASE 12
		LOCALS = アルパカ
	CASE 13
		LOCALS = アザラシ
	;超大型哺乳類
	CASE 14
		LOCALS = ゾウ
	CASE 15
		LOCALS = キリン
	;鳥類
	CASE 16
		LOCALS = ひよこ
	CASE 17
		LOCALS = ペンギン
	CASE 18
		LOCALS = ハシビロコウ
	CASE 19
	;LOCALS = ゾンビフェアリー
		LOCALS = 孔雀
	CASE 20
		LOCALS = ダチョウ
	;爬虫類
	CASE 21
		LOCALS = ワニ
	CASE 22
		LOCALS = コモドドラゴン
	CASE 23
		LOCALS = ビルマニシキヘビ
	CASE 24
		LOCALS = ゾウガメ
ENDSELECT
RETURNF LOCALS

@MAKANAI
#DIM 気力回復量
#DIM 体力回復量

VARSET LOCAL
IF DISH_NAME == "" && !TFLAG:まかない && TIME:2 == 3
	;食事できない人がいるかをチェック
	FOR LOCAL,0,CHARANUM
		SIF TCVAR:LOCAL:空腹時刻 - TIME:1 > 0 && CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
			LOCAL:1 ++
	NEXT
	IF !LOCAL:1 && !RAND:5
		TFLAG:まかない = 1
		PRINTFORML %MOB_NAME()%からまかないを勧められた
		PRINTFORML 頂きますか？
		CALL ASK_YN
		IF RESULT
			PRINTFORML %CALLNAME:MASTER%は丁重に断った
		ELSE
			SELECTCASE RAND:5
				CASE 0,1
					PRINTFORML まあまあの味だ
					体力回復量 = 100
					気力回復量 = 300
				CASE 2,3
					PRINTFORML 結構イケる
					体力回復量 = 150
					気力回復量 = 400
				CASE 4
					PRINTFORML これは旨い！
					体力回復量 = 300
					気力回復量 = 500
			ENDSELECT
			PRINTFORMW %CALLNAME:MASTER%\@GET_TARGETNUM() ? たち# \@は小腹を満たした
			TIME += 15
			FOR LOCAL,0,CHARANUM
				IF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
					CALL RECOVER(LOCAL,体力回復量,"体力")
					CALL RECOVER(LOCAL,気力回復量,"気力")
					CALL 満腹度上昇(LOCAL,"軽食")
					PRINTFORML 体力+{体力回復量}（%CALLNAME:LOCAL%）
					PRINTFORML 気力+{気力回復量}（%CALLNAME:LOCAL%）
				ENDIF
			NEXT
		ENDIF
	ENDIF
ENDIF
@MOB_NAME(ARG)
#FUNCTIONS
SIF !ARG
	ARG = MAIN_MAP
SELECTCASE ARG
CASE 3
	LOCALS = ホフゴブリン
CASE 4
	LOCALS = イナバ
CASE 9
	LOCALS = %ANIMALNAME()%
CASE 10
	LOCALS = 玉兎
ENDSELECT
RETURNF LOCALS
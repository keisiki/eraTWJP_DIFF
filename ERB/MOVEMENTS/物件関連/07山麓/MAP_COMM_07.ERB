;--------------------------------------------------
;	マップ内移動処理拡張パッチ付属
;	命蓮寺MAP用関数
;--------------------------------------------------
@MAP_PLACENAME_7(ARG)
#FUNCTIONS

IF GET_MAPID(ARG) == MAIN_MAP
	SELECTCASE ARG
		CASE 1 TO (MAXROOM - 1)
			RETURNF STR:(ARG + 8000)
		CASE 798
			RETURNF OMANEKI_PLACE()
		CASE 799
			RETURNF "スキマ空間"
		CASE MAXROOM
			RETURNF "道の向こう"
		CASEELSE
			RETURNF "不明"
	ENDSELECT
ELSE
	RETURNF "不明"
ENDIF

@MAP_VIEWING_7(AA,COLOR)
#DIMS REF AA, 0
#DIMS REF COLOR, 0
IF !TFLAG:マップ切り替え
	SELECTCASE CFLAG:MASTER:現在位置
		CASE 720 TO 725,729,730
			TFLAG:マップ切り替え = 3
		CASE 715 TO 719
			TFLAG:マップ切り替え = 2
		CASE 726,727,728
			TFLAG:マップ切り替え = 4
		CASEELSE
			TFLAG:マップ切り替え = 1
	ENDSELECT
ENDIF
CALL DRAW_INFORMATIONLINE(@"%MAPNAME:(TFLAG:マップ切り替え)%")
PRINTFORML 
IF RECOLOREDMAPS
TRYCCALLFORM COLOREDMAP_SANROKU_{TFLAG:マップ切り替え}(AA,COLOR)
CATCH
RECOLOREDMAPS = 0
CALL DRAW_MAP
RECOLOREDMAPS = 1
RETURN
ENDCATCH
ELSE
TRYCALLFORM MAP_SANROKU_{TFLAG:マップ切り替え}(AA)
ENDIF

@MAP_CAN_MOVE_7(ARG, ARG:1)
#FUNCTION
SELECTCASE ARG
	;華扇ハウス
	CASE 720 TO 730
		;本人は除外
		SIF ARG:1 == [[華扇]]
			RETURNF 1
		;中からは動ける
		SIF INRANGE(CFLAG:(ARG:1):現在位置,720,730)
			RETURNF 1
		;恋慕か住み込み
		IF ARG:1 == MASTER && (INRANGE(CFLAG:MASTER:初期位置,720,730) || TALENT:[[華扇]]:恋慕)
			RETURNF 1
		;デート相手が天子か自分が天子
		ELSEIF FLAG:デート相手 == [[天子]] || ARG:1 == [[天子]]
			RETURNF 1
		;教養が足りないとたどり着けない
		ELSEIF ABL:(ARG:1):教養 < 3
			TFLAG:移動不能メッセージ = 1
			SIF ARG:1 == MASTER
				TIME += 30
			RETURNF 0
		ELSE
			;戦闘力3以上必要
			IF ABL:(ARG:1):戦闘能力 < 3 && !FLAG:時間停止
				TFLAG:移動不能メッセージ = 2
				;CFLAG:(ARG:1):現在位置 = 731
				SIF ARG:1 == MASTER
					TIME += 15
				RETURNF 0
			ELSE
				RETURNF 1
			ENDIF
		ENDIF
;正邪宅
	CASE 733
		;あなたは恋慕があれば入れてもらえる
		IF TALENT:[[正邪]]:恋慕 && ARG:1 == MASTER
			RETURNF 1
		;本人は入れる
		ELSEIF ARG:1 == [[正邪]]
			RETURNF 1
		ELSE
			TFLAG:移動不能メッセージ = ARG
			RETURNF 0
		ENDIF
	CASEELSE
		RETURNF 1
ENDSELECT

@MAP_CANNOT_WORD_7(ARG)
SELECTCASE TFLAG:移動不能メッセージ
	CASE 1
		PRINTFORMW さっきから屋敷との距離が一向に縮まらない……
		PRINTFORMW 同じところをぐるぐる回っているかのようだ
		;CALL TARGETSET_CHACK
	CASE 2
		PRINTFORMW 庭にうずくまっていた剣呑な虎が、%CALLNAME:MASTER%の姿を認めて唸り声を上げた
		PRINTFORMW 近づかない方がよさそうだ
		;CALL TARGETSET_CHACK
	CASE 733
		PRINTFORMW どこにあるかわからない…
ENDSELECT

@マップ紹介_7
;とりあえず5行
PRINTFORML 数多くの採集ポイントと２か所の釣りポイントをも備える、山師垂涎の拠点。
PRINTFORML 間欠泉地下センターからは灼熱地獄跡に直通できる他、山頂への短絡路もある。
PRINTFORML 正邪の家や華扇の屋敷は特定の条件を満たさないと入れない。
PRINTFORML 
PRINTFORML 
;PRINTFORML 製材所と伐採ポイントの両方を有するため、製材所の営業時間内であれば、伐採→製材→伐採……
;PRINTFORML と効率よく製材を貯めていくことができる。



@OTHERREGIONS
#DIM 現在地点
#DIM 所要時間
#DIM 現在地MAPID
#DIM GOAL
#DIM ANIMATION_SEED

ANIMATION_SEED = 0

;Map Animation Code
$MAPANIMATION
LINESTART = LINECOUNT
SIF ANIMATERECOLOREDMAPS && !FLAG:70
	ANIMATION_SEED ++

IF RECOLOREDMAPS
	CALL DRAW_COLOREDMAP(ANIMATION_SEED, "GLOBAL")
ELSE
	CALL FIELDMAP
ENDIF

PRINTL どこに向かいますか？
現在地MAPID = GET_MAPID(CFLAG:MASTER:現在位置)
;	// 神社が表示されないのはちょっと寂しいので
PRINTFORM [ 0] - %"博麗神社", 18, LEFT%
IF 現在地MAPID == 0
	SETCOLOR 0 , 255, 0
	PRINTFORM 現在地\@ FLAG:70 ? %"   "% # %" "% \@
	RESETCOLOR
ELSEIF FLAG:70
	PRINTFORM 片道 {TIME_REQUIRED0(5000 + 現在地MAPID) * 3, 3, RIGHT}TSP  
ELSE
	PRINTFORM 片道 {TIME_REQUIRED0(5000 + 現在地MAPID), 3, RIGHT}分  
ENDIF
PRINTL
;	// ここまで神社表示
FOR LOCAL, 5001, 5011
	IF LOCAL == 現在地MAPID + 5000
		PRINTFORM [{現在地MAPID, 2, RIGHT}] - 
		PRINTFORM %NAME_FROM_PLACE(LOCAL), 21, LEFT%
		SETCOLOR 0 , 255, 0
		PRINTFORM 現在地\@ FLAG:70 ? %"    "% # %"  "% \@
		RESETCOLOR
		SIF !(LOCAL % 2)
			PRINTL
		CONTINUE
	ENDIF
	PRINTFORM [{LOCAL - 5000, 2, RIGHT}] - 
	PRINTFORM %NAME_FROM_PLACE(LOCAL), 18, LEFT%
	PRINTFORM 片道 \@ FLAG:70 ? {TIME_REQUIRED(現在地MAPID,LOCAL - 5000) * 3, 3, RIGHT}TSP # {TIME_REQUIRED(現在地MAPID,LOCAL - 5000), 3, RIGHT}分 \@ 
	SIF !(LOCAL % 2)
		PRINTL
NEXT
PRINT [100] - 戻る
$LOOP
;Map Animation Code
IF ANIMATERECOLOREDMAPS && !FLAG:70
	CURRENTREDRAW
	ORIREDRAW = RESULT
	REDRAW ORIREDRAW
	TINPUT ANIMATERECOLOREDMAPS,1234,0,""
ELSE
	INPUT
ENDIF

;戻る
IF RESULT == 100
	RETURN RESULT
;時間停止発動
ELSEIF RESULT == 101
	
ELSEIF RESULT == 現在地MAPID
	;全体マップからも移動できるけど余計に時間かかる,おまけ機能なので
	LOCAL:1 = 1
	現在地点 = (CFLAG:MASTER:現在位置 - (CFLAG:MASTER:デート中 * 100)) / 10
	所要時間 = ABS(現在地点 - 0) * 3
	FOR LOCAL,0,CHARANUM
		SIF CHK_DATENOW(CFLAG:LOCAL:デート中) 
			CFLAG:LOCAL:現在位置 = (CFLAG:LOCAL:デート中 * 100) + (LOCAL:1 * 10)
	NEXT
	IF FLAG:70 == 1
		BASE:MASTER:TSP -= 所要時間 * 2
		IF TFLAG:運搬
			BASE:MASTER:TSP -= 所要時間 / 3
			CFLAG:(TFLAG:運搬):現在位置 = CFLAG:MASTER:現在位置
		ENDIF
	ELSEIF GETBIT(FLAG:瞬間移動, 0) && BASE:MASTER:TSP > 10  && !FLAG:デート相手
		BASE:MASTER:TSP -= 所要時間 * 2
	ELSE
		TIME += 所要時間
		IF CHK_DATENOW(CFLAG:93:デート中) && !GETBIT (FLAG:プール, 0)
			TIME += 所要時間 / 2
			BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - 50,500)
		ENDIF
	ENDIF
	RETURN RESULT
ELSEIF RESULT == MAIN_MAP
	IF FLAG:70 && FLAG:デート相手
		PRINTFORMW 帰るなら時間停止を解除してからにしよう
		RETURN 100
	ENDIF
	CALL 外出先から帰宅
	RETURN MAIN_MAP
ELSEIF RESULT == 1234
	REDRAW 0
	CLEARLINE LINECOUNT - LINESTART
	GOTO MAPANIMATION
ELSE
	GOAL = RESULT
	所要時間 = TIME_REQUIRED(現在地MAPID, GOAL)
	;100分の道中はないんです…
	IF FLAG:デート相手 > 0 && 所要時間 >= 100
		PRINTW 遠すぎて一度には行けない
		CLEARLINE 2
		GOTO LOOP
	;自宅から出発する処理
	ELSEIF GET_MAPID(CFLAG:MASTER:現在位置) == MAIN_MAP
		LOCAL:1 = 0
		FOR LOCAL,1,CHARANUM
			SIF CFLAG:(LOCAL):同行
				LOCAL:1 ++
		NEXT
		IF LOCAL:1 == 1
			;100分の道中はないんです…
			IF 所要時間 >= 100
				PRINTW 遠すぎて一度には行けない
				CLEARLINE 2
				RETURN 100
			ENDIF
			PRINTL デートに出掛けます
			FOR LOCAL,1,CHARANUM
				IF CFLAG:(LOCAL):同行
					CALL SET_DATE(GOAL,LOCAL)
				ENDIF
			NEXT
		;複数人デート処理（未設定）
		ELSEIF LOCAL:1 > 1
		;一人
		ELSE
			IF FLAG:70 && GETBIT(FLAG:瞬間移動, 0) && BASE:MASTER:TSP >= 所要時間 * 3
				RESULT = 1
			ELSE		
				PRINTL TSPを消費して、時間経過なしで移動できます。
				PRINTL TSPを使いますか？
				CALL ASK_M("いいえ", 1, @"はい（TSP{所要時間 * 3}消費）", BASE:MASTER:TSP >= 所要時間 * 3)
			ENDIF
			IF RESULT == 0
				FLAG:70 = 0
				TIME += 所要時間
			ELSEIF RESULT == 1
				BASE:MASTER:TSP -= 所要時間 * 3
			ENDIF
			CALL SET_DATE(GOAL,0)
			RETURN GOAL
		ENDIF
		RETURN GOAL
	ELSEIF FLAG:デート相手 > 0
		PRINTFORML %GET_MAPNAME(GOAL)%へ向かいます
		CALL SET_DATE(GOAL,(FLAG:デート相手))
		RETURN GOAL
	ELSEIF GETBIT(FLAG:瞬間移動, 0) && BASE:MASTER:TSP > 所要時間 * 3 && !FLAG:デート相手
		BASE:MASTER:TSP -= 所要時間 * 3
	ELSEIF FLAG:70
		IF BASE:MASTER:TSP < 所要時間 * 3
			PRINTW TSPが足りません
			CLEARLINE 2
			GOTO LOOP
		ELSE
			BASE:MASTER:TSP -= 所要時間 * 3
		ENDIF
	ELSE
		TIME += 所要時間
		IF CHK_DATENOW(CFLAG:93:デート中) && !GETBIT (FLAG:プール, 0)
			TIME += 所要時間 / 2
			BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - 5 * (所要時間),500)
		ENDIF
	ENDIF
	FOR LOCAL,0,CHARANUM
		IF CHK_DATENOW(CFLAG:LOCAL:デート中)
			CFLAG:LOCAL:現在位置 = GET_ENTRANCE(GOAL)
			CFLAG:LOCAL:デート中 = GOAL
		ENDIF
	NEXT
	RETURN GOAL
ENDIF

@HappenToChild
#DIM YOME
#DIM CHILDNUM
SIF RAND:2
	RETURN
SIF FLAG:70
	RETURN
CALL PickRandGrownChild
YOME = RESULT
CHILDNUM = RESULT:1
VARSET RESULT
SIF YOME <= 0
	RETURN
;自立後の子供描写が発生しないときに発生する
SIF BETWEENTIME(CFLAG:YOME:起床時間, CFLAG:YOME:就寝時間)
	RETURN
SIF CHILD_AREA:YOME:CHILDNUM / 100 != CFLAG:MASTER:デート中
	RETURN
SIF FLAG:日替わりイベント >= 800 || FLAG:日替わりイベント % 10 == YOME % 10
	RETURN
LOCALS = %CHILDNAME:YOME:CHILDNUM%
PRINTFORMW 出かけ際、%LOCALS%とすれ違った
IF CHILD_LOVE:YOME:CHILDNUM < 0
	PRINTFORMW %LOCALS%は%CALLNAME:MASTER%の事に気づいていないようだった…
ELSEIF CHILD_LOVE:YOME:CHILDNUM < 200
	PRINTFORMW %LOCALS%は軽く会釈してきた
ELSE
	IF FLAG:デート相手 != YOME && FLAG:デート相手
		PRINTDATA
			DATAFORM %LOCALS%は%CALLNAME:YOME%ではない女性を連れていたことに嫌味を言ってきた…
			DATAFORM %LOCALS%は%CALLNAME:(FLAG:デート相手)%の事を鋭く睨みつけた…
			DATAFORM %LOCALS%は呆れた様子で%CALLNAME:MASTER%に文句を言ってきた…
			DATAFORM %LOCALS%は%CALLNAME:MASTER%を軽蔑的な視線で見送った…
		ENDDATA
	ELSE
		PRINTDATA
			DATAFORM %LOCALS%は手を振って挨拶してくれた…
			DATAFORM %LOCALS%は再会を歓び抱きついてきた…
			DATAFORM %LOCALS%は凛とした笑顔で見送ってくれた…
			DATAFORM %LOCALS%は高らかに%CALLNAME:MASTER%の名を呼んだ…
		ENDDATA
	ENDIF
	PRINTW 
ENDIF

;-----------------------------------------------------------
;おでかけ関数
;ARGS    呼び出し元の関数,"出掛ける"or"散策する"
;-----------------------------------------------------------
@OTHERREGIONS(ARGS)
#DIM 現在地点
#DIM 所要時間
#DIM 現在地MAPID
#DIM 目的地MAPID
#DIM ANIMATION_SEED
#DIM PREV_REDRAW
#DIM 仮デート相手
#DIM 時間停止ジャンプ
;拠点のマップ表示をリセットしておく
TFLAG:マップ切り替え = 0

ANIMATION_SEED = 0
PREV_REDRAW = CURRENTREDRAW()

;Map Animation Code
$MAPANIMATION
LINESTART = LINECOUNT
SIF ANIMATERECOLOREDMAPS && !FLAG:70
	ANIMATION_SEED ++

IF RECOLOREDMAPS
	CALL DRAW_COLOREDMAP(ANIMATION_SEED, "GLOBAL")
ELSE
	CALL FIELDMAP
ENDIF
;先にデート相手だけ決定しておく,三人以上には対応してません
仮デート相手 = 0
FOR LOCAL, 1, CHARANUM
	IF CFLAG:LOCAL:同行
		仮デート相手 = LOCAL
		BREAK
	ENDIF
NEXT

PRINTL どこに向かいますか？
現在地MAPID = GET_MAPID(CFLAG:MASTER:現在位置)
FOR 目的地MAPID, 0, MAXHOME
	IF 目的地MAPID == 現在地MAPID
		PRINTPLAINFORM [{目的地MAPID, 3}] - %GET_MAPNAME(目的地MAPID), 18, LEFT%
		CALL COLORMESSAGE(@"　現在地　　", C_GREEN, 3)
	ELSE
		IF FLAG:時間停止 || (GETBIT(FLAG:瞬間移動, 0) && !FLAG:デート相手 && !仮デート相手)
			所要時間 = TIME_REQUIRED(現在地MAPID, 目的地MAPID) * 3
			LOCALS = TSP%" "%
		ELSE
			所要時間 = TIME_REQUIRED(現在地MAPID, 目的地MAPID)
			LOCALS = 分　
		ENDIF
		PRINTFORM [{目的地MAPID, 3}] - %GET_MAPNAME(目的地MAPID), 18, LEFT%片道{所要時間, 3} %LOCALS%
	ENDIF
	SIF 目的地MAPID % 2 == 0
		PRINTL
NEXT
SIF 目的地MAPID % 2 == 1
	PRINTL
PRINT [100] - 戻る
SIF 同行人数() == 0
	PRINT 　　　　　　　　　　　　　[ 99] - 時間停止
;Map Animation Code
IF ANIMATERECOLOREDMAPS && !FLAG:70
	REDRAW PREV_REDRAW
	TINPUT ANIMATERECOLOREDMAPS,1234,0,""
ELSE
	INPUT
ENDIF

;戻る
IF RESULT == 100
	RETURN RESULT
;時間停止
ELSEIF RESULT == 99 && 同行人数() == 0
	IF FLAG:時間停止
		FLAG:時間停止 = 0
		RESETBGCOLOR
	ELSE
		FLAG:時間停止 = 1
		SETBGCOLOR C_THE_WORLD
	ENDIF
;帰宅
ELSEIF RESULT == MAIN_MAP
	IF FLAG:時間停止 && FLAG:デート相手
		PRINTFORMW 帰るなら時間停止を解除してからにしよう
	ELSE
		CALL 外出先から帰宅
		RETURN MAIN_MAP
	ENDIF
ELSEIF RESULT == 1234
	PREV_REDRAW = CURRENTREDRAW()
	REDRAW 0
	CLEARLINE LINECOUNT - LINESTART
	GOTO MAPANIMATION
ELSEIF INRANGE(RESULT, 0, MAXHOME - 1) && RESULT != 現在地MAPID
	目的地MAPID = RESULT
	所要時間 = TIME_REQUIRED(現在地MAPID, 目的地MAPID)
	時間停止ジャンプ = 0
	IF ARGS == "出掛ける"
		;一人でお出掛け
		IF 同行人数() == 0
			IF (FLAG:時間停止 || GETBIT(FLAG:瞬間移動, 0)) && BASE:MASTER:TSP >= 所要時間 * 3
				時間停止ジャンプ = 1
			ELSE
				PRINTL TSPを消費して、時間経過なしで移動できます。
				PRINTL TSPを使いますか？
				CALL ASK_M("いいえ", 1, @"はい（消費TSP {所要時間 * 3}）", BASE:MASTER:TSP >= 所要時間 * 3)
				SIF RESULT == 1
					時間停止ジャンプ = 1
			ENDIF
			CALL SET_DATE(目的地MAPID, 0, 所要時間, 時間停止ジャンプ)
			RETURN 目的地MAPID
		;二人でデート
		ELSEIF 同行人数() == 1
			PRINTFORML %GET_MAPNAME(目的地MAPID)%へデートに出掛けます
			CALL SET_DATE(目的地MAPID, 仮デート相手, 所要時間)
			RETURN 目的地MAPID
		;三人以上※未実装
		ELSE
		ENDIF
	ELSEIF ARGS == "散策する"
		IF FLAG:デート相手
			PRINTFORML %GET_MAPNAME(目的地MAPID)%へ向かいます
		ELSEIF FLAG:時間停止
			IF BASE:MASTER:TSP < 所要時間 * 3
				PRINTW TSPが足りません
				RESTART
			ELSE
				時間停止ジャンプ = 1
			ENDIF
		ELSEIF GETBIT(FLAG:瞬間移動, 0) && BASE:MASTER:TSP >= 所要時間 * 3
			時間停止ジャンプ = 1
		ENDIF
		CALL SET_DATE(目的地MAPID, FLAG:デート相手, 所要時間, 時間停止ジャンプ)
		RETURN 目的地MAPID
	ENDIF
ENDIF
RESTART

@HappenToChild
#DIM YOME
#DIM CHILDNUM
SIF RAND:2
	RETURN
SIF FLAG:70
	RETURN
CALL PickRandGrownChild
YOME = RESULT
CHILDNUM = RESULT:1
VARSET RESULT
SIF YOME <= 0
	RETURN
;自立後の子供描写が発生しないときに発生する
SIF BETWEENTIME(CFLAG:YOME:起床時間, CFLAG:YOME:就寝時間)
	RETURN
SIF CHILD_AREA:YOME:CHILDNUM / 100 != CFLAG:MASTER:デート中
	RETURN
SIF FLAG:日替わりイベント >= 800 || FLAG:日替わりイベント % 10 == YOME % 10
	RETURN
LOCALS = %CHILDNAME:YOME:CHILDNUM%
PRINTFORMW 出かけ際、%LOCALS%とすれ違った
IF CHILD_LOVE:YOME:CHILDNUM < 0
	PRINTFORMW %LOCALS%は%CALLNAME:MASTER%の事に気づいていないようだった…
ELSEIF CHILD_LOVE:YOME:CHILDNUM < 200
	PRINTFORMW %LOCALS%は軽く会釈してきた
ELSE
	IF FLAG:デート相手 != YOME && FLAG:デート相手
		PRINTDATA
			DATAFORM %LOCALS%は%CALLNAME:YOME%ではない女性を連れていたことに嫌味を言ってきた…
			DATAFORM %LOCALS%は%CALLNAME:(FLAG:デート相手)%の事を鋭く睨みつけた…
			DATAFORM %LOCALS%は呆れた様子で%CALLNAME:MASTER%に文句を言ってきた…
			DATAFORM %LOCALS%は%CALLNAME:MASTER%を軽蔑的な視線で見送った…
		ENDDATA
	ELSE
		PRINTDATA
			DATAFORM %LOCALS%は手を振って挨拶してくれた…
			DATAFORM %LOCALS%は再会を歓び抱きついてきた…
			DATAFORM %LOCALS%は凛とした笑顔で見送ってくれた…
			DATAFORM %LOCALS%は高らかに%CALLNAME:MASTER%の名を呼んだ…
		ENDDATA
	ENDIF
	PRINTW
ENDIF

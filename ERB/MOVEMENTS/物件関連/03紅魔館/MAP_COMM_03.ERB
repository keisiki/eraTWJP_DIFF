;--------------------------------------------------
;地点名変更関数
;--------------------------------------------------
@MAP_PLACENAME_3(ARG)
#FUNCTIONS
IF GET_MAPID(ARG) == MAIN_MAP
	SELECTCASE ARG
		CASE P304空き小屋
			IF CFLAG:MASTER:初期位置 == ARG
				RETURNF CALLNAME:MASTER + "の小屋"
			ELSE
				RETURNF "空き小屋"
			ENDIF
		CASE SUMIKOMI_ROOM
			IF FLAG:住み込みキャラ
				RETURNF CALLNAME:(FLAG:住み込みキャラ) + "の部屋"
			ELSE
				RETURNF "空き部屋"
			ENDIF
		CASE MINROOM() TO (MAXROOM - 1)
			RETURNF STR:(ARG + 8000)
		CASE OMANEKIBEYA()
			RETURNF OMANEKI_PLACE()
		CASE SUKIMA()
			RETURNF "スキマ空間"
		CASE MAXROOM
			RETURNF "霧の向こう"
		CASEELSE
			RETURNF "不明"
	ENDSELECT
ELSE
	RETURNF "湖の外"
ENDIF

;--------------------------------------------------
;場所ARGがどのエリアにあるか
;表示するマップの番号と連動（TFLAG:マップ切り替え）
;ARG     場所ID
;--------------------------------------------------
@MAP_AREASELECT_3(ARG)
#FUNCTION
SELECTCASE ARG
	;紅魔館・一階＆地下
	CASE P301正門 TO P316一階トイレ, P328地下廊下 TO P333大浴場
		RETURNF 2
	;紅魔館・二階＆三階
	CASE P319二階廊下 TO P327小バルコニー
		RETURNF 3
	;廃洋館
	CASE P334廃洋館・入り口 TO P342リリカ私室
		RETURNF 4
	;霧の湖
	CASEELSE
		RETURNF 1
ENDSELECT

;--------------------------------------------------
;内部構造がある場所の内部の判定
;TFLAG:マップ切り替え  内部察知が必要なマップ番号
;                P_ID  表示ボタンの場所ID
;                Q_ID  判定する内部の場所ID,必ずP_IDと一致した数値も含める
;--------------------------------------------------
@GETMAP_BUILDING_3(P_ID, Q_ID)
#FUNCTION
#DIM P_ID
#DIM Q_ID
IF TFLAG:マップ切り替え == 1
	SELECTCASE P_ID
		;紅魔館
		CASE P301正門
			SELECTCASE Q_ID
			CASE P301正門 TO P333大浴場
				RETURNF 1
			ENDSELECT
		;廃洋館
		CASE P334廃洋館・入り口
			SELECTCASE Q_ID
			CASE P334廃洋館・入り口 TO P342リリカ私室
				RETURNF 1
			ENDSELECT
	ENDSELECT
ELSEIF TFLAG:マップ切り替え == 2
	SELECTCASE P_ID
		;紅魔館・二階＆三階
		CASE P319二階廊下
			SELECTCASE Q_ID
			CASE P319二階廊下 TO P327小バルコニー
				RETURNF 1
			ENDSELECT
	ENDSELECT
ELSEIF TFLAG:マップ切り替え == 3
	SELECTCASE P_ID
		;紅魔館・一階＆地下
		CASE P309広間
			SELECTCASE Q_ID
			CASE P309広間 TO P316一階トイレ, P328地下廊下 TO P333大浴場
				RETURNF 1
			ENDSELECT
	ENDSELECT
ENDIF
RETURNF 0

;--------------------------------------------------
;移動制限設定
;ARG    場所ID
;CHARA  移動するキャラID
;RETURNF 1=移動できる,0=移動できない
;TFLAG:移動不能メッセージ   ↓の@MAP_CANNOT_WORD用の設定
;--------------------------------------------------
@MAP_CAN_MOVE_3(ARG, CHARA)
#FUNCTION
#DIM CHARA
SELECTCASE ARG
	;湖底
	CASE P348霧の湖・湖底
		;水棲でないと入れない
		IF !TALENT:CHARA:水棲
			TFLAG:移動不能メッセージ = 1
			RETURNF 0
		ENDIF
	;誰かが住んでないと入れない
	CASE P304空き小屋, P321空き部屋
		IF !PRIVATEROOM:(ARG % 100) && CFLAG:MASTER:初期位置 != ARG
			TFLAG:移動不能メッセージ = 999
			RETURNF 0
		ENDIF
ENDSELECT
RETURNF 1

;--------------------------------------------------
;移動不能時に表示されるメッセージ
;--------------------------------------------------
@MAP_CANNOT_WORD_3(ARG)
SELECTCASE TFLAG:移動不能メッセージ
	CASE 1
		PRINTFORMW 溺れてしまう…
		CLEARLINE 2
ENDSELECT
REUSELASTLINE

;-----------------------------------------
;拠点専用の情景描写
;WAITはかけないように
;-----------------------------------------
@MAP_UNIQUEMESSAGE_3
IF WITH_MOB()
	CALL MOB_MESSAGE_3
	RETURN 1
ELSEIF !FLAG:時間停止
	IF CFLAG:MASTER:現在位置 == P329倉庫
		PRINTFORML ねこさんがいる
		RETURN 1
	ENDIF
ENDIF

;-----------------------------------------
;拠点の場所IDをお出かけマップ上の場所IDに置換する関数
;ARG      対象の場所ID（拠点マップ）
;RETURNF  差し替え場所ID（お出かけマップ）
;-----------------------------------------
@MAP_SEARCH_REPLACEMENT_3(ARG)
#FUNCTION
SELECTCASE ARG
	CASE P343湖畔, P346大妖精の棲家 TO P348霧の湖・湖底
		RETURNF 霧の湖
	CASE P344レティのかまくら, P345チルノのかまくら
		RETURNF 氷のかまくら
	CASE P334廃洋館・入り口 TO P342リリカ私室
		RETURNF 廃洋館
	CASE P301正門 TO P308西テラス
		RETURNF 正門
	CASE P313厨房, P314食堂
		RETURNF 食堂
	CASE P310図書館 TO P312小悪魔私室
		RETURNF 大図書館
	CASE P328地下廊下 TO P333大浴場
		RETURNF 地下室
	CASEELSE
		RETURNF 広間
ENDSELECT
RETURNF 0

;--------------------------------------------------
;マップの紹介文
;--------------------------------------------------
@マップ紹介_3
;とりあえず5行
PRINTFORML 紅魔館・霧の湖を中心に、プリズムリバー三姉妹の住む廃洋館までを含んだエリア。
PRINTFORML 最大の特徴はここにしか存在しないカジノ。
PRINTFORML 湖の水は酒造に役立つほか釣りもでき、なかなか利便性の高い拠点。
PRINTFORML 吸血鬼の館らしく一部の住人は夜になってから活動を始めるため、必要に応じ起床時間をずらすことも必要だろう。
PRINTFORML 拠点内での仕事の手伝い先にも事欠かないが、こまめに清掃が行われるためか紅魔館の中は案外汚れない。

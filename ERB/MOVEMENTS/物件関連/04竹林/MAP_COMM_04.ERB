;--------------------------------------------------
;地点名変更関数
;--------------------------------------------------
@MAP_PLACENAME_4(ARG)
#FUNCTIONS
IF GET_MAPID(ARG) == MAIN_MAP
	SELECTCASE ARG
		CASE P412空き部屋, P428二階空き部屋, P460空き部屋
			IF CFLAG:MASTER:初期位置 == ARG
				RETURNF CALLNAME:MASTER + "私室"
			ELSE
				RETURNF "空き部屋"
			ENDIF
		CASE P442空き小屋
			IF CFLAG:MASTER:初期位置 == ARG
				RETURNF CALLNAME:MASTER + "の小屋"
			ELSE
				RETURNF "空き小屋"
			ENDIF
		CASE SUMIKOMI_ROOM
			IF FLAG:住み込みキャラ
				RETURNF CALLNAME:(FLAG:住み込みキャラ) + "の小屋"
			ELSE
				RETURNF "廃屋"
			ENDIF
		CASE MINROOM() TO (MAXROOM - 1)
			RETURNF STR:(ARG + 8000)
		CASE OMANEKIBEYA()
			RETURNF OMANEKI_PLACE()
		CASE SUKIMA()
			RETURNF "スキマ空間"
		CASE MAXROOM
			RETURNF "竹林の外"
		CASEELSE
			RETURNF "不明"
	ENDSELECT
ELSE
	RETURNF "竹林の外"
ENDIF

;--------------------------------------------------
;場所ARGがどのエリアにあるか
;表示するマップの番号と連動（TFLAG:マップ切り替え）
;ARG     場所ID
;--------------------------------------------------
@MAP_AREASELECT_4(ARG)
#FUNCTION
SELECTCASE ARG
	;永遠亭
	CASE P403待合室 TO P430地下室
		RETURNF 1
	;夢幻館
	CASE P450玄関 TO P466庭
		RETURNF 2
	;迷いの竹林
	CASEELSE
		RETURNF 3
ENDSELECT

;--------------------------------------------------
;内部構造がある場所の内部の判定
;TFLAG:マップ切り替え  内部察知が必要なマップ番号
;                P_ID  表示ボタンの場所ID
;                Q_ID  判定する内部の場所ID,必ずP_IDと一致した数値も含める
;--------------------------------------------------
@GETMAP_BUILDING_4(P_ID, Q_ID)
#FUNCTION
#DIM P_ID
#DIM Q_ID
IF TFLAG:マップ切り替え == 3
	SELECTCASE P_ID
		;夢幻館
		CASE P450玄関
			SELECTCASE Q_ID
			CASE P450玄関 TO P466庭
				RETURNF 1
			ENDSELECT
	ENDSELECT
ENDIF
RETURNF 0

;--------------------------------------------------
;移動制限設定
;ARG    場所ID
;CHARA  移動するキャラID
;RETURNF 1=移動できる,0=移動できない
;TFLAG:移動不能メッセージ   ↓の@MAP_CANNOT_WORD用の設定
;--------------------------------------------------
@MAP_CAN_MOVE_4(ARG, CHARA)
#FUNCTION
#DIM CHARA
SELECTCASE ARG
	;奥ノ院判定
	CASE P407廊下 TO P430地下室
		;除外
		SIF GROUPMATCH(CHARA, [[鈴仙]], [[てゐ]], [[輝夜]], [[永琳]])
			RETURNF 1
		;月の民も良いよね
		SIF GROUPMATCH(CHARA, [[依姫]], [[豊姫]], [[レイセン]], [[サグメ]])
			RETURNF 1
		;中からは動ける
		SIF INRANGE(CFLAG:CHARA:現在位置,P407廊下, P430地下室)
			RETURNF 1
		;あなた
		IF CHARA == MASTER
			IF 永遠亭侵入許可() || INRANGE(CFLAG:MASTER:初期位置, P401永遠亭入口, P430地下室) || FLAG:時間停止
				RETURNF 1
			ELSE
				TFLAG:移動不能メッセージ = 1
				RETURNF 0
			ENDIF
		;それ以外は入れない
		ELSE
			RETURNF 0
		ENDIF
	;誰かが住んでないと入れない
	CASE P412空き部屋, P428二階空き部屋, P460空き部屋, P442空き小屋
		IF !PRIVATEROOM:(ARG % 100) && CFLAG:MASTER:初期位置 != ARG
			TFLAG:移動不能メッセージ = 999
			RETURNF 0
		ENDIF
ENDSELECT
RETURNF 1

;--------------------------------------------------
;あなたが奥ノ院に入れる判定
;--------------------------------------------------
@永遠亭侵入許可()
#FUNCTION
SIF CFLAG:[[輝夜]]:面識
	RETURNF 1
SIF TALENT:[[鈴仙]]:恋慕 || TALENT:[[てゐ]]:恋慕 || TALENT:[[輝夜]]:恋慕 || TALENT:[[永琳]]:恋慕
	RETURNF 1
SIF GROUPMATCH(FLAG:デート相手, [[鈴仙]], [[てゐ]], [[輝夜]], [[永琳]])
	RETURNF 1
SIF GROUPMATCH(FLAG:なりきり, [[鈴仙]], [[てゐ]], [[輝夜]], [[永琳]])
	RETURNF 1
SIF CFLAG:[[鈴仙]]:同行 || CFLAG:[[てゐ]]:同行 || CFLAG:[[輝夜]]:同行 || CFLAG:[[永琳]]:同行
	RETURNF 1
SIF FLAG:宴会開催フラグ == 3 && TIME >= FLAG:開始時間
	RETURNF 1
RETURNF 0

;--------------------------------------------------
;移動不能時に表示されるメッセージ
;--------------------------------------------------
@MAP_CANNOT_WORD_4(ARG)
SELECTCASE TFLAG:移動不能メッセージ
	CASE 1
		PRINTW 親しい人がいないうちは永遠亭の奥には入れさせてもらえない…
		CLEARLINE 2
ENDSELECT
REUSELASTLINE

;-----------------------------------------
;拠点専用の情景描写
;WAITはかけないように
;-----------------------------------------
@MAP_UNIQUEMESSAGE_4
IF WITH_MOB()
	CALL MOB_MESSAGE_4
	RETURN 1
ENDIF

;-----------------------------------------
;拠点の場所IDをお出かけマップ上の場所IDに置換する関数
;ARG      対象の場所ID（拠点マップ）
;RETURNF  差し替え場所ID（お出かけマップ）
;-----------------------------------------
@MAP_SEARCH_REPLACEMENT_4(ARG)
#FUNCTION
SELECTCASE ARG
	CASE P401永遠亭入口 TO P406来客用トイレ
		RETURNF 永遠亭
	CASE P407廊下 TO P430地下室
		RETURNF 永遠亭奥ノ院
	CASE P431兎の洞穴
		RETURNF 兎の洞穴
	CASE P432影狼の家, P433斜角の竹林, P442空き小屋, P445妹紅の家
		RETURNF 斜角の竹林
	CASE P434迷いの小道, P435井戸, P440廃屋, P441温泉
		RETURNF 迷いの小道
	CASE P436無名の丘, P437メディスンの家
		RETURNF 無名の丘
	CASE P438太陽の畑, P439ラルバの家
		RETURNF 太陽の畑
	CASE P450玄関 TO P466庭
		RETURNF 夢幻館
	CASEELSE
		RETURNF 竹林入り口
ENDSELECT
RETURNF 0

;--------------------------------------------------
;マップの紹介文
;--------------------------------------------------
@マップ紹介_4
;とりあえず5行
PRINTFORML ※紹介文募集中※
PRINTFORML 竹林のみならず幽香と旧作キャラたちが暮らす夢幻館が含まれる。
PRINTFORML 永遠亭の奥へは関係者と同行するか仲が深まらないと通してもらえない。
PRINTFORML 時間停止で無断侵入できるが解除するとつまみ出される。
PRINTFORML 

;-------------------------------------------------
;日付が第〇タームか取得
;-------------------------------------------------
@GET_TERM(ARG)
#FUNCTION

SIF !ARG
	ARG = DAY:3

SIF ARG == 31
	ARG = 30

RETURNF ((ARG - 1) / 5) + 1

;-------------------------------------------------
;当該時刻の指定分後の季節・日付・時刻を取得
;RESULT:0=季節　RESULT:1=日付　RESULT:2=時刻　
;注意 指定分は1年を超えないこと
;-------------------------------------------------
@CALCULATE_ADD_TIME(季節, 日付, 時刻, 指定分)
#DIM 季節
#DIM 日付
#DIM 時刻
#DIM 指定分
#DIM 取得季節
#DIM 取得日付
#DIM 取得時刻

取得季節 = 0
取得日付 = 0
取得時刻 = 0

取得時刻 = 時刻 + 指定分
IF 取得時刻 >= 1440
	LOCAL:0 = 取得時刻
	取得時刻 %= 1440
	取得日付 += LOCAL:0 / 1440
ENDIF

取得日付 += 日付
IF 取得日付 >= 32
	LOCAL:1 = 取得日付
	取得日付 %= 32
	取得季節 += LOCAL:1 / 32
	取得日付 += LOCAL:1 / 32
ENDIF

取得季節 += 季節
IF 取得季節 >= 5
	取得季節 = 1
ENDIF

RETURN 取得季節, 取得日付, 取得時刻

;-------------------------------------------------
;当該タームの指定ターム後の季節・タームを取得
;RESULT:0=季節　RESULT:1=ターム　
;注意 指定タームは1年を超えないこと
;-------------------------------------------------
@CALCULATE_ADD_TERM(季節, ターム, 指定ターム)
#DIM 季節
#DIM ターム
#DIM 指定ターム
#DIM 取得季節
#DIM 取得ターム

取得季節 = 0
取得ターム = 0

取得ターム = ターム + 指定ターム
IF 取得ターム >= 7
	LOCAL = 取得ターム
	取得ターム %= 7
	取得季節 += LOCAL / 7
	取得ターム += LOCAL / 7
ENDIF

取得季節 += 季節
IF 取得季節 >= 5
	取得季節 = 1
ENDIF

RETURN 取得季節, 取得ターム

;-------------------------------------------------
;長期予報
;-------------------------------------------------
@WEATHER_FORECAST_1YEAR

;(天候パッチ)エルニーニョ現象の翌年はラニーニャ現象、ラニーニャ現象の翌年・翌々年は通常、その他は25％でエルニーニョ現象
SELECTCASE YEAR_WEATHER_CONTROL
	CASE 4
		YEAR_WEATHER_CONTROL = 1
	CASE 3
		YEAR_WEATHER_CONTROL = 4
	CASE 2
		YEAR_WEATHER_CONTROL = 3
	CASE RAND:4 == 0
		YEAR_WEATHER_CONTROL = 2
	CASEELSE
		YEAR_WEATHER_CONTROL = 1
ENDSELECT

;-------------------------------------------------
;短期予報
;-------------------------------------------------
@WEATHER_FORECAST_1TERM
#DIM 位置倍率
#DIM 予想経過時間
#DIM 日本海低気圧発生判定
#DIM 南岸低気圧発生判定

;(天候パッチ)長期天候傾向が未設定の時は長期予報を行う
SIF YEAR_WEATHER_CONTROL <= 0
	CALL WEATHER_FORECAST_1YEAR

;タームをまたいだ場合に処理を続行
SIF NOW_TERM == GET_TERM()
	RETURN

;現在のターム数を設定
NOW_TERM = GET_TERM()

;梅雨・秋雨入り判定
IF DAY:2 == 1 && NOW_TERM == 6
	IS_RAINY_SEASON1:0 = 1
	PRINTL ぐずついた空模様だ…
	PRINTW 梅雨入りしたようだ
	CALL ASK_DIARY("梅雨入りした")
ELSEIF DAY:2 == 3 && NOW_TERM == 1
	IS_RAINY_SEASON2 = 1
ENDIF

;現在のタームにおける寒気・暖気の強さを設定
SELECTCASE YEAR_WEATHER_CONTROL
	CASE 3
		SELECTCASE DAY:2
			CASE 2
				POWER_COLD_AIR = MIN(RAND:5 + 2, 5)
				POWER_WARM_AIR = MIN(RAND:5 + 2, 5)
			CASE 4
				POWER_COLD_AIR = MIN(RAND:5 + 2, 5)
				POWER_WARM_AIR = RAND:5 + 1
			CASEELSE
				POWER_COLD_AIR = RAND:5 + 1
				POWER_WARM_AIR = MIN(RAND:5 + 2, 5)
		ENDSELECT
	CASE 2
		SELECTCASE DAY:2
			CASE 4
				POWER_COLD_AIR = MAX(RAND:5, 1)
				POWER_WARM_AIR = MIN(RAND:5 + 2, 5)
			CASEELSE
				POWER_COLD_AIR = RAND:5 + 1
				POWER_WARM_AIR = MAX(RAND:5, 1)
		ENDSELECT
	CASEELSE
		POWER_COLD_AIR = RAND:5 + 1
		POWER_WARM_AIR = RAND:5 + 1
ENDSELECT

;寒気・暖気による作物への影響
IF FLAG:地主 > 0
	IF (POWER_WARM_AIR == 3 || POWER_WARM_AIR == 4) && (DAY:2 == 1 || DAY:2 == 3)
		FLAG:畑仕事の成果 += 10
	ELSEIF POWER_COLD_AIR == 5 && DAY:2 == 1
		FLAG:畑仕事の成果 = MAX(0, FLAG:畑仕事の成果 - 10)
		AMOUNT_COLD_CROP_DAMAGE += 10
	ENDIF
ENDIF

;連続降水カウンター20以上で水害発生の可能性、作物に壊滅的な被害
IF CONTINUOUS_RAINFALL >= 20 && RAND:(MAX(1, (40 - CONTINUOUS_RAINFALL) / 5)) == 0
	IS_FLOOD_CROP_DAMAGE = 1
	FLAG:畑仕事の成果 = 0
ENDIF

;梅雨・秋雨明け判定
IF IS_RAINY_SEASON1:0 == 1 && POWER_WARM_AIR >= 3 && IS_RAINY_SEASON1:1 != 1
	IS_RAINY_SEASON1:1 = 1
	;梅雨末期の線状降水帯発生判定
	IF POWER_WARM_AIR == 5 && RAND:5 == 0 && IS_LINER_RAINBAND:0 == 0
		CALL CALCULATE_ADD_TIME(DAY:2, DAY:3, TIME:0, RAND:4200)
		CALL GENERATE_LINER_RAINBAND(RESULT:0, RESULT:1, RESULT:2)
	ENDIF
ELSEIF IS_RAINY_SEASON1:0 == 1 && (DAY:2 == 3 && NOW_TERM == 1)
	IS_RAINY_SEASON1:0 = 0
	IS_RAINY_SEASON1:1 = 0
	PRINTL いつの間にか夏の蒸し暑さは去っていった
	PRINTW 結局梅雨は明けなかったようだ
	CALL ASK_DIARY("夏が来なかった")
ELSEIF IS_RAINY_SEASON1:1 == 1
	IS_RAINY_SEASON1:0 = 0
	IS_RAINY_SEASON1:1 = 0
	PRINTL 爽やかな夏模様だ！
	PRINTW 梅雨明けしたようだ
	CALL ASK_DIARY("梅雨が明けた")
ELSEIF DAY:2 == 3 && NOW_TERM == 2
	IS_RAINY_SEASON2 = 0
ENDIF

;梅雨による作物への影響
IF FLAG:地主 > 0
	IF DAY:2 == 2 && (NOW_TERM == 1 || NOW_TERM == 2) && IS_RAINY_SEASON1:0 == 1
		FLAG:畑仕事の成果 += 20
	ELSEIF DAY:2 == 2 && NOW_TERM == 4 && IS_RAINY_SEASON1:0 == 1
		FLAG:畑仕事の成果 = MAX(0, FLAG:畑仕事の成果 - 20)
	ELSEIF DAY:2 == 2 && NOW_TERM == 5 && IS_RAINY_SEASON1:0 == 1
		FLAG:畑仕事の成果 = MAX(0, FLAG:畑仕事の成果 - 40)
		IS_COLD_CROP_DAMAGE ++
	ELSEIF DAY:2 == 2 && NOW_TERM == 6 && IS_RAINY_SEASON1:0 == 1
		FLAG:畑仕事の成果 = MAX(0, FLAG:畑仕事の成果 - 80)
		IS_COLD_CROP_DAMAGE ++
	ENDIF
ENDIF

;梅雨・秋雨時以外に寒冷渦の発生判定
IF !IS_RAINY_SEASON1:0 && !IS_RAINY_SEASON2 && POWER_COLD_AIR == 5
	IS_COLD_LOW = 1
	;寒冷渦の勢力圏突入時刻
	TIME_ENTER_TERRITORY_COLD_LOW:0 = DAY:2
	TIME_ENTER_TERRITORY_COLD_LOW:1 = DAY:3 + RAND:2
	TIME_ENTER_TERRITORY_COLD_LOW:2 = RAND:1440
	
	;寒冷渦の勢力圏通過時刻
	CALL CALCULATE_ADD_TIME((TIME_ENTER_TERRITORY_COLD_LOW:0), (TIME_ENTER_TERRITORY_COLD_LOW:1), (TIME_ENTER_TERRITORY_COLD_LOW:2), 2880)
	TIME_PASSAGE_TERRITORY_COLD_LOW:0 = RESULT:0
	TIME_PASSAGE_TERRITORY_COLD_LOW:1 = RESULT:1
	TIME_PASSAGE_TERRITORY_COLD_LOW:2 = RESULT:2
ELSE
	IS_COLD_LOW = 0
	TIME_ENTER_TERRITORY_COLD_LOW:0 = 0
	TIME_ENTER_TERRITORY_COLD_LOW:1 = 0
	TIME_ENTER_TERRITORY_COLD_LOW:2 = 0
ENDIF

;日本海低気圧の消滅判定
IF IS_SoJ_LOW >= 1
	IS_SoJ_LOW = 0
	TIME_ENTER_TERRITORY_SoJ_LOW:0 = 0
	TIME_ENTER_TERRITORY_SoJ_LOW:1 = 0
	TIME_ENTER_TERRITORY_SoJ_LOW:2 = 0
	
	TIME_CLOSEST_SoJ_LOW:0 = 0
	TIME_CLOSEST_SoJ_LOW:1 = 0
	TIME_CLOSEST_SoJ_LOW:2 = 0
	
	TIME_PASSAGE_FAKE_FINE:0 = 0
	TIME_PASSAGE_FAKE_FINE:1 = 0
	TIME_PASSAGE_FAKE_FINE:2 = 0
	
	TIME_PASSAGE_SoJ_LOW:0 = 0
	TIME_PASSAGE_SoJ_LOW:1 = 0
	TIME_PASSAGE_SoJ_LOW:2 = 0
ENDIF

;南岸低気圧の消滅判定
IF IS_SOUTHERN_LOW == 1
	IS_SOUTHERN_LOW = 0
	TIME_ENTER_TERRITORY_SOUTHERN_LOW:0 = 0
	TIME_ENTER_TERRITORY_SOUTHERN_LOW:1 = 0
	TIME_ENTER_TERRITORY_SOUTHERN_LOW:2 = 0
	
	TIME_PASSAGE_SOUTHERN_LOW:0 = 0
	TIME_PASSAGE_SOUTHERN_LOW:1 = 0
	TIME_PASSAGE_SOUTHERN_LOW:2 = 0
ENDIF

;台風の消滅判定
IF (DAY:2 == 1 || (DAY:2 == 3 && NOW_TERM >= 4) || DAY:2 == 4 || TIME_PASSAGE_WIND_ZONE_TYPHOON:0 < DAY:2 || GET_LOCATION_LOW() == "台風一過") && IS_TYPHOON:0 == 1
	IS_TYPHOON:0 = 0
	IS_TYPHOON:1 = 0
	IS_TYPHOON:2 = 0
	IS_TYPHOON:3 = 0
	
	TIME_ENTER_TERRITORY_TYPHOON:0 = 0
	TIME_ENTER_TERRITORY_TYPHOON:1 = 0
	TIME_ENTER_TERRITORY_TYPHOON:2 = 0
	
	TIME_ENTER_WIND_ZONE_TYPHOON:0 = 0
	TIME_ENTER_WIND_ZONE_TYPHOON:1 = 0
	TIME_ENTER_WIND_ZONE_TYPHOON:2 = 0
	
	TIME_CLOSEST_TYPHOON:0 = 0
	TIME_CLOSEST_TYPHOON:1 = 0
	TIME_CLOSEST_TYPHOON:2 = 0
	
	TIME_PASSAGE_EYE_TYPHOON:0 = 0
	TIME_PASSAGE_EYE_TYPHOON:1 = 0
	TIME_PASSAGE_EYE_TYPHOON:2 = 0
	
	TIME_PASSAGE_WIND_ZONE_TYPHOON:0 = 0
	TIME_PASSAGE_WIND_ZONE_TYPHOON:1 = 0
	TIME_PASSAGE_WIND_ZONE_TYPHOON:2 = 0
	
	TIME_ENTER_STORM_AREA_TYPHOON:0 = 0
	TIME_ENTER_STORM_AREA_TYPHOON:1 = 0
	TIME_ENTER_STORM_AREA_TYPHOON:2 = 0
	
	TIME_PASSAGE_STORM_AREA_TYPHOON:0 = 0
	TIME_PASSAGE_STORM_AREA_TYPHOON:1 = 0
	TIME_PASSAGE_STORM_AREA_TYPHOON:2 = 0
	
ENDIF

;線状降水帯の消滅判定
IF (TIME_PASSAGE_LINER_RAINBAND:0 < DAY:2 || GET_LOCATION_LINER_RAINBAND() == "線状降水帯通過") && IS_LINER_RAINBAND:0 >= 1
	IS_LINER_RAINBAND:0 = 0
	IS_LINER_RAINBAND:1 = 0
	
	TIME_ENTER_TERRITORY_LINER_RAINBAND:0 = 0
	TIME_ENTER_TERRITORY_LINER_RAINBAND:1 = 0
	TIME_ENTER_TERRITORY_LINER_RAINBAND:2 = 0
	
	TIME_PASSAGE_LINER_RAINBAND:0 = 0
	TIME_PASSAGE_LINER_RAINBAND:1 = 0
	TIME_PASSAGE_LINER_RAINBAND:2 = 0
	
ENDIF

;台風と寒冷渦が同時に存在するとき迷走台風になる
IF IS_TYPHOON:0 && IS_COLD_LOW
	;台風の強風域通過時刻を延長する
	CALL CALCULATE_ADD_TIME((TIME_PASSAGE_WIND_ZONE_TYPHOON:0), (TIME_PASSAGE_WIND_ZONE_TYPHOON:1), (TIME_PASSAGE_WIND_ZONE_TYPHOON:2), (720 + RAND:360))
	TIME_PASSAGE_WIND_ZONE_TYPHOON:0 = RESULT:0
	TIME_PASSAGE_WIND_ZONE_TYPHOON:1 = RESULT:1
	TIME_PASSAGE_WIND_ZONE_TYPHOON:2 = RESULT:2
	
	;台風の暴風域通過時刻を延長する
	IF IS_TYPHOON:1 >= 3
		CALL CALCULATE_ADD_TIME((TIME_PASSAGE_STORM_AREA_TYPHOON:0), (TIME_PASSAGE_STORM_AREA_TYPHOON:1), (TIME_PASSAGE_STORM_AREA_TYPHOON:2), (720 + RAND:360))
		TIME_PASSAGE_STORM_AREA_TYPHOON:0 = RESULT:0
		TIME_PASSAGE_STORM_AREA_TYPHOON:1 = RESULT:1
		TIME_PASSAGE_STORM_AREA_TYPHOON:2 = RESULT:2
	ENDIF
ENDIF

;夏・秋第1・2ターム時に台風の発生判定
IF (DAY:2 == 2 || (DAY:2 == 3 && NOW_TERM <= 2)) && POWER_WARM_AIR == 5 && !IS_TYPHOON:0
	IS_TYPHOON:0 = 1
	IS_TYPHOON:1 = RAND:5 + 1
	IS_TYPHOON:2 = RAND:3 + 1
	
	;20%の確率で先駆降雨帯発生
	SIF RAND:5 == 0
		IS_TYPHOON:3 = 1
	
	SELECTCASE IS_TYPHOON:2
		CASE 1
			位置倍率 = 7
		CASE 2
			位置倍率 = 10
		CASE 3
			位置倍率 = 9
	ENDSELECT
	
	;台風の勢力圏突入時刻を算定
	CALL CALCULATE_ADD_TERM(DAY:2, NOW_TERM, 1)
	TIME_ENTER_TERRITORY_TYPHOON:0 = RESULT:0
	TIME_ENTER_TERRITORY_TYPHOON:1 = ((RESULT:1 - 1) * 5) + 1
	TIME_ENTER_TERRITORY_TYPHOON:2 = RAND:1440
	
	;台風の強風域突入時刻を算定
	CALL CALCULATE_ADD_TIME((TIME_ENTER_TERRITORY_TYPHOON:0), (TIME_ENTER_TERRITORY_TYPHOON:1), (TIME_ENTER_TERRITORY_TYPHOON:2), 2880)
	TIME_ENTER_WIND_ZONE_TYPHOON:0 = RESULT:0
	TIME_ENTER_WIND_ZONE_TYPHOON:1 = RESULT:1
	TIME_ENTER_WIND_ZONE_TYPHOON:2 = RESULT:2
	
	;勢力3以上だと暴風域を備える
	IF IS_TYPHOON:1 >= 3
		;台風の暴風域突入時刻
		CALL CALCULATE_ADD_TIME((TIME_ENTER_WIND_ZONE_TYPHOON:0), (TIME_ENTER_WIND_ZONE_TYPHOON:1), (TIME_ENTER_WIND_ZONE_TYPHOON:2), (360 * 位置倍率 / 10))
		TIME_ENTER_STORM_AREA_TYPHOON:0 = RESULT:0
		TIME_ENTER_STORM_AREA_TYPHOON:1 = RESULT:1
		TIME_ENTER_STORM_AREA_TYPHOON:2 = RESULT:2
		
		;台風の暴風域通過時刻
		CALL CALCULATE_ADD_TIME((TIME_ENTER_WIND_ZONE_TYPHOON:0), (TIME_ENTER_WIND_ZONE_TYPHOON:1), (TIME_ENTER_WIND_ZONE_TYPHOON:2), (1080 * 位置倍率 / 10))
		TIME_PASSAGE_STORM_AREA_TYPHOON:0 = RESULT:0
		TIME_PASSAGE_STORM_AREA_TYPHOON:1 = RESULT:1
		TIME_PASSAGE_STORM_AREA_TYPHOON:2 = RESULT:2
	ENDIF
	
	;台風の最接近時刻を算定
	CALL CALCULATE_ADD_TIME((TIME_ENTER_WIND_ZONE_TYPHOON:0), (TIME_ENTER_WIND_ZONE_TYPHOON:1), (TIME_ENTER_WIND_ZONE_TYPHOON:2), (720 * 位置倍率 / 10))
	TIME_CLOSEST_TYPHOON:0 = RESULT:0
	TIME_CLOSEST_TYPHOON:1 = RESULT:1
	TIME_CLOSEST_TYPHOON:2 = RESULT:2
	
	;台風の目通過時刻を算定
	CALL CALCULATE_ADD_TIME((TIME_CLOSEST_TYPHOON:0), (TIME_CLOSEST_TYPHOON:1), (TIME_CLOSEST_TYPHOON:2), (10 * IS_TYPHOON:1))
	TIME_PASSAGE_EYE_TYPHOON:0 = RESULT:0
	TIME_PASSAGE_EYE_TYPHOON:1 = RESULT:1
	TIME_PASSAGE_EYE_TYPHOON:2 = RESULT:2
	
	;台風の強風域通過時刻を算定
	CALL CALCULATE_ADD_TIME((TIME_ENTER_WIND_ZONE_TYPHOON:0), (TIME_ENTER_WIND_ZONE_TYPHOON:1), (TIME_ENTER_WIND_ZONE_TYPHOON:2), (1420 * 位置倍率 / 10))
	TIME_PASSAGE_WIND_ZONE_TYPHOON:0 = RESULT:0
	TIME_PASSAGE_WIND_ZONE_TYPHOON:1 = RESULT:1
	TIME_PASSAGE_WIND_ZONE_TYPHOON:2 = RESULT:2
	
	;先駆降雨帯発生時・秋雨時の線状降水帯発生判定
	IF (IS_TYPHOON:3 == 1 || (TIME_ENTER_TERRITORY_TYPHOON:0 == 3 && TIME_ENTER_TERRITORY_TYPHOON:1 <= 5)) && POWER_WARM_AIR == 5 && RAND:4 == 0 && IS_LINER_RAINBAND:0 == 0
		CALL GENERATE_LINER_RAINBAND(TIME_ENTER_TERRITORY_TYPHOON:0, TIME_ENTER_TERRITORY_TYPHOON:1, TIME_ENTER_TERRITORY_TYPHOON:2)
	ENDIF
	
ENDIF

;夏季に寒冷渦発生時の線状降水帯発生判定
IF IS_COLD_LOW && POWER_WARM_AIR == 5 && RAND:4 && IS_LINER_RAINBAND:0 == 0
	CALL CALCULATE_ADD_TIME(DAY:2, DAY:3, TIME:0, RAND:4200)
	CALL GENERATE_LINER_RAINBAND(RESULT:0, RESULT:1, RESULT:2)
ENDIF

;冬に温帯低気圧の発生判定
IF DAY:2 == 4 && !IS_COLD_LOW
	日本海低気圧発生判定 = RAND:5
	南岸低気圧発生判定 = RAND:5
	
	;二つ玉低気圧発生
	IF !日本海低気圧発生判定 && !南岸低気圧発生判定
		IS_SoJ_LOW = 2
		;日本海低気圧の勢力圏突入時刻
		TIME_ENTER_TERRITORY_SoJ_LOW:0 = DAY:2
		TIME_ENTER_TERRITORY_SoJ_LOW:1 = DAY:3 + RAND:3
		TIME_ENTER_TERRITORY_SoJ_LOW:2 = RAND:1440
		
		;日本海低気圧の最接近時刻
		CALL CALCULATE_ADD_TIME((TIME_ENTER_TERRITORY_SoJ_LOW:0), (TIME_ENTER_TERRITORY_SoJ_LOW:1), (TIME_ENTER_TERRITORY_SoJ_LOW:2), 720)
		TIME_CLOSEST_SoJ_LOW:0 = RESULT:0
		TIME_CLOSEST_SoJ_LOW:1 = RESULT:1
		TIME_CLOSEST_SoJ_LOW:2 = RESULT:2
		
		;疑似好天エリア通過時刻
		CALL CALCULATE_ADD_TIME((TIME_CLOSEST_SoJ_LOW:0), (TIME_CLOSEST_SoJ_LOW:1), (TIME_CLOSEST_SoJ_LOW:2), (30 * (RAND:6 + 1)))
		TIME_PASSAGE_FAKE_FINE:0 = RESULT:0
		TIME_PASSAGE_FAKE_FINE:1 = RESULT:1
		TIME_PASSAGE_FAKE_FINE:2 = RESULT:2
		
		;日本海低気圧の通過時刻
		CALL CALCULATE_ADD_TIME((TIME_CLOSEST_SoJ_LOW:0), (TIME_CLOSEST_SoJ_LOW:1), (TIME_CLOSEST_SoJ_LOW:2), 1080)
		TIME_PASSAGE_SoJ_LOW:0 = RESULT:0
		TIME_PASSAGE_SoJ_LOW:1 = RESULT:1
		TIME_PASSAGE_SoJ_LOW:2 = RESULT:2
		
	;日本海低気圧発生
	ELSEIF !日本海低気圧発生判定
		IS_SoJ_LOW = 1
		;日本海低気圧の勢力圏突入時刻
		TIME_ENTER_TERRITORY_SoJ_LOW:0 = DAY:2
		TIME_ENTER_TERRITORY_SoJ_LOW:1 = DAY:3 + RAND:3
		TIME_ENTER_TERRITORY_SoJ_LOW:2 = RAND:1440
		
		;日本海低気圧の最接近時刻
		CALL CALCULATE_ADD_TIME((TIME_ENTER_TERRITORY_SoJ_LOW:0), (TIME_ENTER_TERRITORY_SoJ_LOW:1), (TIME_ENTER_TERRITORY_SoJ_LOW:2), 720)
		TIME_CLOSEST_SoJ_LOW:0 = RESULT:0
		TIME_CLOSEST_SoJ_LOW:1 = RESULT:1
		TIME_CLOSEST_SoJ_LOW:2 = RESULT:2
		
		;日本海低気圧の通過時刻
		CALL CALCULATE_ADD_TIME((TIME_CLOSEST_SoJ_LOW:0), (TIME_CLOSEST_SoJ_LOW:1), (TIME_CLOSEST_SoJ_LOW:2), 1080)
		TIME_PASSAGE_SoJ_LOW:0 = RESULT:0
		TIME_PASSAGE_SoJ_LOW:1 = RESULT:1
		TIME_PASSAGE_SoJ_LOW:2 = RESULT:2
		
	;南岸低気圧発生
	ELSEIF !南岸低気圧発生判定
		IS_SOUTHERN_LOW = 1
		;南岸低気圧の勢力圏突入時刻
		TIME_ENTER_TERRITORY_SOUTHERN_LOW:0 = DAY:2
		TIME_ENTER_TERRITORY_SOUTHERN_LOW:1 = DAY:3 + RAND:3
		TIME_ENTER_TERRITORY_SOUTHERN_LOW:2 = RAND:1440
		
		;南岸低気圧の通過時刻
		CALL CALCULATE_ADD_TIME((TIME_ENTER_TERRITORY_SOUTHERN_LOW:0), (TIME_ENTER_TERRITORY_SOUTHERN_LOW:1), (TIME_ENTER_TERRITORY_SOUTHERN_LOW:2), 1440)
		TIME_PASSAGE_SOUTHERN_LOW:0 = RESULT:0
		TIME_PASSAGE_SOUTHERN_LOW:1 = RESULT:1
		TIME_PASSAGE_SOUTHERN_LOW:2 = RESULT:2
	ENDIF
ENDIF

;春・秋に温暖低気圧の発生判定
IF (DAY:2 == 1 || DAY:2 == 3) && !IS_COLD_LOW && !IS_RAINY_SEASON1:0 && !IS_RAINY_SEASON2
	;春・秋の温帯低気圧の勢力圏突入時刻
	TIME_ENTER_TERRITORY_EXTRATROPICAL_LOW:0 = DAY:2
	TIME_ENTER_TERRITORY_EXTRATROPICAL_LOW:1 = DAY:3 + 1 + RAND:2
	TIME_ENTER_TERRITORY_EXTRATROPICAL_LOW:2 = RAND:1440
	
	;春・秋の温帯低気圧の温暖前線通過時刻
	CALL CALCULATE_ADD_TIME((TIME_ENTER_TERRITORY_EXTRATROPICAL_LOW:0), (TIME_ENTER_TERRITORY_EXTRATROPICAL_LOW:1), (TIME_ENTER_TERRITORY_EXTRATROPICAL_LOW:2), 1440)
	TIME_PASSAGE_WARM_FRONT:0 = RESULT:0
	TIME_PASSAGE_WARM_FRONT:1 = RESULT:1
	TIME_PASSAGE_WARM_FRONT:2 = RESULT:2
	
	;春・秋の温帯低気圧の寒冷前線時刻
	CALL CALCULATE_ADD_TIME((TIME_PASSAGE_WARM_FRONT:0), (TIME_PASSAGE_WARM_FRONT:1), (TIME_PASSAGE_WARM_FRONT:2), 720)
	TIME_PASSAGE_COLD_FRONT:0 = RESULT:0
	TIME_PASSAGE_COLD_FRONT:1 = RESULT:1
	TIME_PASSAGE_COLD_FRONT:2 = RESULT:2
	
	;春・秋の温帯低気圧の勢力圏通過時刻
	CALL CALCULATE_ADD_TIME((TIME_PASSAGE_COLD_FRONT:0), (TIME_PASSAGE_COLD_FRONT:1), (TIME_PASSAGE_COLD_FRONT:2), 180)
	TIME_PASSAGE_EXTRATROPICAL_LOW:0 = RESULT:0
	TIME_PASSAGE_EXTRATROPICAL_LOW:1 = RESULT:1
	TIME_PASSAGE_EXTRATROPICAL_LOW:2 = RESULT:2
;温帯低気圧の消滅判定
ELSE
	TIME_ENTER_TERRITORY_EXTRATROPICAL_LOW:0 = 0
	TIME_ENTER_TERRITORY_EXTRATROPICAL_LOW:1 = 0
	TIME_ENTER_TERRITORY_EXTRATROPICAL_LOW:2 = 0
	
	TIME_PASSAGE_WARM_FRONT:0 = 0
	TIME_PASSAGE_WARM_FRONT:1 = 0
	TIME_PASSAGE_WARM_FRONT:2 = 0
	
	TIME_PASSAGE_COLD_FRONT:0 = 0
	TIME_PASSAGE_COLD_FRONT:1 = 0
	TIME_PASSAGE_COLD_FRONT:2 = 0
	
	TIME_PASSAGE_EXTRATROPICAL_LOW:0 = 0
	TIME_PASSAGE_EXTRATROPICAL_LOW:1 = 0
	TIME_PASSAGE_EXTRATROPICAL_LOW:2 = 0
ENDIF
;-------------------------------------------------
;1日予報
;-------------------------------------------------
@WEATHER_FORECAST_1DAY

;(天候パッチ)長期天候傾向が未設定の時は長期予報を行う
SIF YEAR_WEATHER_CONTROL <= 0
	CALL WEATHER_FORECAST_1YEAR

;天候変更禁止を解除する
FORBIDDEN_CHANGE_WEATHER = 0

;霧発生フラグをリセット
CAN_STRATUS = 0

;(天候パッチ)1日の大気安定度をリセット
ATMOSPHERIC_INSTABILITY = 0

;積雲/積乱雲の発生判定をリセット
CAN_CUMULUS:0 = 0
CAN_CUMULUS:1 = 0
CAN_CUMULUS:2 = 0
TIME_ENTER_TERRITORY_CUMULUS = 0
TIME_CLOSEST_CUMULUS = 0

;霧の発生判定
SIF !IS_COLD_LOW && !IS_TYPHOON:0 && !IS_SoJ_LOW && !IS_SOUTHERN_LOW && !RAND:10
	CAN_STRATUS = 1

;(天候パッチ)1日の大気安定度を設定
ATMOSPHERIC_INSTABILITY = RAND:(8 - POWER_WARM_AIR) + (7 - POWER_WARM_AIR - POWER_COLD_AIR)
SIF DAY:2 == 2 && (IS_COLD_LOW || GET_LOCATION_LOW() == "台風勢力圏")
	ATMOSPHERIC_INSTABILITY -= 6

;積雲/積乱雲の発生判定
IF DAY:2 == 2 && POWER_WARM_AIR >= 2 && !IS_COLD_LOW && (GET_LOCATION_LOW() == "台風遠方" || GET_LOCATION_LOW() == "台風一過" || GET_LOCATION_LOW() == "低気圧未発生")
	SELECTCASE ATMOSPHERIC_INSTABILITY
		CASE IS >= 4
			CAN_CUMULUS:0 = 1
		CASE IS >= 0
			CAN_CUMULUS:0 = 2
		CASEELSE
			CAN_CUMULUS:0 = 3
	ENDSELECT
	SIF !RAND:4
		CAN_CUMULUS:1 = 1
ELSEIF DAY:2 == 2 && (IS_COLD_LOW || GET_LOCATION_LOW() == "台風勢力圏")
	SELECTCASE ATMOSPHERIC_INSTABILITY
		CASE IS >= 0
			CAN_CUMULUS:0 = 2
			SIF !RAND:4
				CAN_CUMULUS:1 = 1
		CASE IS >= -3
			CAN_CUMULUS:0 = 3
			SIF !RAND:2
				CAN_CUMULUS:1 = 1
		CASE IS >= -6
			CAN_CUMULUS:0 = 4
			SIF !RAND:2
				CAN_CUMULUS:1 = 1
		CASE IS >= -8
			CAN_CUMULUS:0 = 4
			CAN_CUMULUS:1 = 1
		CASEELSE
			CAN_CUMULUS:0 = 5
			CAN_CUMULUS:1 = 1
	ENDSELECT
;積雲/積乱雲の発生判定をリセット
ELSE
	CAN_CUMULUS:0 = 0
	CAN_CUMULUS:1 = 0
	CAN_CUMULUS:2 = 0
	TIME_ENTER_TERRITORY_CUMULUS = 0
	TIME_CLOSEST_CUMULUS = 0
ENDIF

IF CAN_CUMULUS:1 == 1
	;積雲/積乱雲の勢力圏突入時刻
	TIME_ENTER_TERRITORY_CUMULUS = 840 + RAND:120
	
	;積雲/積乱雲の最接近時刻
	TIME_CLOSEST_CUMULUS = TIME_ENTER_TERRITORY_CUMULUS + 15 + RAND:15
	
	;積雲/積乱雲の勢力圏通過時刻
	SELECTCASE CAN_CUMULUS:0
		CASE IS <= 3
			TIME_PASSAGE_CUMULUS = TIME_CLOSEST_CUMULUS + 30 + RAND:15
		CASE 4
			TIME_PASSAGE_CUMULUS = TIME_CLOSEST_CUMULUS + 60  + (RAND:2 * 60) + RAND:30
		CASE 5
			TIME_PASSAGE_CUMULUS = TIME_CLOSEST_CUMULUS + 60  + (RAND:3 * 60) + RAND:30
	ENDSELECT
ENDIF

;かなとこ雲とスーパーセルの場合風の強さ判定
IF CAN_CUMULUS:0 == 4 && CAN_CUMULUS:1 == 1
	IF RAND:(40 + ATMOSPHERIC_INSTABILITY) == 0
		CAN_CUMULUS:2 = 2
	ELSE
		CAN_CUMULUS:2 = 1
	ENDIF
ELSEIF CAN_CUMULUS:0 == 5 && CAN_CUMULUS:1 == 1
	IF RAND:5 == 0
		CAN_CUMULUS:2 = 2
	ELSE
		CAN_CUMULUS:2 = 1
	ENDIF
ENDIF

;-------------------------------------------------
;低気圧の位置判定
;-------------------------------------------------
@GET_LOCATION_LOW
#FUNCTIONS

;台風
IF IS_TYPHOON:0
	;台風の勢力圏突入時刻以前
	IF TIME_ENTER_TERRITORY_TYPHOON:0 > DAY:2
		RETURNF "台風遠方"
	ELSEIF TIME_ENTER_TERRITORY_TYPHOON:0 == DAY:2 && TIME_ENTER_TERRITORY_TYPHOON:1 > DAY:3
		RETURNF "台風遠方"
	ELSEIF TIME_ENTER_TERRITORY_TYPHOON:0 == DAY:2 && TIME_ENTER_TERRITORY_TYPHOON:1 == DAY:3 && TIME_ENTER_TERRITORY_TYPHOON:2 > TIME:0
		RETURNF "台風遠方"
	;台風の強風域突入時刻以前
	ELSEIF TIME_ENTER_WIND_ZONE_TYPHOON:0 > DAY:2
		RETURNF "台風勢力圏"
	ELSEIF TIME_ENTER_WIND_ZONE_TYPHOON:0 == DAY:2 && TIME_ENTER_WIND_ZONE_TYPHOON:1 > DAY:3
		RETURNF "台風勢力圏"
	ELSEIF TIME_ENTER_WIND_ZONE_TYPHOON:0 == DAY:2 && TIME_ENTER_WIND_ZONE_TYPHOON:1 == DAY:3 && TIME_ENTER_WIND_ZONE_TYPHOON:2 > TIME:0
		RETURNF "台風勢力圏"
	;台風に暴風域があり台風の暴風域突入時刻以前
	ELSEIF IS_TYPHOON:1 >= 3 && TIME_ENTER_STORM_AREA_TYPHOON:0 > DAY:2
		RETURNF "台風接近強風域"
	ELSEIF IS_TYPHOON:1 >= 3 && TIME_ENTER_STORM_AREA_TYPHOON:0 == DAY:2 && TIME_ENTER_STORM_AREA_TYPHOON:1 > DAY:3
		RETURNF "台風接近強風域"
	ELSEIF IS_TYPHOON:1 >= 3 && TIME_ENTER_STORM_AREA_TYPHOON:0 == DAY:2 && TIME_ENTER_STORM_AREA_TYPHOON:1 == DAY:3 && TIME_ENTER_STORM_AREA_TYPHOON:2 > TIME:0
		RETURNF "台風接近強風域"
	;台風に暴風域がなく台風の最接近時刻以前
	ELSEIF IS_TYPHOON:1 < 3 && TIME_CLOSEST_TYPHOON:0 > DAY:2
		RETURNF "台風接近強風域"
	ELSEIF IS_TYPHOON:1 < 3 && TIME_CLOSEST_TYPHOON:0 == DAY:2 && TIME_CLOSEST_TYPHOON:1 > DAY:3
		RETURNF "台風接近強風域"
	ELSEIF IS_TYPHOON:1 < 3 && TIME_CLOSEST_TYPHOON:0 == DAY:2 && TIME_CLOSEST_TYPHOON:1 == DAY:3 && TIME_CLOSEST_TYPHOON:2 > TIME:0
		RETURNF "台風接近強風域"
	;台風に暴風域があり台風の最接近時刻以前
	ELSEIF IS_TYPHOON:1 >= 3 && TIME_CLOSEST_TYPHOON:0 > DAY:2
		RETURNF "台風接近暴風域"
	ELSEIF IS_TYPHOON:1 >= 3 && TIME_CLOSEST_TYPHOON:0 == DAY:2 && TIME_CLOSEST_TYPHOON:1 > DAY:3
		RETURNF "台風接近暴風域"
	ELSEIF IS_TYPHOON:1 >= 3 && TIME_CLOSEST_TYPHOON:0 == DAY:2 && TIME_CLOSEST_TYPHOON:1 == DAY:3 && TIME_CLOSEST_TYPHOON:2 > TIME:0
		RETURNF "台風接近暴風域"
	;台風の目通過時刻以前
	ELSEIF TIME_PASSAGE_EYE_TYPHOON:0 > DAY:2
		RETURNF "台風最接近"
	ELSEIF TIME_PASSAGE_EYE_TYPHOON:0 == DAY:2 && TIME_PASSAGE_EYE_TYPHOON:1 > DAY:3
		RETURNF "台風最接近"
	ELSEIF TIME_PASSAGE_EYE_TYPHOON:0 == DAY:2 && TIME_PASSAGE_EYE_TYPHOON:1 == DAY:3 && TIME_PASSAGE_EYE_TYPHOON:2 > TIME:0
		RETURNF "台風最接近"
	;台風に暴風域があり暴風域通過時刻以前
	ELSEIF IS_TYPHOON:1 >= 3 && TIME_PASSAGE_STORM_AREA_TYPHOON:0 > DAY:2
		RETURNF "台風通過暴風域"
	ELSEIF IS_TYPHOON:1 >= 3 && TIME_PASSAGE_STORM_AREA_TYPHOON:0 == DAY:2 && TIME_PASSAGE_STORM_AREA_TYPHOON:1 > DAY:3
		RETURNF "台風通過暴風域"
	ELSEIF IS_TYPHOON:1 >= 3 && TIME_PASSAGE_STORM_AREA_TYPHOON:0 == DAY:2 && TIME_PASSAGE_STORM_AREA_TYPHOON:1 == DAY:3 && TIME_PASSAGE_STORM_AREA_TYPHOON:2 > TIME:0
		RETURNF "台風通過暴風域"
	;台風の強風域通過時刻以前
	ELSEIF TIME_PASSAGE_WIND_ZONE_TYPHOON:0 > DAY:2
		RETURNF "台風通過強風域"
	ELSEIF TIME_PASSAGE_WIND_ZONE_TYPHOON:0 == DAY:2 && TIME_PASSAGE_WIND_ZONE_TYPHOON:1 > DAY:3
		RETURNF "台風通過強風域"
	ELSEIF TIME_PASSAGE_WIND_ZONE_TYPHOON:0 == DAY:2 && TIME_PASSAGE_WIND_ZONE_TYPHOON:1 == DAY:3 && TIME_PASSAGE_WIND_ZONE_TYPHOON:2 > TIME:0
		RETURNF "台風通過強風域"
	ELSE
		RETURNF "台風一過"
	ENDIF
;寒冷渦
ELSEIF IS_COLD_LOW
	;寒冷渦の勢力圏突入時刻以前
	IF TIME_ENTER_TERRITORY_COLD_LOW:0 > DAY:2
		RETURNF "寒冷渦遠方"
	ELSEIF TIME_ENTER_TERRITORY_COLD_LOW:0 == DAY:2 && TIME_ENTER_TERRITORY_COLD_LOW:1 > DAY:3
		RETURNF "寒冷渦遠方"
	ELSEIF TIME_ENTER_TERRITORY_COLD_LOW:0 == DAY:2 && TIME_ENTER_TERRITORY_COLD_LOW:1 == DAY:3 && TIME_ENTER_TERRITORY_COLD_LOW:2 > TIME:0
		RETURNF "寒冷渦遠方"
	;寒冷渦の勢力圏通過時刻以前
	ELSEIF TIME_PASSAGE_TERRITORY_COLD_LOW:0 > DAY:2
		RETURNF "寒冷渦接近"
	ELSEIF TIME_PASSAGE_TERRITORY_COLD_LOW:0 == DAY:2 && TIME_PASSAGE_TERRITORY_COLD_LOW:1 > DAY:3
		RETURNF "寒冷渦接近"
	ELSEIF TIME_PASSAGE_TERRITORY_COLD_LOW:0 == DAY:2 && TIME_PASSAGE_TERRITORY_COLD_LOW:1 == DAY:3 && TIME_PASSAGE_TERRITORY_COLD_LOW:2 > TIME:0
		RETURNF "寒冷渦接近"
	ELSE
		RETURNF "寒冷渦通過"
	ENDIF
;二つ玉低気圧
ELSEIF IS_SoJ_LOW == 2
	;二つ玉低気圧の勢力圏突入時刻以前
	IF TIME_ENTER_TERRITORY_SoJ_LOW:0 > DAY:2
		RETURNF "二つ玉低気圧遠方"
	ELSEIF TIME_ENTER_TERRITORY_SoJ_LOW:0 == DAY:2 && TIME_ENTER_TERRITORY_SoJ_LOW:1 > DAY:3
		RETURNF "二つ玉低気圧遠方"
	ELSEIF TIME_ENTER_TERRITORY_SoJ_LOW:0 == DAY:2 && TIME_ENTER_TERRITORY_SoJ_LOW:1 == DAY:3 && TIME_ENTER_TERRITORY_SoJ_LOW:2 > TIME:0
		RETURNF "二つ玉低気圧遠方"
	;二つ玉低気圧の最接近時刻以前
	ELSEIF TIME_CLOSEST_SoJ_LOW:0 > DAY:2
		RETURNF "二つ玉低気圧接近"
	ELSEIF TIME_CLOSEST_SoJ_LOW:0 == DAY:2 && TIME_CLOSEST_SoJ_LOW:1 > DAY:3
		RETURNF "二つ玉低気圧接近"
	ELSEIF TIME_CLOSEST_SoJ_LOW:0 == DAY:2 && TIME_CLOSEST_SoJ_LOW:1 == DAY:3 && TIME_CLOSEST_SoJ_LOW:2 > TIME:0
		RETURNF "二つ玉低気圧接近"
	;疑似好天エリア通過時刻以前
	ELSEIF TIME_PASSAGE_FAKE_FINE:0 > DAY:2
		RETURNF "疑似好天"
	ELSEIF TIME_PASSAGE_FAKE_FINE:0 == DAY:2 && TIME_PASSAGE_FAKE_FINE:1 > DAY:3
		RETURNF "疑似好天"
	ELSEIF TIME_PASSAGE_FAKE_FINE:0 == DAY:2 && TIME_PASSAGE_FAKE_FINE:1 == DAY:3 && TIME_PASSAGE_FAKE_FINE:2 > TIME:0
		RETURNF "疑似好天"
	;二つ玉低気圧の通過時刻以前
	ELSEIF TIME_PASSAGE_SoJ_LOW:0 > DAY:2
		RETURNF "二つ玉低気圧通過中"
	ELSEIF TIME_PASSAGE_SoJ_LOW:0 == DAY:2 && TIME_PASSAGE_SoJ_LOW:1 > DAY:3
		RETURNF "二つ玉低気圧通過中"
	ELSEIF TIME_PASSAGE_SoJ_LOW:0 == DAY:2 && TIME_PASSAGE_SoJ_LOW:1 == DAY:3 && TIME_PASSAGE_SoJ_LOW:2 > TIME:0
		RETURNF "二つ玉低気圧通過中"
	ELSE
		RETURNF "二つ玉低気圧通過"
	ENDIF
;日本海低気圧
ELSEIF IS_SoJ_LOW == 1
	;日本海低気圧の勢力圏突入時刻以前
	IF TIME_ENTER_TERRITORY_SoJ_LOW:0 > DAY:2
		RETURNF "日本海低気圧遠方"
	ELSEIF TIME_ENTER_TERRITORY_SoJ_LOW:0 == DAY:2 && TIME_ENTER_TERRITORY_SoJ_LOW:1 > DAY:3
		RETURNF "日本海低気圧遠方"
	ELSEIF TIME_ENTER_TERRITORY_SoJ_LOW:0 == DAY:2 && TIME_ENTER_TERRITORY_SoJ_LOW:1 == DAY:3 && TIME_ENTER_TERRITORY_SoJ_LOW:2 > TIME:0
		RETURNF "日本海低気圧遠方"
	;日本海低気圧の最接近時刻以前
	ELSEIF TIME_CLOSEST_SoJ_LOW:0 > DAY:2
		RETURNF "日本海低気圧接近"
	ELSEIF TIME_CLOSEST_SoJ_LOW:0 == DAY:2 && TIME_CLOSEST_SoJ_LOW:1 > DAY:3
		RETURNF "日本海低気圧接近"
	ELSEIF TIME_CLOSEST_SoJ_LOW:0 == DAY:2 && TIME_CLOSEST_SoJ_LOW:1 == DAY:3 && TIME_CLOSEST_SoJ_LOW:2 > TIME:0
		RETURNF "日本海低気圧接近"
	;日本海低気圧の通過時刻以前
	ELSEIF TIME_PASSAGE_SoJ_LOW:0 > DAY:2
		RETURNF "日本海低気圧通過中"
	ELSEIF TIME_PASSAGE_SoJ_LOW:0 == DAY:2 && TIME_PASSAGE_SoJ_LOW:1 > DAY:3
		RETURNF "日本海低気圧通過中"
	ELSEIF TIME_PASSAGE_SoJ_LOW:0 == DAY:2 && TIME_PASSAGE_SoJ_LOW:1 == DAY:3 && TIME_PASSAGE_SoJ_LOW:2 > TIME:0
		RETURNF "日本海低気圧通過中"
	ELSE
		RETURNF "日本海低気圧通過"
	ENDIF
;南岸低気圧
ELSEIF IS_SOUTHERN_LOW
	;南岸低気圧の勢力圏突入時刻以前
	IF TIME_ENTER_TERRITORY_SOUTHERN_LOW:0 > DAY:2
		RETURNF "南岸低気圧遠方"
	ELSEIF TIME_ENTER_TERRITORY_SOUTHERN_LOW:0 == DAY:2 && TIME_ENTER_TERRITORY_SOUTHERN_LOW:1 > DAY:3
		RETURNF "南岸低気圧遠方"
	ELSEIF TIME_ENTER_TERRITORY_SOUTHERN_LOW:0 == DAY:2 && TIME_ENTER_TERRITORY_SOUTHERN_LOW:1 == DAY:3 && TIME_ENTER_TERRITORY_SOUTHERN_LOW:2 > TIME:0
		RETURNF "南岸低気圧遠方"
	;南岸低気圧の通過時刻以前
	ELSEIF TIME_PASSAGE_SOUTHERN_LOW:0 > DAY:2
		RETURNF "南岸低気圧接近"
	ELSEIF TIME_PASSAGE_SOUTHERN_LOW:0 == DAY:2 && TIME_PASSAGE_SOUTHERN_LOW:1 > DAY:3
		RETURNF "南岸低気圧接近"
	ELSEIF TIME_PASSAGE_SOUTHERN_LOW:0 == DAY:2 && TIME_PASSAGE_SOUTHERN_LOW:1 == DAY:3 && TIME_PASSAGE_SOUTHERN_LOW:2 > TIME:0
		RETURNF "南岸低気圧接近"
	ELSE
		RETURNF "南岸低気圧通過"
	ENDIF
;春・秋の温帯低気圧
ELSEIF (DAY:2 == 1 || DAY:2 == 3) && !IS_RAINY_SEASON1 && !IS_RAINY_SEASON2
	;温暖前線の勢力圏突入時刻以前
	IF TIME_ENTER_TERRITORY_EXTRATROPICAL_LOW:0 > DAY:2
		RETURNF "温暖前線遠方"
	ELSEIF TIME_ENTER_TERRITORY_EXTRATROPICAL_LOW:0 == DAY:2 && TIME_ENTER_TERRITORY_EXTRATROPICAL_LOW:1 > DAY:3
		RETURNF "温暖前線遠方"
	ELSEIF TIME_ENTER_TERRITORY_EXTRATROPICAL_LOW:0 == DAY:2 && TIME_ENTER_TERRITORY_EXTRATROPICAL_LOW:1 == DAY:3 && TIME_ENTER_TERRITORY_EXTRATROPICAL_LOW:2 > TIME:0
		RETURNF "温暖前線遠方"
	;温暖前線通過時刻以前
	ELSEIF TIME_PASSAGE_WARM_FRONT:0 > DAY:2
		RETURNF "温暖前線接近"
	ELSEIF TIME_PASSAGE_WARM_FRONT:0 == DAY:2 && TIME_PASSAGE_WARM_FRONT:1 > DAY:3
		RETURNF "温暖前線接近"
	ELSEIF TIME_PASSAGE_WARM_FRONT:0 == DAY:2 && TIME_PASSAGE_WARM_FRONT:1 == DAY:3 && TIME_PASSAGE_WARM_FRONT:2 > TIME:0
		RETURNF "温暖前線接近"
	;寒冷前線通過時刻以前
	ELSEIF TIME_PASSAGE_COLD_FRONT:0 > DAY:2
		RETURNF "暖域"
	ELSEIF TIME_PASSAGE_COLD_FRONT:0 == DAY:2 && TIME_PASSAGE_COLD_FRONT:1 > DAY:3
		RETURNF "暖域"
	ELSEIF TIME_PASSAGE_COLD_FRONT:0 == DAY:2 && TIME_PASSAGE_COLD_FRONT:1 == DAY:3 && TIME_PASSAGE_COLD_FRONT:2 > TIME:0
		RETURNF "暖域"
	;温帯低気圧の勢力圏通過時刻以前
	ELSEIF TIME_PASSAGE_EXTRATROPICAL_LOW:0 > DAY:2
		RETURNF "寒冷前線通過"
	ELSEIF TIME_PASSAGE_EXTRATROPICAL_LOW:0 == DAY:2 && TIME_PASSAGE_EXTRATROPICAL_LOW:1 > DAY:3
		RETURNF "寒冷前線通過"
	ELSEIF TIME_PASSAGE_EXTRATROPICAL_LOW:0 == DAY:2 && TIME_PASSAGE_EXTRATROPICAL_LOW:1 == DAY:3 && TIME_PASSAGE_EXTRATROPICAL_LOW:2 > TIME:0
		RETURNF "寒冷前線通過"
	ELSE
		RETURNF "温帯低気圧通過"
	ENDIF
ENDIF

RETURNF "低気圧未発生"

;-------------------------------------------------
;積雲/積乱雲の位置判定
;-------------------------------------------------
@GET_LOCATION_CUMULUS
#FUNCTIONS

IF CAN_CUMULUS:0 && CAN_CUMULUS:1
	IF TIME_ENTER_TERRITORY_CUMULUS > TIME:0
		RETURNF "積乱雲視認"
	ELSEIF TIME_CLOSEST_CUMULUS > TIME:0
		RETURNF "積乱雲接近"
	ELSEIF TIME_PASSAGE_CUMULUS > TIME:0
		RETURNF "積乱雲直下"
	ELSE
		RETURNF "積乱雲通過"
	ENDIF
ELSEIF CAN_CUMULUS:0
	RETURNF "積乱雲遠方"
ENDIF

RETURNF "積乱雲未発生"

;-------------------------------------------------
;台風の襲来判定
;-------------------------------------------------
@GET_LOCATION_AREA_TYPHOON
#FUNCTION
#DIMS 低気圧位置

低気圧位置 = %GET_LOCATION_LOW()%

SIF 低気圧位置 == "台風接近強風域" || 低気圧位置 == "台風接近暴風域" || 低気圧位置 == "台風最接近" || 低気圧位置 == "台風通過暴風域" || 低気圧位置 == "台風通過強風域"
	RETURNF 1

RETURNF 0

;-------------------------------------------------
;線状降水帯の発生処理
;-------------------------------------------------
@GENERATE_LINER_RAINBAND(発生季節 = -1, 発生日時 = -1, 発生時間 = 0)
#DIM 発生季節
#DIM 発生日時
#DIM 発生時間
#DIM 継続時間
#DIM 到達時間

SIF 発生季節 == -1
	発生季節 = DAY:2
SIF 発生日時 == -1
	発生日時 = DAY:3

;線状降水帯の勢力決定
IS_LINER_RAINBAND:0 = 1
IF RAND:5 == 0
	IS_LINER_RAINBAND:1 = 2
	継続時間 = 420 +  RAND:180
ELSE
	IS_LINER_RAINBAND:1 = 1
	継続時間 = 180 + RAND:120
ENDIF

;線状降水帯の勢力圏突入時刻
到達時間 = RAND:1800
CALL CALCULATE_ADD_TIME(発生季節, 発生日時, 発生時間, 到達時間)
TIME_ENTER_TERRITORY_LINER_RAINBAND:0 = RESULT:0
TIME_ENTER_TERRITORY_LINER_RAINBAND:1 = RESULT:1
TIME_ENTER_TERRITORY_LINER_RAINBAND:2 = RESULT:2

;線状降水帯の通過時刻
CALL CALCULATE_ADD_TIME(発生季節, 発生日時, 発生時間, 到達時間 + 継続時間)
TIME_PASSAGE_LINER_RAINBAND:0 = RESULT:0
TIME_PASSAGE_LINER_RAINBAND:1 = RESULT:1
TIME_PASSAGE_LINER_RAINBAND:2 = RESULT:2

;-------------------------------------------------
;線状降水帯の襲来判定
;-------------------------------------------------
@GET_LOCATION_LINER_RAINBAND
#FUNCTIONS
IF IS_LINER_RAINBAND:0 >= 1
	;線状降水帯の勢力圏突入時刻以前
	IF TIME_ENTER_TERRITORY_LINER_RAINBAND:0 > DAY:2
		RETURNF "線状降水帯遠方"
	ELSEIF TIME_ENTER_TERRITORY_LINER_RAINBAND:0 == DAY:2 && TIME_ENTER_TERRITORY_LINER_RAINBAND:1 > DAY:3
		RETURNF "線状降水帯遠方"
	ELSEIF TIME_ENTER_TERRITORY_LINER_RAINBAND:0 == DAY:2 && TIME_ENTER_TERRITORY_LINER_RAINBAND:1 == DAY:3 && TIME_ENTER_TERRITORY_LINER_RAINBAND:2 > TIME:0
		RETURNF "線状降水帯遠方"
	;線状降水帯の通過時刻以前
	ELSEIF TIME_PASSAGE_LINER_RAINBAND:0 > DAY:2
		RETURNF "線状降水帯勢力圏"
	ELSEIF TIME_PASSAGE_LINER_RAINBAND:0 == DAY:2 && TIME_PASSAGE_LINER_RAINBAND:1 > DAY:3
		RETURNF "線状降水帯勢力圏"
	ELSEIF TIME_PASSAGE_LINER_RAINBAND:0 == DAY:2 && TIME_PASSAGE_LINER_RAINBAND:1 == DAY:3 && TIME_PASSAGE_LINER_RAINBAND:2 > TIME:0
		RETURNF "線状降水帯勢力圏"
	ELSE
		RETURNF "線状降水帯通過"
	ENDIF
ELSE
	RETURNF "線状降水帯未発生"
ENDIF

;-------------------------------------------------
;外の世界で発令されている気象警報・注意報を取得
;-------------------------------------------------
;大雨
@GET_WEATHER_WARNING_HEAVY_RAIN
#FUNCTIONS
#DIMS 低気圧位置
#DIMS 線状降水帯位置
#DIMS 積乱雲位置

低気圧位置 = %GET_LOCATION_LOW()%
線状降水帯位置 = %GET_LOCATION_LINER_RAINBAND()%
積乱雲位置 = %GET_LOCATION_CUMULUS()%

IF CONTINUOUS_RAINFALL >= 15
	RETURNF "大雨特別警報"
ELSEIF 線状降水帯位置 == "線状降水帯勢力圏" && IS_LINER_RAINBAND:1 == 2
	RETURNF "大雨特別警報"
ELSEIF IS_TYPHOON:1 >= 5 && IS_TYPHOON:2 == 3 && POWER_WARM_AIR >= 5 && GET_LOCATION_AREA_TYPHOON() == 1
	RETURNF "大雨特別警報"
ELSEIF CONTINUOUS_RAINFALL >= 10
	RETURNF "大雨警報(土砂災害警戒情報)"
ELSEIF 線状降水帯位置 == "線状降水帯勢力圏"
	RETURNF "大雨警報(土砂災害警戒情報)"
ELSEIF 線状降水帯位置 == "線状降水帯遠方" && IS_LINER_RAINBAND:1 == 2 && TIME_ENTER_TERRITORY_LINER_RAINBAND:1 == DAY:3 && TIME_ENTER_TERRITORY_LINER_RAINBAND:2 <= TIME:0 + 180
	RETURNF "大雨警報(土砂災害警戒情報)"
ELSEIF (GET_LOCATION_AREA_TYPHOON() == 1 || 低気圧位置 == "台風勢力圏") && IS_TYPHOON:3 == 1
	RETURNF "大雨警報"
ELSEIF GET_LOCATION_AREA_TYPHOON() == 1
	RETURNF "大雨警報"
ELSEIF CAN_CUMULUS:0 == 5 && (積乱雲位置 == "積乱雲視認" || 積乱雲位置 == "積乱雲接近" || 積乱雲位置 == "積乱雲直下")
	RETURNF "大雨警報"
ELSEIF CAN_CUMULUS:0 == 4 && (積乱雲位置 == "積乱雲接近" || 積乱雲位置 == "積乱雲直下")
	RETURNF "大雨警報"
ELSEIF CONTINUOUS_RAINFALL >= 5
	RETURNF "大雨警報"
ELSEIF 線状降水帯位置 == "線状降水帯遠方" && IS_LINER_RAINBAND:1 == 2 && TIME_ENTER_TERRITORY_LINER_RAINBAND:1 == DAY:3
	RETURNF "大雨警報"
ELSEIF TIME:5 == 14
	RETURNF "大雨警報"
ELSEIF 線状降水帯位置 == "線状降水帯遠方" && TIME_ENTER_TERRITORY_LINER_RAINBAND:1 == DAY:3 && TIME_ENTER_TERRITORY_LINER_RAINBAND:2 <= TIME:0 + 180
	RETURNF "大雨注意報"
ELSEIF GET_LOCATION_AREA_TYPHOON() == 1 || 低気圧位置 == "台風勢力圏"
	RETURNF "大雨注意報"
ELSEIF 低気圧位置 == "寒冷渦接近" && DAY:2 != 4 && POWER_WARM_AIR != 1
	RETURNF "大雨注意報"
ELSEIF CONTINUOUS_RAINFALL >= 1
	RETURNF "大雨注意報"
ELSEIF CAN_CUMULUS:0 == 5 && 積乱雲位置 == "積乱雲遠方"
	RETURNF "大雨注意報"
ELSEIF CAN_CUMULUS:0 == 4 && (積乱雲位置 == "積乱雲視認" || (積乱雲位置 == "積乱雲遠方" && TIME:0 >= 720))
	RETURNF "大雨注意報"
ELSEIF CAN_CUMULUS:0 == 3 && (積乱雲位置 == "積乱雲接近" || 積乱雲位置 == "積乱雲直下")
	RETURNF "大雨注意報"
ELSEIF TIME:5 == 5
	RETURNF "大雨注意報"
ELSE
	RETURNF "大雨注意報未発令"
ENDIF

;大雪
@GET_WEATHER_WARNING_HEAVY_SNOWFALL
#FUNCTIONS
#DIMS 低気圧位置

低気圧位置 = %GET_LOCATION_LOW()%
IF CONTINUOUS_SNOWFALL >= 30
	RETURNF "大雪特別警報"
ELSEIF 低気圧位置 == "二つ玉低気圧接近" || 低気圧位置 == "疑似好天" || 低気圧位置 == "二つ玉低気圧通過中"
	RETURNF "大雪警報"
ELSEIF 低気圧位置 == "日本海低気圧通過中"
	RETURNF "大雪警報"
ELSEIF 低気圧位置 == "寒冷渦接近" && (DAY:2 == 4 || POWER_WARM_AIR == 1) && DAY:2 != 3
	RETURNF "大雪警報"
ELSEIF CONTINUOUS_SNOWFALL >= 15
	RETURNF "大雪警報"
ELSEIF 低気圧位置 == "日本海低気圧接近"
	RETURNF "大雪注意報"
ELSEIF 低気圧位置 == "南岸低気圧接近"
	RETURNF "大雪注意報"
ELSEIF 低気圧位置 == "寒冷渦遠方" && DAY:2 == 4
	RETURNF "大雪注意報"
ELSEIF CONTINUOUS_SNOWFALL >= 5
	RETURNF "大雪注意報"
ELSE
	RETURNF "大雪注意報未発令"
ENDIF

;暴風・暴風雪
@GET_WEATHER_WARNING_STORM
#FUNCTIONS
#DIMS 低気圧位置

低気圧位置 = %GET_LOCATION_LOW()%
IF (低気圧位置 == "疑似好天" || 低気圧位置 == "二つ玉低気圧通過中") && POWER_COLD_AIR >= 4 && POWER_WARM_AIR >= 5
	RETURNF "暴風雪特別警報"
ELSEIF WIND_VELOCITY >= 3 && TIME:5 >= 8 && TIME:5 <= 12
	RETURNF "暴風雪特別警報"
ELSEIF IS_TYPHOON:1 >= 5 && POWER_COLD_AIR == 1 && POWER_WARM_AIR >= 5 && GET_LOCATION_AREA_TYPHOON() == 1
	RETURNF "暴風特別警報"
ELSEIF WIND_VELOCITY >= 3
	RETURNF "暴風特別警報"
ELSEIF 低気圧位置 == "疑似好天" || 低気圧位置 == "二つ玉低気圧通過中"
	RETURNF "暴風雪警報"
ELSEIF 低気圧位置 == "日本海低気圧通過中" && POWER_COLD_AIR >= 4
	RETURNF "暴風雪警報"
ELSEIF (低気圧位置 == "寒冷渦接近" || (低気圧位置 == "寒冷渦遠方" && (TIME_ENTER_TERRITORY_COLD_LOW:1 == DAY:3 && TIME_ENTER_TERRITORY_COLD_LOW:2 <= TIME:0 + 180))) && (DAY:2 == 4 || POWER_WARM_AIR == 1) && DAY:2 != 3
	RETURNF "暴風雪警報"
ELSEIF WIND_VELOCITY >= 2 && TIME:5 >= 8 && TIME:5 <= 12
	RETURNF "暴風雪警報"
ELSEIF GET_LOCATION_AREA_TYPHOON() == 1 && IS_TYPHOON:1 >= 3
	RETURNF "暴風警報"
ELSEIF 低気圧位置 == "寒冷渦接近" || (低気圧位置 == "寒冷渦遠方" && (TIME_ENTER_TERRITORY_COLD_LOW:1 == DAY:3 && TIME_ENTER_TERRITORY_COLD_LOW:2 <= TIME:0 + 180))
	RETURNF "暴風警報"
ELSEIF WIND_VELOCITY >= 2
	RETURNF "暴風警報"
ELSEIF 低気圧位置 == "二つ玉低気圧接近"
	RETURNF "風雪注意報"
ELSEIF 低気圧位置 == "日本海低気圧接近" && POWER_COLD_AIR >= 4
	RETURNF "風雪注意報"
ELSEIF WIND_VELOCITY >= 1 && TIME:5 >= 8 && TIME:5 <= 12
	RETURNF "風雪注意報"
ELSEIF GET_LOCATION_AREA_TYPHOON() == 1
	RETURNF "強風注意報"
ELSEIF (低気圧位置 == "温暖前線接近" || 低気圧位置 == "暖域") && (POWER_WARM_AIR >= 4 || (DAY:2 == 1 && NOW_TERM == 1))
	RETURNF "強風注意報"
ELSEIF WIND_VELOCITY >= 1
	RETURNF "強風注意報"
ELSE
	RETURNF "強風注意報未発令"
ENDIF

;雷・竜巻
@GET_WEATHER_WARNING_LIGHTNING
#FUNCTIONS
#DIMS 低気圧位置
#DIMS 線状降水帯位置
#DIMS 積乱雲位置

低気圧位置 = %GET_LOCATION_LOW()%
線状降水帯位置 = %GET_LOCATION_LINER_RAINBAND()%
積乱雲位置 = %GET_LOCATION_CUMULUS()%

IF CAN_CUMULUS:2 == 2 && (積乱雲位置 == "積乱雲接近" || 積乱雲位置 == "積乱雲直下")
	RETURNF "雷注意報(竜巻注意情報)"
ELSEIF GET_HEAT_THUNDERSTORM() == 1 && GET_FRONTAL_THUNDERSTORM() == 1 && ATMOSPHERIC_INSTABILITY <= -2
	RETURNF "雷注意報(竜巻注意情報)"
ELSEIF DAY:2 == 2 && (IS_COLD_LOW || 低気圧位置 == "台風勢力圏")
	RETURNF "雷注意報"
ELSEIF CAN_CUMULUS:0 == 3 && (積乱雲位置 == "積乱雲接近" || 積乱雲位置 == "積乱雲直下")
	RETURNF "雷注意報"
ELSEIF 線状降水帯位置 == "線状降水帯勢力圏"
	RETURNF "雷注意報"
ELSEIF (DAY:2 == 1 || DAY:2 == 3) && (低気圧位置 == "寒冷前線通過" || 低気圧位置 == "暖域") && POWER_WARM_AIR >= 3 && POWER_COLD_AIR >= 4
	RETURNF "雷注意報"
ELSEIF GET_HEAT_THUNDERSTORM() == 1
	RETURNF "雷注意報"
ELSEIF GET_FRONTAL_THUNDERSTORM() == 1
	RETURNF "雷注意報"
ELSE
	RETURNF "雷注意報未発令"
ENDIF

;低温
@GET_WEATHER_WARNING_COLD
#FUNCTIONS

IF POWER_COLD_AIR == 5 && DAY:2 == 1
	RETURNF "低温注意報"
ELSEIF IS_COLD_CROP_DAMAGE >= 2
	RETURNF "低温注意報"
ELSE
	RETURNF "低温注意報未発令"
ENDIF

;なだれ
@GET_WEATHER_WARNING_AVALANCHE
#FUNCTIONS
#DIMS 暴風情報

暴風情報 = %GET_WEATHER_WARNING_STORM()%

IF POSSIBLITY_AVALANCHE >= 1
	RETURNF "なだれ注意報"
ELSEIF DAY:2 == 1 && NOW_TERM <= 2 && SNOW_COVERAGE + CONTINUOUS_SNOWFALL >= 7
	RETURNF "なだれ注意報"
ELSEIF SNOW_COVERAGE + CONTINUOUS_SNOWFALL >= 10
	RETURNF "なだれ注意報"
ELSEIF SNOW_COVERAGE >= 15 && CONTINUOUS_SNOWFALL >= 6
	RETURNF "なだれ注意報"
ELSEIF SNOW_COVERAGE >= 5 && CONTINUOUS_SNOWFALL >= 5 && 暴風情報 != "強風注意報未発令"
	RETURNF "なだれ注意報"
ELSE
	RETURNF "なだれ注意報未発令"
ENDIF

;早期注意情報（警報級の可能性）
@GET_WEATHER_ADVISORY_probability_subsequent_WARNING
#FUNCTIONS
#DIMS 低気圧位置
#DIMS 線状降水帯位置
#DIMS 積乱雲位置

低気圧位置 = %GET_LOCATION_LOW()%
線状降水帯位置 = %GET_LOCATION_LINER_RAINBAND()%
積乱雲位置 = %GET_LOCATION_CUMULUS()%

IF (線状降水帯位置 == "線状降水帯遠方" || 線状降水帯位置 == "線状降水帯勢力圏") && TIME_ENTER_TERRITORY_LINER_RAINBAND:1 <= DAY:3 +1
	RETURNF "警報級の可能性［高］"
ELSEIF IS_LINER_RAINBAND:1
	RETURNF "警報級の可能性［中］"
ELSEIF 低気圧位置 == "台風遠方" && IS_TYPHOON:3 == 1 && TIME_ENTER_TERRITORY_TYPHOON:0 == DAY:2 && TIME_ENTER_TERRITORY_TYPHOON:1 <= DAY:3 + 2
	RETURNF "警報級の可能性［高］"
ELSEIF 低気圧位置 == "台風遠方" && TIME_ENTER_TERRITORY_TYPHOON:0 == DAY:2 && TIME_ENTER_TERRITORY_TYPHOON:1 <= DAY:3 + 1
	RETURNF "警報級の可能性［中］"
ELSEIF GET_LOCATION_AREA_TYPHOON() == 1 || 低気圧位置 == "台風勢力圏"
	RETURNF "警報級の可能性［高］"
ELSEIF GET_HEAT_THUNDERSTORM() && GET_FRONTAL_THUNDERSTORM()
	RETURNF "警報級の可能性［高］"
ELSEIF IS_COLD_LOW
	RETURNF "警報級の可能性［中］"
ELSEIF CONTINUOUS_RAINFALL >= 3
	RETURNF "警報級の可能性［中］"
ELSEIF (IS_SoJ_LOW == 2 && TIME_ENTER_TERRITORY_SoJ_LOW:1 <= DAY:3 + 1) && 低気圧位置 != "二つ玉低気圧通過" && 低気圧位置 != "日本海低気圧通過"
	RETURNF "警報級の可能性［高］"
ELSEIF IS_SoJ_LOW && 低気圧位置 != "二つ玉低気圧通過" && 低気圧位置 != "日本海低気圧通過"
	RETURNF "警報級の可能性［中］"
ELSE
	RETURNF "早期注意情報未発表"
ENDIF

;-------------------------------------------------
;熱雷発生フラグ判定
;-------------------------------------------------
@GET_HEAT_THUNDERSTORM
#FUNCTION
#DIMS 積乱雲位置
#DIMS 線状降水帯位置
#DIMS 低気圧位置

低気圧位置 = %GET_LOCATION_LOW()%
線状降水帯位置 = %GET_LOCATION_LINER_RAINBAND()%
積乱雲位置 = %GET_LOCATION_CUMULUS()%

IF 線状降水帯位置 == "線状降水帯勢力圏"
	RETURNF 1
ELSEIF 積乱雲位置 == "積乱雲直下"
	IF CAN_CUMULUS:0 == 3 && ATMOSPHERIC_INSTABILITY <= -3
		RETURNF 1
	ELSEIF CAN_CUMULUS:0 >= 4
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ELSEIF 積乱雲位置 == "積乱雲接近"
	IF CAN_CUMULUS:0 == 4 && ATMOSPHERIC_INSTABILITY <= -7
		RETURNF 1
	ELSEIF CAN_CUMULUS:0 >= 5
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ELSEIF IS_TYPHOON:3 == 1 && 低気圧位置 == "台風勢力圏" && ATMOSPHERIC_INSTABILITY <= -1
	RETURNF 1
ELSEIF (IS_RAINY_SEASON1:0 == 1 || IS_RAINY_SEASON2 == 1) && POWER_WARM_AIR >= 4 && ATMOSPHERIC_INSTABILITY <= 0
	RETURNF 1
ELSEIF (DAY:2 == 1 || DAY:2 == 3) && (低気圧位置 == "温暖前線接近" || 低気圧位置 == "寒冷前線通過" || 低気圧位置 == "寒冷渦接近") && POWER_WARM_AIR >= 5 && ATMOSPHERIC_INSTABILITY <= -1
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

;-------------------------------------------------
;界雷発生フラグ判定
;-------------------------------------------------
@GET_FRONTAL_THUNDERSTORM
#FUNCTION
#DIMS 低気圧位置

低気圧位置 = %GET_LOCATION_LOW()%

IF (DAY:2 == 1 || DAY:2 == 3) && (低気圧位置 == "寒冷前線通過" || 低気圧位置 == "寒冷渦接近") && POWER_WARM_AIR >= 3 && POWER_COLD_AIR >= 4
	RETURNF 1
ELSEIF IS_RAINY_SEASON1:1 == 1
	RETURNF 1
ELSEIF IS_RAINY_SEASON1:0 == 1 && POWER_COLD_AIR >= 4
	RETURNF 1
ELSEIF (低気圧位置 == "二つ玉低気圧接近" || 低気圧位置 == "二つ玉低気圧通過中") && POWER_WARM_AIR >= 3 && POWER_COLD_AIR >= 3
	RETURNF 1
ELSEIF (低気圧位置 == "日本海低気圧接近" || 低気圧位置 == "日本海低気圧通過中") && POWER_WARM_AIR >= 3 && POWER_COLD_AIR >= 3
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

;-------------------------------------------------
;天候が雷であるか
;-------------------------------------------------
@GET_WEATHER_THUNDERSTORM
#FUNCTION

IF (GET_HEAT_THUNDERSTORM() == 1 || GET_FRONTAL_THUNDERSTORM() == 1) && TIME:5 != 1 && (FLAG:異常気象 == 0 || FLAG:異常気象 == 9 || FLAG:異常気象 == 10) && !FORBIDDEN_CHANGE_WEATHER
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

;-------------------------------------------------
;天候システム上の時間帯に応じた名前を返す
;-------------------------------------------------
@GET_NAME_WEATHER_TIMEZOME(ARG = -1)
#FUNCTIONS

SIF ARG == -1
	ARG = TIME

SELECTCASE ARG
	;AM0~3
	CASE 0 TO 179
		RETURNF "未明"
	;AM3~6
	CASE 180 TO 359
		RETURNF "明け方"
	;AM6~9
	CASE 360 TO 539
		RETURNF "朝"
	;AM9~12
	CASE 540 TO 719
		RETURNF "昼前"
	;PM0~3
	CASE 720 TO 899
		RETURNF "昼過ぎ"
	;PM3~6
	CASE 900 TO 1079
		RETURNF "夕方"
	;PM6~9
	CASE 1080 TO 1259
		RETURNF "夜のはじめ頃"
	;PM9~12
	CASE IS >= 1260
		RETURNF "夜遅く"
	CASEELSE
		RETURNF "時間帯エラー"
ENDSELECT

;-------------------------------------------------
;台風接近時・猛吹雪時にFLAG:異常気象を設定する
;-------------------------------------------------
@SET_ABNORMAL_WEATHER

IF GET_LOCATION_AREA_TYPHOON() == 1
	FLAG:異常気象 = 9
ELSEIF TIME:5 >= 8 && TIME:5 <= 13 && GET_HEAT_THUNDERSTORM() == 0 && GET_FRONTAL_THUNDERSTORM() == 0 && WIND_VELOCITY >= 2
	FLAG:異常気象 = 10
ELSE
	FLAG:異常気象 = 0
ENDIF

;-------------------------------------------------
;時刻・天候に応じて背景色を変更
;2022/11/23改造
;	・設定すべき背景色と現在背景色を比較し、同じ色なら変更を飛ばす処理(@SET_MAP_WEATHER_BGCOLOR_NEW)
;	・背景色変更処理の前にREDRAW 0をセットし、関数終了後に戻す処理
;	・USE_NEWに0をセットすることで旧処理に戻せる
;-------------------------------------------------
@SET_MAP_WEATHER_BGCOLOR
#DIM CURRED       ;CURRENT REDRAW。CURRENTREDRAWを保存
#DIM USE_NEW = 1  ;0にすると旧来の関数で実行する
#DIM SECOND , 2   ;時間計測用
#DIMS TEMPS       ;デバッグ表示用
CURRED = CURRENTREDRAW()
;#;TEMPS = @SET_MAP_WEATHER_BGCOLOR_%\@USE_NEW == 1 ? NEW # OLD \@%開始時REDRAW＝{CURRED}、

;#;SECOND:0 = GETMILLISECOND()
REDRAW 0

;#;TEMPS += @"実行時REDRAW＝{CURRENTREDRAW()}、"

IF USE_NEW
	CALL SET_MAP_WEATHER_BGCOLOR_NEW
ELSE
	CALL SET_MAP_WEATHER_BGCOLOR_OLD
ENDIF

REDRAW CURRED
;#;SECOND:1 = GETMILLISECOND()

;#;PRINTFORML %TEMPS%における描画所要時間＝{SECOND:1 - SECOND:0}

@SET_MAP_WEATHER_BGCOLOR_NEW
#DIMS 低気圧位置
#DIMS 積乱雲位置
#DIM NEXTCOLOR
SIF FLAG:時間停止 || !FLAG:背景色推移
	RETURN

低気圧位置 = %GET_LOCATION_LOW()%
積乱雲位置 = %GET_LOCATION_CUMULUS()%

SELECTCASE TIME:0
	;AM4~6　早朝
	CASE 240 TO 359
		SELECTCASE TIME:5
			CASE 6, 7, 11
				NEXTCOLOR = C_MAP_霧_夜
			CASE 8, 9, 10, 12
				NEXTCOLOR = C_MAP_雪_夜
			CASEELSE
				NEXTCOLOR = C_MAP_晴れ_早朝
		ENDSELECT
	
	;AM6~8　正午前
	CASE 360 TO 479
		SELECTCASE TIME:5
			CASE IS <= 3
				IF 低気圧位置 == "台風勢力圏"
					NEXTCOLOR = C_MAP_夕焼け_台風
				ELSEIF 低気圧位置 == "寒冷渦接近" || IS_COLD_LOW || 低気圧位置 == "二つ玉低気圧接近" || 低気圧位置 == "日本海低気圧接近" || 低気圧位置 == "南岸低気圧接近" || 低気圧位置 == "温暖前線接近"
					NEXTCOLOR = C_MAP_夕焼け_低気圧
				ELSE
					NEXTCOLOR = C_MAP_晴れ_朝
				ENDIF
			CASE 6, 7, 11
				NEXTCOLOR = C_MAP_霧_昼
			CASE 4, 5, 13, 14, 15
				NEXTCOLOR = C_MAP_雨_昼
			CASE 8, 9, 10, 12
				NEXTCOLOR = C_MAP_雪_昼
			CASEELSE
				NEXTCOLOR = C_MAP_晴れ_朝
		ENDSELECT
	
	;AM8~12　正午前
	CASE 480 TO 719
		SELECTCASE TIME:5
			CASE 0, 1, 2
				NEXTCOLOR = C_MAP_晴れ_朝
			CASE 3
				NEXTCOLOR = C_MAP_曇り_昼
			CASE 6, 7, 11
				NEXTCOLOR = C_MAP_霧_昼
			CASE 4, 5, 13, 14, 15
				NEXTCOLOR = C_MAP_雨_昼
			CASE 8, 9, 10, 12
				NEXTCOLOR = C_MAP_雪_昼
			CASEELSE
				NEXTCOLOR = C_MAP_晴れ_朝
		ENDSELECT
	
	;PM12~6　昼
	CASE 720 TO 1079
		SELECTCASE 積乱雲位置
			CASE "積乱雲接近", "積乱雲直下"
				SELECTCASE CAN_CUMULUS:0
				CASE 3, 4, 5
					NEXTCOLOR = C_MAP_積乱雲_昼
				CASEELSE
					NEXTCOLOR = C_MAP_曇り_昼
				ENDSELECT
			CASEELSE
				SELECTCASE TIME:5
					CASE 0, 1
						NEXTCOLOR = C_MAP_晴れ_昼
					CASE 2
						NEXTCOLOR = C_MAP_薄曇_昼
					CASE 3
						NEXTCOLOR = C_MAP_曇り_昼
					CASE 6, 7, 11
						NEXTCOLOR = C_MAP_霧_昼
					CASE 4, 5, 13, 14, 15
						NEXTCOLOR = C_MAP_雨_昼
					CASE 8, 9, 10, 12
						NEXTCOLOR = C_MAP_雪_昼
					CASEELSE
						NEXTCOLOR = C_MAP_晴れ_昼
				ENDSELECT
		ENDSELECT
	
	;PM6~8　夕
	CASE 1080 TO 1199
		SELECTCASE TIME:5
			CASE IS <= 3
				IF 低気圧位置 == "台風勢力圏"
					NEXTCOLOR = C_MAP_夕焼け_台風
				ELSEIF 低気圧位置 == "寒冷渦接近" || IS_COLD_LOW || 低気圧位置 == "二つ玉低気圧接近" || 低気圧位置 == "日本海低気圧接近" || 低気圧位置 == "南岸低気圧接近" || 低気圧位置 == "温暖前線接近"
					NEXTCOLOR = C_MAP_夕焼け_低気圧
				ELSE
					NEXTCOLOR = C_MAP_夕焼け_通常
				ENDIF
			CASE 6, 7, 11
				NEXTCOLOR = C_MAP_霧_昼
			CASE 4, 5, 13, 14, 15
				NEXTCOLOR = C_MAP_雨_昼
			CASE 8, 9, 10, 12
				NEXTCOLOR = C_MAP_雪_昼
			CASEELSE
				NEXTCOLOR = C_MAP_夕焼け_通常
		ENDSELECT
	
	;PM8~AM4　夜・深夜
	CASE 0 TO 239, IS >= 1200
		SELECTCASE TIME:5
			CASE 0, 1, 2
				NEXTCOLOR = C_MAP_晴れ_夜
			CASE 6, 7, 11
				NEXTCOLOR = C_MAP_霧_夜
			CASE 3, 4, 5, 13, 14, 15
				NEXTCOLOR = C_MAP_曇り_夜
			CASE 8, 9, 10, 12
				NEXTCOLOR = C_MAP_雪_夜
			CASEELSE
				NEXTCOLOR = C_MAP_晴れ_夜
		ENDSELECT
ENDSELECT

IF NEXTCOLOR != GETBGCOLOR()
	SETBGCOLOR NEXTCOLOR
	;#;PRINTL （背景色を変更）
ENDIF

@SET_MAP_WEATHER_BGCOLOR_OLD
#DIMS 低気圧位置
#DIMS 積乱雲位置

SIF FLAG:時間停止 || !FLAG:背景色推移
	RETURN

低気圧位置 = %GET_LOCATION_LOW()%
積乱雲位置 = %GET_LOCATION_CUMULUS()%

SELECTCASE TIME:0
	;AM4~6　早朝
	CASE 240 TO 359
		SELECTCASE TIME:5
			CASE 6, 7, 11
				SETBGCOLOR C_MAP_霧_夜
			CASE 8, 9, 10, 12
				SETBGCOLOR C_MAP_雪_夜
			CASEELSE
				SETBGCOLOR C_MAP_晴れ_早朝
		ENDSELECT
	
	;AM6~8　正午前
	CASE 360 TO 479
		SELECTCASE TIME:5
			CASE IS <= 3
				IF 低気圧位置 == "台風勢力圏"
					SETBGCOLOR C_MAP_夕焼け_台風
				ELSEIF 低気圧位置 == "寒冷渦接近" || IS_COLD_LOW || 低気圧位置 == "二つ玉低気圧接近" || 低気圧位置 == "日本海低気圧接近" || 低気圧位置 == "南岸低気圧接近" || 低気圧位置 == "温暖前線接近"
					SETBGCOLOR C_MAP_夕焼け_低気圧
				ELSE
					SETBGCOLOR C_MAP_晴れ_朝
				ENDIF
			CASE 6, 7, 11
				SETBGCOLOR C_MAP_霧_昼
			CASE 4, 5, 13, 14, 15
				SETBGCOLOR C_MAP_雨_昼
			CASE 8, 9, 10, 12
				SETBGCOLOR C_MAP_雪_昼
			CASEELSE
				SETBGCOLOR C_MAP_晴れ_朝
		ENDSELECT
	
	;AM8~12　正午前
	CASE 480 TO 719
		SELECTCASE TIME:5
			CASE 0, 1, 2
				SETBGCOLOR C_MAP_晴れ_朝
			CASE 3
				SETBGCOLOR C_MAP_曇り_昼
			CASE 6, 7, 11
				SETBGCOLOR C_MAP_霧_昼
			CASE 4, 5, 13, 14, 15
				SETBGCOLOR C_MAP_雨_昼
			CASE 8, 9, 10, 12
				SETBGCOLOR C_MAP_雪_昼
			CASEELSE
				SETBGCOLOR C_MAP_晴れ_朝
		ENDSELECT
	
	;PM12~6　昼
	CASE 720 TO 1079
		SELECTCASE 積乱雲位置
			CASE "積乱雲接近", "積乱雲直下"
				SELECTCASE CAN_CUMULUS:0
				CASE 3, 4, 5
					SETBGCOLOR C_MAP_積乱雲_昼
				CASEELSE
					SETBGCOLOR C_MAP_曇り_昼
				ENDSELECT
			CASEELSE
				SELECTCASE TIME:5
					CASE 0, 1
						SETBGCOLOR C_MAP_晴れ_昼
					CASE 2
						SETBGCOLOR C_MAP_薄曇_昼
					CASE 3
						SETBGCOLOR C_MAP_曇り_昼
					CASE 6, 7, 11
						SETBGCOLOR C_MAP_霧_昼
					CASE 4, 5, 13, 14, 15
						SETBGCOLOR C_MAP_雨_昼
					CASE 8, 9, 10, 12
						SETBGCOLOR C_MAP_雪_昼
					CASEELSE
						SETBGCOLOR C_MAP_晴れ_昼
				ENDSELECT
		ENDSELECT
	
	;PM6~8　夕
	CASE 1080 TO 1199
		SELECTCASE TIME:5
			CASE IS <= 3
				IF 低気圧位置 == "台風勢力圏"
					SETBGCOLOR C_MAP_夕焼け_台風
				ELSEIF 低気圧位置 == "寒冷渦接近" || IS_COLD_LOW || 低気圧位置 == "二つ玉低気圧接近" || 低気圧位置 == "日本海低気圧接近" || 低気圧位置 == "南岸低気圧接近" || 低気圧位置 == "温暖前線接近"
					SETBGCOLOR C_MAP_夕焼け_低気圧
				ELSE
					SETBGCOLOR C_MAP_夕焼け_通常
				ENDIF
			CASE 6, 7, 11
				SETBGCOLOR C_MAP_霧_昼
			CASE 4, 5, 13, 14, 15
				SETBGCOLOR C_MAP_雨_昼
			CASE 8, 9, 10, 12
				SETBGCOLOR C_MAP_雪_昼
			CASEELSE
				SETBGCOLOR C_MAP_夕焼け_通常
		ENDSELECT
	
	;PM8~AM4　夜・深夜
	CASE 0 TO 239, IS >= 1200
		SELECTCASE TIME:5
			CASE 0, 1, 2
				SETBGCOLOR C_MAP_晴れ_夜
			CASE 6, 7, 11
				SETBGCOLOR C_MAP_霧_夜
			CASE 3, 4, 5, 13, 14, 15
				SETBGCOLOR C_MAP_曇り_夜
			CASE 8, 9, 10, 12
				SETBGCOLOR C_MAP_雪_夜
			CASEELSE
				SETBGCOLOR C_MAP_晴れ_夜
		ENDSELECT
ENDSELECT



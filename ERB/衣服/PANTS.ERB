;きょうのぱんつ処理の洗練
;ARGのはくことができるパンツを抽選する
;元々の今日のぱんつ関数だと処理上限がないのでシャッフルする
;FISHER_YATES_SHAFFLEつかおうとしたらF関数内部でCALLすんのムリだったでござる
@今日のぱんつ(ARG)
#FUNCTION
#LOCALSIZE 2
#LOCALSSIZE 1
#DIM TODAYS_PANTS
#DIM RAND_PANTS, MAXPANTS
#DIM P_TABLE

VARSET RAND_PANTS , 0
FOR LOCAL:0 , 0 , MAXPANTS
	RAND_PANTS:(LOCAL:0) = LOCAL:0
NEXT
FOR LOCAL:0 , 0 , MAXPANTS - 1
	LOCAL:1 = RAND(LOCAL:0 , MAXPANTS)
	SWAP RAND_PANTS:(LOCAL:0), RAND_PANTS:(LOCAL:1)
NEXT

FOR P_TABLE, 0, MAXPANTS
	IF GET_INT(ARG, "下半身下着_ずらし可能", RAND_PANTS:P_TABLE, "はける")
		TODAYS_PANTS = RAND_PANTS:P_TABLE
		BREAK
	ELSE
		CONTINUE
	ENDIF
	SIF P_TABLE == MAXPANTS - 1
		THROW %CALLNAME:ARG%のぱんつ抽選に失敗
NEXT
RETURNF TODAYS_PANTS

;互換用
@PANTSNAME(ARG, CHARA)
#FUNCTIONS
#DIM CHARA

CALLF 下半身下着チェック(CHARA)
RETURNF GET_STR(CHARA, CLOTHES_PARTS_TO_CATEGORY("下半身下着２"), ARG, "名前")

@PANTS_DESCRIPTION(ARG, CHARA)
#FUNCTIONS
#DIM CHARA
RETURNF GET_STR(CHARA, CLOTHES_PARTS_TO_CATEGORY("下半身下着２"), ARG, "描写")

@PANTS_GET(ARG,ARGS)
#DIM 種類
種類 = EQUIP:ARG:下半身下着２
CALL PANTS_GETTING(ARG, 種類)

CFLAG:ARG:ノーパン = 1
EQUIP:ARG:下半身下着２ = 0
CALL CLOTHES_SETTING_TRAIN(ARG)

SIF CFLAG:ARG:パジャマ == 1
	CFLAG:ARG:パジャマパンツ = 0

SIF ARGS == "もらう"
	CFLAG:ARG:ノーパン = 2

@PANTS_GETTING(ARG, 種類)
#DIM 種類
#DIM スキマ送り成否
スキマ送り成否 = 0
PANTS_TEMP:ARG:種類 += 1
IF ITEM:コレクションのスキマ
	IF FLAG:Cスキマ自動仕様
		CALL PANTS_SUKIMA(ARG, 種類)
		スキマ送り成否 = RESULT
	ELSEIF FLAG:パンツ輸送 == 0
		PRINTFORML コレクションのスキマを使いますか？（残り：{ITEM:コレクションのスキマ}）
		CALL ASK_YN
		IF !RESULT
			CALL PANTS_SUKIMA(ARG, 種類)
			スキマ送り成否 = RESULT
		ENDIF
	ENDIF
ENDIF
SIF !スキマ送り成否
	FLAG:パンツ輸送 = 1
RETURN スキマ送り成否

@PANTS_SUKIMA(ARG, 種類)
#DIM 種類
#DIM スキマ送り成否
スキマ送り成否 = 0
IF PANTS_TEMP:ARG:種類
	CFLAG:ARG:(種類 + パンツ管理) += PANTS_TEMP:ARG:種類 
	PANTS_TEMP:ARG:種類 = 0
	PRINTFORMW %CALLNAME:MASTER%はコレクションのスキマを開き、%CALLNAME:ARG%の%PANTSNAME(種類,ARG)%を放り込んだ
	ITEM:コレクションのスキマ -= 1
	スキマ送り成否 = 1
ENDIF
RETURN スキマ送り成否

@SHOW_HAVE_COLLECTION
IF ITEM:コレクションのスキマ == 0
	CALL DISPLAY_ALL_COLLECTION(1,0)
	PRINTW 
ELSE
	PRINTFORMW コレクションのスキマを使いますか？（残り：{ITEM:コレクションのスキマ}）
	DRAWLINE
	CALL DISPLAY_ALL_COLLECTION(1,1)
	DRAWLINE
	PRINTFORML [0] 使わない
	INPUT
	LOCAL = RESULT
	CALL PANTS_SUKIMA(LOCAL / 100, LOCAL % 100)
	;パンツ輸送対策
	SIF RESULT
		CALL SPEND_HAVE_COLLECTION(LOCAL)
ENDIF


;### 濡れて透けうるパンツを穿いてるか判定用式中関数 ################
;ARG=キャラ番号、返り値１は透けうる、０は透けえない
;る～こと口上からの移植
@PANTIES_TRANSPARENCY(ARG)
#FUNCTION

;パンツを奪ったorもらったor元から穿いてない場合は０
IF CFLAG:ARG:ノーパン || (CFLAG:ARG:205 == 0 && CFLAG:ARG:206 == 0)
	RETURNF 0
;ノーパンではない場合
ELSE
	;いま現在パンツを穿いている場合
	IF EQUIP:ARG:下半身下着１ || EQUIP:ARG:下半身下着２
		IF EQUIP:ARG:下半身下着１
			RETURNF GET_INT(ARG, "下半身下着_ずらし不可", EQUIP:ARG:下半身下着１, "透ける(前)")
		ELSEIF EQUIP:ARG:下半身下着２
			RETURNF GET_INT(ARG, "下半身下着_ずらし可能", EQUIP:ARG:下半身下着２, "透ける(前)")
		ENDIF
	;いま現在穿いてないがあとで穿きなおす場合はそのパンツが透けうるかいなかを返す
	ELSE
		;下半身下着１
		IF CFLAG:ARG:205
			RETURNF GET_INT(ARG, "下半身下着_ずらし不可", CFLAG:ARG:205, "透ける(前)")
		;下半身下着２
		ELSEIF CFLAG:ARG:206
			RETURNF GET_INT(ARG, "下半身下着_ずらし可能", CFLAG:ARG:206, "透ける(前)")
		ENDIF
	ENDIF
ENDIF

;該当なければ透けないことにする
RETURNF 0

@PANTS_ALREADY_COLLECTED(ARG)
#FUNCTION
#DIM 種類
SIF !ARG
	ARG = TARGET
;はいてるパンツの所有確認
IF EQUIP:ARG:下半身下着２
	FOR 種類,0,MAXPANTS
		IF 種類 == EQUIP:ARG:下半身下着２
			SIF CFLAG:ARG:(種類 + パンツ管理) > 0
				RETURNF 1
		ENDIF
	NEXT
ENDIF
RETURNF 0
;-----------------------------------------------------------
;狩った鳥獣リストの表示
;-----------------------------------------------------------
@SHOW_COLLECTION_HUNTED
#DIM 選んだ獣
#DIM 全種数
#DIM 狩った種数
#DIM 狩った総数
#DIM 消去行数
#DIM 改行

VARSET LOCAL
全種数 = 0
狩った種数 = 0
狩った総数 = 0
消去行数 = 0
改行 = 0

CLEARLINE 1
PRINTFORML 今までに狩った鳥獣のリスト
DRAWLINE
FOR LOCAL, 1, 61
	CALL CRITTER_DATA(LOCAL)
	;獣が無名でない
	IF CritterName != ""
		全種数 ++
		SIF LOCAL < 10
			PRINTFORM  
		IF CAPTURE_RECORD:LOCAL
			狩った種数 ++
			PRINTFORM [{LOCAL}] %CritterName, 20, LEFT%
			SETCOLOR C_L_GRAY
			PRINTFORM （狩った頭数：
			SELECTCASE CAPTURE_RECORD:LOCAL
				CASE IS < 10
					PRINTFORM     
				CASE IS < 100
					PRINTFORM    
				CASE IS < 1000
					PRINTFORM   
				CASE IS < 10000
					PRINTFORM  
			ENDSELECT
			狩った総数 += CAPTURE_RECORD:LOCAL
			IF STRCOUNT(CritterTribe, "鳥/")
				PRINTFORM {CAPTURE_RECORD:LOCAL}羽 ）
			ELSE
				PRINTFORM {CAPTURE_RECORD:LOCAL}匹 ）
			ENDIF
		ELSE
			PRINTFORM [{LOCAL}] ????????????                                
		ENDIF
		改行 ++
		IF 改行 == 2
			PRINTL 
			改行 = 0
		ELSE
			PRINT 　　　
		ENDIF
	ENDIF
	RESETCOLOR
	;獣関連の変数リセット
	CALL CRITTER_V_RESET
NEXT
PRINTL 
SETCOLOR 0x00FF00
PRINTFORM 狩った鳥獣の種類：{狩った種数}／{全種数}
IF 狩った総数
	PRINTFORML 　狩った鳥獣の総数：{狩った総数}頭・羽
ELSE
	PRINTL 
ENDIF
RESETCOLOR
DRAWLINE
PRINT [0] - 戻る

$LOOP
INPUT

選んだ獣 = RESULT
SELECTCASE 選んだ獣
	CASE 1 TO 61
		CALL CRITTER_DATA(選んだ獣)
		IF CritterName != ""
			CLEARLINE 1
			CALL PRINT_CRITTERINFO(選んだ獣)
			IF CAPTURE_RECORD:選んだ獣
				消去行数 = 5
			ELSE
				消去行数 = 2
			ENDIF
			CLEARLINE 消去行数
			GOTO LOOP
		ELSE
			CLEARLINE 1
			GOTO LOOP
		ENDIF
	CASE 0
		RETURN
		;獣関連の変数リセット
		CALL CRITTER_V_RESET
	CASEELSE
		CLEARLINE 1
		GOTO LOOP
ENDSELECT

;-----------------------------------------------------------
;選択した獣の説明／ヒント表記
;ARG …選んだ獣番号
;-----------------------------------------------------------
@PRINT_CRITTERINFO(ARG)

DRAWLINE
IF CAPTURE_RECORD:ARG
	FONTSTYLE 1
	PRINTPLAINFORM [{ARG}] %CritterName%
	FONTREGULAR
	IF STRCOUNT(CritterTribe, "鳥/")
		PRINTFORML （狩った数：{CAPTURE_RECORD:ARG}羽 ）
	ELSE
		PRINTFORML （狩った数：{CAPTURE_RECORD:ARG}頭 ）
	ENDIF
	IF ABL:MASTER:教養 < 3
		CALL COLORMESSAGE(@"　　（教養Lvを上げると、分布域・猟期がわかる…）",C_L_GRAY,1)
	ELSE
		PRINTFORML 　　%CritterHint%
	ENDIF
	PRINTFORML 　　◇平均的な収穫物： %ITEMNAME:CritterMeat% × {CritterMeat_Value} ◇
	PRINTFORML 　　%CritterIntro%
ELSE
	PRINTPLAINFORM [{ARG}] ?????????????
	PRINTFORML 　　%CritterHint%
ENDIF
WAIT


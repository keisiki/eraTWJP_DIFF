;-----------------------------------------------------------
;狩った鳥獣リストの表示
;-----------------------------------------------------------
@SHOW_COLLECTION_HUNTED
#DIM 選んだ獣
#DIM 全種数
#DIM 狩った種数
#DIM 狩った総数
#DIM 消去行数
#DIM 改行

VARSET LOCAL
全種数 = 0
狩った種数 = 0
狩った総数 = 0
消去行数 = 0
改行 = 0

CLEARLINE 1
PRINTFORML 今までに狩った鳥獣のリスト
DRAWLINE
FOR LOCAL, 1, 51
	CALL CRITTER_DATA(LOCAL)
	;獣が無名でない
	IF CritterName != ""
		全種数 ++
		SIF LOCAL < 10
			PRINTFORM  
		IF CAPTURE_RECORD:LOCAL
			狩った種数 ++
			PRINTFORM [{LOCAL}] %CritterName, 20, LEFT%
			SETCOLOR C_L_GRAY
			PRINTFORM （狩った頭数：
			SELECTCASE CAPTURE_RECORD:LOCAL
				CASE IS < 10
					PRINTFORM     
				CASE IS < 100
					PRINTFORM    
				CASE IS < 1000
					PRINTFORM   
				CASE IS < 10000
					PRINTFORM  
			ENDSELECT
			狩った総数 += CAPTURE_RECORD:LOCAL
			PRINTFORM {CAPTURE_RECORD:LOCAL}匹 ）
		ELSE
			PRINTFORM [{LOCAL}] ????????????                                
		ENDIF
		改行 ++
		IF 改行 == 2
			PRINTL 
			改行 = 0
		ELSE
			PRINT 　　　
		ENDIF
	ENDIF
	RESETCOLOR
	;獣関連の変数リセット
	CALL CRITTER_V_RESET
NEXT
SETCOLOR 0x00FF00
PRINTFORM 狩った鳥獣の種類：{狩った種数}／{全種数}
IF 狩った総数
	PRINTFORML 　狩った鳥獣の総数：{狩った総数}頭・羽
ELSE
	PRINTL 
ENDIF
RESETCOLOR
DRAWLINE
PRINT [0] - 戻る

$LOOP
INPUT

選んだ獣 = RESULT
SELECTCASE 選んだ獣
	CASE 1 TO 50
		CALL CRITTER_DATA(選んだ獣)
		IF CritterName != ""
			CLEARLINE 1
			CALL PRINT_CRITTERINFO(選んだ獣)
			IF CAPTURE_RECORD:選んだ獣
				消去行数 = 5
			ELSE
				消去行数 = 2
			ENDIF
			CLEARLINE 消去行数
			GOTO LOOP
		ELSE
			CLEARLINE 1
			GOTO LOOP
		ENDIF
	CASE 0
		RETURN
		;獣関連の変数リセット
		CALL CRITTER_V_RESET
	CASEELSE
		CLEARLINE 1
		GOTO LOOP
ENDSELECT

;-----------------------------------------------------------
;選択した獣の説明／ヒント表記
;ARG …選んだ獣番号
;-----------------------------------------------------------
@PRINT_CRITTERINFO(ARG)

DRAWLINE
IF CAPTURE_RECORD:ARG
	FONTSTYLE 1
	PRINTPLAINFORM [{ARG}] %CritterName%
	FONTREGULAR
	PRINTFORML （狩った数：{CAPTURE_RECORD:ARG}頭 ）
	IF ABL:MASTER:教養 < 3
		CALL COLORMESSAGE(@"　　（教養Lvを上げると、分布域・猟期がわかる…）",C_L_GRAY,1)
	ELSE
		CALL HUNTING_HINT(ARG)
	ENDIF
	PRINTFORML 　　◇平均的な収穫物： %ITEMNAME:CritterMeat% × {CritterMeat_Value} ◇
	PRINTFORML 　　%CritterIntro%
ELSE
	PRINTPLAINFORM [{ARG}] ?????????????
	CALL HUNTING_HINT(ARG)
ENDIF
WAIT

;-----------------------------------------------------------
;選択した獣のヒント文
;ARG …選んだ獣番号
;-----------------------------------------------------------
@HUNTING_HINT(ARG)

SELECTCASE ARG
	CASE 1 ;ネズミ
		PRINTFORML 　　あらゆる環境に分布する／小型獣
	CASE 2 ;イタチ
		PRINTFORML 　　極端に薄暗い環境以外に分布／小型獣
	CASE 3 ;テン
		PRINTFORML 　　極端に薄暗い環境以外に分布／小型獣
	CASE 4 ;ミンク
		PRINTFORML 　　極端に薄暗い環境以外に分布／小型獣
	CASE 5 ;リス
		PRINTFORML 　　竹林と極端に薄暗い環境以外に分布。冬眠する／小型獣
	CASE 6 ;アライグマ
		PRINTFORML 　　高山と極端に薄暗い環境以外に分布／中型獣
	CASE 7 ;タヌキ
		PRINTFORML 　　高山と極端に薄暗い環境以外に分布／中型獣
	CASE 8 ;アナグマ
		PRINTFORML 　　高山以外に分布。冬眠する／中型獣
	CASE 9 ;ハクビシン
		PRINTFORML 　　高山と極端に薄暗い環境以外に分布／中型獣
	CASE 10 ;キツネ
		PRINTFORML 　　極端に薄暗い環境以外に分布／中型獣
	CASE 11 ;ノイヌ
		PRINTFORML 　　人界に近い環境を好む／中型獣
	CASE 12 ;ノネコ
		PRINTFORML 　　人界に近い環境を好む／中型獣
	CASE 13 ;ノウサギ
		PRINTFORML 　　極端に薄暗い環境以外に分布／中型獣
	CASE 14 ;ヌートリア
		PRINTFORML 　　沼や川など水場を好む／中型獣
	CASE 15 ;イノシシ
		PRINTFORML 　　極端に薄暗い環境以外に分布。冬に最も脂が乗る／大型獣
	CASE 16 ;シカ
		PRINTFORML 　　竹林と極端に薄暗い環境以外に分布。秋に最も脂が乗る／大型獣
	CASE 17 ;カモシカ
		PRINTFORML 　　竹林と極端に薄暗い環境以外に分布。秋に最も脂が乗る／大型獣
	CASE 18 ;サル
		PRINTFORML 　　人界から離れた環境を好む／中型獣
	CASE 19 ;ツキノワグマ
		PRINTFORML 　　人界から離れた環境を好む。冬眠する。秋に最も脂が乗る／大型獣
	CASE 20 ;ヘビ
		PRINTFORML 　　高山以外に分布。冬眠する／小型獣
	CASE 21 ;オオコウモリ
		PRINTFORML 　　薄暗い環境を好む。冬眠する／中型獣
	CASE 22 ;モグラ
		PRINTFORML 　　薄暗い環境を好む／小型獣
	CASE 23 ;デスワーム
		PRINTFORML 　　希少種。薄暗い環境の地中に潜んでいるらしい／大型獣
	CASE 24 ;ツチノコ
		PRINTFORML 　　希少種。林野に隠れ潜んでいるらしい／中型獣
	CASE 25 ;スライム
		PRINTFORML 　　……獣？　薄暗い環境を好むらしい／中型獣
	CASE 26 ;マタンゴ
		PRINTFORML 　　希少種。キノコの生える環境に現れるらしい／大型獣
	CASE 27 ;ヤベオオツノジカ
		PRINTFORML 　　希少種。神聖な森の霊獣として現れると言われる／大型獣
	CASE 28 ;ハナイズミモリウシ
		PRINTFORML 　　希少種。高山にひっそりと住まうと言われる／大型獣
ENDSELECT

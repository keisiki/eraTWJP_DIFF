@BONUS
#DIM PAGE
#DIM CONST MAXPAGE = 6
;PAGE 0=面識 1=好感度 2=パンツ 3=陥落 4=孕ませ 5=技能 6=弾幕勝負

CALL 実績フラグ移行
CALL ボーナス素質判定

TRYCALLFORM PRINT_BONUS_{PAGE}

DRAWLINE
PRINTFORML 
PRINTFORMC [998] 前のページ
PRINTFORMC ページ {PAGE + 1} / {MAXPAGE + 1}
PRINTFORMC [999] 次のページ
PRINTL 
PRINTC  
PRINTFORMC [0] 戻る
INPUT
SELECTCASE RESULT
	CASE 0
		RETURN
	CASE 998
		PAGE --
	CASE 999
		PAGE ++
	CASEELSE
		TRYCALLFORM REWARD_BONUS_{PAGE}(RESULT)
ENDSELECT
SIF PAGE >= MAXPAGE + 1
	PAGE = 0
SIF PAGE < 0
	PAGE = MAXPAGE
RESTART

@ボーナス素質判定(ARG)
;面識2,居場所察知
IF GETBIT(FLAG:面識ボーナス取得状況, 2)
	SETBIT TALENT:MASTER:居場所察知, 0
ELSE
	CLEARBIT TALENT:MASTER:居場所察知, 0
ENDIF

;面識3,集合
TALENT:MASTER:集合 = GETBIT(FLAG:面識ボーナス取得状況, 3)

;面識4,居場所察知Ⅱ
IF GETBIT(FLAG:面識ボーナス取得状況, 4)
	SETBIT TALENT:MASTER:居場所察知, 1
ELSE
	CLEARBIT TALENT:MASTER:居場所察知, 1
ENDIF

;面識5,千客万来
TALENT:MASTER:千客万来 = GETBIT(FLAG:面識ボーナス取得状況, 5)

;好感度5,汚部屋察知
TALENT:MASTER:汚部屋察知 = GETBIT(FLAG:好感度ボーナス取得状況, 5)

;好感度6,ぱんつ密輸
TALENT:MASTER:ぱんつ密輸 = GETBIT(FLAG:好感度ボーナス取得状況, 6)

;好感度8,両手に花
TALENT:MASTER:両手に花 = GETBIT(FLAG:好感度ボーナス取得状況, 8)

;パンツ2,劣情察知
IF GETBIT(FLAG:パンツボーナス取得状況, 2)
	SETBIT TALENT:MASTER:劣情察知, 0
ELSE
	CLEARBIT TALENT:MASTER:劣情察知, 0
ENDIF

;パンツ5,劣情察知Ⅱ
IF GETBIT(FLAG:パンツボーナス取得状況, 5)
	SETBIT TALENT:MASTER:劣情察知, 1
ELSE
	CLEARBIT TALENT:MASTER:劣情察知, 1
ENDIF

;陥落3,謎の魅力
TALENT:MASTER:謎の魅力 = GETBIT(FLAG:陥落ボーナス取得状況, 3)

;陥落4,回復早い
TALENT:MASTER:回復速度 = GETBIT(FLAG:陥落ボーナス取得状況, 4)

;陥落5,生理周期可視化
FLAG:1007 = GETBIT(FLAG:陥落ボーナス取得状況, 5)

;陥落7,魅力
TALENT:MASTER:魅力 = GETBIT(FLAG:陥落ボーナス取得状況, 7)

;LOOP使ってまとめた方がいいかもしれないが、調整したり改変したりしやすいのでこのままに
;孕ませ1,万能
TALENT:MASTER:万能孕ませ = GETBIT(FLAG:孕ませボーナス取得状況, 1)

;孕ませ2,巫女
TALENT:MASTER:巫女孕ませ = GETBIT(FLAG:孕ませボーナス取得状況, 2)

;孕ませ3,妖精
TALENT:MASTER:妖精孕ませ = GETBIT(FLAG:孕ませボーナス取得状況, 3)

;孕ませ4,神霊
TALENT:MASTER:神霊孕ませ = GETBIT(FLAG:孕ませボーナス取得状況, 4)

;孕ませ5,付喪神
TALENT:MASTER:付喪神孕ませ = GETBIT(FLAG:孕ませボーナス取得状況, 5)

;孕ませ6,鬼
TALENT:MASTER:鬼孕ませ = GETBIT(FLAG:孕ませボーナス取得状況, 6)

;孕ませ7,天狗
TALENT:MASTER:天狗孕ませ = GETBIT(FLAG:孕ませボーナス取得状況, 7)

;孕ませ8,蓬莱
TALENT:MASTER:蓬莱孕ませ = GETBIT(FLAG:孕ませボーナス取得状況, 8)

;孕ませ9,妖怪
TALENT:MASTER:妖怪孕ませ = GETBIT(FLAG:孕ませボーナス取得状況, 9)

;;孕ませ10,禁断の知識
;TALENT:MASTER:禁断の知識 = GETBIT(FLAG:孕ませボーナス取得状況, 10)

;孕ませ11,幽霊
TALENT:MASTER:幽霊孕ませ = GETBIT(FLAG:孕ませボーナス取得状況, 11)

;孕ませ12,人形
TALENT:MASTER:人形孕ませ = GETBIT(FLAG:孕ませボーナス取得状況, 12)

;技能1,Ｃ鈍感無視
TALENT:MASTER:Ｃ鈍感無視 = GETBIT(FLAG:技能ボーナス取得状況, 1)

;技能2,Ｂ鈍感無視
TALENT:MASTER:Ｂ鈍感無視 = GETBIT(FLAG:技能ボーナス取得状況, 2)

;技能3,Ｖ鈍感無視
TALENT:MASTER:Ｖ鈍感無視 = GETBIT(FLAG:技能ボーナス取得状況, 3)

;技能4,Ａ鈍感無視
TALENT:MASTER:Ａ鈍感無視 = GETBIT(FLAG:技能ボーナス取得状況, 4)

;技能5,魅惑
TALENT:MASTER:魅惑 = GETBIT(FLAG:技能ボーナス取得状況, 5)

;弾幕勝負2,グレイズ上手
TALENT:MASTER:グレイズ上手 = GETBIT(FLAG:弾幕勝負ボーナス取得状況, 2)

;弾幕勝負4,霊力充填
TALENT:MASTER:霊力充填 = GETBIT(FLAG:弾幕勝負ボーナス取得状況, 4)

;弾幕勝負5,エクステンド
TALENT:MASTER:エクステンド = GETBIT(FLAG:弾幕勝負ボーナス取得状況, 5)

;弾幕勝負6,ルナシューター
TALENT:MASTER:ルナシューター = GETBIT(FLAG:弾幕勝負ボーナス取得状況, 6)

@BONUS_CAUTION
CALL COLORMESSAGE(@"今の段階でこの実績を戻すと再取得することができません", C_ORANGE, 1)
CALL COLORMESSAGE(@"本当に戻してよろしいですか？", C_ORANGE, 1)
CALL ASK_YN
RETURN RESULT

;----------------------------------------
;面識ボーナス
;----------------------------------------
@PRINT_BONUS_0
PRINTFORML ★現在の面識人数：{面識カウント(), 4}人
FOR LOCAL,1,6
	IF !GETBIT (FLAG:面識ボーナス取得状況, LOCAL) && !CONDITION_面識ボーナス(LOCAL, 1)
		PRINTPLAINFORM [{LOCAL}] - 解放条件：面識{CONDITION_面識ボーナス(LOCAL)}人以上
	ELSE
		IF GETBIT (FLAG:面識ボーナス取得状況, LOCAL)
			IF !REMOVABLE_BONUS_0(LOCAL)
				SETCOLOR 100, 100, 100
				PRINTPLAINFORM [{LOCAL}] - 面識{CONDITION_面識ボーナス(LOCAL)}人ボーナス【済】%BONUS_NAME_0(LOCAL)%
			ELSE
				IF !CONDITION_面識ボーナス(LOCAL, 1)
					SETCOLOR C_ORANGE
				ELSE
					SETCOLOR C_P_BLUE
				ENDIF
				PRINTFORM [{LOCAL}] - 面識{CONDITION_面識ボーナス(LOCAL)}人ボーナス【済】%BONUS_NAME_0(LOCAL)%
			ENDIF
		ELSE
			SETCOLOR C_AQUA
			PRINTFORM [{LOCAL}] - 面識{CONDITION_面識ボーナス(LOCAL)}人ボーナス
		ENDIF
		RESETCOLOR
	ENDIF
	PRINTL 
NEXT

@CONDITION_面識ボーナス(ARG, ARG:1)
;ARG:1が真ならボーナス取れるか返す
#FUNCTION
#DIM 必要数

SELECTCASE ARG
	CASE 1
		必要数 = 10
	CASE 2
		必要数 = 30
	CASE 3
		必要数 = 50
	CASE 4
		必要数 = 80
	CASE 5
		必要数 = 120
ENDSELECT

IF ARG:1
	IF 面識カウント() >= 必要数
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ELSE
	RETURNF 必要数
ENDIF

@REWARD_BONUS_0(ARG)
SIF GETBIT (FLAG:面識ボーナス取得状況, ARG) && !REMOVABLE_BONUS_0(ARG)
	RETURN
IF !CONDITION_面識ボーナス(ARG, 1)
	;周回後、満たしていないが取得している状態
	IF GETBIT (FLAG:面識ボーナス取得状況, ARG)
		CALL BONUS_CAUTION
		SIF RESULT
			RETURN
	ELSE
		RETURN
	ENDIF
ENDIF
INVERTBIT FLAG:面識ボーナス取得状況, ARG
SIF !GETBIT (FLAG:面識ボーナス取得状況, ARG)
	RETURN
SELECTCASE ARG
	CASE 1
		PRINTW \5000得ました。
		MONEY += 5000
	CASE 2
		;好感度から移動
		PRINTW 【少女の居場所を察知する程度の能力】を得ました。
	CASE 3
		PRINTW 集合コマンドを使用できるようになりました。
	CASE 4
		PRINTW 【少女の来訪を察知する程度の能力】を得ました。
	CASE 5
		;パンツ8から移行
		PRINTW 【千客万来】を得ました。
	CASEELSE
		RETURN
ENDSELECT

@BONUS_NAME_0(ARG)
#FUNCTIONS
SELECTCASE ARG
	CASE 1
		RETURNF "\\5000"
	CASE 2
		RETURNF "【少女の居場所を察知する程度の能力】"
	CASE 3
		RETURNF "集合コマンド"
	CASE 4
		RETURNF "【少女の来訪を察知する程度の能力】"
	CASE 5
		RETURNF "【千客万来】"
ENDSELECT

@REMOVABLE_BONUS_0(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 2, 3, 4, 5
		RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT

@面識カウント
#FUNCTION
#DIM 面識カウント
面識カウント = 0
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:面識
		面識カウント ++
NEXT
RETURNF 面識カウント
;----------------------------------------
;好感度ボーナス
;----------------------------------------

@PRINT_BONUS_1
VARSET RESULT
PRINTFORML ★現在の最大好感度：{最大好感度(), 7}\@ RESULT:1 ? （%CALLNAME:(RESULT:1)%） # \@
FOR LOCAL,1,9
	IF !GETBIT (FLAG:好感度ボーナス取得状況, LOCAL) && !CONDITION_好感度ボーナス(LOCAL, 1)
		PRINTPLAINFORM [{LOCAL}] - 解放条件：好感度{CONDITION_好感度ボーナス(LOCAL)}突破
	ELSE
		IF GETBIT (FLAG:好感度ボーナス取得状況, LOCAL)
			IF !REMOVABLE_BONUS_1(LOCAL)
				SETCOLOR 100, 100, 100
				PRINTPLAINFORM [{LOCAL}] - 好感度{CONDITION_好感度ボーナス(LOCAL)}突破ボーナス 【済】%BONUS_NAME_1(LOCAL)%
			ELSE
				IF !CONDITION_好感度ボーナス(LOCAL, 1)
					SETCOLOR C_ORANGE
				ELSE
					SETCOLOR C_P_BLUE
				ENDIF
				PRINTFORM [{LOCAL}] - 好感度{CONDITION_好感度ボーナス(LOCAL)}突破ボーナス 【済】%BONUS_NAME_1(LOCAL)%
			ENDIF
		ELSE
			SETCOLOR C_AQUA
			PRINTFORM [{LOCAL}] - 好感度{CONDITION_好感度ボーナス(LOCAL)}突破ボーナス
		ENDIF
		RESETCOLOR
	ENDIF
	PRINTFORML 
NEXT

@CONDITION_好感度ボーナス(ARG, ARG:1)
;ARG:1が真なら好感度ボーナス取れるか返す
#FUNCTION
#DIM 必要好感度
SELECTCASE ARG
	CASE 1
		必要好感度 = 100
	CASE 2
		必要好感度 = 500
	CASE 3
		必要好感度 = 1000
	CASE 4
		必要好感度 = 10000
	CASE 5
		必要好感度 = 30000
	CASE 6
		必要好感度 = 50000
	CASE 7
		必要好感度 = 100000
	CASE 8
		必要好感度 = 1000000
ENDSELECT

IF ARG:1
	IF 最大好感度() >= 必要好感度
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ELSE
	RETURNF 必要好感度
ENDIF

@REWARD_BONUS_1(ARG)
SIF GETBIT (FLAG:好感度ボーナス取得状況, ARG) && !REMOVABLE_BONUS_1(ARG)
	RETURN
IF !CONDITION_好感度ボーナス(ARG, 1)
	;周回後、満たしていないが取得している状態
	IF GETBIT (FLAG:好感度ボーナス取得状況, ARG)
		CALL BONUS_CAUTION
		SIF RESULT
			RETURN
	ELSE
		RETURN
	ENDIF
ENDIF
INVERTBIT FLAG:好感度ボーナス取得状況, ARG
SIF !GETBIT (FLAG:好感度ボーナス取得状況, ARG)
	RETURN
SELECTCASE ARG
	CASE 1
		PRINTW \5000得ました。
		MONEY += 5000
	CASE 2
		PRINTW 100カリスマ得ました。
		MONEY:2 += 100
	CASE 3
		PRINTW 体力の上限が上がりました。
		MAXBASE:MASTER:体力 += 100
	CASE 4
		;4.620proto現在ない？
		PRINTW ごめんなさい、それ来月からなんですよ。
		;面識に移動
		;PRINTW 【少女の居場所を察知する程度の能力】を得ました。
		;TALENT:MASTER:居場所察知 = 1
	CASE 5
		PRINTW 【汚い部屋を察知する程度の能力】を得ました。
	CASE 6
		PRINTW 【ぱんつ密輸】を得ました。身体検査を回避しやすくなります。
	CASE 7
		PRINTFORMW 精力の上限が上がりました。
		MAXBASE:MASTER:精力 += 100
	CASE 8
		PRINTW 【両手に花】を得ました。恋人を二人まで作れるようになります（未実装）。
	CASEELSE
		RETURN
ENDSELECT

@BONUS_NAME_1(ARG)
#FUNCTIONS
SELECTCASE ARG
	CASE 1
		RETURNF "\\5000"
	CASE 2
		RETURNF "100カリスマ"
	CASE 3
		RETURNF "最大体力+100"
	CASE 4
		;RETURNF "煮込み雑炊"
	CASE 5
		RETURNF "【汚い部屋を察知する程度の能力】"
	CASE 6
		RETURNF "【ぱんつ密輸】"
	CASE 7
		RETURNF "最大精力+100"
	CASE 8
		RETURNF "【両手に花】"
ENDSELECT

@REMOVABLE_BONUS_1(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 5, 6, 8
		RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT

@最大好感度
#FUNCTION
#DIM 最大好感度
FOR LOCAL,1,CHARANUM - 1
	IF CFLAG:LOCAL:好感度 > 最大好感度
		最大好感度 = CFLAG:LOCAL:好感度
		RESULT:1 = LOCAL
	ENDIF
NEXT
RETURNF 最大好感度
;----------------------------------------
;パンツボーナス
;----------------------------------------

@CONDITION_パンツボーナス(ARG, ARG:1)
;ARG:1が真ならボーナス取れるか返す
#FUNCTION
#DIM 必要種数
SELECTCASE ARG
	CASE 1
		必要種数 = 10
	CASE 2
		必要種数 = 30
	CASE 3
		必要種数 = 100
	CASE 4
		必要種数 = 300
	CASE 5
		必要種数 = 500
	CASE 6
		必要種数 = 750
	CASE 7
		必要種数 = 1000
	CASE 8
		必要種数 = 2000
	CASEELSE
		必要種数 = 9999
ENDSELECT

IF ARG:1
	IF FLAG:獲得パンツ種数 >= 必要種数
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ELSE
	RETURNF 必要種数
ENDIF

@PRINT_BONUS_2
PRINTFORML ★現在の獲得パンツ種類数：{FLAG:獲得パンツ種数, 3}種
FOR LOCAL,1,9
	IF !GETBIT (FLAG:パンツボーナス取得状況, LOCAL) && !CONDITION_パンツボーナス(LOCAL, 1)
		PRINTPLAINFORM [{LOCAL}] - 解放条件：パンツを{CONDITION_パンツボーナス(LOCAL)}種類集める
	ELSE
		IF GETBIT (FLAG:パンツボーナス取得状況, LOCAL)
			IF !REMOVABLE_BONUS_2(LOCAL)
				SETCOLOR 100, 100, 100
				PRINTPLAINFORM [{LOCAL}] - パンツ{CONDITION_パンツボーナス(LOCAL)}種収集ボーナス 【済】%BONUS_NAME_2(LOCAL)%
			ELSE
				IF !CONDITION_パンツボーナス(LOCAL, 1)
					SETCOLOR C_ORANGE
				ELSE
					SETCOLOR C_P_BLUE
				ENDIF
				PRINTFORM [{LOCAL}] - パンツ{CONDITION_パンツボーナス(LOCAL)}種収集ボーナス 【済】%BONUS_NAME_2(LOCAL)%
			ENDIF
		ELSE
			SETCOLOR C_AQUA
			PRINTFORM [{LOCAL}] - パンツ{CONDITION_パンツボーナス(LOCAL)}種収集ボーナス
		ENDIF
		RESETCOLOR
	ENDIF
	PRINTL 
NEXT

@REWARD_BONUS_2(ARG)
SIF GETBIT (FLAG:パンツボーナス取得状況, ARG) && !REMOVABLE_BONUS_2(ARG)
	RETURN
IF !CONDITION_パンツボーナス(ARG, 1)
	;周回後、満たしていないが取得している状態（パンツは引き継がれるので現在は関係ない）
	IF GETBIT (FLAG:パンツボーナス取得状況, ARG)
		CALL BONUS_CAUTION
		SIF RESULT
			RETURN
	ELSE
		RETURN
	ENDIF
ENDIF
INVERTBIT FLAG:パンツボーナス取得状況, ARG
SIF !GETBIT (FLAG:パンツボーナス取得状況, ARG)
	RETURN
SELECTCASE ARG
	CASE 1
		CALL AddEXP("清掃経験", MASTER, 30)
	CASE 2
		PRINTFORML 神への献身を積み始めた%CALLNAME:MASTER%は、その奇跡の片鱗に触れることが許された
		PRINTFORMW %CALLNAME:MASTER%は【性欲を持て余している少女を見抜く程度の能力】を得た
	CASE 3
		PRINTW 体力の上限が上がりました。
		MAXBASE:MASTER:体力 += 300
	CASE 4
		PRINTL おぱんつ様から祭壇を賜りました。
		PRINTFORMW 自室で願掛けや天の声を聞くことができるようになります。
		ITEM:祭壇 = 1
	CASE 5
		PRINTFORML 神への献身を重ねた%CALLNAME:MASTER%は、さらなる奇跡に触れることが許された
		PRINTFORMW %CALLNAME:MASTER%は【性欲を持て余している少女を探り当てる程度の能力】を得た
	CASE 6
	;上昇量500だったのを200にして300を2000種ボーナスに
		PRINTW 体力の上限が上がりました。
		MAXBASE:MASTER:体力 += 200
	CASE 7
		;回復早いは恋人に移行,代わりに新たに精力アップ
		PRINTW 精力の上限が上がりました。
		MAXBASE:MASTER:精力 += 100
	CASE 8
		;千客万来は面式5に移行
		PRINTW 体力の上限が上がりました。
		MAXBASE:MASTER:体力 += 300
ENDSELECT

@BONUS_NAME_2(ARG)
#FUNCTIONS
SELECTCASE ARG
	CASE 1
		RETURNF "清掃経験+30"
	CASE 2
		RETURNF "【性欲を持て余している少女を見抜く程度の能力】"
	CASE 3
		RETURNF "最大体力+300"
	CASE 4
		RETURNF "祭壇"
	CASE 5
		RETURNF "【性欲を持て余している少女を探り当てる程度の能力】"
	CASE 6
		RETURNF "最大体力+200"
	CASE 7
		RETURNF "最大精力+100"
	CASE 8
		RETURNF "最大体力+300"
ENDSELECT

@REMOVABLE_BONUS_2(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 2, 5
		RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT

;----------------------------------------
;陥落ボーナス
;----------------------------------------
@PRINT_BONUS_3

#DIM 恋慕カウント
#DIM セフレカウント
#DIM 危険日生ハメ
#DIM 純愛派
#DIM 合意なし
#DIM 合意ゲット

恋慕カウント = 0
セフレカウント = 0
危険日生ハメ = 0
純愛派 = 0
合意ゲット = 0
VARSET BONUS_3_ABLE

FOR LOCAL,1,CHARANUM
	SIF TALENT:LOCAL:行きずり
		CONTINUE
	SIF TALENT:LOCAL:恋慕
		恋慕カウント ++
	SIF TALENT:LOCAL:セフレ
		セフレカウント ++
	SIF CFLAG:LOCAL:生解禁 >= 2
		危険日生ハメ ++
	SIF CFLAG:LOCAL:清い交際
		純愛派 ++
	SIF CFLAG:LOCAL:ヤラせちゃった
		合意なし ++
	SIF CFLAG:LOCAL:既成事実 & 合意_うふふ
		合意ゲット ++
NEXT

BONUS_3_DESCRIPTION:1 = 恋慕取得
SIF 恋慕カウント >= 1
	BONUS_3_ABLE:1 = 1

BONUS_3_DESCRIPTION:2 = 恋慕3人
SIF 恋慕カウント >= 3
	BONUS_3_ABLE:2 = 1

BONUS_3_DESCRIPTION:3 = 恋慕5人
SIF 恋慕カウント >= 5
	BONUS_3_ABLE:3 = 1

BONUS_3_DESCRIPTION:4 = 恋人取得
SIF TALENT:MASTER:恋人
	BONUS_3_ABLE:4 = 1

BONUS_3_DESCRIPTION:5 = 危険日生ハメの合意取得
SIF 危険日生ハメ
	BONUS_3_ABLE:5 = 1

BONUS_3_DESCRIPTION:6 = セフレ取得
SIF セフレカウント >= 1
	BONUS_3_ABLE:6 = 1

BONUS_3_DESCRIPTION:7 = セフレ3人
SIF セフレカウント >= 3
	BONUS_3_ABLE:7 = 1

BONUS_3_DESCRIPTION:8 = セフレ5人
SIF セフレカウント >= 5
	BONUS_3_ABLE:8 = 1

BONUS_3_DESCRIPTION:9 = 時姦せずに恋慕取得
SIF 純愛派
	BONUS_3_ABLE:9 = 1

BONUS_3_DESCRIPTION:10 = セックスの合意を得る
SIF 合意ゲット
	BONUS_3_ABLE:10 = 1

BONUS_3_DESCRIPTION:11 = 合意を得ずに意識のある相手と行為に及ぶ
SIF 合意なし
	BONUS_3_ABLE:11 = 1

PRINTFORML ★現在の恋慕：{恋慕カウント, 3}人／セフレ：{セフレカウント, 3}人
FOR LOCAL,1,12
	IF !BONUS_3_ABLE:LOCAL && !GETBIT (FLAG:陥落ボーナス取得状況, LOCAL)
		PRINTPLAINFORM [{LOCAL}] - 解放条件：%BONUS_3_DESCRIPTION:LOCAL%
	ELSE
		IF GETBIT (FLAG:陥落ボーナス取得状況, LOCAL)
			IF !REMOVABLE_BONUS_3(LOCAL)
				SETCOLOR 100, 100, 100
				PRINTPLAINFORM [{LOCAL}] - %BONUS_3_DESCRIPTION:LOCAL%ボーナス【済】%BONUS_NAME_3(LOCAL)%
			ELSE
				IF !BONUS_3_ABLE:LOCAL
					SETCOLOR C_ORANGE
				ELSE
					SETCOLOR C_P_BLUE
				ENDIF
				PRINTFORM [{LOCAL}] - %BONUS_3_DESCRIPTION:LOCAL%ボーナス【済】%BONUS_NAME_3(LOCAL)%
			ENDIF
		ELSE
			SETCOLOR C_AQUA
			PRINTFORM [{LOCAL}] - %BONUS_3_DESCRIPTION:LOCAL%ボーナス
		ENDIF
		RESETCOLOR
	ENDIF
	PRINTL 
NEXT

@REWARD_BONUS_3(ARG)
SIF GETBIT (FLAG:陥落ボーナス取得状況, ARG) && !REMOVABLE_BONUS_3(ARG)
	RETURN
IF !BONUS_3_ABLE:ARG
	;周回後、満たしていないが取得している状態
	IF GETBIT (FLAG:陥落ボーナス取得状況, ARG)
		CALL BONUS_CAUTION
		SIF RESULT
			RETURN
	ELSE
		RETURN
	ENDIF
ENDIF
INVERTBIT FLAG:陥落ボーナス取得状況, ARG
SIF !GETBIT (FLAG:陥落ボーナス取得状況, ARG)
	RETURN
SELECTCASE ARG
	CASE 1
		PRINTW 気力の上限が上がりました。
		MAXBASE:MASTER:気力 += 100
	CASE 2
		;生理周期は生解禁へ移行
		;恋慕ボーナス3つ目だったのを2つ目に移行
		PRINTW 気力の上限が上がりました。
		MAXBASE:MASTER:気力 += 300
	CASE 3
		;4つ目から謎の魅力を移行
		;気力アップは2つ目に移行
		PRINTW 【謎の魅力】を得ました。
	CASE 4
		;パンツボーナスから移行
		PRINTW 【回復早い】を得ました。
	CASE 5
		PRINTW 生理周期が分かるようになりました。
	CASE 6
		;時姦経験100から移行
		PRINTW 精力の上限が上がりました。
		MAXBASE:MASTER:精力 += 100
	CASE 7
		;新規
		PRINTW 【魅力】を得ました。
	CASE 8
		;新規
		PRINTW 精力の上限が上がりました。
		MAXBASE:MASTER:精力 += 100
	CASE 9
		;新規
		PRINTW 気力の上限が上がりました。
		MAXBASE:MASTER:気力 += 300
	CASE 10
		PRINTFORMW 100カリスマ得ました。
		MONEY:2 += 100
	CASE 11
		ITEM:ミラダの石 = 1
		PRINTFORMW 愛欲を独占するミラダの石ミレニアムを得ました。
ENDSELECT

@BONUS_NAME_3(ARG)
#FUNCTIONS
SELECTCASE ARG
	CASE 1
		RETURNF "最大気力+100"
	CASE 2
		RETURNF "最大気力+300"
	CASE 3
		RETURNF "【謎の魅力】"
	CASE 4
		RETURNF "【回復早い】"
	CASE 5
		RETURNF "生理周期可視化"
	CASE 6
		RETURNF "最大精力+100"
	CASE 7
		RETURNF "【魅力】"
	CASE 8
		RETURNF "最大精力+100"
	CASE 9
		RETURNF "最大気力+300"
	CASE 10
		RETURNF "まだ"
ENDSELECT

@REMOVABLE_BONUS_3(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 3, 4, 5, 7
		RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT

;----------------------------------------
;孕ませボーナス
;----------------------------------------

@PRINT_BONUS_4
#DIM 孕ませた種族数
VARSET BONUS_4_ABLE
FOR LOCAL,1,CHARANUM
	;妊娠してるか出産経験あり
	;追加種族の関係で1キャラで2種類獲得できる場合もある（人形＋メイドのる～こと）
	IF CFLAG:LOCAL:妊娠経過日数 || CFLAG:LOCAL:子供人数
		SIF CHECK_CHARA(LOCAL, "巫女")
			BONUS_4_ABLE:2 = 1
		SIF CHECK_CHARA(LOCAL, "妖精")
			BONUS_4_ABLE:3 = 1
		SIF CHECK_CHARA(LOCAL, "神霊")
			BONUS_4_ABLE:4 = 1
		SIF CHECK_CHARA(LOCAL, "付喪神")
			BONUS_4_ABLE:5 = 1
		SIF CHECK_CHARA(LOCAL, "鬼")
			BONUS_4_ABLE:6 = 1
		SIF CHECK_CHARA(LOCAL, "天狗")
			BONUS_4_ABLE:7 = 1
		SIF CHECK_CHARA(LOCAL, "蓬莱人")
			BONUS_4_ABLE:8 = 1
		;鬼と天狗は別途
		SIF CHECK_CHARA(LOCAL, "妖怪") && !CHECK_CHARA(LOCAL, "鬼") && !CHECK_CHARA(LOCAL, "天狗")
			BONUS_4_ABLE:9 = 1
		SIF CHECK_CHARA(LOCAL, "魔法使い")
			BONUS_4_ABLE:10 = 1
		SIF CHECK_CHARA(LOCAL, "幽霊")
			BONUS_4_ABLE:11 = 1
		SIF CHECK_CHARA(LOCAL, "人形")
			BONUS_4_ABLE:12 = 1
		SIF CHECK_CHARA(LOCAL, "メイド")
			BONUS_4_ABLE:14 = 1
	ENDIF
NEXT
;BONUS_4_ABLE:2～12、14の合計
孕ませた種族数 = SUMARRAY(BONUS_4_ABLE, 2, 13) + BONUS_4_ABLE:14
SIF (孕ませた種族数) >= 5
	BONUS_4_ABLE:1 = 1

BONUS_4_DESCRIPTION:1 = 5種族以上
BONUS_4_DESCRIPTION:2 = 巫女
BONUS_4_DESCRIPTION:3 = 妖精
BONUS_4_DESCRIPTION:4 = 神霊
BONUS_4_DESCRIPTION:5 = 付喪神
BONUS_4_DESCRIPTION:6 = 鬼
BONUS_4_DESCRIPTION:7 = 天狗
BONUS_4_DESCRIPTION:8 = 蓬莱人
BONUS_4_DESCRIPTION:9 = 妖怪
BONUS_4_DESCRIPTION:10 = 魔法使い
BONUS_4_DESCRIPTION:11 = 幽霊
BONUS_4_DESCRIPTION:12 = 人形

BONUS_4_DESCRIPTION:13 = 行きずりの相手
BONUS_4_DESCRIPTION:14 = メイド

SIF FLAG:行きずり孕ませ数
	BONUS_4_ABLE:13 = 1

PRINTFORML ★現在の孕ませた種族数：{孕ませた種族数, 2}種
FOR LOCAL,1,15
	SIF BONUS_4_DESCRIPTION:LOCAL == ""
		CONTINUE
	IF !BONUS_4_ABLE:LOCAL && !GETBIT (FLAG:孕ませボーナス取得状況, LOCAL)
		PRINTPLAINFORM [{LOCAL}] - 解放条件：%BONUS_4_DESCRIPTION:LOCAL%を孕ませる
	ELSE
		IF GETBIT (FLAG:孕ませボーナス取得状況, LOCAL)
			IF !REMOVABLE_BONUS_4(LOCAL)
				SETCOLOR 100, 100, 100
				PRINTPLAINFORM [{LOCAL}] - %BONUS_4_DESCRIPTION:LOCAL%孕ませボーナス【済】%BONUS_NAME_4(LOCAL)%
			ELSE
				IF !BONUS_4_ABLE:LOCAL
					SETCOLOR C_ORANGE
				ELSE
					SETCOLOR C_P_BLUE
				ENDIF
				PRINTFORM [{LOCAL}] - %BONUS_4_DESCRIPTION:LOCAL%孕ませボーナス【済】%BONUS_NAME_4(LOCAL)%
			ENDIF
		ELSE
			SETCOLOR C_AQUA
			PRINTFORM [{LOCAL}] - %BONUS_4_DESCRIPTION:LOCAL%を孕ませる
		ENDIF
		RESETCOLOR
	ENDIF
	PRINTL 
NEXT

@REWARD_BONUS_4(ARG)
SIF GETBIT (FLAG:孕ませボーナス取得状況, ARG) && !REMOVABLE_BONUS_4(ARG)
	RETURN
IF !BONUS_4_ABLE:ARG
	;周回後、満たしていないが取得している状態
	IF GETBIT (FLAG:孕ませボーナス取得状況, ARG)
		CALL BONUS_CAUTION
		SIF RESULT
			RETURN
	ELSE
		RETURN
	ENDIF
ENDIF
INVERTBIT FLAG:孕ませボーナス取得状況, ARG
SIF !GETBIT (FLAG:孕ませボーナス取得状況, ARG)
	RETURN
SELECTCASE ARG
	CASE 1
		PRINTW 【万能孕ませ】を得ました。
	CASE 2
		PRINTW 【巫女孕ませ】を得ました。
	CASE 3
		PRINTW 【妖精孕ませ】を得ました。
	CASE 4
		PRINTW 【神霊孕ませ】を得ました。
	CASE 5
		PRINTW 【付喪神孕ませ】を得ました。
	CASE 6
		PRINTW 【鬼孕ませ】を得ました。
	CASE 7
		PRINTW 【天狗孕ませ】を得ました。
	CASE 8
		PRINTW 【蓬莱孕ませ】を得ました。
	CASE 9
		PRINTW 【妖怪孕ませ】を得ました。
	CASE 10
		PRINTW 【禁断の知識】を得ました。
		TALENT:MASTER:禁断の知識 = 1
	CASE 11
		PRINTW 【幽霊孕ませ】を得ました。
	CASE 12
		PRINTW 【人形孕ませ】を得ました。
	CASE 13
		;新規
		PRINTW 気力の上限が上がりました。
		MAXBASE:MASTER:気力 += 300
	CASE 14
		;新規
		PRINTW 料理経験を得ました。
		EXP:MASTER:料理経験 += 150
ENDSELECT

@BONUS_NAME_4(ARG)
#FUNCTIONS
SELECTCASE ARG
	CASE 1
		RETURNF "【万能孕ませ】"
	CASE 2
		RETURNF "【巫女孕ませ】"
	CASE 3
		RETURNF "【妖精孕ませ】"
	CASE 4
		RETURNF "【神霊孕ませ】"
	CASE 5
		RETURNF "【付喪神孕ませ】"
	CASE 6
		RETURNF "【鬼孕ませ】"
	CASE 7
		RETURNF "【天狗孕ませ】"
	CASE 8
		RETURNF "【蓬莱孕ませ】"
	CASE 9
		RETURNF "【妖怪孕ませ】"
	CASE 10
		RETURNF "【禁断の知識】"
	CASE 11
		RETURNF "【幽霊孕ませ】"
	CASE 12
		RETURNF "【人形孕ませ】"
	CASE 13
		RETURNF "最大気力+300"
	CASE 14
		RETURNF "料理経験+150"
ENDSELECT

@REMOVABLE_BONUS_4(ARG)
#FUNCTION
SELECTCASE ARG
	;ほぼ全て1なのでELSEが他とは逆
	CASE 10, 13, 14
		RETURNF 0
	CASEELSE
		RETURNF 1
ENDSELECT

;----------------------------------------
;技能ボーナス
;----------------------------------------

@PRINT_BONUS_5

#DIM 淫乳人数
#DIM 淫壺人数
#DIM 尻穴狂い人数
#DIM キス魔人数
#DIM 自慰狂い人数
淫乳人数 = 0
淫壺人数 = 0
尻穴狂い人数 = 0
キス魔人数 = 0
自慰狂い人数 = 0

VARSET BONUS_5_ABLE

FOR LOCAL,1,CHARANUM
	SIF TALENT:LOCAL:淫乳 || GETBIT (CFLAG:LOCAL:引き継ぎ, 5)
		淫乳人数 ++
	SIF TALENT:LOCAL:淫壺 || GETBIT (CFLAG:LOCAL:引き継ぎ, 3)
		淫壺人数 ++
	SIF TALENT:LOCAL:尻穴狂い || GETBIT (CFLAG:LOCAL:引き継ぎ, 4)
		尻穴狂い人数 ++
	SIF TALENT:LOCAL:キス魔 || GETBIT (CFLAG:LOCAL:引き継ぎ, 6)
		キス魔人数 ++
	SIF TALENT:LOCAL:自慰狂い
		自慰狂い人数 ++
NEXT

BONUS_5_DESCRIPTION:1 = 自慰狂い3人
SIF 自慰狂い人数 >= 3
	BONUS_5_ABLE:1 = 1

BONUS_5_DESCRIPTION:2 = 淫乳3人
SIF 淫乳人数 >= 3
	BONUS_5_ABLE:2 = 1

BONUS_5_DESCRIPTION:3 = 淫壺3人
SIF 淫壺人数 >= 3
	BONUS_5_ABLE:3 = 1

BONUS_5_DESCRIPTION:4 = 尻穴狂い3人
SIF 尻穴狂い人数 >= 3
	BONUS_5_ABLE:4 = 1

BONUS_5_DESCRIPTION:5 = キス魔3人
SIF キス魔人数 >= 3
	BONUS_5_ABLE:5 = 1

PRINTFORML ★現在の自慰狂い：{自慰狂い人数, 3}人／淫乳：{淫乳人数, 3}人／淫壺：{淫壺人数, 3}人／尻穴狂い：{尻穴狂い人数, 3}人／キス魔：{キス魔人数, 3}人
SIF FLAG:周回数
	PRINTFORML 　（自慰狂い以外は過去に取得したことのあるキャラも含まれます）
FOR LOCAL,1,6
	;スキマがないので
;	SIF BONUS_5_DESCRIPTION:LOCAL == ""
;		CONTINUE
	IF !BONUS_5_ABLE:LOCAL && !GETBIT (FLAG:技能ボーナス取得状況, LOCAL)
		PRINTPLAINFORM [{LOCAL}] - 解放条件：%BONUS_5_DESCRIPTION:LOCAL%
	ELSE
		IF GETBIT (FLAG:技能ボーナス取得状況, LOCAL)
			IF !REMOVABLE_BONUS_5(LOCAL)
				SETCOLOR 100, 100, 100
				PRINTPLAINFORM [{LOCAL}] - %BONUS_5_DESCRIPTION:LOCAL%ボーナス【済】%BONUS_NAME_5(LOCAL)%
			ELSE
				IF !BONUS_5_ABLE:LOCAL
					SETCOLOR C_ORANGE
				ELSE
					SETCOLOR C_P_BLUE
				ENDIF
				PRINTFORM [{LOCAL}] - %BONUS_5_DESCRIPTION:LOCAL%ボーナス【済】%BONUS_NAME_5(LOCAL)%
			ENDIF
		ELSE
			SETCOLOR C_AQUA
			PRINTFORM [{LOCAL}] - %BONUS_5_DESCRIPTION:LOCAL%
		ENDIF
		RESETCOLOR
	ENDIF
	PRINTL 
NEXT

@REWARD_BONUS_5(ARG)
SIF GETBIT (FLAG:技能ボーナス取得状況, ARG) && !REMOVABLE_BONUS_5(ARG)
	RETURN
IF !BONUS_5_ABLE:ARG
	;周回後、満たしていないが取得している状態
	IF GETBIT (FLAG:技能ボーナス取得状況, ARG)
		CALL BONUS_CAUTION
		SIF RESULT
			RETURN
	ELSE
		RETURN
	ENDIF
ENDIF
INVERTBIT FLAG:技能ボーナス取得状況, ARG
SIF !GETBIT (FLAG:技能ボーナス取得状況, ARG)
	RETURN
SELECTCASE ARG
	CASE 1
		PRINTW 【Ｃ鈍感無視】を得ました。
	CASE 2
		PRINTW 【Ｂ鈍感無視】を得ました。
	CASE 3
		PRINTW 【Ｖ鈍感無視】を得ました。
	CASE 4
		PRINTW 【Ａ鈍感無視】を得ました。
	CASE 5
		PRINTW 【魅惑】を得ました。
ENDSELECT

@BONUS_NAME_5(ARG)
#FUNCTIONS
SELECTCASE ARG
	CASE 1
		RETURNF "【Ｃ鈍感無視】"
	CASE 2
		RETURNF "【Ｂ鈍感無視】"
	CASE 3
		RETURNF "【Ｖ鈍感無視】】"
	CASE 4
		RETURNF "【Ａ鈍感無視】"
	CASE 5
		RETURNF "【魅惑】"
ENDSELECT

@REMOVABLE_BONUS_5(ARG)
#FUNCTION
;全て1
RETURNF 1

SELECTCASE ARG
	CASE 0
		RETURNF 0
	CASEELSE
ENDSELECT

;----------------------------------------
;弾幕勝負ボーナス
;----------------------------------------
@PRINT_BONUS_6
PRINTFORML ★現在の弾幕勝負勝利人数：{弾幕勝負勝利カウント(), 4}人
FOR LOCAL,1,7
	IF !GETBIT (FLAG:弾幕勝負ボーナス取得状況, LOCAL) && !CONDITION_弾幕勝負ボーナス(LOCAL, 1)
		IF LOCAL != 6
			PRINTPLAINFORM [{LOCAL}] - 解放条件：弾幕勝負勝利{CONDITION_弾幕勝負ボーナス(LOCAL)}人以上
		ELSE
			PRINTPLAINFORM [{LOCAL}] - 解放条件：依姫とヘカーティアに弾幕勝負で勝利する
		ENDIF
	ELSE
		IF GETBIT (FLAG:弾幕勝負ボーナス取得状況, LOCAL)
			IF !REMOVABLE_BONUS_6(LOCAL)
				SETCOLOR 100, 100, 100
				PRINTPLAINFORM [{LOCAL}] - 弾幕勝負勝利{CONDITION_弾幕勝負ボーナス(LOCAL)}人ボーナス【済】%BONUS_NAME_6(LOCAL)%
			ELSE
				IF !CONDITION_弾幕勝負ボーナス(LOCAL, 1)
					SETCOLOR C_ORANGE
				ELSE
					SETCOLOR C_P_BLUE
				ENDIF
				IF LOCAL != 6
					PRINTFORM [{LOCAL}] - 弾幕勝負勝利{CONDITION_弾幕勝負ボーナス(LOCAL)}人ボーナス【済】%BONUS_NAME_6(LOCAL)%
				ELSE
					PRINTFORM [{LOCAL}] - 依姫とヘカーティアに弾幕勝負で勝利するボーナス【済】%BONUS_NAME_6(LOCAL)%
				ENDIF
			ENDIF
		ELSE
			SETCOLOR C_AQUA
			IF LOCAL != 6
				PRINTFORM [{LOCAL}] - 弾幕勝負勝利{CONDITION_弾幕勝負ボーナス(LOCAL)}人ボーナス
			ELSE
				PRINTFORM [{LOCAL}] - 依姫とヘカーティアに弾幕勝負で勝利するボーナス
			ENDIF
		ENDIF
		RESETCOLOR
	ENDIF
	PRINTL 
NEXT

@CONDITION_弾幕勝負ボーナス(ARG, ARG:1)
;ARG:1が真ならボーナス取れるか返す
#FUNCTION
#DIM 必要数

SELECTCASE ARG
	CASE 1
		必要数 = 10
	CASE 2
		必要数 = 25
	CASE 3
		必要数 = 50
	CASE 4
		必要数 = 75
	CASE 5
		必要数 = 100
ENDSELECT

IF ARG:1
	IF 弾幕勝負勝利カウント() >= 必要数 && ARG < 6
		RETURNF 1
	ELSEIF CFLAG:98:弾幕勝負勝利 && CFLAG:113:弾幕勝負勝利 && ARG == 6
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ELSE
	RETURNF 必要数
ENDIF

@REWARD_BONUS_6(ARG)
SIF GETBIT (FLAG:弾幕勝負ボーナス取得状況, ARG)
	RETURN
IF !CONDITION_弾幕勝負ボーナス(ARG, 1)
	;周回後、満たしていないが取得している状態
	IF GETBIT (FLAG:弾幕勝負ボーナス取得状況, ARG)
		CALL BONUS_CAUTION
		SIF RESULT
			RETURN
	ELSE
		RETURN
	ENDIF
ENDIF
INVERTBIT FLAG:弾幕勝負ボーナス取得状況, ARG
SIF !GETBIT (FLAG:弾幕勝負ボーナス取得状況, ARG)
	RETURN
SELECTCASE ARG
	CASE 1
		PRINTW 戦闘経験を得ました。
		EXP:MASTER:戦闘経験 += 30
	CASE 2
		PRINTL 【グレイズ上手】を得ました。
		PRINTW 弾幕勝負で『回避に専念する』を選択した際に受けるダメージが減ります。
	CASE 3
		PRINTW 体力の上限が上がりました。
		MAXBASE:MASTER:体力 += 300
	CASE 4
		PRINTL 【霊力充填】を得ました。
		PRINTW 弾幕勝負で『回避に専念する』を選択した直後のダイス値が増えます。
	CASE 5
		PRINTL 【エクステンド】を得ました。
		PRINTW 弾幕勝負で初期残機が+1されます。
	CASE 6
		PRINTL 【ルナシューター】を得ました。
		PRINTW 弾幕勝負でボムの所持数が+1され、ボムの効果値が上がります。
	CASEELSE
		RETURN
ENDSELECT

@BONUS_NAME_6(ARG)
#FUNCTIONS
SELECTCASE ARG
	CASE 1
		RETURNF "戦闘経験+30"
	CASE 2
		RETURNF "【グレイズ上手】"
	CASE 3
		RETURNF "最大体力+300"
	CASE 4
		RETURNF "【霊力充填】"
	CASE 5
		RETURNF "【エクステンド】"
	CASE 6
		RETURNF "【ルナシューター】"
ENDSELECT

@REMOVABLE_BONUS_6(ARG)
#FUNCTION
;全て1
RETURNF 1

SELECTCASE ARG
	CASE 0
		RETURNF 0
	CASEELSE
ENDSELECT

@弾幕勝負勝利カウント
#FUNCTION
#DIM 弾幕勝負勝利カウント
弾幕勝負勝利カウント = 0
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:弾幕勝負勝利
		弾幕勝負勝利カウント ++
NEXT
RETURNF 弾幕勝負勝利カウント

;----------------------------------------

@実績フラグ移行
SIF GETBIT (FLAG:701,0)
	SETBIT FLAG:好感度ボーナス取得状況,1
SIF GETBIT (FLAG:701,12)
	SETBIT FLAG:好感度ボーナス取得状況,2
SIF GETBIT (FLAG:701,2)
	SETBIT FLAG:好感度ボーナス取得状況,3
;好感度ボーナスの居場所察知は面識30人に移動
;SIF GETBIT (FLAG:701,3)
;	SETBIT FLAG:好感度ボーナス取得状況,4
SIF GETBIT (FLAG:701,5)
	SETBIT FLAG:好感度ボーナス取得状況,5
SIF GETBIT (FLAG:701,9)
	SETBIT FLAG:好感度ボーナス取得状況,6

;居場所察知
SIF GETBIT (FLAG:701,3)
	SETBIT FLAG:面識ボーナス取得状況,2
;集合
SIF GETBIT (FLAG:701,10)
	SETBIT FLAG:面識ボーナス取得状況,3

;千客万来は面識へ
SIF GETBIT (FLAG:701,8)
	SETBIT FLAG:面識ボーナス取得状況,5

SIF GETBIT (FLAG:701,1)
	SETBIT FLAG:パンツボーナス取得状況,1
SIF GETBIT (FLAG:701,16)
	SETBIT FLAG:パンツボーナス取得状況,2
SIF GETBIT (FLAG:701,4)
	SETBIT FLAG:パンツボーナス取得状況,3
SIF GETBIT (FLAG:701,13)
	SETBIT FLAG:パンツボーナス取得状況,4
SIF GETBIT (FLAG:701,15)
	SETBIT FLAG:パンツボーナス取得状況,5
IF GETBIT (FLAG:701,7)
	SETBIT FLAG:パンツボーナス取得状況,6
	SETBIT FLAG:パンツボーナス取得状況,8
ENDIF
;回復早いは恋人に
SIF GETBIT (FLAG:701,6)
	SETBIT FLAG:陥落ボーナス取得状況,4
;恋慕5人の可視化は生解禁へ
SIF GETBIT (FLAG:702,10)
	SETBIT FLAG:陥落ボーナス取得状況,5

;時姦経験100からセフレに
SIF GETBIT (FLAG:702,8)
	SETBIT FLAG:陥落ボーナス取得状況,6

;指舌３
SIF GETBIT (FLAG:702,0)
	SETBIT FLAG:技能ボーナス取得状況,1
;淫乳5
SIF GETBIT (FLAG:702,1)
	SETBIT FLAG:技能ボーナス取得状況,2
;淫壺5人
SIF GETBIT (FLAG:702,2)
	SETBIT FLAG:技能ボーナス取得状況,3
;尻穴狂い5人
SIF GETBIT (FLAG:702,3)
	SETBIT FLAG:技能ボーナス取得状況,4
;キス魔５
SIF GETBIT (FLAG:702,4)
	SETBIT FLAG:技能ボーナス取得状況,5

SIF GETBIT (FLAG:702,5)
	SETBIT FLAG:陥落ボーナス取得状況,1
;可視化は保存してないので2はスキップ
SIF GETBIT (FLAG:702,6)
	SETBIT FLAG:陥落ボーナス取得状況,3
SIF GETBIT (FLAG:702,7)
	SETBIT FLAG:陥落ボーナス取得状況,4
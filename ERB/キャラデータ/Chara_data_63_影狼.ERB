@CHARAMOVE_DATA_63(ARG, ARG:2)
LOCAL = 63
SELECTCASE ARG
	;連動設定（RETURN=依存するキャラ, RESULT:1～3=対等に連動するキャラ）
	CASE 1
		;水曜日は連動
		SIF YOUBI_MATCH(DAY, "水")
			RETURN [[わかさぎ姫]]
	;特殊生活時間設定（CFLAG:LOCAL:生活時間補正を設定する）
	CASE 3
	;移動傾向（±～10=行きやすい/行きにくい, -100=行かない）
	CASE 4
		SELECTCASE ARG:2
			;夢幻館には行かない
			CASE P450玄関 TO P466庭
				RETURN -100
			;永遠亭の奥には行かない
			CASE P405調剤室, P407廊下 TO P430地下室
				RETURN -100
			;他人の家には入らない
			CASE P437メディスンの家, P439ラルバの家, P443ミスティアの家, P445妹紅の家
				RETURN -100
			;自宅,斜角の竹林,迷いの小道,夜雀の屋台,竹林入口によく居る
			CASE P432影狼の家, P433斜角の竹林, P434迷いの小道, P444夜雀の屋台, P447竹林入口
				RETURN 5
		ENDSELECT
	;出現場所
	CASE 7
		SELECTCASE ARG:2
			CASE 広場, 商家町, 呑屋小道
				RETURN 1
			CASE 霧の湖
				RETURN 1
			CASE 竹林入り口, 迷いの小道, 斜角の竹林
				RETURN 1
		ENDSELECT
	;引力設定（CFLAG:LOCAL:引力点を設定する）
	CASE 8
ENDSELECT

@EXIST_キャラデータ63
#LOCALSIZE 1
#LOCALSSIZE 1

@キャラデータ63(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "呼び名"
	CALLF MAKE_STR(V_NAME, "影狼")
;こっから衣装設定
CASE "今日のパジャマ"
	CALLF MAKE_STR(V_NAME, "パジャマ（ズボン）セット")

CASE "普段着：外衣"
	CALLF MAKE_INT(V_NAME, CLOTHES_ID("外衣", "ケープ"))
CASE "普段着：靴"
	CALLF MAKE_INT(V_NAME, CLOTHES_ID("靴", "靴"))
CASE "普段着：靴下"
	CALLF MAKE_INT(V_NAME, CLOTHES_ID("靴下", "靴下"))
CASE "普段着：ワンピース"
	CALLF MAKE_INT(V_NAME, CLOTHES_ID("ワンピース", "ワンピース"))
CASE "普段着：その他１"
	CALLF MAKE_INT(V_NAME, CLOTHES_ID("その他１", "ブローチ"))
	
CASE "普段着：帽子"
	IF GET_MAPID(CFLAG:ARG:現在位置) == 2 && CFLAG:ARG:基本服装セット == (100 + [[影狼]])
		CALLF MAKE_INT(V_NAME, CLOTHES_ID("帽子", "頭巾"))
	ELSEIF CFLAG:ARG:口上セレクタ == 0 && CFLAG:ARG:基本服装セット == (100 + [[影狼]]) && TCVAR:ARG:363 == 101
		CALLF MAKE_INT(V_NAME, CLOTHES_ID("帽子", "麦藁帽子"))
	ENDIF
CASE "普段着：その他２"
	IF CFLAG:ARG:口上セレクタ == 0 && CFLAG:ARG:基本服装セット == (100 + [[影狼]]) && TCVAR:ARG:363 == 2
		CALLF MAKE_INT(V_NAME, CLOTHES_ID("その他２", "マフラー"))
	ENDIF

CASE "固有下着形容詞"
	CALLF MAKE_STR(V_NAME, "“菊に盃”の絵柄をあしらった")
CASE "固有下半身下着名"
	CALLF MAKE_STR(V_NAME, "麻布のパンツ")
CASE "固有上半身下着"
	CALLF MAKE_INT(V_NAME, 1)
CASE "固有上半身下着名"
	CALLF MAKE_STR(V_NAME, "麻布のブラ")
CASE "固有下着着用条件"
	CALLF MAKE_INT(V_NAME, 1)
CASE "固有下着透ける(前)"
	CALLF MAKE_INT(V_NAME, 1)
CASE "固有下着透ける(後)"
	CALLF MAKE_INT(V_NAME, 1)
CASE "固有下着生地厚さ"
	CALLF MAKE_INT(V_NAME, 2)

;料理関連
CASE "料理：好きな味"
	CALLF MAKE_STR(V_NAME, "揚げ物/甘い/ガッツリ/濃厚/野趣/月見/芸術的/芸術的/魚料理/肉料理/肉料理/肉料理/")
CASE "料理：嫌いな味"
	CALLF MAKE_STR(V_NAME, "あっさり/淡白/辛い/辛い/虫肉/野菜/すっぱい/天狗流" + "謎料理/キノコ/" * 3 + "スパイス/" * 5)
CASE "料理：助手効果"
	CALLF MAKE_STR(V_NAME, "濃い味/得意料理＊肉料理＊/")
;贈り物関連
CASE "感性：好き"
	{
	CALLF MAKE_STR(V_NAME, "和風/イヌの/血に飢えた/動物/繊細/" + "シンプル/赤/自然/" * 2 + 
	"装飾/月/牙/爪/芸術的/美しい/シック/" * 3
	 + "オシャレ/オオカミの/満月の/" * 5)
	}
CASE "感性：嫌い"
	{
	CALLF MAKE_STR(V_NAME, "奇妙/金属/" + "派手/ロリータ/マニアック/" * 2 
	+ "壮大/威厳/刃物/天狗の/河童の/鬼の/" * 3 + "機械/" * 5+ "首輪/" * 10)
	}
ENDSELECT

;-------------------------------------------------
;固有料理設定
;DISH_NAME          料理名
;RESULTS            料理の種類（軽食/主食/デザート）
;CALL 材料設定("")  材料の指定。"汎用"を入れると自動で汎用食材になる
;DISH_TASTE         味の設定
;DISH_COMMENT       （任意）一行コメント
;DISH_TIME          （任意）調理にかかる時間
;DISH_MAXLIMIT      （任意）料理の持続時間
;-------------------------------------------------
@SPECIAL_MENU_63
DISH_NAME = ジビエ料理
RESULTS = 主食
CALL 材料設定("獣肉", "鳥肉", "ヤマドリ")
DISH_TASTE = 肉料理/獣肉/鳥肉/しょっぱい/ガッツリ/濃厚/温かい/野趣/野趣/野趣/豪快/強精/
DISH_COMMENT = 狩猟した肉を使った狩人の料理。[強精]が付く。
RETURN 1

;IMAGE_I63_特殊表情(CHARA)--------------------------------------------------------------------
;キャラクター特有の表情表示条件
;時間停止中でも維持する表情はこちら
;使用時注意点:
;	RESULTS:0に表情を格納
;	RESULTS:1にマウスオーバー時の表情を格納すること
;	"通常","笑顔","怒り","発情","睡眠"は本編で実装済み
;-------------------------------------------------------------------------------------------
@IMAGE_I63_特殊表情(CHARA)
#DIM CHARA
IF CFLAG:CHARA:差し替え適用 == 1
	;差し替え画像
	IF FLAG:立ち絵種類
		;顔絵
		IF MARK:CHARA:反発刻印 >= 1
			RESULTS:1 = ガチギレ
		ELSEIF TALENT:CHARA:恋慕 && !MARK:CHARA:反発刻印 && TALENT:CHARA:機嫌 >= 0
			IF CFLAG:CHARA:うふふ
				RESULTS:0 = デレデレ
				RESULTS:1 = デレデレ
				IF CFLAG:CHARA:溜まってる度 > 500
					IF CFLAG:CHARA:うふふ == 2
						RESULTS:0 = 発情3
						RESULTS:1 = 発情3
					ELSE
						RESULTS:0 = 誘惑
						RESULTS:1 = 誘惑
					ENDIF
				ENDIF
				IF GETPALAMLV(PALAM:CHARA:欲情, 15) >= 8
					RESULTS:0 = トロ笑い
					RESULTS:1 = トロ笑い
					SIF IS_SEXING(CHARA)
						RESULTS:1 = キス顔
				ENDIF
				IF CFLAG:CHARA:口上セレクタ == 0 && TCVAR:CHARA:362 >= 5
				;前ターンに超強絶頂
					RESULTS:0 = 絶頂
					RESULTS:1 = キス顔
				ELSEIF CFLAG:CHARA:口上セレクタ == 0 && TCVAR:CHARA:362 >= 2
	 				RESULTS:0 = トロ顔
					RESULTS:1 = キス顔
				ENDIF
			ELSEIF !CFLAG:CHARA:うふふ
				SIF BASE:CHARA:ムード >= 1400
					RESULTS:0 = デレデレ
				;やった後は平常時でもデレ顔にする予定
				;SIF TCVAR:CHARA:361
				;	RESULTS:0 = デレデレ
				IF (TCVAR:CHARA:発情 && CFLAG:CHARA:溜まってる度 > 200) || CFLAG:CHARA:溜まってる度 >= 750
					RESULTS:1 = 発情2
					SIF TALENT:CHARA:自己愛 >= 0 && BASE:CHARA:ムード >= 500
						RESULTS:1 = 誘惑
				ELSE
					SIF !(GET_MAPID(CFLAG:CHARA:現在位置) == 2 && CFLAG:CHARA:基本服装セット == (100 + [[影狼]]))
						RESULTS:1 = ウィンク
				ENDIF
				SIF CFLAG:CHARA:口上セレクタ == 0 && CFLAG:CHARA:妊娠経過日数 >= 40 && !TCVAR:CHARA:363
					RESULTS:1 = ボテ
			ENDIF
		ELSE
			;ELSEIF 
			;ENDIF
		ENDIF
	ELSE
		;立ち絵
	ENDIF
ELSE
	;通常画像
	IF FLAG:立ち絵種類
		;顔絵
	ELSE
		;立ち絵
	ENDIF
ENDIF

;IMAGE_I63_特殊エフェクトチェック(CHAR, エフェクト)--------------------------------------------
;キャラクター特有のエフェクト判定  特殊エフェクトを直接指定したい場合に参照
;引数 "エフェクト" に固有のエフェクトが含まれるかを判定する
;例の通り、STRFINDを使用してください
;-------------------------------------------------------------------------------------------
@IMAGE_I63_特殊エフェクトチェック(CHARA, エフェクト)
#DIM CHARA
#DIMS エフェクト

IF CFLAG:CHARA:差し替え適用 == 1
	;差し替え画像
	SIF STRFIND(エフェクト, "ハート") != -1
		RETURN 1
ENDIF

RETURN 0

;IMAGE_I63_特殊エフェクト(CHARA)--------------------------------------------------------------------
;キャラクター特有のエフェクト表示条件
;使用時注意点:
;	RESULTS配列に特殊エフェクトを格納
;	RETURNで追加するエフェクト数を返却すること
;-------------------------------------------------------------------------------------------
@IMAGE_I63_特殊エフェクト(CHARA)
#DIM CHARA
#DIM DYNAMIC エフェクト数 = 0
VARSET RESULTS

IF CFLAG:CHARA:差し替え適用 == 1
	;差し替え画像
;	IF FLAG:立ち絵種類
;		IF TALENT:CHARA:恋慕 && !MARK:CHARA:反発刻印 && TALENT:CHARA:機嫌 >= 0
;			;IF PREVCOM == 300
;			IF CFLAG:CHARA:うふふ && TCVAR:362 > 0
;				RESULTS:エフェクト数 = ハート
;				エフェクト数 ++
;			ENDIF
;		ENDIF
;	ENDIF
ENDIF

RETURN エフェクト数

;IMAGE_I63_特殊服装(CHARA)-------------------------------------------------------------------
;キャラクター特有の服装表示条件
;エフェクトを共有する服装差分はこちらで指定する
;使用時注意点:
;	RESULTSに特殊服装を格納すること
;	書式は %服装%%特殊服装% 適宜"_"等入れること
;-------------------------------------------------------------------------------------------
@IMAGE_I63_特殊服装(CHARA)
#DIM CHARA
VARSET RESULTS

IF CFLAG:CHARA:差し替え適用 == 1
	;差し替え画像
	IF CHECK_NUDE(CHARA)
		;裸
		IF FLAG:立ち絵種類
			;顔絵
		ELSE
			;立ち絵
		ENDIF
	ELSE
		IF CHECK_CLO("bra_visible")
			IF GROUPMATCH(EQUIP:CHARA:上半身下着１, 11, 12, 14, 15, 19, 20)
				RESULTS = _下着B
			ELSE
				RESULTS = _下着A
			ENDIF
		ELSEIF GET_MAPID(CFLAG:CHARA:現在位置) == 2 && CFLAG:CHARA:基本服装セット == (100 + [[影狼]]) && !CFLAG:CHARA:うふふ
			RESULTS = _赤ずきん
		;服
		ELSEIF CFLAG:CHARA:衣装一時変更 == 55 || (CFLAG:CHARA:基本服装セット == 55 && CFLAG:CHARA:衣装一時変更==0)
			IF FLAG:立ち絵種類
				;顔絵
				RESULTS = _バニー
			ENDIF
		ELSEIF CFLAG:CHARA:口上セレクタ == 0 && TCVAR:CHARA:363 == 2
			RESULTS = _冬服
		ELSEIF CFLAG:CHARA:口上セレクタ == 0 && TCVAR:CHARA:363 == 101
			RESULTS = _白ワンピ
		ENDIF
	ENDIF
ENDIF
;********************************************************************************************
@IMAGE_EX_I63_PREリソースチェック(CHARA, 表示形式, 服装, 表情, 表情_マウスオーバー, 差分, 乳量)
#DIM CHARA
#DIMS 表示形式
#DIMS 服装
#DIMS 表情
#DIMS 表情_マウスオーバー
#DIMS 差分
#DIMS 乳量

SIF CFLAG:CHARA:差し替え適用 == 1
	CALL COMPOSE_SPRITE_63
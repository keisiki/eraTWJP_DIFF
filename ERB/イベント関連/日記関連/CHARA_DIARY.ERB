@CHARA_DIARY_COM(C_ID)
#DIM DCOUNT
#DIM PAGENUM
#DIM C_ID
#DIM CLOSE
RESULT = 0
PAGENUM = 0
CLOSE = 0
;コマンドで呼び出されたとき
CALL KOJO_MESSAGE_SEND("DIARY", 0, C_ID, 0, 0, "EXIST")
IF !RESULT
	PRINTFORMW まだ何も書かれていないようだ…
	RETURN
ENDIF

FOR DCOUNT, 1, MAX_DIARY_PAGE:C_ID:0 + 1
	SIF GROUPMATCH(DIARY:C_ID:DCOUNT, 1, 3)
		PAGENUM ++
NEXT
IF PAGENUM == 0
	PRINTFORMW まだ何も書かれていないようだ…
	RETURN
ENDIF
FOR DCOUNT, 1, MAX_DIARY_PAGE:C_ID:0 + 1
	SIF !PAGESET:C_ID:DCOUNT
		BREAK
	CALL KOJO_MESSAGE_SEND("DIARY", 0, C_ID, PAGESET:C_ID:DCOUNT, DCOUNT, "TEXT", "コマンド")
	DRAWLINE
	;未読(3)だったら既読(1)にする
	SIF DIARY:C_ID:(PAGESET:C_ID:DCOUNT) == 3
		DIARY:C_ID:(PAGESET:C_ID:DCOUNT) = 1
	IF PAGENUM > 1
		CALL ASK_M("次のページ ", 1, " 5ページ先", DCOUNT + 5 <= PAGENUM, "10ページ先", DCOUNT + 10 <= PAGENUM, "前のページ", DCOUNT > 1, "日記を閉じる", 1)
		IF RESULT == 0
			CONTINUE
		ELSEIF RESULT == 1
			DCOUNT += 4
		ELSEIF RESULT == 2
			DCOUNT += 9
		ELSEIF RESULT == 3
			DCOUNT += -2
		ELSEIF RESULT == 4
			CLOSE = 1
			BREAK
		ENDIF
	ENDIF
NEXT
IF !CLOSE
	PRINTFORMW 日記はここで終わっている… 
ELSE
	PRINTFORMW %CALLNAME:MASTER%は読むのを止めた
ENDIF

@CHARA_DIARY_AFTERTRAIN
#DIM CHARA
#DIM DCOUNT
#DIM PAGECOUNT

;毎日終わるとき
FOR CHARA, 1, CHARANUM 
	CALL KOJO_MESSAGE_SEND("DIARY", 0, CHARA, 0, 0, "EXIST")
	SIF !RESULT
		CONTINUE
	;PRINTFORML %CALLNAME:CHARA%の日記を確認
	TARGET = CHARA
	;日記の書き込み判定
	CALL KOJO_MESSAGE_SEND("DIARY", 0, CHARA, 0, 0, "BEFORE_CHECK")
	FOR DCOUNT, 1, MAX_DIARY_PAGE:CHARA:0 + 1
		;最初にヒットした未読(2)を表示
		IF DIARY:CHARA:DCOUNT == 2
			;ページセットしてから口上色を付けて表示
			CALL CHARA_DIARY_PAGESETTING(CHARA, DCOUNT)
			PAGECOUNT = RESULT
			CALL KOJO_MESSAGE_SEND("DIARY", 0, CHARA, PAGESET:CHARA:PAGECOUNT, PAGECOUNT, "TEXT", "デイリー")
			PRINTL
			;既読フラグをつける
			DIARY:(NO:CHARA):DCOUNT = 1
			DRAWLINE
			BREAK
		;未読でない場合スキップ
		ELSE
			CONTINUE
		ENDIF
	NEXT
	;日記は毎日一回だけ。呼び出されなかった日記IDのフラグをリセットする
	;恋慕とか告白とか大事なイベントは順番を前にしたほうがいいです。
	FOR DCOUNT, 1, MAX_DIARY_PAGE:CHARA:0 + 1
		IF DIARY:CHARA:DCOUNT == 2
			DIARY:CHARA:DCOUNT = 0
		ENDIF
	NEXT
	CALL KOJO_MESSAGE_SEND("DIARY", 0, CHARA, 0, 0, "AFTER_CHECK")
NEXT


@MASTER_DAIRY_INPUT
#DIMS 内容行,40
#DIM 行数
VARSET 内容行

行数 = 0
$INPUT_LOOP_DAIRY

PRINTL
SETCOLOR C_YELLOW
PRINTFORML ・ %GET_MONTH()% {DAY:3,2,RIGHT}日 （%GET_DAY()%） %時刻表示(TIME)%
FOR LOCAL,0,行数
	PRINTFORML 　　%内容行:LOCAL%
NEXT
RESETCOLOR
PRINTL
PRINTBUTTON "[98] - 前の行を削除する", "98"
PRINT 　　
PRINTBUTTON "[99] - 日記を仕上げる", "99"
PRINTL

INPUTS

IF RESULTS == "98"
	IF 行数 > 0
		内容行:行数 = 
		行数 -= 1
		CLEARLINE 1
	ENDIF
	
	CLEARLINE (行数+5)
	GOTO INPUT_LOOP_DAIRY
ELSEIF RESULTS == "99"
	LOCALS = 
	FOR LOCAL,0,行数-1
		LOCALS += @"%内容行:LOCAL%<br>"
	NEXT
	SIF 行数>0
		LOCALS += @"%内容行:(行数-1)%"
	
	RESULTS = %LOCALS%
	RETURN
ELSEIF 行数 == 39
	SETCOLOR C_YELLOW
	PRINTFORMW ライン数はすでに上限に
	RESETCOLOR
	SIF RESULTS != ""
		CLEARLINE 1
	CLEARLINE (行数+5)
	GOTO INPUT_LOOP_DAIRY
ELSE
	内容行:行数 = %RESULTS%
	
	SIF RESULTS != ""
		CLEARLINE 1
	CLEARLINE (行数+4)
	行数 += 1
	GOTO INPUT_LOOP_DAIRY
ENDIF


@MASTER_DIARY_WRITE,内容
#DIMS 内容

;上限超えたら2つ消す
IF STRCOUNT(SAVESTR:1,"//") >= 100
	SPLIT SAVESTR:1,"//",LOCALS
	SAVESTR:1 =
	FOR LOCAL,0,99
		SAVESTR:1 = %LOCALS:LOCAL%%SAVESTR:1%
	NEXT
ENDIF
SAVESTR:1 = ・ %GET_MONTH()% {DAY:3,2,RIGHT}日 （%GET_DAY()%） %時刻表示(TIME)%_L_%内容%//%SAVESTR:1%

@READ_MASTER_DIARY
#DIMS 表示内容,100
#DIMS 内容,2
#DIM PAGE

SPLIT SAVESTR:1,"//",表示内容
FOR LOCAL,0,100
	SIF 表示内容:LOCAL != ""
		PAGE ++
NEXT

PRINTFORML ---| %CALLNAME:MASTER%の日記 |-------------------------------------------------------------------------------------
FOR LOCAL,0,100
	IF 表示内容:LOCAL != ""
		SPLIT 表示内容:LOCAL,"_L_",内容
		PRINTL
		PRINTFORML PAGE.{1+LOCAL}
		PRINTL
		HTML_PRINT @"%内容:0%<br><br>%内容:1%<br>"
		PRINTL
		IF PAGE > 1
			PRINTL --------------------------------------------------------------------------------------------------------------------
			CALL ASK_YN("次のページ ","日記を閉じる")
			IF RESULT
				BREAK
			ENDIF
			CLEARLINE 3
		ENDIF
	ENDIF
NEXT
PRINTFORMW 日記はここで終わっている… 


@SHARE_MASTER_DIARY
#DIMS 表示内容,100
#DIMS 内容,2
#DIM PAGE
#DIM READPAGE

SPLIT SAVESTR:1,"//",表示内容
FOR LOCAL,0,100
	SIF 表示内容:LOCAL != ""
		PAGE ++
NEXT

SIF PAGE >= 1
	READPAGE = RAND:PAGE

IF PAGE == 0
	CALL SPTALK,TARGET,,,@"「何も書かれてないじゃない……」//%CALLNAME:TARGET%は日記帳を閉じた。"
	RETURN 0
ENDIF

PRINTFORML %CALLNAME:TARGET%はページを適当にめくって読み始めた…
PRINTL
CALL PRINT_FACE,TARGET

IF 表示内容:READPAGE != ""
	SPLIT 表示内容:READPAGE,"_L_",内容
	PRINTL
	PRINTFORML PAGE.{1+READPAGE}
	PRINTL
	HTML_PRINT @"%内容:0%<br><br>%内容:1%<br>"
	PRINTL
	PRINTW
ENDIF
;===========================================================================
;日記に書き込む処理
;指定されたCHARAのDIARY番号(PAGENUM)を最後の空白ページにセット
;何ページに書いたかを返り値にする
;===========================================================================
@CHARA_DIARY_PAGESETTING(CHARA, PAGENUM)
#DIM CHARA
#DIM PAGENUM
FOR LOCAL, 1, MAX_DIARY_PAGE:CHARA:0 + 1
	IF !PAGESET:CHARA:LOCAL
		PAGESET:CHARA:LOCAL = PAGENUM
		RETURN LOCAL
	ENDIF
NEXT





;===========================================================================
;日記保存文件処理
;===========================================================================
@WRITE_MASTER_DAIRY_FILE
DRAWLINE
FOR LOCAL,0,20
	LOADTEXT (1700+LOCAL)
	
	IF RESULTS != ""
		PRINTBUTTON @"日記帳[{LOCAL}]", LOCAL
		PRINTFORML 
	ELSE
		PRINTBUTTON "[なし]", LOCAL
		PRINTFORML 
	ENDIF
	RESETCOLOR
NEXT
DRAWLINE
PRINTBUTTON "[やめる]", 99

$INPUT_LOOP_DAIRY_FILE
INPUT
LOCAL = RESULT

SIF LOCAL == 99
	RETURN -1
IF !INRANGE(LOCAL, 0, 20-1)
	CLEARLINE 1
	GOTO INPUT_LOOP_DAIRY_FILE
ENDIF

RESULT = LOCAL

SAVETEXT SAVESTR:1, (1700+RESULT)
SIF !RESULT
	CALL COLORMESSAGE("保存に失敗する",C_YELLOW,2,1)


@READ_MASTER_DAIRY_FILE
DRAWLINE
FOR LOCAL,0,20
	LOADTEXT (1700+LOCAL)
	
	IF RESULTS != ""
		PRINTBUTTON @"日記帳[{LOCAL}]", LOCAL
		PRINTFORML 
	ELSE
		PRINTBUTTON "[なし]", -1
		PRINTFORML 
	ENDIF
	RESETCOLOR
NEXT
DRAWLINE
PRINTBUTTON "[やめる]", 99

$INPUT_LOOP_DAIRY_FILE
INPUT
LOCAL = RESULT

SIF LOCAL == 99
	RETURN -1
IF !INRANGE(LOCAL, 0, 20-1) || LOCAL == -1
	CLEARLINE 1
	GOTO INPUT_LOOP_DAIRY_FILE
ENDIF

LOADTEXT (1700+RESULT)
SAVESTR:1 = %RESULTS%

@ASK_DAIRY(ARGS)
PRINTFORML この出来事を日記に残しますか？
CALL ASK_YN
IF !RESULT
	CALL MASTER_DIARY_WRITE, @"%ARGS%"
ENDIF

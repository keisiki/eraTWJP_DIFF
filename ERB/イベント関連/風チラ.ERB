@WINDY_PANCHIRA

;デフォルトの確率は25%
LOCAL = 25

;時止め中だと発生しない
SIF FLAG:70
	RETURN 0

;あなた一人だけだと発生しない
SIF TARGET <= 0
	RETURN 0

;対象が睡眠中だと発生しない
SIF CFLAG:睡眠
	RETURN 0

;屋外（屋根の有無は問わない）でないと発生しない
SIF !OUTROOF(CFLAG:MASTER:現在位置) && INROOM(CFLAG:MASTER:現在位置)
	RETURN 0

;地底以外だと晴れか曇りでないと発生しない
SIF GET_MAPID(CFLAG:MASTER:現在位置) != 9 && !(TIME:5 >= 0 && TIME:5 <= 3) 
	RETURN 0

;地底以外で台風、および吹雪だと発生しない
SIF GET_MAPID(CFLAG:MASTER:現在位置) != 9 && GROUPMATCH(FLAG:異常気象, 9, 10)
	RETURN 0

;会話、仕事を手伝う、自由行動に付き合う、休憩、掃除、戦闘訓練、待つのいずれかでないと発生しない
SIF !(SELECTCOM == 300 || SELECTCOM == 304 || SELECTCOM == 368 || SELECTCOM == 403 || SELECTCOM == 410 || SELECTCOM == 411 || SELECTCOM == 440)
	RETURN 0

;一部の自由行動では発生しない
IF SELECTCOM == 368
	SELECTCASE Activity_Type:TARGET
	CASE 2,3,7,11,13,15,16,18
		RETURN 0
	ENDSELECT
ENDIF

;一度発生したら5ターンのクールタイムあり
SIF FLAG:風チラCT > 0
	RETURN 0

;暴風以上だとパンツどころではないので発生しない
SIF WIND_VELOCITY >= 2
	RETURN 0

;妖怪の山（山頂）だと発生確率上昇
SIF GET_MAPID(CFLAG:MASTER:現在位置) == 8
	LOCAL += 10

;地底以外で強風だと発生確率上昇
SIF WIND_VELOCITY == 1 && GET_MAPID(CFLAG:MASTER:現在位置) != 9
	LOCAL += 10

$LOOP
IF RAND:2 == 0
	SIF EQUIP:TARGET:ズボン
		RETURN 0
	SIF !(EQUIP:TARGET:スカート || EQUIP:TARGET:ワンピース || EQUIP:TARGET:着物) || EQUIP:TARGET:0 == 0
		RETURN 0
	;着物だと発生確率低下
	SIF !(EQUIP:TARGET:スカート || EQUIP:TARGET:ワンピース) && EQUIP:TARGET:着物
		LOCAL -= 10
	;デート中だと発生確率低下
	SIF CHK_DATENOW(CFLAG:TARGET:デート中)
		LOCAL -= 10
	IF RAND:100 < LOCAL + CFLAG:TARGET:めくれ補正
		CALL PANCHIRA
	ELSE
		RETURN 0		
	ENDIF
ELSE
	SIF EQUIP:MASTER:ズボン
		RETURN 0
	SIF !(EQUIP:MASTER:スカート || EQUIP:MASTER:ワンピース || EQUIP:MASTER:着物) || EQUIP:MASTER:0 == 0
		RETURN 0
	SIF !(EQUIP:MASTER:スカート || EQUIP:MASTER:ワンピース) && EQUIP:MASTER:着物
		LOCAL -= 10
	SIF CHK_DATENOW(CFLAG:TARGET:デート中)
		LOCAL -= 10
	IF RAND:100 < LOCAL
		CALL REV_PANCHIRA
	ELSE
		RETURN 0		
	ENDIF
ENDIF
RETURN 1

@PANCHIRA
DRAWLINE
IF GET_MAPID(CFLAG:MASTER:現在位置) != 9 && WIND_VELOCITY == 1
	PRINTFORM 折から吹き荒れる強風が急に勢いを増し、一瞬気を緩めた%CALLNAME:TARGET%の
ELSE
	PRINTFORM 唐突に一陣の風が巻き起こり、油断していた%CALLNAME:TARGET%の
ENDIF
IF EQUIP:TARGET:スカート
	PRINTFORM %SHOW_BOTTOM(TARGET,1)%
ELSEIF EQUIP:TARGET:着物
	PRINTFORM %SHOW_TOP(TARGET,1)%の裾
ELSEIF EQUIP:TARGET:ワンピース
	PRINTFORM %SHOW_TOP(TARGET,2)%の裾
ENDIF
PRINTFORM を
SIF GET_MAPID(CFLAG:MASTER:現在位置) != 9 && WIND_VELOCITY == 1
	PRINTFORM 盛大に
PRINTFORML 捲り上げた
 
PRINTFORM %CALLNAME:TARGET%

SELECTCASE SELECTCOM

CASE 300
	PRINTFORM と近くで一緒に話していた
CASE 304
	IF CFLAG:TARGET:350 == 46
		PRINTFORM の遊びに参加していた
	ELSE
		PRINTFORM の仕事を手伝っていた
	ENDIF
CASE 368
	SELECTCASE Activity_Type:TARGET
	CASE 0
		PRINTFORM の考え事について質問していた
	CASE 1
		PRINTFORM の遊びに付き合っていた
	CASE 4
		PRINTFORM の掃除を手伝っていた
	CASE 5
		PRINTFORM の運動に付き合っていた
	CASE 6
		PRINTFORM が読んでいる本について質問していた
	CASE 8
		IF TFLAG:193 == -2
			SELECTCASE TALENT:TARGET:音楽知識
			CASE 1
				PRINTFORM の演奏を聞いていた
			CASE 2
				PRINTFORM の歌を聞いていた
			CASE 3
				PRINTFORM の舞を眺めていた
			ENDSELECT
		ELSE
			SELECTCASE TALENT:TARGET:音楽知識
			CASE 1
				PRINTFORM の演奏に合わせてセッションしていた
			CASE 2
				PRINTFORM の歌に合わせて伴奏していた
			CASE 3
				PRINTFORM の舞に合わせて伴奏していた
			ENDSELECT
		ENDIF
	CASE 9
		PRINTFORM の採集を手伝っていた
	CASE 10
		IF TFLAG:193 < 0
			PRINTFORM が釣りをするのを眺めていた
		ELSE
			PRINTFORM の横で釣りをしていた
		ENDIF
	CASE 12
		PRINTFORM と一緒にのんびりしていた
	CASE 14
		PRINTFORM の買い物を手伝っていた
	CASE 17
		PRINTFORM の雪作りを手伝っていた
	ENDSELECT
CASE 403
	PRINTFORM と一緒に一息ついていた
CASE 410
	IF TFLAG:193
		PRINTFORM と一緒に掃除をしていた
	ELSE
		PRINTFORM が見ている前で掃除をしていた
	ENDIF
CASE 411
	PRINTFORM と一緒に訓練をしていた
CASE 440
	IF CHK_DATENOW(CFLAG:TARGET:デート中)
		PRINTFORM と一緒に連れ立って行動していた
	ELSE
		PRINTFORM の丁度近くに居合わせていた
	ENDIF

ENDSELECT
PRINTFORML %CALLNAME:MASTER%は、

IF EQUIP:TARGET:スカート
	PRINTFORM %SHOW_BOTTOM(TARGET,1)%
ELSEIF EQUIP:TARGET:着物
	PRINTFORM %SHOW_TOP(TARGET,1)%
ELSEIF EQUIP:TARGET:ワンピース
	PRINTFORM %SHOW_TOP(TARGET,2)%
ENDIF

PRINTFORM に隠された

IF !EQUIP:TARGET:下半身下着２
	PRINTFORM 剥き出しの下半身
ELSE
	PRINTFORM %PANTS_DESCRIPTION(EQUIP:TARGET:下半身下着２, TARGET)%%PANTSNAME(EQUIP:TARGET:下半身下着２, TARGET)%
ENDIF
PRINTL をバッチリ見てしまった…
WAIT

CALL KOJO_MESSAGE_SEND("PANCHIRA",0,TARGET)
CALL PANCHIRA_SOURCE

RETURN 1


@REV_PANCHIRA
DRAWLINE
IF GET_MAPID(CFLAG:MASTER:現在位置) != 9 && WIND_VELOCITY == 1
	PRINTFORM 折から吹き荒れる強風が急に勢いを増した拍子に、一瞬気を緩めてしまった%CALLNAME:TARGET%は
ELSE
	PRINTFORM 不意に足元から巻き起こった悪戯な風に、油断していた%CALLNAME:MASTER%は
ENDIF
IF EQUIP:MASTER:スカート
	PRINTFORM %SHOW_BOTTOM(MASTER,1)%
ELSEIF EQUIP:MASTER:着物
	PRINTFORM %SHOW_TOP(MASTER,1)%の裾
ELSEIF EQUIP:MASTER:ワンピース
	PRINTFORM %SHOW_TOP(MASTER,2)%の裾
ENDIF
PRINTFORM を
SIF GET_MAPID(CFLAG:MASTER:現在位置) != 9 && WIND_VELOCITY == 1
	PRINTFORM 盛大に

PRINTFORM 捲り上げられてしま

IF SELECTCOM == 368 && Activity_Type:TARGET == 10 && RAND:2 == 0
	PRINTFORML ったが、
	PRINTFORM 釣りに夢中な%CALLNAME:TARGET%は、%CALLNAME:TARGET%が晒した痴態に気が付かなかったようだ…
	WAIT
	FLAG:風チラCT = 6
	RETURN 0
ELSE
	PRINTFORML い、
ENDIF

IF SELECTCOM == 440
	IF CHK_DATENOW(CFLAG:TARGET:デート中)
		PRINTFORM 近くで
	ELSE
		PRINTFORM たまたま
	ENDIF
ENDIF

PRINTFORM %CALLNAME:MASTER%

SELECTCASE SELECTCOM

CASE 300
	PRINTFORM と近くで話していた
CASE 304
	IF CFLAG:TARGET:350 == 46
		PRINTFORM を遊びに誘った
	ELSE
		PRINTFORM の手を借りながら仕事を進めていた
	ENDIF
CASE 368
	SELECTCASE Activity_Type:TARGET
	CASE 0
		PRINTFORM の目の前で考え事をしていた
	CASE 1
		PRINTFORM の遊びに付き合っていた
	CASE 4
		PRINTFORM の手を借りながら掃除を進めていた
	CASE 5
		PRINTFORM と一緒に運動していた
	CASE 6
		PRINTFORM の目の前で本を読んでいた
	CASE 8
		IF TFLAG:193 == -2
			SELECTCASE TALENT:TARGET:音楽知識
			CASE 1
				PRINTFORM の目の前で演奏を披露していた
			CASE 2
				PRINTFORM の目の前で歌を披露していた
			CASE 3
				PRINTFORM の目の前で舞を披露していた
			ENDSELECT
		ELSE
			SELECTCASE TALENT:TARGET:音楽知識
			CASE 1
				PRINTFORM のセッションに合わせて演奏を披露していた
			CASE 2
				PRINTFORM の伴奏に合わせて歌を披露していた
			CASE 3
				PRINTFORM の伴奏に合わせて舞を披露していた
			ENDSELECT
		ENDIF
	CASE 9
		PRINTFORM に手伝ってもらいながら採集を続けていた
	CASE 10
		IF TFLAG:193 < 0
			PRINTFORM の視線の先で釣りをしていた
		ELSE
			PRINTFORM の横で釣りをしていた
		ENDIF
	CASE 12
		PRINTFORM と一緒にのんびりしていた
	CASE 14
		PRINTFORM の手を借りながら買い物を楽しんでいた
	CASE 17
		PRINTFORM の手を借りながら雪作りを進めていた
	ENDSELECT
CASE 403
	PRINTFORM と一緒に一息ついていた
CASE 410
	IF TFLAG:193
		PRINTFORM と一緒に掃除をしていた
	ELSE
		PRINTFORM が掃除をする様を眺めていた
	ENDIF
CASE 411
	PRINTFORM と一緒に訓練をしていた
CASE 440
	PRINTFORM の様子を見ていた
ENDSELECT
PRINTFORML %CALLNAME:TARGET%に
IF !EQUIP:MASTER:下半身下着２
	PRINTFORM 剥き出しになった下半身
ELSE
	PRINTFORM 身に着けている%PANTS_DESCRIPTION(EQUIP:MASTER:下半身下着２, MASTER)%%PANTSNAME(EQUIP:MASTER:下半身下着２, MASTER)%
ENDIF
PRINTL をバッチリ見られてしまった…
WAIT

CALL KOJO_MESSAGE_SEND("REV_PANCHIRA",0,TARGET)
CALL REV_PANCHIRA_SOURCE

RETURN 1

@PANCHIRA_SOURCE
IF TALENT:恋人
	IF !EQUIP:TARGET:下半身下着２
		SOURCE:TARGET:情愛 += 600
		SOURCE:TARGET:欲情 += 700
		SOURCE:TARGET:誘惑 += 800
		SOURCE:TARGET:露出 += 1000
	ELSE
		SOURCE:TARGET:情愛 += 600
		SOURCE:TARGET:欲情 += 500
		SOURCE:TARGET:誘惑 += 500
		SOURCE:TARGET:露出 += 700
	ENDIF
ELSEIF TALENT:恋慕
	IF !EQUIP:TARGET:下半身下着２
		SOURCE:TARGET:情愛 += 400
		SOURCE:TARGET:欲情 += 700
		SOURCE:TARGET:誘惑 += 800
		SOURCE:TARGET:露出 += 1000
	ELSE
		SOURCE:TARGET:情愛 += 400
		SOURCE:TARGET:欲情 += 500
		SOURCE:TARGET:誘惑 += 500
		SOURCE:TARGET:露出 += 700
	ENDIF
ELSEIF TALENT:愛欲 || TALENT:セフレ
	IF !EQUIP:TARGET:下半身下着２
		SOURCE:TARGET:欲情 += 600
		SOURCE:TARGET:誘惑 += 800
		SOURCE:TARGET:露出 += 1000
	ELSE
		SOURCE:TARGET:欲情 += 400
		SOURCE:TARGET:誘惑 += 500
		SOURCE:TARGET:露出 += 700
	ENDIF
ELSEIF TALENT:思慕
	IF !EQUIP:TARGET:下半身下着２
		SOURCE:TARGET:欲情 += 300
		SOURCE:TARGET:誘惑 += 400
		SOURCE:TARGET:露出 += 800
		SOURCE:TARGET:逸脱 += 10
	ELSE
		SOURCE:TARGET:欲情 += 100
		SOURCE:TARGET:誘惑 += 100
		SOURCE:TARGET:露出 += 500
	ENDIF
ELSE
	IF !EQUIP:TARGET:下半身下着２
		SOURCE:TARGET:露出 += 800
		SOURCE:TARGET:逸脱 += 20
		SOURCE:TARGET:反感 += 10
	ELSE
		SOURCE:TARGET:露出 += 500
		SOURCE:TARGET:反感 += 10
	ENDIF
ENDIF

CALL SOURCE_情愛(TARGET)
CALL SOURCE_欲情(TARGET)
CALL SOURCE_誘惑(TARGET)
CALL SOURCE_露出(TARGET)
CALL SOURCE_逸脱(TARGET)
CALL SOURCE_反感(TARGET)
FLAG:風チラCT = 6
CFLAG:TARGET:下半身下着確認 = 1

RETURN 1



@REV_PANCHIRA_SOURCE
IF TALENT:MASTER:性別 == 2
	IF TALENT:恋人
		IF !EQUIP:MASTER:下半身下着２
			SOURCE:TARGET:情愛 += 600
			SOURCE:TARGET:欲情 += 700
			SOURCE:TARGET:歓楽 += 500
			SOURCE:TARGET:辱め += 600
			SOURCE:MASTER:露出 += 1000
		ELSE
			SOURCE:TARGET:情愛 += 600
			SOURCE:TARGET:欲情 += 300
			SOURCE:TARGET:歓楽 += 400
			SOURCE:TARGET:辱め += 500
			SOURCE:MASTER:露出 += 700
		ENDIF
	ELSEIF TALENT:恋慕
		IF !EQUIP:MASTER:下半身下着２
			SOURCE:TARGET:情愛 += 400
			SOURCE:TARGET:欲情 += 700
			SOURCE:TARGET:歓楽 += 500
			SOURCE:TARGET:辱め += 600
			SOURCE:MASTER:露出 += 1000
		ELSE
			SOURCE:TARGET:情愛 += 400
			SOURCE:TARGET:欲情 += 300
			SOURCE:TARGET:歓楽 += 400
			SOURCE:TARGET:辱め += 500
			SOURCE:MASTER:露出 += 700
		ENDIF
	ELSEIF TALENT:愛欲 || TALENT:セフレ
		IF !EQUIP:MASTER:下半身下着２
			SOURCE:TARGET:欲情 += 500
			SOURCE:TARGET:歓楽 += 300
			SOURCE:TARGET:辱め += 400
			SOURCE:MASTER:露出 += 1000
		ELSE
			SOURCE:TARGET:欲情 += 200
			SOURCE:TARGET:歓楽 += 300
			SOURCE:TARGET:辱め += 300
			SOURCE:MASTER:露出 += 700
		ENDIF
	ELSEIF TALENT:思慕
		IF !EQUIP:MASTER:下半身下着２
			SOURCE:TARGET:欲情 += 100
			SOURCE:TARGET:逸脱 += 20
			SOURCE:MASTER:露出 += 800
		ELSE
			SOURCE:TARGET:歓楽 += 100
			SOURCE:TARGET:辱め += 100
			SOURCE:MASTER:露出 += 500
		ENDIF
	ELSE
		IF !EQUIP:MASTER:下半身下着２
			SOURCE:TARGET:逸脱 += 50
			SOURCE:TARGET:反感 += 50
			SOURCE:MASTER:露出 += 800
		ELSE
			SOURCE:TARGET:歓楽 += 50
			SOURCE:MASTER:露出 += 500
		ENDIF
	ENDIF
ELSE
	IF TALENT:恋人
		IF !EQUIP:MASTER:下半身下着２
			SOURCE:TARGET:情愛 += 600
			SOURCE:TARGET:欲情 += 700
			SOURCE:TARGET:歓楽 += 600
			SOURCE:TARGET:辱め += 800
			SOURCE:MASTER:露出 += 1000
		ELSE
			SOURCE:TARGET:情愛 += 600
			SOURCE:TARGET:欲情 += 500
			SOURCE:TARGET:歓楽 += 500
			SOURCE:TARGET:辱め += 500
			SOURCE:MASTER:露出 += 700
		ENDIF
	ELSEIF TALENT:恋慕
		IF !EQUIP:MASTER:下半身下着２
			SOURCE:TARGET:情愛 += 400
			SOURCE:TARGET:欲情 += 700
			SOURCE:TARGET:歓楽 += 600
			SOURCE:TARGET:辱め += 700
			SOURCE:MASTER:露出 += 1000
		ELSE
			SOURCE:TARGET:情愛 += 400
			SOURCE:TARGET:欲情 += 500
			SOURCE:TARGET:歓楽 += 500
			SOURCE:TARGET:辱め += 400
			SOURCE:MASTER:露出 += 700
		ENDIF
	ELSEIF TALENT:愛欲 || TALENT:セフレ
		IF !EQUIP:MASTER:下半身下着２
			SOURCE:TARGET:欲情 += 600
			SOURCE:TARGET:歓楽 += 500
			SOURCE:TARGET:辱め += 700
			SOURCE:MASTER:露出 += 1000
		ELSE
			SOURCE:TARGET:欲情 += 400
			SOURCE:TARGET:歓楽 += 400
			SOURCE:TARGET:辱め += 400
			SOURCE:MASTER:露出 += 700
		ENDIF
	ELSEIF TALENT:思慕
		IF !EQUIP:MASTER:下半身下着２
			SOURCE:TARGET:欲情 += 400
			SOURCE:TARGET:歓楽 += 200
			SOURCE:TARGET:辱め += 300
			SOURCE:TARGET:逸脱 += 10
			SOURCE:MASTER:露出 += 800
		ELSE
			SOURCE:TARGET:欲情 += 400
			SOURCE:TARGET:歓楽 += 200
			SOURCE:TARGET:辱め += 100
			SOURCE:MASTER:露出 += 500
		ENDIF
	ELSE
		IF !EQUIP:MASTER:下半身下着２
			SOURCE:TARGET:歓楽 += 50
			SOURCE:TARGET:逸脱 += 10
			SOURCE:MASTER:露出 += 800
		ELSE
			SOURCE:TARGET:歓楽 += 100
			SOURCE:MASTER:露出 += 500
		ENDIF
	ENDIF
ENDIF

CALL SOURCE_情愛(TARGET)
CALL SOURCE_欲情(TARGET)
CALL SOURCE_歓楽(TARGET)
CALL SOURCE_辱め(TARGET)
CALL SOURCE_逸脱(TARGET)
CALL SOURCE_反感(TARGET)
CALL SOURCE_露出(MASTER)
FLAG:風チラCT = 6

RETURN 1

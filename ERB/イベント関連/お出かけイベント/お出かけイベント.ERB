@お出かけイベント
#DIM GO_OUT_EVENT, GO_OUT_MAX_NO
;お出かけイベントは一日一回
SIF GETBIT(TFLAG:一日一回, 11)
	RETURN
VARSET GO_OUT_EVENT, -1
CALLF FUNC_FISHER_YATES_SHAFFLE(GO_OUT_EVENT, GO_OUT_MAX_NO)
FOR COUNT, 0, GO_OUT_MAX_NO
	RESULT = 0
	TRYCALLFORM GO_OUT_CONDITION_EV{GO_OUT_EVENT:COUNT}
	IF RESULT
		SETBIT TFLAG:一日一回, 11
		BREAK
	ENDIF
NEXT
RETURN

@PICK_RUNINTO(MAP_ID)
#DIM C_ID
#DIM MAP_ID
SIF GETBIT(TFLAG:固有遭遇イベント, MAP_ID)
	RETURN
SETBIT TFLAG:固有遭遇イベント, MAP_ID
CALL FISHER_YATES_SHAFFLE(RANDOM_CHARANUM)
;ランダムで50人ピックアップ
FOR COUNT, 0, 50
	C_ID = SHAFFLE_ARRAY:COUNT
	;出禁/拠点住まい、あなたを弾く
	SIF CFLAG:C_ID:出禁 || CFLAG:C_ID:神社在住 || !C_ID
		CONTINUE
	;訪問予定/仕事/宴会参加キャラを弾く
	SIF CFLAG:C_ID:拠点来訪 || 仕事日チェック(C_ID) || CFLAG:C_ID:宴会参加
		CONTINUE
	;活動時間外/衰弱中キャラ/もう会った子を弾く
	SIF !VISIT(C_ID) || CFLAG:C_ID:衰弱 || !TCVAR:C_ID:今日会ったか
		CONTINUE
	;自宅があるかよく行く地域じゃないと駄目
	SIF CFLAG:C_ID:自宅位置 / 100 != MAP_ID && CFLAG:C_ID:よく行く地域 != MAP_ID
		CONTINUE
	;ここまで通ったキャラをチェック
	RESULT = 0
	TRYCALLFORM RUN_INTO_{C_ID}(MAP_ID)
	SIF RESULT == -1
		CONTINUE
	;その子を出現させる処理
	CFLAG:C_ID:遭遇位置 = MAP_ID * 100 + 10
	CFLAG:C_ID:現在位置 = CFLAG:C_ID:遭遇位置
	CFLAG:C_ID:最後に会った日 = DAY
	TCVAR:C_ID:今日会ったか = 1
	IF RESULT == 0
		PRINTFORMW %CALLNAME:C_ID%に出くわした
		CALL KOJO_MESSAGE_SEND("EVENT",1,C_ID,9,0)
	ENDIF
	BREAK
NEXT
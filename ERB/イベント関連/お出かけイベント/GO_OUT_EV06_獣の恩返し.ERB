@GO_OUT_CONDITION_EV6
IF !RAND:10
	CALL GO_OUT_EV6
ELSE
	RETURN 0
ENDIF
@GO_OUT_EV6
#DIM PAIR
#DIM LUCK
#DIM SPENT
#DIMS PREY
SPENT = 1
PREY = %\@ RAND:2 ? 狸 # 狐 \@%
PAIR = FLAG:デート相手
LUCK = CFLAG:MASTER:今日の運勢 / 100 + TFLAG:幸運補正

PRINTFORML 道中、猟師の罠にかかった%PREY%を見かけた
IF (GROUPMATCH(PAIR, [[藍]], [[典]]) && PREY == "狐") || (PAIR == [[マミゾウ]] && PREY == "狸")
	PRINTFORMW %CALLNAME:PAIR%が何か言いたげにこちらを見つめている
	PRINTFORML %CALLNAME:MASTER%が罠を外してやると%PREY%は%CALLNAME:PAIR%にお礼を言って逃げていった…
	PRINTFORMW 
	RETURN 1
ELSEIF (GROUPMATCH(PAIR, [[藍]], [[典]]) && PREY == "狸") || (PAIR == [[マミゾウ]] && PREY == "狐")
	PRINTFORMW %CALLNAME:PAIR%は罠に一瞥をくれただけでそのまま歩いていく
	PRINTFORML %CALLNAME:MASTER%も慌てて後を追いかけた…
	PRINTFORMW 
	RETURN 1
ENDIF
PRINTFORML このままでは間違いなく捕まるが…
CALL ASK_M("猟の邪魔はしない", 1, "助けてやる", 1)
IF !RESULT
	PRINTFORML かわいそうだがどうすることもできない
	SIF PAIR
		PRINTFORM %CALLNAME:PAIR%も後ろ髪を引かれるようだが、
	PRINTFORMDL そのままにして\@ PAIR ? 二人 # %CALLNAME:MASTER% \@は帰路についた
	PRINTFORMW
	RETURN 1
ENDIF
PRINTFORML かわいそうなので助けてやることにした
PRINTFORMW ケガをしているが簡単な手当てですぐに歩けそうだ
PRINTDW …………
PRINTDW ……
PRINTDW …
IF LUCK > 4
	PRINTFORMW 手当てが終わると%PREY%は茂みの陰に消えてしまった
	PRINTFORML …と思ったら何かを咥えて%CALLNAME:MASTER%の元へ戻ってきた
	PRINTFORML 助けてあげたお礼に違いない
	;最高級素材をランダム
	LOCAL:0 = 625, 630, 639, 640, 643, 646
	CALL GET_ITEM(ITEMNAME:(LOCAL:(RAND:6)), 1, 2)
	PRINTFORML 
	IF !(GO_OUT_FLAG_06 || PAIR || GETBIT(TFLAG:一日一回, 9)) && LUCK > 7
		GO_OUT_FLAG_06 = DAY
		SETBIT TFLAG:一日一回, 9
		CALL MAKE_YUKIZURI(RANDOM_CHARANUM, @"化け%PREY%")
		CFLAG:RANDOM_CHARANUM:よく行く地域 = (MAIN_MAP == 5) ? 7 # 5
		CFLAG:RANDOM_CHARANUM:好感度 = REQUIRED_FAVOR_思慕
		CFLAG:RANDOM_CHARANUM:信頼度 = REQUIRED_TRUST_思慕
		ABL:RANDOM_CHARANUM:親密 = REQUIRED_AFFECTION_思慕
		ABL:RANDOM_CHARANUM:従順 = REQUIRED_AFFECTION_思慕
		CSTR:RANDOM_CHARANUM:モブ子プロフィール１ = %CALLNAME:RANDOM_CHARANUM%：%PRINT_DATE_F(DAY)%に%GET_MAPNAME(CFLAG:MASTER:デート中)%の帰り道で助けた化け%PREY%
		BASE:RANDOM_CHARANUM:体力 -= 50
		PRINTFORML …おや！？　%PREY%の様子が…！
		PRINTFORMW なんと目の前で%PREY%がケモミミ少女に変化した！
		CALL CHARA_STATE(RANDOM_CHARANUM)
		DRAWLINE
		CALL COLORMESSAGE(@"処女喪失履歴：%CSTR:RANDOM_CHARANUM:処女喪失履歴%",C_PINK,1)
		PRINTFORMW 
		PRINTFORML %PREY%娘は%CALLNAME:RANDOM_CHARANUM%と名乗り%CALLNAME:MASTER%に何度も感謝を述べている
		SIF !INRANGE(TIME, 540, 1140)
			GOTO SKIP
		PRINTFORML お礼に家までついていくと言っているが…
		CALL ASK_M("今日はそのまま帰す", 1, "ついてきてもらう", 1)
		IF RESULT
			FLAG:デート相手 = RANDOM_CHARANUM
			CFLAG:RANDOM_CHARANUM:現在位置 = CFLAG:MASTER:現在位置
			CFLAG:RANDOM_CHARANUM:同行 = 300
			CFLAG:MASTER:同行 = 300
			CFLAG:RANDOM_CHARANUM:眠気覚まし = 300
			CFLAG:RANDOM_CHARANUM:拠点来訪 = 1
			PRINTFORML %CALLNAME:MASTER%は%CALLNAME:RANDOM_CHARANUM%を連れて帰ることにした
			PRINTFORML %CALLNAME:RANDOM_CHARANUM%は熱っぽい視線をこちらに送っている…
			GOTO END_PHASE
		ENDIF
		PRINTFORML ケガもしているのでそのまま帰すことにした
		$SKIP
		PRINTFORMW %CALLNAME:RANDOM_CHARANUM%は今度\@ CFLAG:RANDOM_CHARANUM:よく行く地域 == 5 ? 魔法の森 # 山の山麓 \@まで遊びに来てほしいと言って去っていった…
		CALL COLORMESSAGE(@"%CALLNAME:RANDOM_CHARANUM%と知り合いになった",C_YELLOW,2,1)
		CFLAG:RANDOM_CHARANUM:現在位置 = SUKIMA()
		GOTO END_PHASE
	ENDIF
	PRINTFORML 助けた%PREY%に見送られながら\@ PAIR ? 二人 # %CALLNAME:MASTER% \@は帰路についた
ELSE
	IF PAIR
		PRINTFORML 手当てをしている間も%PREY%は暴れて%CALLNAME:MASTER%を引っかきまくった
		CALL RECOVER(MASTER, -200, "体力")
		WAIT
		PRINTFORML おまけに手当てが終わると%PREY%はダッシュで逃げていった…何という恩知らず！
		PRINTFORM %CALLNAME:PAIR%
		SELECTCASE CFLAG:PAIR:性格傾向
			CASE 1
				PRINTFORMW が心配そうに%CALLNAME:MASTER%を見ている…
			CASE 2, 3
				PRINTFORMW は%CALLNAME:MASTER%を見て\@ CFLAG:PAIR:性格傾向 == 2 ? 大笑いして # くすくすと笑って \@いる…
			CASE 4
				PRINTFORMW がなんともいえない微妙な表情をしている…
			CASE 5
				PRINTFORMW は憐れみの目を%CALLNAME:MASTER%に向けている…
		ENDSELECT
		PRINTFORML 特に何も得ることもなく二人は帰路についた
	ELSE
		PRINTFORMW 手当てを終えると%PREY%はダッシュで逃げていった…何という恩知らず！
		PRINTFORML しかたないので帰ろうとするが後ろに誰かいるようだ
		PRINTFORMW 振り返るとそこには…獲物を逃されてお怒りの猟師がいた…！
		PRINTFORML 
		PRINTFORML %CALLNAME:MASTER%は頭をはたかれた！
		CALL RECOVER(MASTER, -100, "体力")
		WAIT
		PRINTFORML 猟師に説教された…
		CALL RECOVER(MASTER, -300, "気力")
		WAIT
		PRINTFORML ヒドい目にあった%CALLNAME:MASTER%はトボトボと帰路についた
		SPENT = 4
	ENDIF
ENDIF
$END_PHASE
PRINTFORML +{15 * SPENT}分
PRINTFORMW 
TIME += (15 * SPENT)
RETURN 1

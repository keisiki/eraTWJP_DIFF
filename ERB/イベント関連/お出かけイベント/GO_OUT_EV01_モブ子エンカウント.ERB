@GO_OUT_CONDITION_EV1
RESULT = 0
;デート中は発生しない
SIF RAND:3 || FLAG:デート相手
	RETURN 0
;その日物色済みだと無理
SIF GETBIT(TFLAG:一日一回, 9)
	RETURN 0
CALL GO_OUT_EV1
RETURN RESULT

@GO_OUT_EV1
#DIM ENEMY
#DIMS ENEMY_RACE
#DIM モブ残機
#DIM LOOT
#DIM LOOT_CANDIDATE, 17 = 51, 52, 53, 55, 56, 58, 59, 81, 82, 83, 84, 121, 131, 500, 501, 502, 510
#DIM CONST CANDIDATE_N = 17
#DIM REWARD
#DIM 発情フラグ
#DIM 満月バフ効果
発情フラグ = 0
満月バフ効果 = 0
;相手が変わるとかやりたい
SELECTCASE CFLAG:MASTER:デート中
	;湖
	CASE 3
		ENEMY_RACE = 妖精
	;竹林
	CASE 4
		IF !RAND:3
			ENEMY_RACE = イナバ
		ELSE
			ENEMY_RACE = 妖精
		ENDIF
	;魔法の森
	CASE 5
		ENEMY_RACE '= TEXTR("化け狸/茨たぬき")
	;三途の川
	CASE 6
		ENEMY_RACE '= TEXTR("化け狐/化け猫")
	;山麓
	CASE 7
		ENEMY_RACE = 河童
	;山頂
	CASE 8
		ENEMY_RACE = 白狼天狗
	;地底
	CASE 9
		ENEMY_RACE = 鬼
	;月
	CASE 10
		ENEMY_RACE = 玉兎
	CASEELSE
		RETURN 0
ENDSELECT

PRINTFORMW 道中、不意に%ENEMY_RACE%が襲い掛かってきた！
SETBIT TFLAG:一日一回, 9
;桃色の風による影響
IF (FLAG:異常気象 == 8 && TCVAR:RANDOM_CHARANUM:発情) || TCVAR:RANDOM_CHARANUM:願掛け桃色の風
	発情フラグ = 1
ENDIF
CALL MAKE_YUKIZURI(RANDOM_CHARANUM, ENEMY_RACE, 1)
ENEMY = RANDOM_CHARANUM

;満月時
IF TALENT:ENEMY:動物耳 && MOON_WATCH(DAY:3) == 9
	発情フラグ = 2
	CALL COLORMESSAGE(@"満月の光が%CALLNAME:ENEMY%の本能をかき立てる…！",C_YELLOW,1)
	CALL COLORMESSAGE(@"%CALLNAME:ENEMY%の戦闘力がアップ！",C_YELLOW,1)
	満月バフ効果 = 1 + RAND:(MAX(2, ABL:MASTER:戦闘能力 - ABL:ENEMY:戦闘能力))
	ABL:ENEMY:戦闘能力 += 満月バフ効果
ENDIF
IF 発情フラグ
	TCVAR:ENEMY:発情 = 1
	CFLAG:ENEMY:溜まってる度 += LIMIT((10 + RAND:20 + RAND:20 + RAND:20) * ABL:ENEMY:欲望,200,1000)
	
	IF 発情フラグ == 2
		;満月時は危険日確定
		CFLAG:ENEMY:900 = 7
	ELSE
		SELECTCASE FLAG:抱負
			CASE 6
				;抱負が子孫繁栄で未達成の場合は危険日確定
				CFLAG:ENEMY:900 = 7
			CASE 9
				;子孫繁栄達成済み、かつ、安全日の場合は再設定
				SIF ESTRUS_CYCLE(ENEMY) <= 15
					CFLAG:ENEMY:900 = RAND:10
		ENDSELECT
	ENDIF
ENDIF

HANDICAP_FIXED = RAND:(ABL:ENEMY:戦闘能力 * 5 + 1) + 10
HANDICAP_RAND = 0

CALL KOJO_MESSAGE_SEND("DANMAKU", 0, ENEMY, 1, -1, "戦闘前")
TEQUIP:ENEMY:上半身着衣状況 = 1

CALL CHARA_STATE(ENEMY)
	DRAWLINE
	CALL COLORMESSAGE(@"処女喪失履歴：%CSTR:ENEMY:処女喪失履歴%",C_PINK,1)
	PRINTFORML 
CALL DANMAKU_OPPONENT_STATUS(ENEMY)
IF HANDICAP_FIXED
	CALL COLORMESSAGE(@"地形効果：%CALLNAME:MASTER%のダイス値が{HANDICAP_FIXED}低下",C_YELLOW,1)
	;【三粒の天滴】の効果
	IF ITEM:三粒の天滴
		HANDICAP_FIXED /= 2
		CALL COLORMESSAGE(@"土着神の呪いにより、地形効果が{HANDICAP_FIXED}に半減！",C_YELLOW,1)
	ENDIF
ENDIF
$LOOP
CALL ASK_M("返り討ちにしてやる",1,"逃げる",1,"ルール説明",1)
SELECTCASE RESULT
	CASE 0
		IF BASE:MASTER:気力 < 1000 || BASE:MASTER:体力 < 1000
			PRINTFORML 今は疲れているため戦力が下がりますが、それでも戦いますか？
			CALL ASK_YN()
			IF RESULT == 0
				HANDICAP_FIXED += MAX(0, 1000 - BASE:MASTER:気力)
				HANDICAP_FIXED += MAX(0, 1000 - BASE:MASTER:体力)
				CALL COLORMESSAGE(@"疲労により、%CALLNAME:MASTER%のダイス値が{HANDICAP_FIXED}低下",C_YELLOW,1)
			ELSE
				PRINTFORML %CALLNAME:MASTER%はすたこら逃げ出した
				RETURN 1
			ENDIF
		ENDIF
	CASE 1
		PRINTFORML %CALLNAME:MASTER%はすたこら逃げ出した
		RETURN 1
	CASE 2
		CALL SHOW_DANMAKU_RULE
		GOTO LOOP
ENDSELECT
TARGET = ENEMY
REWARD = MAX(ABL:ENEMY:戦闘能力 * 10 + RAND:5, 1)
;米とか酒とか
LOOT = LOOT_CANDIDATE:(RAND:CANDIDATE_N)
TIME += 60
CFLAG:ENEMY:面識 = 1
PRINTFORMW 勝負開始！！
CALL DANMAKU_BATTLE(TARGET, 1)

モブ残機 = RESULT
CALL RECOVER(MASTER, -500, "体力")
CALL RECOVER(MASTER, -500, "気力")
CALL KOJO_MESSAGE_SEND("DANMAKU", モブ残機, TARGET, 1, -1, "戦闘後")
SIF モブ残機 < 3
	EXP:MASTER:戦闘経験 += ABL:ENEMY:戦闘能力 * 3
SIF モブ残機 < 1
	EXP:MASTER:戦闘経験 += ABL:ENEMY:戦闘能力
ABL:ENEMY:戦闘能力 -= 満月バフ効果
IF モブ残機 > 0
	;モブ娘勝利（MASTERの敗戦）処理
	SETCOLOR C_RED
	IF TCVAR:ENEMY:発情 && HAS_PENIS(MASTER)
		;CALL KOJO_MESSAGE_SEND("DANMAKU", モブ残機, TARGET, 1, -1, "逆レ開始")
		PRINTFORML %CALLNAME:ENEMY%は動けなくなった%CALLNAME:MASTER%を組み敷いた
		PRINTFORML %CALLNAME:MASTER%の喉笛を、生暖かい吐息が吹き降りていく…
		PRINTFORML もはやこれまでと思った次の瞬間、性器が熱い感触に包まれた
		PRINTFORML 恐る恐る下腹を見下ろすと、ニヤニヤと笑みを浮かべる%CALLNAME:ENEMY%のワレメに%CALLNAME:MASTER%のペニスが根元まで収まっている
		IF 発情フラグ == 1
			PRINTFORM 桃色の風を受けた
		ELSEIF 発情フラグ == 2
			PRINTFORM 満月の夜に発情期を迎えた
		ENDIF
		PRINTFORML %CALLNAME:ENEMY%は獣性の渇きを癒すため、%CALLNAME:MASTER%を滅茶苦茶に犯し、嬲り、蹂躙していった…
		PRINTFORMW ………
		PRINTFORMW ……
		PRINTFORMW …
		PRINTFORMW 気が済むまで%CALLNAME:MASTER%を貪り尽くした%CALLNAME:ENEMY%は、ぐったりした%CALLNAME:MASTER%を打ち捨てて去って行った
		FLAG:気絶中断 = 100 + CFLAG:MASTER:現在位置 / 100
		CFLAG:MASTER:デート中 = CFLAG:MASTER:現在位置 / 100
		CALL LOST_GYAKURE(ENEMY)
	ELSEIF ITEM:LOOT
		PRINTFORMW 負けた%CALLNAME:MASTER%は%ITEMNAME:LOOT%を取られてしまった…
		ITEM:LOOT --
	ELSEIF MONEY:2 >= REWARD
		PRINTFORMW 負けた%CALLNAME:MASTER%は{REWARD}カリスマ取られてしまった…
		MONEY:2 -= REWARD
	ELSE
		REWARD *= 300
		PRINTFORMW 負けた%CALLNAME:MASTER%は\@ MONEY > REWARD ? %金額表示(REWARD)%# 有り金すべて\@取られてしまった…
		MONEY = MAX(MONEY - REWARD, 0)
	ENDIF
	RESETCOLOR
	RETURN 1
ENDIF

;source操作他
TCVAR:ENEMY:弾幕勝負不能 = 1
CFLAG:ENEMY:弾幕勝負勝利 = 1
CFLAG:ENEMY:負け犬 = 1
CFLAG:ENEMY:よく行く地域 = CFLAG:MASTER:デート中

PRINTFORML %CALLNAME:MASTER%は地べたに膝をつく%CALLNAME:ENEMY%に…
CALL ASK_M("供物にする",1,"カツアゲ",1,"体で償わせる",1, "ミラダの石", ITEM:ミラダの石)
SELECTCASE RESULT
	CASE 0
		PRINTFORML %CALLNAME:MASTER%が%CALLNAME:ENEMY%に手をかざして祈りの言葉を唱えると、服を透かして%CALLNAME:ENEMY%の下着が淡く輝き始めた
		PRINTFORML やがて超自然の炎が熾り、%CALLNAME:ENEMY%も衣服も害することなく下着だけが燃え尽きた
		PRINTFORML 神威を目の当たりにした%CALLNAME:ENEMY%は恐れをなして逃げて行った
		CALL BUFF_BASE(MASTER,BASE_TSP,500, 1)
		CALL BUFF_BASE(MASTER,BASE_精力,500, 1)
		FLAG:篤信 += 10
		;DELCHARA RANDOM_CHARANUM
		;ADDCHARA RANDOM_CHARANUM
		TARGET = 0
		RETURN 1
	CASE 1
		PRINTFORMW オラッ！ジャンプしろジャンプ！
		MONEY:2 += REWARD
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:ENEMY%から{REWARD}カリスマまきあげた
		IF RAND:5 < ABL:ENEMY:戦闘能力
			PRINTFORMW さらに%ITEMNAME:LOOT%もまきあげた
			ITEM:LOOT ++
		ENDIF
		TARGET = 0
		;DELCHARA RANDOM_CHARANUM
		;ADDCHARA RANDOM_CHARANUM
		RETURN 1
	CASE 2
		PRINTFORML %CALLNAME:ENEMY%に、巫女に突き出されたくなければ今日一日%CALLNAME:MASTER%に従うように命じた
		IF TALENT:ENEMY:マゾ
			PRINTFORM %CALLNAME:ENEMY%はこの後に行われるであろう%TEXTR("償い/お仕置き/仕返し/懲罰/懲らしめ")%を
			PRINTFORM %TEXTR("予想/予感/期待/想像")%して、
			PRINTFORML %TEXTR("胸が高鳴って/うずうずして/待ち望んで/ぞくぞくして")%いるようだ
			PALAM:ENEMY:欲情 = PALAMLV:3
			ABL:ENEMY:欲望 = MAX(3, ABL:ENEMY:欲望)
			CALL COLORMESSAGE(@"%CALLNAME:ENEMY%を人前でなければいつでも押し倒せるようになった",C_YELLOW,1)
		ELSE
			MARK:ENEMY:反発刻印 = 1
			CALL COLORMESSAGE(@"%CALLNAME:ENEMY%に反発刻印がついたが、人前でなければいつでも押し倒せるようになった",C_YELLOW,1)
		ENDIF
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:ENEMY%を連れて意気揚々と帰路についた
	CASE 3
		PRINTFORML 睨みつけてくる%CALLNAME:ENEMY%を見返しながら、%CALLNAME:MASTER%はミラダの石を握りしめた
		PRINTFORML すると%CALLNAME:ENEMY%は視線を逸らして俯いてしまった
		CFLAG:ENEMY:体目当て = 1
		ITEM:ミラダの石 --
		ABL:ENEMY:親密 = MAX(3, ABL:ENEMY:親密)
		ABL:ENEMY:欲望 = MAX(3, ABL:ENEMY:欲望)
		CFLAG:ENEMY:溜まってる度 = 1000
		PRINTFORML %CALLNAME:MASTER%は%CALLNAME:ENEMY%の肩に手を置いて、今日一日従うように命じた
		PRINTFORML %CALLNAME:ENEMY%はやけに素直に受け入れた
		CALL COLORMESSAGE(@"%CALLNAME:ENEMY%を人前でなければいつでも押し倒せるようになった",C_YELLOW,1)
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:ENEMY%を連れて意気揚々と帰路についた
ENDSELECT

FLAG:デート相手 = TARGET
CFLAG:TARGET:同行 = 180
CFLAG:MASTER:同行 = 180
CFLAG:ENEMY:眠気覚まし = 600
CFLAG:ENEMY:拠点来訪 = 1
CFLAG:ENEMY:来訪時間 = 0
CFLAG:ENEMY:帰宅時間 = 1440 + CFLAG:ENEMY:眠気覚まし
CFLAG:ENEMY:就寝時間 = 1440 + CFLAG:ENEMY:眠気覚まし
CFLAG:ENEMY:起床時間 = 0
CSTR:ENEMY:モブ子プロフィール２ = 無様にも返り討ちにされ今はもう大人しい

RETURN 1

@LOST_GYAKURE(ARG)
#DIM 性交回数
#DIM 射精回数
性交回数 = MAX(1, CFLAG:ARG:溜まってる度 / 100)
射精回数 = MAX(1, BASE:MASTER:精力 / 100)
BASE:MASTER:体力 = 0
BASE:MASTER:気力 = 0
;BASE:MASTER:精力 = 0
;射精処理
CALL ADD_SAMEN_V(ARG, BASE:MASTER:精力)
EXP:ARG:精液経験 += 射精回数
EXP:ARG:膣射経験 += 射精回数
EXP:ARG:Ｖ性交経験 += 性交回数
EXP:ARG:Ｖ経験 += 性交回数
EXP:MASTER:絶頂経験 += 射精回数
EXP:MASTER:射精経験 += 射精回数
EXP:MASTER:挿入経験 += 性交回数
CFLAG:ARG:溜まってる度 = 0
IF TALENT:ARG:処女
	CSTR:ARG:処女喪失履歴 = 発情中に%CALLNAME:MASTER%を襲って捨てた
	TALENT:ARG:処女 = 0
ENDIF
CSTR:ARG:モブ子プロフィール２ = %CALLNAME:MASTER%を貪り尽くしたことがある
CALL SET_SLEEP(1,MASTER,0)
CFLAG:MASTER:就寝時間 = DAY * 1440 + TIME + 120 + RAND:120
TFLAG:106 = 0
BEGIN AFTERTRAIN

;IF TIME:2 >= 5 && TIME:2 <= 7 && TIME:5 < 2
;	CALL COLORMESSAGE(@"満月の光が%CALLNAME:TARGET%の本能をかき立てる…！",C_YELLOW,1)
;	CALL COLORMESSAGE(@"%CALLNAME:TARGET%の戦闘力がアップ！",C_YELLOW,1)
;ENDIF

;モブ娘エンカウント率上昇
@MOB_BATTLE_ENCOUNT
#FUNCTION
#DIM 月の状態
IF FLAG:異常気象 == 8 && TCVAR:RANDOM_CHARANUM:発情
	RETURNF 1
ELSEIF  TCVAR:RANDOM_CHARANUM:願掛け桃色の風
	RETURNF 1
ELSE
	月の状態 = MOON_WATCH(DAY:3)
	;満月
	IF 月の状態 == 9
		SELECTCASE FLAG:抱負
			CASE 6
				;抱負が子孫繁栄
				RETURNF 1
			CASE 9
				;子孫繁栄達成済み
				SIF RAND:5 == 0
					RETURNF 1
		ENDSELECT
		SIF RAND:10 == 0
			RETURNF 1
	;新月
	ELSEIF 月の状態 == 1 && RAND:3 == 0
		RETURNF 1
	ENDIF
ENDIF
RETURNF 0

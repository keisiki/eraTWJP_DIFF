@GO_OUT_CONDITION_EV0
#DIM PAIR
PAIR = FLAG:デート相手
SIF RAND:10 || GROUPMATCH(PAIR, 0, RANDOM_CHARANUM) 
	RETURN 0
SIF !TALENT:PAIR:思慕 && !TALENT:PAIR:恋慕
	RETURN 0
SIF MARK:PAIR :反発刻印 || TALENT:PAIR :機嫌 < 0
	RETURN 0
CALL GO_OUT_EV0(PAIR)

@GO_OUT_EV0(ARG)
#DIM MULTIPLE

IF TALENT:ARG:幼稚
	PRINTFORMW 帰り道は%CALLNAME:ARG%%TEXTR("とかけっこ/と競争/を肩車")%しながら帰ってきた
	CALL PRINT_FACE, ARG, "笑顔" ,,, "照れ"
	PRINTFORML %CALLNAME:ARG%はとても楽しそうだ…
	MULTIPLE = 12
ELSEIF TALENT:ARG:恋人
	PRINTFORMW 帰り道は%CALLNAME:ARG%と腕を組みながら帰ってきた
	CALL PRINT_FACE, ARG, "笑顔",,, "照れ"
	PRINTFORML デートの最後まで幸せな気分になれた…
	MULTIPLE = 13
ELSEIF TALENT:ARG:恋慕
	PRINTFORMW %CALLNAME:ARG%と手を繋ぎながら帰ることにした
	CALL PRINT_FACE, ARG, "笑顔",,, "照れ"
	PRINTFORML %CALLNAME:ARG%はとても嬉しそうだ…
	MULTIPLE = 12
ELSE
	PRINTFORMW 帰り道は%CALLNAME:ARG%と手を繋ぎながら帰ってきた
	CALL PRINT_FACE, ARG, "通常",,, "照れ"
	PRINTFORML なんだか%CALLNAME:ARG%との距離が近づいた気がする…
	MULTIPLE = 11
ENDIF
PRINTFORMW 
BASE:ARG:ムード = MIN(BASE:ARG:ムード / 10 * MULTIPLE, MAXBASE:ARG:ムード)
SIF !TALENT:ARG:幼稚
	BASE:ARG:理性 = MAX(BASE:ARG:理性 - MULTIPLE * 5, 0)
RETURN 1

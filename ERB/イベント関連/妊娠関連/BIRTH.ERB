@BIRTH(ARG)
#DIM CHILDNUM
DRAWLINE
;妊娠消去、出産経験、性別の決定、誕生日決定
TALENT:ARG:妊娠 = 0
EXP:ARG:出産経験 += 1
CHILDNUM = EXP:ARG:出産経験 % 10
SIF !EXP:ARG:出産経験 % 10
	CHILDNUM = 10
IF FLAG:子供の性別
	CHILD_SEX:ARG:CHILDNUM = FLAG:子供の性別
ELSE
	CHILD_SEX:ARG:CHILDNUM = RAND:2 + 1
ENDIF
CHILD_BIRTHDAY:ARG:CHILDNUM = DAY
CHILD_BIRTHDATE:ARG:CHILDNUM = (DAY / 124 + (DAY % 124 ? 1 # 0)) * 1000
CHILD_BIRTHDATE:ARG:CHILDNUM += DAY:2 * 100 + DAY:3
CHILD_WEATHER:ARG:CHILDNUM = %GET_WEATHER()%
IF CFLAG:ARG:無自覚妊娠
	CALL KOJO_MESSAGE_SEND("EVENT",25,ARG,4,CHILDNUM)
	CHILD_LOVE:ARG:CHILDNUM = -100
ELSEIF TALENT:ARG:恋慕
	CALL KOJO_MESSAGE_SEND("EVENT",25,ARG,1,CHILDNUM)
	CHILD_LOVE:ARG:CHILDNUM = 300
ELSEIF TALENT:ARG:思慕
	CALL KOJO_MESSAGE_SEND("EVENT",25,ARG,2,CHILDNUM)
	CHILD_LOVE:ARG:CHILDNUM = 200
ELSE
	CALL KOJO_MESSAGE_SEND("EVENT",25,ARG,3,CHILDNUM)
	CHILD_LOVE:ARG:CHILDNUM = 100
ENDIF
;あらかじめ自立先を決めちゃう（自立するときにもっかい決める）
CALL 自立先選定(ARG, CHILDNUM)
PRINTFORMW %CALLNAME:ARG%は、%CALLNAME:MASTER%の子供を無事出産しました
PRINTFORMW \@ CHILD_SEX:ARG:CHILDNUM == 1 ? %CALLNAME:ARG%に似た元気な女の子 # %CALLNAME:MASTER%に似た元気な男の子 \@ です
PRINTFORMW %CALLNAME:ARG%は産まれたばかりの赤ん坊を受け取ると、優しく抱きかかえ母乳を飲ませ始めた
SIF TALENT:ARG:母性 == 0
	PRINTFORMW %CALLNAME:ARG%は[母性]に目覚めた！
PRINTL ******************************************************************
PRINTFORML 第{CHILDNUM}子　母：%CALLNAME:ARG%　父：%CALLNAME:MASTER%
PRINTFORML 　性別：\@ CHILD_SEX:ARG:CHILDNUM == 1 ? 女 # 男 \@
PRINTFORM 誕生日：
CALL PRINT_DATE(DAY)
PRINTFORML  %FESTIVAL()% -%GET_WEATHER()%-
;PRINTFORML 　身長：
;PRINTFORML 　体重：
;PRINTFORML 　種族：
;PRINTFORML 髪の色：
;PRINTFORML 目の色：
PRINTL ******************************************************************


;育児中・母性追加
CALL CHILD_NAME(ARG)
CFLAG:ARG:育児人数 += 1
CFLAG:MASTER:育児人数 += 1
CFLAG:ARG:出産経過日 = 1
CFLAG:ARG:妊娠経過日数 = 0
CFLAG:ARG:無自覚妊娠 = 0
CFLAG:ARG:身に覚え_母 = 0
CFLAG:ARG:身に覚え_父 = 0
CFLAG:ARG:妊娠自覚状態 = 0 
TALENT:ARG:母性 = 1

CFLAG:ARG:産休中 = 0

EXP:ARG:愛情経験 += 10
CFLAG:ARG:子供人数 += 1
CFLAG:MASTER:子供人数 += 1
IF FLAG:甲斐性無
	PRINTFORML 我が子の誕生に%CALLNAME:MASTER%は
	CALL ASK_YN("親としての責任感が芽生えるのを感じた","今後起こりうる様々な事態への不安を感じた")
	IF !RESULT
		PRINTFORMW %CALLNAME:MASTER%は甲斐性無しではなくなった
	ELSE
		PRINTFORMW 人の性格がそんなに簡単に変えられ成長できるなら誰も苦労しません
		PRINTFORMW %CALLNAME:MASTER%は前以上に頼りない性格のまま、%CALLNAME:ARG%と仲良く育児に臨むのでした
	ENDIF
	FLAG:甲斐性無 = RESULT
ENDIF
		

@CHILD_NAME(YOME)
#DIM YOME
#DIM CHILDNUM
#DIMS PROPOSED_NAME, 10
CHILDNUM = EXP:YOME:出産経験 % 10
SIF !EXP:YOME:出産経験 % 10
	CHILDNUM = 10
IF CHILD_SEX:YOME:CHILDNUM == 1
	PROPOSED_NAME:0 = %ランダムキャラ_命名(YOME)%
ELSE
	PROPOSED_NAME:0 = %男の子_命名(YOME, CHILDNUM)%
ENDIF
$LOOP0
PRINTL 子供の名前を決めましょう
PRINTFORML 
CALL ASK_M("自分で決める",1, "ランダム候補一覧",1, @"%CALLNAME:YOME%の提案に従って「%PROPOSED_NAME:0%」と名付ける",1)
;CALL ASK_M("自分で決める",1,"ランダム候補一覧",1,"「メキシコに吹く熱風！」という意味の「サンタナ」というのはどうかな！",1)
SELECTCASE RESULT
	CASE 0
		$LOOP1
		PRINTL 子供の名前を入力してください
		INPUTS
		IF RESULTS == ""
			GOTO LOOP1
		ELSE
			PROPOSED_NAME:0 = %RESULTS%
			CALL CHK_CHILDNAME_DUPLICATION(PROPOSED_NAME:0)
			PRINTFORML %PROPOSED_NAME:0%でいいですか？
			CALL ASK_YN
			IF RESULT
				GOTO LOOP1
			ELSE
				CHILDNAME:YOME:CHILDNUM = %PROPOSED_NAME:0%
			ENDIF
		ENDIF
	CASE 1
		$LOOP2
		FOR LOCAL,1,10
			IF CHILD_SEX:YOME:CHILDNUM == 1
				PROPOSED_NAME:LOCAL = %ランダムキャラ_命名(YOME)%
			ELSE
				PROPOSED_NAME:LOCAL = %男の子_命名(YOME, CHILDNUM)%
			ENDIF
			PRINTFORML [{LOCAL}] - %PROPOSED_NAME:LOCAL%
		NEXT
		DRAWLINE
		PRINTFORML [99] - 候補再生成
		PRINTFORML [100] - 戻る
		$LOOP3
		INPUT
		SELECTCASE RESULT
			CASE 1 TO 9
				LOCAL = RESULT
				CALL CHK_CHILDNAME_DUPLICATION(PROPOSED_NAME:LOCAL)
				PRINTFORML %PROPOSED_NAME:LOCAL%でいいですか？
				CALL ASK_YN
				IF RESULT
					GOTO LOOP0
				ELSE
					CHILDNAME:YOME:CHILDNUM = %PROPOSED_NAME:LOCAL%
				ENDIF
			CASE 99
				GOTO LOOP2
			CASE 100
				GOTO LOOP0
			CASEELSE
				GOTO LOOP3
		ENDSELECT
	CASE 2
		;PRINTFORML 怒った%CALLNAME:YOME%が名前を決めてしまった…
		PRINTFORML %CALLNAME:YOME%は嬉しそうに我が子へ呼びかけている
		CHILDNAME:YOME:CHILDNUM = %PROPOSED_NAME:0%
ENDSELECT

PRINTFORML %CHILDNAME:YOME:CHILDNUM%という名前になりました
TALENT:YOME:育児中 = CHILDNUM
;----------------------------------------------------
;そのキャラに特定の年齢の子供がいたら返す関数
;----------------------------------------------------
@CHK_HAVE_CHILD(ARG,ARG:1,ARG:2)
#FUNCTION
#DIM 年齢
SIF !CFLAG:ARG:育児人数
	RETURNF 0

FOR LOCAL, 1, 11
	年齢 = DAY - CHILD_BIRTHDAY:ARG:LOCAL
	SIF INRANGE(年齢,ARG:1,ARG:2-1)
		RETURNF LOCAL
NEXT

RETURNF 0
;----------------------------------------------------
;誕生日の子供がいるか返す関数
;eraTW4.570proto_bugfix_m05での仕様変更
;・REFを使用し、配列を書き換えるF関数にした（引数を全て渡さないとエラーになる）
;・仕様上、配列の想定最大要素数は10*キャラクタ数上限となる（＝RESULTの要素数を突破してエラーが出たので仕様変更）
;・BREAKやCONTINUEを使用して処理を効率化
;----------------------------------------------------
@CHILD_HAPPY_BIRTHDAY(CNT_BIRTHDAY, NAME_CHILD, AGE_CHILD)
#FUNCTION
#DIM CHARA
#DIM CHILDNUM
#DIM  REF CNT_BIRTHDAY
#DIMS REF NAME_CHILD
#DIM  REF AGE_CHILD
FOR CHARA, 1, CHARANUM
	SIF !CFLAG:CHARA:子供人数
		CONTINUE
	FOR CHILDNUM, 1, MIN(11, CFLAG:CHARA:子供人数 + 1)
		;名無し≒未登録なら次のキャラへ
		SIF !STRLENS(CHILDNAME:CHARA:CHILDNUM)
			BREAK
		;誕生日以外はスキップ
		SIF (DAY - CHILD_BIRTHDAY:CHARA:CHILDNUM) % 124
			CONTINUE
		CNT_BIRTHDAY ++
		NAME_CHILD:CNT_BIRTHDAY = %CHILDNAME:CHARA:CHILDNUM%
		AGE_CHILD:CNT_BIRTHDAY = (DAY - CHILD_BIRTHDAY:CHARA:CHILDNUM) / 124
	NEXT
NEXT
RETURNF CNT_BIRTHDAY

;子供の名前被りチェック
@CHK_CHILDNAME_DUPLICATION(ARGS)
#DIM C_ID
FOR C_ID,1,CHARANUM
	;"FOR LOCAL,0,10"から修正
	FOR LOCAL,1,11
		SIF CHILDNAME:C_ID:LOCAL == ARGS
			CALL COLORMESSAGE(@"%CALLNAME:C_ID%に同名の子供がいます",C_YELLOW,1)
	NEXT
NEXT
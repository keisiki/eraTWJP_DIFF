@DAILY_CONDITION_EV7

IF !RAND:15 && MAIN_MAP == 9
	CALL DAILY_EV7
	RETURN 1
ELSE
	RETURN -1
ENDIF

@DAILY_EV7
#DIM 人数
#DIM YUGI
VARSET LOCAL
YUGI = !RAND:4
IF YUGI
	LOCALS = 勇儀たち
ELSE
	LOCALS = 鬼たち
ENDIF
人数 = 1 + RAND:5
PRINTFORML %CALLNAME:MASTER%が床に就こうとした時、呑んだくれた{人数}人の鬼が転がり込んできた！
SIF YUGI
	PRINTFORML しかも勇儀までいる……
PRINTFORMW どうやらここに居座って呑み続けるつもりらしい……
PRINTFORML 
;他の選択肢は後で
;ASK_M("受け入れる","酒を渡して出て行ってもらう","ぶちのめす")
PRINTFORML %CALLNAME:MASTER%はヤケクソで%LOCALS%を受け入れた
PRINTFORML %LOCALS%は遠慮の欠片も見せずに持参の酒とツマミを広げて馬鹿騒ぎをはじめた
LOCAL:1 = 1
FOR LOCAL,0,人数
	LOCAL:1 += RAND:3
NEXT
SIF YUGI
	LOCAL:1 += RAND:3 + RAND:3

FOR LOCAL,0,LOCAL:1
	PRINTFORMW …
	PRINTFORMW ……
	PRINTFORML %CALLNAME:MASTER%は無理矢理酒を飲まされた
	SELECTCASE RAND:3
		CASE 0
			PRINTFORMW 普通にいい酒だ
			LOCAL:2 = 200
		CASE 1
			PRINTFORMW 結構強い酒だ
			LOCAL:2 = 400
		CASE 2
			PRINTFORMW これはキツい！
			LOCAL:2 = 700
	ENDSELECT
	PRINTFORMW 
	BASE:MASTER:酒気 += LOCAL:2
	IF YUGI
		CALL CHANGE_CFLAG(2,64,3)
		CALL CHANGE_CFLAG(4,64,1)
	ENDIF
	IF BASE:MASTER:酒気 > MAXBASE:MASTER:酒気
		PRINTFORML 「ゴボーッ！」
		PRINTFORML %CALLNAME:MASTER%は吐いた！
		YOGORE:(CFLAG:MASTER:初期位置) += 1500
		BASE:MASTER:気力 -= 500
		MAXBASE:MASTER:酒気 += 3
		PRINTFORMW %LOCALS%はゲラゲラ笑っている…
		IF BASE:MASTER:気力 < 0
			PRINTFORMW %CALLNAME:MASTER%は倒れた！
			BASE:MASTER:気力 -= 1000
			LOCAL:3 ++
			BREAK
		ELSE
			BASE:MASTER:酒気 /= 2
		ENDIF
	ELSEIF BASE:MASTER:酒気 > MAXBASE:MASTER:酒気 * 3 / 4
		PRINTFORMW 気持ち悪くなってきた…
	ENDIF
NEXT

IF !LOCAL:3
	PRINTFORML %CALLNAME:MASTER%は執拗なアルハラに耐えきった！
	IF YUGI
		CFLAG:64:好感度 += 150
		CFLAG:64:信頼度 += 100
	ENDIF
	MAXBASE:MASTER:酒気 += 20
ENDIF
CFLAG:MASTER:徹夜 = 1
YOGORE:(CFLAG:MASTER:初期位置) += 1000
MAXBASE:MASTER:酒気 = LIMIT(MAXBASE:MASTER:酒気,1500,3000)
PRINTFORML 明け方%LOCALS%は去っていった
PRINTFORML 後には寝不足の%CALLNAME:MASTER%と大量の空き瓶\@ LOCAL:3? とゲロ臭い部屋# \@が残された
PRINTW 
RETURN 1
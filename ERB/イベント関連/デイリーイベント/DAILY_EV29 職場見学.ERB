@DAILY_CONDITION_EV29
SIF !CFLAG:[[影狼]]:1005
	RETURN -1
CALL DAILY_EV29
RETURN 1

@DAILY_EV29
CFLAG:[[影狼]]:1005 = 0
FLAG_DAILY_EV29_LASTTIME = DAY
CFLAG:[[影狼]]:最後に来訪した日 = DAY
CFLAG:[[わかさぎ姫]]:最後に来訪した日 = DAY
CFLAG:[[蛮奇]]:最後に来訪した日 = DAY
IF !GETBIT(FLAG_DAILY_EV29, 0)
	CALL DAILY_EV29_0
ELSE
	CALL DAILY_EV29_1
ENDIF
RETURN 1

;初回用
@DAILY_EV29_0
CALL PRINT_FACE, [[影狼]], "通常", "服", "", ""
PRINTFORMW 「来たわねー」
CALL PRINT_FACE, [[わかさぎ姫]], "通常", "服", "", ""
PRINTFORMW 「今日はよろしくね」
CALL PRINT_FACE, [[影狼]], "通常", "服", "", ""
PRINTFORML 「どうこの恰好？かなり人間っぽくない？」
CALL ASK_YN("割と","多分バレない")
CALL PRINT_FACE, [[影狼]], "笑顔", "服", "", ""
PRINTFORMW 「でしょー？それじゃ、蛮奇のところへしゅっぱーつ！」
PRINTFORMW ………
PRINTFORMW ……
PRINTFORMW …
CALL PRINT_FACE, [[わかさぎ姫]], "通常", "服", "", ""
PRINTFORMW 「いいお店ね」
CALL PRINT_FACE, [[影狼]], "通常", "服", "", ""
PRINTFORMW 「給仕服似合ってるじゃない」
CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
PRINTFORMW 「ゲッ……なんでこんなところに……」
PRINTFORML 「%CALLNAME:MASTER%の仕業か」
PRINTFORML 「なんでこいつら連れてきた」
IF TALENT:[[影狼]]:恋慕
	CALL ASK_M("蛮奇に会いたくて",1,"影狼の頼みとあらば断れない",1,"この煮込み雑炊一つください",1)
ELSE
	CALL ASK_M("蛮奇に会いたくて",1,"影狼に頼まれたから",1,"この煮込み雑炊一つください",1)
ENDIF
SELECTCASE RESULT
	CASE 0
		CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
		PRINTFORML 「連れてきた理由になってないだろ」
		PRINTFORML 「まあいい、さっさと食ってさっさと帰れ」
		CALL CHANGE_CFLAG(2,[[蛮奇]],50)
		CALL CHANGE_CFLAG(4,[[蛮奇]],10)
	CASE 1
		IF TALENT:[[影狼]]:恋慕
			CALL PRINT_FACE, [[影狼]], "発情", "服", "", ""
			PRINTFORML 「もう、すぐそういうこと言うんだから、もう……」
			CALL CHANGE_CFLAG(2,[[影狼]],100)
			CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
			PRINTFORMW 「影狼うざい」
		ELSE
			CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
			PRINTFORML 「こっちの都合も考えろよ」
			PRINTFORML 「まあいい、さっさと食ってさっさと帰れ」
		ENDIF
	CASE 2
		CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
		PRINTFORMW 「それは今やってない」
		PRINTFORMW 「この世の終わりみたいな顔してないでさっさと他のもん注文しろ」
		DAILY_EV29_NIKOMI ++
ENDSELECT
PRINTFORMW ………
PRINTFORMW ……
PRINTFORMW …
CALL BUFF_BASE(MASTER,BASE_体力,150,1)
CALL BUFF_BASE(MASTER,BASE_気力,150,1)
CALL BUFF_BASE([[影狼]],BASE_体力,150,1)
CALL BUFF_BASE([[影狼]],BASE_気力,150,1)
CALL BUFF_BASE([[わかさぎ姫]],BASE_体力,150,1)
CALL BUFF_BASE([[わかさぎ姫]],BASE_気力,150,1)
CALL PRINT_FACE, [[わかさぎ姫]], "通常", "服", "", ""
PRINTFORMW 「美味しかったね」
PRINTFORMW 「あーそうだお金要るんだった」
CALL PRINT_FACE, [[わかさぎ姫]], "笑顔", "服", "", ""
PRINTFORMW 「私は持ってないけどね！」
CALL PRINT_FACE, [[影狼]], "笑顔", "服", "", ""
PRINTFORMW 「私も！」
CALL 画像セット(@"顔絵_服_通常_{[[影狼]]}", 0, 0, 100, 0, 0, "", "デフォルト顔絵")
CALL 画像セット(@"顔絵_服_通常_{[[わかさぎ姫]]}", 0, 0, 100, 0, 0, "", "デフォルト顔絵")
CALL 画像一斉表示("左", 0, 1)
PRINTFORMW 二人は揃って%CALLNAME:MASTER%の方を見た
CALL COLORMESSAGE(@"所持金:%金額表示(MONEY)%",C_YELLOW,1)
CALL ASK_M("立て替える（\10000）", MONEY >= 10000, "蛮奇の方を見る",1)
SELECTCASE RESULT
	CASE 0
		MONEY -= 10000
	CASE 1
		CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
		PRINTFORMW 「なんで私の方を見るんだよ」
		CALL PRINT_FACE, [[蛮奇]], "怒り", "服", "", ""
		IF MONEY >= 10000
			PRINTFORMW 「ちゃんと持ってるじゃねーか！払えよ！」
			MONEY -= 10000
		ELSE
			CALL EV29_無銭飲食
		ENDIF
ENDSELECT

IF !FLAG_DAILY_EV29_DEBT
	CALL PRINT_FACE, [[わかさぎ姫]], "笑顔", "服", "", ""
	PRINTFORMW 「ありがとー！」
	CALL PRINT_FACE, [[影狼]], "笑顔", "服", "", ""
	PRINTFORMW 「今度埋め合わせするわね」
	CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
	PRINTFORMW 「もう来るなよ」
	;通い狼に影響
	CFLAG:[[影狼]]:1004 += 3
	CFLAG:[[影狼]]:1006 ++
	FLAG_DAILY_EV29_PAID ++
	CALL CHANGE_CFLAG(2,[[影狼]],100)
	CALL CHANGE_CFLAG(4,[[影狼]],50)
	CALL CHANGE_CFLAG(2,[[わかさぎ姫]],100)
	CALL CHANGE_CFLAG(4,[[わかさぎ姫]],50)
	CALL CHANGE_CFLAG(2,[[蛮奇]],100)
	CALL CHANGE_CFLAG(4,[[蛮奇]],50)
ELSE
	CALL CHANGE_CFLAG(2,[[影狼]],50)
	CALL CHANGE_CFLAG(2,[[わかさぎ姫]],50)
ENDIF
SETBIT FLAG_DAILY_EV29, 0


;二回目以降
@DAILY_EV29_1
#DIM PAYMENT
#DIM C_ID
IF GROUPMATCH(TIME:5, 4, 5, 6)
	CALL PRINT_FACE, [[わかさぎ姫]], "笑顔", "服", "", ""
	PRINTFORMW 「今日は降ってるから泳いで来れたの」
	CALL PRINT_FACE, [[影狼]], "笑顔", "服", "", ""
	PRINTFORMW 「いつもこうなら楽なんだけどね」
ELSEIF !RAND:2
	CALL PRINT_FACE, [[影狼]], "通常", "服", "", ""
	PRINTFORMW 「あら、お待たせしちゃった？」
	CALL PRINT_FACE, [[わかさぎ姫]], "通常", "服", "", ""
	PRINTFORMW 「今日は早いのね」
ELSEIF !RAND:4
	CALL PRINT_FACE, [[わかさぎ姫]], "通常", "服", "", ""
	PRINTFORMW 「今日は%CALLNAME:MASTER%が押してくれる？」
	CALL PRINT_FACE, [[影狼]], "笑顔", "服", "", ""
	PRINTFORMW 「濡れタオル巻いてあるからちょっと重いわよー？」
ELSE
	CALL PRINT_FACE, [[わかさぎ姫]], "通常", "服", "", ""
	PRINTFORMW 「こんばんわ」
	CALL PRINT_FACE, [[影狼]], "通常", "服", "", ""
	PRINTFORMW 「さ、行きましょ」
ENDIF
PRINTFORMW ………
PRINTFORMW ……
PRINTFORMW …
IF !RAND:5
	CALL SPTALK, [[影狼]], "通常", 0, @"「ｸﾝｸﾝ……今日は%TEXTR("お肉/お魚/野菜/山菜")%が美味しいと思うわ」", 1
	CALL SPTALK, [[わかさぎ姫]], "通常", 0, @"「影狼ちゃん匂い嗅ぐのやめなよ」", 1
ELSE
	IF !RAND:2
		CALL SPTALK, [[わかさぎ姫]], "通常", 0, @"「こんばんわー」", 1
	ELSE
		CALL SPTALK, [[わかさぎ姫]], "通常", 0, @"「今日は割と%TEXTR("混んでる/空いてる")%ね」", 1
	ENDIF
	CALL SPTALK, [[影狼]], "通常", 0, @"「%TEXTR("また来たわよー/その髪型いいわね/ねえ今日の姫のファッション可愛くない？")%」", 1
ENDIF

CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
IF FLAG_DAILY_EV29_DEBT > 0
	PRINTFORML 「今日は金あるんだろうな」
ELSEIF !RAND:10
	PRINTFORML 「狙いすましたように来るなお前ら、シフト変えたのに……」
ELSEIF !RAND:5
	PRINTFORML 「ぶぶ漬け出してやりたい……」
ELSE
	PRINTFORML 「またお前らか」
ENDIF
PRINTFORML 「ご注文は？」
C_ID = 0
CALL ASK_M("本日のおすすめ",1,"影狼と同じやつ",1,"わかさぎ姫と同じやつ",1,"この煮込み雑炊一つください",1)
;CALL ASK_M("本日のおすすめ",1,"影狼と同じやつ",1,"わかさぎ姫と同じやつ",1,"この煮込み雑炊一つください",1, "パーッといく(\30000)",MONEY >= 30000)
SELECTCASE RESULT
	CASE 0
		C_ID = [[蛮奇]]
	CASE 1
		C_ID = [[影狼]]
	CASE 2
		C_ID = [[わかさぎ姫]]
	CASE 3
		DAILY_EV29_NIKOMI ++
		IF DAY:2 == 2
			PRINTFORMW 「この暑いのに物好きなことで」
		ELSEIF DAILY_EV29_NIKOMI >= 5 && !RAND:5
			PRINTFORML 「%CALLNAME:MASTER%はそればっかだな」
			PRINTFORML 「そんなに旨いの、それ」
			CALL ASK_YN("旨い","別に")
			IF !RESULT
				PRINTFORML 「ふーん」
			ELSE
				PRINTFORML 「お前はそういうやつだよ」
			ENDIF
		ENDIF
ENDSELECT
IF C_ID
	CALL CHANGE_CFLAG(2,C_ID,100)
	CALL CHANGE_CFLAG(4,C_ID,50)
ENDIF
PRINTFORMW ………
PRINTFORMW ……
PRINTFORMW …
CALL BUFF_BASE(MASTER,BASE_体力,150,1)
CALL BUFF_BASE(MASTER,BASE_気力,150,1)
CALL BUFF_BASE([[影狼]],BASE_体力,150,1)
CALL BUFF_BASE([[影狼]],BASE_気力,150,1)
CALL BUFF_BASE([[わかさぎ姫]],BASE_体力,150,1)
CALL BUFF_BASE([[わかさぎ姫]],BASE_気力,150,1)
PAYMENT = 0
IF FLAG_DAILY_EV29_DEBT > 0
	CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
	PRINTFORML 「この間のもまとめて払えよ」
	CALL COLORMESSAGE(@"所持金:%金額表示(MONEY)%　勘定:%金額表示(10000)%　借金:%金額表示(FLAG_DAILY_EV29_DEBT)%",C_YELLOW,1)
	CALL ASK_M("耳を揃えて返す", MONEY >= 10000 + FLAG_DAILY_EV29_DEBT, "今日のお代だけで勘弁して貰う", MONEY >= 10000,"ツケで",1)
	SELECTCASE RESULT
		CASE 0
			PRINTFORMW 「よろしい」
			MONEY -= (10000 + FLAG_DAILY_EV29_DEBT)
			FLAG_DAILY_EV29_DEBT = 0
			FLAG_DAILY_EV29_PAID ++
			CALL CHANGE_CFLAG(2,[[蛮奇]],100)
			CALL CHANGE_CFLAG(4,[[蛮奇]],50)
			PAYMENT = 1
		CASE 1
			PRINTFORMW 「さっさと返せ」
			MONEY -= 10000
			FLAG_DAILY_EV29_PAID ++
			PAYMENT = 1
		CASE 2
			CALL PRINT_FACE, [[蛮奇]], "怒り", "服", "", ""
			PRINTFORML 「ナメてんのか」
			FLAG_DAILY_EV29_DEBT += 10000
	ENDSELECT
ELSE
	CALL SPTALK, [[蛮奇]], "通常", 0, @"「お勘定」", 1
	CALL COLORMESSAGE(@"所持金:%金額表示(MONEY)%　勘定:%金額表示(10000)%",C_YELLOW,1)
	IF !GETBIT(FLAG_DAILY_EV29, 1)
		CALL ASK_M("払う", MONEY >= 10000, "払えない",1)
	ELSE
		CALL ASK_M("払う", MONEY >= 10000, "ツケで",1)
	ENDIF
	SELECTCASE RESULT
		CASE 0
			MONEY -= 10000
		CASE 1
			CALL EV29_無銭飲食
	ENDSELECT
ENDIF
;ちゃんと払った
IF PAYMENT
	CALL PRINT_FACE, [[影狼]], "笑顔", "服", "", ""
	PRINTFORML 「御馳走様！」
	CALL PRINT_FACE, [[わかさぎ姫]], "笑顔", "服", "", ""
	PRINTFORML 「ありがとー！」
	CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
	PRINTFORMW 「お前らも当然のようにタカるな」
	;通い狼に影響
	CFLAG:[[影狼]]:1004 += 3
	CFLAG:[[影狼]]:1006 ++
	FLAG_DAILY_EV29_PAID ++
	CALL CHANGE_CFLAG(2,[[影狼]],100)
	CALL CHANGE_CFLAG(4,[[影狼]],50)
	CALL CHANGE_CFLAG(2,[[わかさぎ姫]],100)
	CALL CHANGE_CFLAG(4,[[わかさぎ姫]],50)
	CALL CHANGE_CFLAG(2,[[蛮奇]],100)
	CALL CHANGE_CFLAG(4,[[蛮奇]],50)
	IF !RAND:10
		CALL PRINT_FACE, [[影狼]], "通常", "服", "", ""
		PRINTFORML 「今日のはちょっと塩気が強かったかな」
		CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
		PRINTFORMW 「やかましい」
	ELSEIF !RAND:5
		CALL PRINT_FACE, [[影狼]], "通常", "服", "", ""
		PRINTFORML 「またお肉持っていくわね」
		CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
		PRINTFORMW 「生じゃないやつで頼む」
	ELSEIF !RAND:6
		CALL PRINT_FACE, [[影狼]], "通常", "服", "", ""
		PRINTFORML 「ところで次の会合なんだけど」
		CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
		PRINTFORMW 「ここでその話はするな」
	ELSEIF !RAND:10
		CALL PRINT_FACE, [[わかさぎ姫]], "通常", "服", "", ""
		PRINTFORML 「今度の川下り応援に来てよ」
		CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
		PRINTFORMW 「行けたらね」
	ELSE
		CALL PRINT_FACE, [[わかさぎ姫]], "笑顔", "服", "", ""
		PRINTFORML 「また来るね」
		CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
		PRINTFORMW 「来ないでいい」
	ENDIF
;払ってない
ELSE
	CALL SPTALK, [[蛮奇]], "怒り", 0, @"「さっさと返せよ」", 1
	CALL CHANGE_CFLAG(2,[[影狼]],50)
	CALL CHANGE_CFLAG(2,[[わかさぎ姫]],50)
ENDIF



@EV29_無銭飲食
IF !GETBIT(FLAG_DAILY_EV29, 1)
	CALL PRINT_FACE, [[蛮奇]], "怒り", "服", "", ""
	PRINTFORMW 「マジで金ないのかよ！どーすんだよ！」
	PRINTFORMW 不審に思ったのか厨房から声が飛んだ
	CALL PRINT_FACE, [[蛮奇]], "通常", "服", "", ""
	PRINTFORMW 「なんでもないでーす！」
	PRINTFORMW 「仕方ない……不足分は私が立て替えてやる」
	CALL PRINT_FACE, [[影狼]], "笑顔", "服", "", ""
	PRINTFORMW 「助かる！」
	CALL PRINT_FACE, [[わかさぎ姫]], "笑顔", "服", "", ""
	PRINTFORMW 「蛮奇ちゃん大好き！」
	PRINTFORML 
	CALL ASK_YN("すぐ返します","蛮奇ちゃん大好き！")
	IF !RESULT
		CALL PRINT_FACE, [[蛮奇]], "怒り", "服", "", ""
		PRINTFORMW 「当たり前だ」
	ELSE
		CALL PRINT_FACE, [[蛮奇]], "怒り", "服", "", ""
		PRINTFORMW 「黙れアホ」
	ENDIF
ELSE
	CALL SPTALK, [[蛮奇]], "怒り", 0, @"「%TEXTR("調子乗るな/ナメてんのか/貧乏人め")%」", 1
ENDIF
SETBIT FLAG_DAILY_EV29, 1
FLAG_DAILY_EV29_DEBT += 10000
CALL COLORMESSAGE(@"蛮奇に借金しました　類型\\{FLAG_DAILY_EV29_DEBT}",C_YELLOW,2)
@DAILY_CONDITION_EV20
SIF RAND:15 > 0
	RETURN -1
SIF CFLAG:[[蛮奇]]:神社在住
	RETURN -1
SIF CFLAG:[[蛮奇]]:出禁
	RETURN -1
SIF !TALENT:[[蛮奇]]:思慕 && ! TALENT:[[蛮奇]]:恋慕
	RETURN -1
CALL DAILY_EV20
RETURN 1

@DAILY_EV20
#DIM 来客数

PRINTFORML 夜中、窓の方から声が聞こえた
PRINTFORMW %CALLNAME:MASTER%が眠い目をこすりながら窓を開けると、生首が浮かんでいた
PRINTFORML ……赤蛮奇の頭だ
PRINTFORML 通りがかったから寄ってみたという
CALL ASK_YN("入れてやる","眠い")
IF RESULT
	PRINTFORMW 眠かったので追い返した
	RETURN
ENDIF
PRINTFORML 入れてやることにした

来客数 = 1
IF ABL:[[蛮奇]]:親密 >= 10 && !RAND:3
	PRINTFORML 窓から生首が入ってきた……が、一つだけではなかった
	FOR LOCAL,2,ABL:73:親密 - 5
		SIF !RAND:2
			CONTINUE
		来客数 ++
		PRINTFORMW {来客数}つめの生首が入ってきた
		SIF 来客数 >= 9
			BREAK
	NEXT
	SIF 来客数 == 1
		PRINTFORML と思ったが、やっぱり一つだった
	SIF 来客数 >= 4
		PRINTFORMW これだけ同じ顔に取り囲まれると、さすがに居心地が悪い……
ENDIF
IF TIME:5 >= 4
	PRINTFORML 濡れていたのでバスタオルを渡した
	PRINTFORML タオルの上で転げまわる生首というのはなかなか面白い光景だ
ELSE
	SELECTCASE RAND:3
		CASE 0
			PRINTFORML 目線の高さを合わせるため、机の上に座布団を置いて乗ってもらった
			SIF 来客数 > 1
				PRINTFORML 机の上に生首が並ぶ光景はさながら獄門か首実検だ
		CASE 1
			PRINTFORML 茶を出したら飲み辛そうにしていたのでストローを差した
			PRINTFORML 飲んだものは一体どこへ行くんだろう……
		CASE 2
			PRINTFORML 後頭部に葉っぱが引っかかっていたので取ってやった
	ENDSELECT
ENDIF
PRINTFORMW 
PRINTFORMW …
PRINTFORMW ……
PRINTFORMW ………

IF RAND:100 < ABL:[[蛮奇]]:親密 * 3 + TALENT:[[蛮奇]]:恋慕 * 15
	PRINTFORM しばらく談笑したのち、生首\@ 来客数 > 1 ? たち# \@は明日は胴体ごと来ると言って
	IF !PRIVATEROOM:(CFLAG:MASTER:初期位置 % 100) && TALENT:[[蛮奇]]:恋慕
		PRINTFORML 帰ろうとしたので、どうせ明日来るなら泊まっていけばと言った
		PRINTFORML %CALLNAME:MASTER%は枕元に生首を\@ 来客数 > 1 ? 並べて# 置いて\@眠りに就いた……
	ELSE
		PRINTFORML 帰っていった
	ENDIF
	CFLAG:[[蛮奇]]:翌日来訪 = 1
ELSE
	PRINTFORML しばらく談笑したのち、生首\@ 来客数 > 1 ? たち# \@は窓から帰っていった
	SIF !RAND:2
		CFLAG:[[蛮奇]]:翌日来訪 = 1
ENDIF
;システムメッセージつき好感度変化
;最後の引数だけ好感度が変化
CALL CHANGE_CFLAG(2,[[蛮奇]],50)

;システムメッセージつき信頼度変化
;最後の引数だけ信頼度が変化
CALL CHANGE_CFLAG(4,[[蛮奇]],20)

;睡眠時間減少
CFLAG:MASTER:就寝時間 += 60
CALL COLORMESSAGE("睡眠時間が1時間減った",C_YELLOW,2)
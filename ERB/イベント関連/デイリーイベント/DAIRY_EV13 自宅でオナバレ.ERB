@DAILY_CONDITION_EV13
#DIM 対象

SIF RAND:(10 - TALENT:MASTER:劣情察知)> 0
	RETURN -1
対象 = 0
CALL FISHER_YATES_SHAFFLE(CHARANUM - 1)
FOR LOCAL:10, 0, CHARANUM - 1
	LOCAL = SHAFFLE_ARRAY:(LOCAL:10) + 1
	SIF !CFLAG:LOCAL:神社在住
		CONTINUE
	IF CFLAG:LOCAL:溜まってる度 >  700
		対象 = LOCAL
		BREAK
	ENDIF
NEXT
SIF !対象
	RETURN -1

CALL DAILY_EV13(対象)
RETURN 1


@DAILY_EV13(ARG)
PRINTFORMW 夜中に目が覚めてしまった
PRINTFORMW 寝なおそうと思ったがどうにも寝付けそうにない……
CALL ASK_YN("出歩く","部屋にいる")
IF RESULT
	PRINTFORM %CALLNAME:MASTER%は眠くなるまで
	PRINTDATA
		DATAFORM コレクションを整理・鑑賞して過ごした
		DATAFORM 美や善やパンツについて思索を深めた
		DATAFORM 壁のシミと談笑した
	ENDDATA
	PRINTFORML ……
	CALL COLORMESSAGE("心が豊かになり、一時的に気力とTSPがちょっと増えた",C_YELLOW,2,1)
	CALL BUFF_BASE(MASTER,BASE_気力,100)
	CALL BUFF_BASE(MASTER,BASE_TSP,100)
ELSEIF !RAND:4 || (!RAND:4 && ABL:ARG:親密 >= 7)
	PRINTFORMW 適当に歩きまわり、\@ CFLAG:MASTER:311 == CFLAG:ARG:311 ? 自室まで戻ってきたところ、何やら物音が聞こえた# さてそろそろ戻ろうかと思ったところ、%CALLNAME:ARG%の部屋から何か聞こえたので寄ってみた\@
	CALL ONABARE(ARG)
	CFLAG:MASTER:徹夜 = 1
ELSEIF ITEM:兎符「開運大紋」
		PRINTL おや、開運大紋の様子が…？
		CALL ASK_YN("取り出す","放っておく")
		IF !RESULT
			ITEM:兎符「開運大紋」 --
			SETCOLOR COLOR ("YELLOW")
			PRINTFORMW 兎符「開運大紋」が発動！
			RESETCOLOR
			PRINTFORMW 適当に歩きまわり、\@ CFLAG:MASTER:311 == CFLAG:ARG:311 ? 自室まで戻ってきたところ、何やら物音が聞こえた# さてそろそろ戻ろうかと思ったところ、%CALLNAME:ARG%の部屋から何か聞こえたので寄ってみた\@
			CALL ONABARE(ARG)
			CFLAG:MASTER:徹夜 = 1
		ELSEIF !RAND:2
			GOTO OYATU
		ELSE
			GOTO NANIMONAI
		ENDIF
ELSEIF !RAND:2
	$OYATU
	PRINTFORML 小腹が空いた。それに何か食べたら眠くなるかもしれない。
	PRINTFORMW %CALLNAME:MASTER%は台所へ行って戸棚を漁ると、旨そうなおやつを見つけた。
	PRINTFORMW %CALLNAME:MASTER%の購入したものではない。しかし誰かが%CALLNAME:MASTER%のために用意していてくれた可能性も捨てきれない。
	PRINTFORML 
	CALL COLORMESSAGE("ﾄﾞｳｼﾖｳ､ﾓｳｽｸﾞｼｮｳﾐｷｹﾞﾝｶﾞｷﾁｬｳﾜ!ﾀﾞﾚｶﾊﾔｸﾀﾍﾞﾃｸﾚﾅｲｶｼﾗ!",C_YELLOW,2,2)
	PRINTFORML 
	PRINTFORML おやつもこう言っている。ありがたく頂くことにしよう。
	CALL BUFF_BASE(MASTER,BASE_気力,300)
	CALL COLORMESSAGE("おいしいおやつを食べて一時的に気力が増えた",C_YELLOW,2,1)
	PRINTFORMW %CALLNAME:MASTER%は歯を磨き直して機嫌よく眠りに就いた。
	CALL COLORMESSAGE(@"なぜか%CALLNAME:ARG%の機嫌が悪くなった……",C_RED,2,1)
	TALENT:ARG:機嫌 = -1
ELSE
	$NANIMONAI
	PRINTFORMW 出歩いてはみたが特に面白いこともなく、大人しく寝直すことにした
	CFLAG:MASTER:就寝時間 += 60
	CALL COLORMESSAGE("睡眠時間が1時間減った",C_YELLOW,2,1)
ENDIF

@EVENTTURNEND
#DIM 養育費

;宴会当日に一日が終わると宴会フラグOFF
;IF DAY >= FLAG:34 && FLAG:宴会開催フラグ
IF TIME + 1440 * DAY >= FLAG:開始時間 + 1440 * FLAG:開始日 && FLAG:宴会開催フラグ
	;会場となった場所は荒れ果てる、拠点外でも対象地点の汚れ増加させるため宴会場ではない
	SIF YOGORE:(FLAG:32) < 300
		YOGORE:(FLAG:32) += 1000
	FLAG:宴会開催フラグ = 0
	FLAG:31 = 0
	FLAG:32 = 0
	TFLAG:宴会場 = 0
	FLAG:33 = 0
	FLAG:34 = 0
	FLAG:35 = 0
	FLAG:36 = 0
	;宴会会場表示 = 
	FLAG:宴会差し入れ = 0
	FOR LOCAL,0,CHARANUM
		CFLAG:LOCAL:350 = 0
		MAXBASE:LOCAL:仕事量 = 0
		BASE:LOCAL:仕事量 = 0
		CFLAG:LOCAL:宴会参加 = 0
		CFLAG:LOCAL:行動 = 0
	NEXT
ENDIF
;霊廟フラグOFF
FLAG:17 = 0
DRAWLINE
PRINTFORML 
;SETCOLOR C_PINK
;FONTBOLD
FOR LOCAL,0,CHARANUM
	;この3つはあなたも取得可能
	IF EXP:LOCAL:キス経験 > 300 && ABL:LOCAL:Ｍ感覚 > 5 && !TALENT:LOCAL:キス魔
		PRINTFORML 数多のキスを交わして心の拠り所を見出した%CALLNAME:LOCAL%は、キスをしていないと不安でたまらなくなってしまった…
		;陥落素質取得口上,6,キス魔
		CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 6)
		PRINTFORMW %CALLNAME:LOCAL%は[キス魔]を得た
		TALENT:LOCAL:キス魔 = 1
	ENDIF
	IF EXP:LOCAL:調教自慰経験 > 200 && EXP:LOCAL:自慰経験 > 500 && !TALENT:LOCAL:自慰狂い
		PRINTFORML 欲望の吐き出し方を知り尽くした%CALLNAME:LOCAL%は、四六時中自慰の事を考えるようになった…
		;陥落素質取得口上,7,自慰狂い
		CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 7)
		PRINTFORMW %CALLNAME:LOCAL%は[自慰狂い]を得た
		TALENT:LOCAL:自慰狂い = 1
	ENDIF
	IF EXP:LOCAL:子宮口開発経験 > 50 && !TALENT:LOCAL:ポルチオ性感
		PRINTFORML 繰り返し子宮口を攻められた%CALLNAME:LOCAL%は、苦痛よりも快楽を感じるようになってきたようだ…
		;陥落素質取得口上,8,ポルチオ性感
		CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 8)
		PRINTFORMW %CALLNAME:LOCAL%は[ポルチオ性感]を得た
		TALENT:LOCAL:ポルチオ性感 = 1
	ENDIF
;	IF FLAG:おもらし有効 > 0 && EXP:LOCAL:放尿経験 > 1000 * (10 - 2 * TALENT:LOCAL:中毒しやすい) / 10 && !TALENT:LOCAL:おもらし癖
;		PRINTFORML 絶頂時に放尿し続けてきた%CALLNAME:LOCAL%の身体は、今や放尿するのが癖になってしまう程に緩くなってしまった…
;		;陥落素質取得口上,9,おもらし癖
;		CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 9)
;		PRINTFORMW %CALLNAME:LOCAL%は[おもらし癖]を得た
;		TALENT:LOCAL:おもらし癖 = 1
;	ENDIF
	;以下はあなたはスキップ
	SIF LOCAL == MASTER
		CONTINUE
	;恋慕取得
	IF 恋慕取得条件(LOCAL)
		;面霊気は特別あつかい
		IF LOCAL == 57
			PRINTFORMW %CALLNAME:LOCAL%は%CALLNAME:MASTER%への思いを自覚すると同時、口元を緩めた
			PRINTFORMW 自然な笑顔を浮かべる%CALLNAME:LOCAL%は、表情を得た様だ
			;陥落素質取得口上,1,恋慕
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 1)
			PRINTFORMW %CALLNAME:LOCAL%の\@ TALENT:LOCAL:思慕 ? [思慕] # [愛欲] \@は[恋慕]になった
			FLAG:10 ++
		;愛欲の有無で分岐
		ELSEIF TALENT:LOCAL:愛欲
			PRINTFORMW %CALLNAME:LOCAL%は身も心も%CALLNAME:MASTER%に満たされることに悦びを感じている…
			PRINTL 爛れた関係の果てに実った恋心、それもまた一つの恋のカタチ…
			;陥落素質取得口上,1,恋慕
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 1)
			PRINTFORMW %CALLNAME:LOCAL%の[愛欲]は[恋慕]になった
		ELSE
			PRINTFORML %CALLNAME:LOCAL%は%CALLNAME:MASTER%と過ごす時間に幸せを感じているようだ…
			;陥落素質取得口上,1,恋慕
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 1)
			PRINTFORMW %CALLNAME:LOCAL%の[思慕]は[恋慕]になった
			FLAG:10 ++
		ENDIF
		SIF !MARK:LOCAL:時姦刻印 && !TALENT:LOCAL:愛欲 && !CFLAG:LOCAL:睡眠姦 && !CFLAG:LOCAL:泥酔姦
			CFLAG:LOCAL:清い交際 = 1
		TALENT:LOCAL:恋慕 = 1
		TALENT:LOCAL:思慕 = 0
		TALENT:LOCAL:愛欲 = 0
		TALENT:LOCAL:セフレ = 0
		
		;楽譜入手処理
		PRINTL
		RESULT = 楽譜入手_陥落(LOCAL)
		SIF RESULT != -1
			PRINTL
	;思慕取得
	;セフレ／愛人の取得で分岐
	ELSEIF 思慕取得条件(LOCAL)
		IF TALENT:LOCAL:セフレ
			PRINTFORML %CALLNAME:LOCAL%は%CALLNAME:MASTER%の身体以外のことにも興味が有るようだ
			;陥落素質取得口上,2,思慕
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 2)
			PRINTFORMW %CALLNAME:LOCAL%は[思慕]を得た
		ELSE
			PRINTFORML %CALLNAME:LOCAL%は%CALLNAME:MASTER%のことが気になるようだ…
			;陥落素質取得口上,2,思慕
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 2)
			PRINTFORMW %CALLNAME:LOCAL%は[思慕]を得た
		ENDIF
		TALENT:LOCAL:思慕 = 1
	;愛欲取得
	;奉仕快楽の分岐がまだないので、取得条件に併合
	ELSEIF 愛欲取得条件(LOCAL)
		;思慕持ち、心より身体を選んだ的な
		IF TALENT:LOCAL:思慕
			PRINTFORML %CALLNAME:LOCAL%は%CALLNAME:MASTER%と心以上に体のつながりに満足しているようだ…
			PRINTFORML 目に見えない絆よりも目に見える証を欲してこれからも身体を重ねるのだろう
			;陥落素質取得口上,3,愛欲
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 3)
			PRINTFORMW %CALLNAME:LOCAL%は[愛欲]を得た
		ELSE
			PRINTFORML %CALLNAME:LOCAL%は%CALLNAME:MASTER%との情欲におぼれているようだ…
			;陥落素質取得口上,3,愛欲
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 3)
			PRINTFORMW %CALLNAME:LOCAL%は[愛欲]を得た
		ENDIF
			FLAG:10 ++
			TALENT:LOCAL:愛欲 = 1
			TALENT:LOCAL:思慕 = 0
			
			;楽譜入手処理
			PRINTL
			RESULT = 楽譜入手_陥落(LOCAL)
			SIF RESULT != -1
				PRINTL
	;セフレ／愛人取得
	;セフレと愛人の違いは思慕の有無
	ELSEIF セフレ取得条件(LOCAL)
		;思慕取得済＋セフレ未取得
		IF TALENT:LOCAL:思慕 && !TALENT:LOCAL:セフレ
			PRINTFORML %CALLNAME:LOCAL%は%CALLNAME:MASTER%となら快楽に溺れてもいいと思い始めたようだ…
			;陥落素質取得口上,4,愛人
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 4)
			PRINTFORMW %CALLNAME:LOCAL%は%CALLNAME:MASTER%の[愛人]になった
			TALENT:LOCAL:セフレ = 2
		;思慕取得済＋セフレ取得済
		ELSEIF TALENT:LOCAL:思慕 && TALENT:LOCAL:セフレ == 1
			PRINTFORML %CALLNAME:LOCAL%は%CALLNAME:MASTER%に単なる肉体関係以上のものを望んでいるようだ…
			;陥落素質取得口上,4,愛人
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 4)
			PRINTFORMW %CALLNAME:LOCAL%は[愛人]になった
			TALENT:LOCAL:セフレ = 2
		;思慕未取得＋セフレ未取得ならまずセフレ取得
		ELSEIF !TALENT:LOCAL:セフレ
			PRINTFORML %CALLNAME:LOCAL%は%CALLNAME:MASTER%から与えられる快楽に溺れつつある…
			;陥落素質取得口上,5,セフレ
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 5)
			PRINTFORMW %CALLNAME:LOCAL%は%CALLNAME:MASTER%の[セフレ]になってしまった
			TALENT:LOCAL:セフレ = 1
		ENDIF
	ENDIF
	IF EXP:LOCAL:演奏経験 >= 1000 && ABL:LOCAL:技巧 > 3 && !TALENT:LOCAL:音楽知識
		PRINTFORMW %CALLNAME:LOCAL%は楽器演奏に造詣が深くなった
		PRINTFORMW %CALLNAME:LOCAL%は[楽器知識]を得た
		;PRINTFORMW %CALLNAME:LOCAL%は音楽に造詣深くなった
		;PRINTFORMW %CALLNAME:LOCAL%は[音楽知識]を得た
		TALENT:LOCAL:音楽知識 = 1
		;技能レベルアップ口上,7-1,音楽知識
		CALL KOJO_MESSAGE_SEND("EVENT", 34, LOCAL, 7, 1)
	ELSEIF EXP:LOCAL:演奏経験 >=5000 && !TALENT:LOCAL:音感
		PRINTFORMW %CALLNAME:LOCAL%の音感はかなり鍛えられてきたようだ
		PRINTFORMW %CALLNAME:LOCAL%は[相対音感]を得た
		TALENT:LOCAL:音感 = 1
		;技能レベルアップ口上,7-2,相対音感
		CALL KOJO_MESSAGE_SEND("EVENT", 34, LOCAL, 7, 2)
	ENDIF
	IF CFLAG:LOCAL:不機嫌
		CFLAG:LOCAL:不機嫌 --
		IF !CFLAG:LOCAL:不機嫌
			SIF MARK:LOCAL:反発刻印
				CFLAG:LOCAL:不機嫌 ++
		ENDIF
	ENDIF
	;EXキャラの恋慕時はメンバー追加イベントフラグを立てる
	IF !CFLAG:LOCAL:神社在住
		SIF TALENT:LOCAL:恋慕 && EXP:LOCAL:愛情経験 >= 200
			FLAG:1 = 3
	ENDIF
NEXT

IF EXP:MASTER:異常清掃経験 > (500 + 100 * FLAG:850) && ABL:MASTER:清掃技能 > 3 && !TALENT:MASTER:清掃中毒
	PRINTFORMW %CALLNAME:MASTER%は恐ろしいことに気付いてしまった
	PRINTFORMW 寝ても覚めても、時を止めている間も、少女たちと肌を重ねている時ですら、
	PRINTFORMW 部屋が散らかっていないか気になって仕方がないのだ
	PRINTFORMW もはや%CALLNAME:MASTER%の心が安らぐのはこうして掃除をしている間のみ
	PRINTFORMW %CALLNAME:MASTER%の掃除へのこだわりは、取り返しのつかない域に達していた
	PRINTFORMW %CALLNAME:MASTER%は[清掃中毒]を得た
	TALENT:MASTER:清掃中毒 = 1
ENDIF
;MASTERを除く全員のプリセット処理
FOR LOCAL,1,CHARANUM
	;気力の回復
	BASE:LOCAL:気力 = MAXBASE:LOCAL:気力

	LOCAL:1 = MAXBASE:LOCAL:体力 / 3

	;回復早い、回復遅い
	LOCAL:1 = LOCAL:1 * 3 / (3 - TALENT:LOCAL:回復速度)
	
	;添い寝してたら回復速度向上
	SIF CFLAG:LOCAL:添い寝中
		LOCAL:1 *= 2
	SIF CFLAG:LOCAL:徹夜
		LOCAL:1 /= 2
	CFLAG:LOCAL:徹夜 = 0
	BASE:LOCAL:体力 = MIN(MAXBASE:LOCAL:体力, BASE:LOCAL:体力 + LOCAL:1)

	;南無三カード発行者が居たら発行フラグOFF
	CFLAG:LOCAL:328 = 0
NEXT
;全員のプリセット処理
FOR LOCAL,1,CHARANUM
	BASE:LOCAL:精力 = MIN(MAXBASE:LOCAL:精力, BASE:LOCAL:精力 + MAXBASE:LOCAL:精力 * BASE:LOCAL:体力 / MAXBASE:LOCAL:体力)
	
	;隠密中のキャラが居たらフラグOFF
	CALL SET_SNEAK(0,LOCAL,1)
	;CFLAG:LOCAL:隠密中 = 0
	
	;イタズラ中のキャラが居たらフラグOFF
	CALL SET_PRANK(0,LOCAL,1)
	;CFLAG:LOCAL:イタズラ = 0
	
NEXT

;宴会の開催判定
SIF FLAG:3 && FLAG:宴会開催フラグ == 0
	CALL ENKAI_SETTING

IF CFLAG:MASTER:育児人数
	養育費 = CFLAG:MASTER:育児人数 * 1000 / (1 + FLAG:甲斐性無)
	PRINTFORML %CALLNAME:MASTER%は子供の養育費として\\{養育費}支払った。
	MONEY -= 養育費
ENDIF

CALL RANK_POINT
PRINTW  

FLAG:0 = 0

;モブ子のセーブ
IF TALENT:RANDOM_CHARANUM:行きずり == 1 && GETBIT(TFLAG:一日一回, 9)
	PRINTFORML %CALLNAME:MASTER%は今日出会った%CALLNAME:RANDOM_CHARANUM%のことを……
	CALL ASK_YN("忘れる","覚えておく")
	IF RESULT
		VARSET TCVAR:RANDOM_CHARANUM:0, 0
		VARSET EX:RANDOM_CHARANUM:0, 0
		VARSET TEQUIP:RANDOM_CHARANUM:0, 0
		VARSET PALAM:RANDOM_CHARANUM:0, 0
		CALL InitializeBeforeTrain(RANDOM_CHARANUM)
		CFLAG:RANDOM_CHARANUM:900 ++
		CALL SAVE_MOBGIRL
		SIF RESULT == -1
			GOTO FORGET_MOB
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:RANDOM_CHARANUM%\@ TALENT:RANDOM_CHARANUM:風俗嬢 ? でまた遊びたい # にまた会えたらいいな \@と思った
	ELSE
		$FORGET_MOB
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:RANDOM_CHARANUM%のことを綺麗さっぱり忘れた
	ENDIF
ENDIF
IF TALENT:RANDOM_CHARANUM:行きずり == 2 && GETBIT(TFLAG:一日一回, 9)
	CALL MOBGIRL_PROFILE2()
	VARSET TCVAR:RANDOM_CHARANUM:0, 0
	VARSET EX:RANDOM_CHARANUM:0, 0
	VARSET TEQUIP:RANDOM_CHARANUM:0, 0
	VARSET PALAM:RANDOM_CHARANUM:0, 0
	CALL InitializeBeforeTrain(RANDOM_CHARANUM)
	SAVECHARA TOSTR(CFLAG:RANDOM_CHARANUM:モブ子セーブスロット), CSTR:RANDOM_CHARANUM:モブ子プロフィール１, RANDOM_CHARANUM
	[IF DEBUG]
		PRINTFORMW %CALLNAME:RANDOM_CHARANUM%の情報を更新しました。
	[ENDIF]
ENDIF

BEGIN SHOP

@RANK_POINT
VARSET LOCAL
;清潔度
LOCAL:2 = EX:MASTER:今日の掃除回数 / 10
PRINTFORML 今日掃除したゴミの量{EX:MASTER:今日の掃除回数}
PRINTFORML 清掃によるボーナス \\{LOCAL:2}
IF DAY:2 == 4 && DAY:3 == 31
	PRINT (x2)  【大掃除ボーナス】
	LOCAL:2 *= 2
ENDIF
;拠点の主の好感度と信頼度が上昇
LOCAL:3 = MIN(10, EX:MASTER:今日の掃除回数 / 2000)
IF LOCAL:3
	PRINTFORML 掃除を頑張ったことで、%CALLNAME:拠点_大家%からの評価が上がった
	CFLAG:拠点_大家:好感度 += LOCAL:3 * 2
	CFLAG:拠点_大家:信頼度 += LOCAL:3
	IF LOCAL:3 / 3 > 0
		PRINTFORML 掃除を頑張ったので、さらに安物食材を{LOCAL:3 / 3}個貰った
		ITEM:安物食材 += LOCAL:3 / 3
	ENDIF
ENDIF
PRINTL  
;手伝い報酬
LOCAL:4 = TCVAR:MASTER:手伝った量

SIF LOCAL:4 > 0
	PRINTFORML 仕事手伝いによるボーナス \\{LOCAL:4}

MONEY += LOCAL:2 + LOCAL:4

;好感度上昇
IF FLAG:12 && MONEY:2 < CM_LIMIT
	LOCAL:5 = MAX(1,FLAG:12 / 100)
	LOCAL:5 = MIN(LOCAL:5, CM_LIMIT - MONEY:2)
	PRINTFORML 好感度上昇ボーナス ＋{LOCAL:5}カリスマ
	MONEY:2 += LOCAL:5
	PRINTFORML 現在のカリスマ：%三桁区切(MONEY:2)%　カリスマ保有上限：%三桁区切(CM_LIMIT)%
ENDIF
FLAG:12 = 0

PRINTW  

@恋慕取得条件(ARG)
#FUNCTION
SIF TALENT:ARG:恋慕
	RETURNF 0
SIF CFLAG:ARG:好感度 < REQUIRED_FAVOR_恋慕
	RETURNF 0
SIF CFLAG:ARG:信頼度 < REQUIRED_TRUST_恋慕
	RETURNF 0
SIF ABL:ARG:親密 < REQUIRED_AFFECTION_恋慕
	RETURNF 0
SIF MARK:ARG:反発刻印
	RETURNF 0
SIF TALENT:ARG:愛欲 && !FLAG:陥落路線変化
	RETURNF 0
RETURNF 1

@思慕取得条件(ARG)
#FUNCTION
SIF TALENT:ARG:愛欲
	RETURNF 0
SIF TALENT:ARG:恋慕
	RETURNF 0
SIF TALENT:ARG:思慕
	RETURNF 0
SIF CFLAG:ARG:好感度 < REQUIRED_FAVOR_思慕
	RETURNF 0
SIF CFLAG:ARG:信頼度 < REQUIRED_TRUST_思慕
	RETURNF 0
SIF ABL:ARG:親密 < REQUIRED_AFFECTION_思慕
	RETURNF 0
SIF TALENT:ARG:セフレ && !FLAG:陥落路線変化
	RETURNF 0
RETURNF 1

@愛欲取得条件(ARG)
#FUNCTION
SIF TALENT:ARG:恋慕
	RETURNF 0
SIF TALENT:ARG:愛欲
	RETURNF 0
SIF !TALENT:ARG:セフレ
	RETURNF 0
SIF CFLAG:ARG:好感度 < REQUIRED_FAVOR_愛欲
	RETURNF 0
SIF ABL:ARG:欲望 < REQUIRED_DESIRE_愛欲
	RETURNF 0
SIF (EXP:ARG:絶頂経験 - EXP:ARG:無自覚絶頂経験) < REQUIRED_ECSTACY_愛欲
	RETURNF 0
SIF (ABL:ARG:欲望 - ABL:ARG:親密) < 1
	RETURNF 0
RETURNF 1

@セフレ取得条件(ARG)
#FUNCTION
SIF TALENT:ARG:恋人
	RETURNF 0
SIF TALENT:ARG:恋慕
	RETURNF 0
SIF TALENT:ARG:セフレ == 2
	RETURNF 0
SIF CFLAG:ARG:好感度 <= REQUIRED_FAVOR_セフレ
	RETURNF 0
SIF ABL:ARG:欲望 < REQUIRED_DESIRE_セフレ
	RETURNF 0
SIF (EXP:ARG:絶頂経験 - EXP:ARG:無自覚絶頂経験) < REQUIRED_ECSTACY_セフレ
	RETURNF 0
SIF (ABL:ARG:欲望 - ABL:ARG:親密) < 0
	RETURNF 0
RETURNF 1

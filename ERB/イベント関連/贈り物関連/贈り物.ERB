;-------------------------------------------------
;贈り物を生成する関数
;RESULT:0  GIFT_ID
;RESULT:1  値段
;RESULTS   名前
;-------------------------------------------------
@CREATE_GIFT(AREA)
#DIM AREA
#DIM MAIN
#DIM ADJECT
#DIM BASIC_PRICE
#DIM MAGNIF_PRICE
#DIMS GIFTNAME_MAIN
#DIMS GIFTNAME_ADJECT
VARSET RESULT

CALL DRAW_GIFT_MAIN(AREA)
MAIN = RESULT:0
BASIC_PRICE = RESULT:1
GIFTNAME_MAIN = %RESULTS:0%

CALL DRAW_GIFT_ADJECT(AREA, RESULTS:1)
ADJECT = RESULT:0
MAGNIF_PRICE = RESULT:1
GIFTNAME_ADJECT = %RESULTS:0%

;値段の設定
RESULT:1 = BASIC_PRICE * MAGNIF_PRICE / 100
;半額セールとか値引き要素等
SIF FLAG:土産屋大安売り >= 100 && FLAG:土産屋大安売り % 100 == CFLAG:MASTER:デート中
	TIMES RESULT:1, 0.50

;名前の設定
RESULTS = %GIFTNAME_ADJECT%%GIFTNAME_MAIN%
;IDの設定
RESULT:0 = ADJECT * 1000 + MAIN
RETURN RESULT:0

;-------------------------------------------------
;キャラクターに依存した贈り物を生成する関数
;-------------------------------------------------
@CREATE_GIFT_FROM_CHARA(CHARA, SCORE_MIN, SCORE_MAX)
#DIM CHARA
#DIM SCORE_MIN
#DIM SCORE_MAX
#DIM GIFT_ID
#DIM MAIN
#DIM ADJECT
#DIM BASIC_PRICE
#DIM MAGNIF_PRICE
#DIMS GIFTNAME
#DIMS GIFTNAME_MAIN
#DIMS GIFTNAME_ADJECT
#DIM SCORE
#DIMS GIFT_SENSE
VARSET RESULT
SIF SCORE_MAX <= SCORE_MIN
	SCORE_MAX = 1000

CALL DRAW_GIFT_MAIN(99)
MAIN = RESULT:0
BASIC_PRICE = RESULT:1
GIFTNAME_MAIN = %RESULTS:0%

CALL DRAW_GIFT_ADJECT(99, RESULTS:1)
ADJECT = RESULT:0
MAGNIF_PRICE = RESULT:1
GIFTNAME_ADJECT = %RESULTS:0%

;値段の設定
RESULT:1 = BASIC_PRICE * MAGNIF_PRICE / 100
;名前の設定
GIFTNAME = %GIFTNAME_ADJECT%%GIFTNAME_MAIN%
;IDの設定
GIFT_ID = ADJECT * 1000 + MAIN

;評価
CALL REVIEW_GIFT(CHARA, GIFT_ID)
SCORE = RESULT
GIFT_SENSE = %RESULTS%

IF INRANGE(RESULT, SCORE_MIN, SCORE_MAX)
	RESULT:1 = SCORE
	RESULTS = %GIFTNAME%
	RESULTS:1 = %GIFT_SENSE%
	RETURN GIFT_ID
ELSE
	RESTART
ENDIF

;-------------------------------------------------
;贈り物本体を抽選する関数
;-------------------------------------------------
@DRAW_GIFT_MAIN(AREA)
#DIM AREA
#DIM CATEGORY_MAIN
#DIM GIFT_MAIN
#DIM カテゴリ上限
VARSET RESULT
カテゴリ上限 = 52
WHILE !RESULT
	CATEGORY_MAIN = RAND:カテゴリ上限 + 1
	GIFT_MAIN = CATEGORY_MAIN * 10 + RAND:10
	CALL GIFTDATA_MAIN(GIFT_MAIN, AREA)
WEND
RETURN GIFT_MAIN

;-------------------------------------------------
;贈り物の形容詞を抽選する関数
;-------------------------------------------------
@DRAW_GIFT_ADJECT(AREA, SENSE)
#DIM AREA
#DIM CATEGORY_ADJECT
#DIM GIFT_ADJECT
#DIMS SENSE
#DIM カテゴリ上限
VARSET RESULT
カテゴリ上限 = 21
WHILE !RESULT
	CATEGORY_ADJECT = RAND:カテゴリ上限 + 1
	GIFT_ADJECT = CATEGORY_ADJECT * 100 + RAND:100
	CALL GIFTDATA_ADJECT(GIFT_ADJECT, AREA, SENSE)
WEND
RETURN GIFT_ADJECT

;-------------------------------------------------
;贈り物を評価する関数
;-------------------------------------------------
@REVIEW_GIFT(CHARA, GIFT_ID, ORIGINAL_SENSE, ORIGINAL_PRICE)
#DIM CHARA
#DIM GIFT_ID
#DIMS SENSE_CHARA_GOOD
#DIMS SENSE_CHARA_BAD
#DIMS SENSE_GIFT
#DIM SENSE_GOOD
#DIM SENSE_BAD
#DIM MAGNIF_PRICE
#DIM BASIC_PRICE
#DIMS ORIGINAL_SENSE
#DIM ORIGINAL_PRICE

IF ORIGINAL_SENSE != ""
	SENSE_GIFT = %ORIGINAL_SENSE%
	IF ORIGINAL_PRICE >= 10000
		SENSE_GIFT += "高級/"
	ELSEIF ORIGINAL_PRICE < 1500
		SENSE_GIFT += "安物/"
	ENDIF
ELSE
	;SENSE抽出
	SENSE_GIFT = 
	CALL GIFTDATA_ADJECT(GET_GIFTDATA(GIFT_ID, "形容詞"))
	SENSE_GIFT += RESULTS:1
	MAGNIF_PRICE = RESULT:1

	CALL GIFTDATA_MAIN(GET_GIFTDATA(GIFT_ID, "本体"))
	SENSE_GIFT += RESULTS:1
	BASIC_PRICE = RESULT:1

	;定価を見て10,000以上なら高級、1,500未満なら安物
	IF BASIC_PRICE * MAGNIF_PRICE / 100 >= 10000
		SENSE_GIFT += "高級/"
	ELSEIF BASIC_PRICE * MAGNIF_PRICE / 100 < 1500
		SENSE_GIFT += "安物/"
	ENDIF
ENDIF


;キャラクターの感性
SENSE_CHARA_GOOD = %GET_STR(, "キャラデータ", CHARA, "感性：好き")%
SENSE_CHARA_BAD = %GET_STR(, "キャラデータ", CHARA, "感性：嫌い")%

;共通設定：好き
;［幼稚］はキャラクターが好き
SIF TALENT:CHARA:幼稚
	SENSE_CHARA_GOOD += "キャラクター/" * 3
;［妖精］は自然が好き
SIF CHECK_CHARA(CHARA, "妖精")
	SENSE_CHARA_GOOD += "自然/" * 3
;［付喪神］は付喪神と道具が好き
SIF CHECK_CHARA(CHARA, "付喪神")
	SENSE_CHARA_GOOD += "付喪神/道具/" * 3
;妖怪は妖怪が好き
SIF CHECK_CHARA(CHARA, "妖怪")
	SENSE_CHARA_GOOD += "妖怪/" * 3
;CSVの戦闘能力があるならスペルカードは好き
SIF CSVABL(CHARA, GETNUM(ABL, "戦闘能力")) > 0
	SENSE_CHARA_GOOD += "スペルカード/" * 3
;キャノンボールはみんなが欲しがる
SENSE_CHARA_GOOD += "キャノンボール/"

;共通設定：嫌い
;粗悪はみんな嫌い
SIF 1
	SENSE_CHARA_BAD += "粗悪/" * 6
;妖怪・幽霊は巫術が好きでないなら巫術は嫌い
IF (CHECK_CHARA(CHARA, "妖怪") || CHECK_CHARA(CHARA, "幽霊")) && !STRCOUNT(SENSE_CHARA_GOOD, "巫術/")
	SENSE_CHARA_BAD += "巫術/" * 3
	;法具が好きでないなら法具は嫌い
	SIF !STRCOUNT(SENSE_CHARA_GOOD, "法具/")
		SENSE_CHARA_BAD += "法具/" * 3
ENDIF
;妖怪ではなく妖術が好きでないなら妖術は嫌い,もしくは巫術が好きなら妖術は嫌い
IF (!CHECK_CHARA(CHARA, "妖怪") && !STRCOUNT(SENSE_CHARA_GOOD, "妖術/")) || STRCOUNT(SENSE_CHARA_GOOD, "巫術/")
	SENSE_CHARA_BAD += "妖術/" * 3
	;さらに骨、血、毒も同様
	SIF !STRCOUNT(SENSE_CHARA_GOOD, "骨/")
		SENSE_CHARA_BAD += "骨/" * 3
	SIF !STRCOUNT(SENSE_CHARA_GOOD, "血/")
		SENSE_CHARA_BAD += "血/" * 3
	SIF !STRCOUNT(SENSE_CHARA_GOOD, "毒/")
		SENSE_CHARA_BAD += "毒/" * 3
ENDIF
;酒耐性が下戸なら酒は嫌い
IF TALENT:CHARA:酒耐性 < -1
	SENSE_CHARA_BAD += "酒/" * 3
ELSEIF TALENT:CHARA:酒耐性 < 0
	SENSE_CHARA_BAD += "酒/"
ENDIF
;CSVの戦闘能力がないならスペルカードはちょっと…
SIF CSVABL(CHARA, GETNUM(ABL, "戦闘能力")) <= 0
	SENSE_CHARA_BAD += "スペルカード/"

;評価得点
SENSE_GOOD = 300 + 100 * STR_MULTI_COUNT(SENSE_CHARA_GOOD, SENSE_GIFT)

;;ムードによる補正,※封印しました
;IF BASE:CHARA:ムード >= MAXBASE:CHARA:ムード
;	TIMES SENSE_GOOD, 1.00
;ELSEIF BASE:CHARA:ムード >= MAXBASE:CHARA:ムード / 2
;	TIMES SENSE_GOOD, 0.80
;ELSEIF BASE:CHARA:ムード >= MAXBASE:CHARA:ムード / 4
;	TIMES SENSE_GOOD, 0.60
;ELSE
;	TIMES SENSE_GOOD, 0.40
;ENDIF

;;自宅地域による補正,※封印しました
;SIF CFLAG:CHARA:デート中  == CFLAG:CHARA:自宅位置 / 100
;	TIMES SENSE_GOOD, 0.80

;祭日による補正
SIF FESTIVAL() != ""
	TIMES SENSE_GOOD, 1.25

;評価減点
SENSE_BAD = 300 + 100 * STR_MULTI_COUNT(SENSE_CHARA_BAD, SENSE_GIFT)

;総評
RESULT = MIN(SENSE_GOOD * 1000 / (SENSE_GOOD + SENSE_BAD), 999)
[IF_DEBUG]
;	PRINTFORML 好き：%SENSE_CHARA_GOOD%
;	PRINTFORML 嫌い：%SENSE_CHARA_BAD%
;	PRINTFORML 品物：%SENSE_GIFT%
;	PRINTFORML 得点：{SENSE_GOOD}
;	PRINTFORML 減点：{SENSE_BAD}
;	PRINTFORML 結果：{RESULT}
;	WAIT
[ENDIF]
RESULTS = %SENSE_GIFT%
RETURN RESULT


;-------------------------------------------------
;一日の終わりに贈り物をお気に入りにするかの関数
;-------------------------------------------------
@GIFT_FAVORITE
#DIM CHARA
#DIM GIFT_ID
#DIM GIFT_ADJECT
#DIM GIFT_MAIN
#DIM GIFT_SCORE, 3
#DIMS GIFT_NAME
#DIMS SENSE
FOR CHARA, 1, CHARANUM
	SIF !TCVAR:CHARA:今日の贈り物
		CONTINUE
	
	GIFT_ID = TCVAR:CHARA:今日の贈り物
	GIFT_ADJECT = GET_GIFTDATA(GIFT_ID, "形容詞")
	GIFT_MAIN = GET_GIFTDATA(GIFT_ID, "本体")
	GIFT_SCORE:1 = GET_GIFTDATA(GIFT_ID, "得点")
	
	;贈り物データの参照
	CALL GIFTDATA_ADJECT(GIFT_ADJECT)
	LOCALS:1 = %RESULTS:0%
	SENSE = %RESULTS:1%
	CALL GIFTDATA_MAIN(GIFT_MAIN)
	SENSE += RESULTS:1
	LOCALS:2 = %RESULTS:0%
	CALL GIFTNAME(TCVAR:CHARA:今日の贈り物, CHARA)
	GIFT_NAME = %RESULTS%
;	PRINTFORMW SCORE{GIFT_SCORE:1}
	
	
	PRINTL 
	PRINTFORM %CALLNAME:CHARA%は%CALLNAME:MASTER%に貰った%GIFT_NAME%
	IF GIFT_SCORE:1 >= 700
		IF STRCOUNT(SENSE, "握る/")
			PRINTFORMW を大事そうに握りしめて眠りについた……。
		ELSEIF STRCOUNT(SENSE, "抱く/")
			PRINTFORMW に思いを馳せながら抱きしめた……。
		ELSEIF STRCOUNT(SENSE, "身につける/")
			PRINTFORMW を試しに身につけ、鏡の前で顔を赤らめた……。
		ELSE
			PRINTFORMW をうっとりと眺めてから眠りについた……。
		ENDIF
		CALL KOJO_MESSAGE_SEND("GIFT", 2, CHARA, GIFT_ID, GIFT_SCORE:1, GIFT_NAME, SENSE)
		;現在の贈り物と比較してお気に入りにする
		IF CFLAG:CHARA:貰った贈り物
			GIFT_SCORE:2 = GET_GIFTDATA(CFLAG:CHARA:貰った贈り物, "得点")
		ELSE
			GIFT_SCORE:2 = 0
		ENDIF
		IF GIFT_SCORE:1 >= GIFT_SCORE:2
			CFLAG:CHARA:貰った贈り物 = TCVAR:CHARA:今日の贈り物
			CALL COLORMESSAGE(@"%GIFT_NAME%は%CALLNAME:CHARA%のお気に入りになった！", C_YELLOW, 2)
			CALL KOJO_MESSAGE_SEND("GIFT", 3, CHARA, GIFT_ID, GIFT_SCORE:1, GIFT_NAME, SENSE)
		ENDIF
	ELSEIF GIFT_SCORE:1 >= 400
		IF STRCOUNT(SENSE, "握る/")
			PRINTFORMW を棚の上に置いた……。
		ELSEIF STRCOUNT(SENSE, "抱く/")
			PRINTFORMW を机の上に置いた……。
		ELSEIF STRCOUNT(SENSE, "身につける/")
			PRINTFORMW をタンスにしまった……。
		ELSE
			PRINTFORMW をとりあえず保管した……。
		ENDIF
		CALL KOJO_MESSAGE_SEND("GIFT", 4, CHARA, GIFT_ID, GIFT_SCORE:1, GIFT_NAME, SENSE)
	ELSE
		PRINTFORMW をこっそり質に入れた……。
		CALL KOJO_MESSAGE_SEND("GIFT", 5, CHARA, GIFT_ID, GIFT_SCORE:1, GIFT_NAME, SENSE)
	ENDIF
	PRINTL 
NEXT

;-------------------------------------------------
;Look内で貰った贈り物を表示する関数
;-------------------------------------------------
@LOOK_GIFT(CHARA)
#DIM CHARA
#DIM GIFT_NAME
#DIM GIFT_SCORE
;うふふ中は表示しない
SIF CFLAG:CHARA:うふふ
	RETURN

;お気に入り
IF CFLAG:CHARA:貰った贈り物
	PRINTL 
	PRINT 【お気に】
	CALL COLORMESSAGE("★", C_YELLOW, 0)
	CALL GIFTNAME(CFLAG:CHARA:貰った贈り物, CHARA)
	PRINTFORM %RESULTS%
	[IF_DEBUG]
		CALL PRINT_GIFTDATA(CFLAG:CHARA:貰った贈り物, CHARA, "デバッグ")
	[ENDIF]
;お気に入りでない場合
ELSEIF CFLAG:CHARA:贈り物ストック
	GIFT_SCORE = GET_GIFTDATA(CFLAG:CHARA:贈り物ストック, "得点")
	;400未満は携帯しない
	IF GIFT_SCORE < 400
		RETURN
	ELSE
		;評価得点が高いほど携帯しやすい
		SIF CFLAG:CHARA:今日の運勢 > GIFT_SCORE
			RETURN
	ENDIF
	PRINTL 
	PRINT 【 携帯 】
	CALL GIFTNAME(CFLAG:CHARA:贈り物ストック, CHARA)
	PRINTFORM %RESULTS%
ENDIF

;-------------------------------------------------
;贈り物データを表示する関数
;-------------------------------------------------
@PRINT_GIFTDATA(GIFT_ID, CHARA, ARGS)
#DIM GIFT_ID
#DIM CHARA
#DIM 回数
#DIM GIFT_SCORE
#DIM 貰った日
#DIM 買った場所
#DIM GIFT_ADJECT
#DIM GIFT_MAIN
#DIMS GIFT_NAME
#DIM MAGNIF_PRICE
#DIM SELL_AREA_ADJECT
#DIM BASIC_PRICE
#DIM SELL_AREA_MAIN
#DIMS SENSE

回数 = GET_GIFTDATA(GIFT_ID, "回数")
GIFT_SCORE = GET_GIFTDATA(GIFT_ID, "得点")
貰った日 = GET_GIFTDATA(GIFT_ID, "日付")
買った場所 = GET_GIFTDATA(GIFT_ID, "場所")
GIFT_ADJECT = GET_GIFTDATA(GIFT_ID, "形容詞")
GIFT_MAIN = GET_GIFTDATA(GIFT_ID, "本体")
CALL GIFTDATA_ADJECT(GIFT_ADJECT)
MAGNIF_PRICE = RESULT:1
SELL_AREA_ADJECT = RESULT:2
IF GIFT_ADJECT == 9999
	IF ARGS == "あなたの"
		LOCALS:1 = %GIFT_GAVE_ORI_ADJECT:CHARA:(回数 %100)%
	ELSE
		LOCALS:1 = %GIFT_GOT_ORI_ADJECT:CHARA:(回数 %100)%
	ENDIF
ELSE
	LOCALS:1 = %RESULTS:0%
ENDIF
SENSE = %RESULTS:1%
CALL GIFTDATA_MAIN(GIFT_MAIN)
BASIC_PRICE = RESULT:1
SELL_AREA_MAIN = RESULT:2
IF GIFT_MAIN == 999
	IF ARGS == "あなたの"
		LOCALS:2 = %GIFT_GAVE_ORI_MAIN:CHARA:(回数 %100)%
	ELSE
		LOCALS:2 = %GIFT_GOT_ORI_MAIN:CHARA:(回数 %100)%
	ENDIF
ELSE
	LOCALS:2 = %RESULTS:0%
ENDIF
IF ARGS == "あなたの"
	CALL GIFTNAME(GIFT_ID, CHARA, 1)
	GIFT_NAME = %RESULTS%
ELSE
	CALL GIFTNAME(GIFT_ID, CHARA)
	GIFT_NAME = %RESULTS%
ENDIF
SENSE += RESULTS:1

IF ARGS == "あなたの"
	PRINTFORM [{回数, 2}] %GIFT_NAME, 36, LEFT%
	CALL REVIEW_STARS(GIFT_SCORE)
	PRINT 　
	CALL PRINT_DATE(貰った日)
	IF 買った場所 == 99
		PRINTFORM %CALLNAME:CHARA%から直接貰った
	ELSE
		PRINTFORM %GET_MAPNAME(買った場所)%からの帰り道で\@ 回数 == 1 ? 初めて # \@貰った
	ENDIF
	PRINTL 
ELSEIF ARGS == "キャラの"
	IF CFLAG:CHARA:貰った贈り物 == GIFT_ID
		CALL COLORMESSAGE("★", C_YELLOW, 0)
		PRINTFORM %GIFT_NAME, 34, LEFT%
	ELSE
		PRINTFORM %GIFT_NAME, 36, LEFT%
	ENDIF
	CALL REVIEW_STARS(GIFT_SCORE)
	PRINT 　
	CALL PRINT_DATE(貰った日)
;	PRINTFORM %GIFTSHOP_NAME(買った場所)%で\@ 回数 == 1 ? 初めて # {回数}回目に \@買ってあげた
	PRINTFORM \@ 買った場所 == 99 ? 特注して # %GIFTSHOP_NAME(買った場所)%で \@\@ 回数 == 1 ? 初めて # {回数}回目に \@買ってあげた
	PRINTL 
ELSEIF ARGS == "デバッグ"
	PRINTL 
	CALL PRINT_DATE(貰った日)
;	PRINTFORM %GIFTSHOP_NAME(買った場所)%で\@ 回数 == 1 ? 初めて # {回数}回目に \@買ってあげた
	PRINTFORM \@ 買った場所 == 99 ? 特注して # %GIFTSHOP_NAME(買った場所)%で \@\@ 回数 == 1 ? 初めて # {回数}回目に \@買ってあげた
	PRINTFORML 定価は{BASIC_PRICE} * {MAGNIF_PRICE}\% = \\{BASIC_PRICE * MAGNIF_PRICE / 100}
	PRINTFORML 評価得点は　{GIFT_SCORE }点
	PRINTFORM この形容詞が売ってるのは
	SIF GETBIT(SELL_AREA_ADJECT, 0)
		PRINT 　神社
	SIF GETBIT(SELL_AREA_ADJECT, 1)
		PRINT 　寺　
	SIF GETBIT(SELL_AREA_ADJECT, 2)
		PRINT 　人里
	SIF GETBIT(SELL_AREA_ADJECT, 3)
		PRINT 　湖　
	SIF GETBIT(SELL_AREA_ADJECT, 4)
		PRINT 　竹林
	SIF GETBIT(SELL_AREA_ADJECT, 5)
		PRINT 　森　
	SIF GETBIT(SELL_AREA_ADJECT, 6)
		PRINT 　冥界
	SIF GETBIT(SELL_AREA_ADJECT, 7)
		PRINT 　山麓
	SIF GETBIT(SELL_AREA_ADJECT, 8)
		PRINT 　山頂
	SIF GETBIT(SELL_AREA_ADJECT, 9)
		PRINT 　地底
	SIF GETBIT(SELL_AREA_ADJECT, 10)
		PRINT 　月　
	PRINTL 
	PRINTFORM この本体が売ってるのは　
	SIF GETBIT(SELL_AREA_MAIN, 0)
		PRINT 　神社
	SIF GETBIT(SELL_AREA_MAIN, 1)
		PRINT 　寺　
	SIF GETBIT(SELL_AREA_MAIN, 2)
		PRINT 　人里
	SIF GETBIT(SELL_AREA_MAIN, 3)
		PRINT 　湖　
	SIF GETBIT(SELL_AREA_MAIN, 4)
		PRINT 　竹林
	SIF GETBIT(SELL_AREA_MAIN, 5)
		PRINT 　森　
	SIF GETBIT(SELL_AREA_MAIN, 6)
		PRINT 　冥界
	SIF GETBIT(SELL_AREA_MAIN, 7)
		PRINT 　山麓
	SIF GETBIT(SELL_AREA_MAIN, 8)
		PRINT 　山頂
	SIF GETBIT(SELL_AREA_MAIN, 9)
		PRINT 　地底
	SIF GETBIT(SELL_AREA_MAIN, 10)
		PRINT 　月　
	PRINTL 
	IF STRCOUNT(SENSE, "握る/")
		PRINTFORML 握れるものだ
	ELSEIF STRCOUNT(SENSE, "抱く/")
		PRINTFORML 抱けるものだ
	ELSEIF STRCOUNT(SENSE, "身につける/")
		PRINTFORML 身につけるものだ
	ELSE
		PRINTFORML 眺めるものだ
	ENDIF
	PRINTFORML SENSE：%SENSE%
	RETURN
ENDIF
;-------------------------------------------------
;贈り物データを抽出する関数
;-------------------------------------------------
@GET_GIFTDATA(GIFT_ID, ARGS)
#FUNCTION
#DIM GIFT_ID
SELECTCASE ARGS
CASE "回数"
	RETURNF GIFT_ID / 10000000000000000
CASE "得点"
	RETURNF GIFT_ID / 10000000000000 % 1000
CASE "日付"
	RETURNF GIFT_ID / 1000000000 % 10000
CASE "場所"
	RETURNF GIFT_ID / 10000000 % 100
CASE "形容詞"
	RETURNF GIFT_ID / 1000 % 10000
CASE "本体"
	RETURNF GIFT_ID % 1000
ENDSELECT

;-------------------------------------------------
;贈り物の名前を出力する関数
;FUNCTIONSにしたかったけどCALLできなくて泣く泣くこのカタチに…
;-------------------------------------------------
@GIFTNAME(GIFT_ID, CHARA, FROM)
#DIM CHARA
#DIM GIFT_ID
#DIM GIFT_ADJECT
#DIM GIFT_MAIN
#DIM FROM
#DIM 回数
GIFT_ADJECT = GET_GIFTDATA(GIFT_ID, "形容詞")
GIFT_MAIN = GET_GIFTDATA(GIFT_ID, "本体")
回数 = GET_GIFTDATA(GIFT_ID, "回数") % 100
IF GIFT_ADJECT == 9999
	IF FROM
		LOCALS:1 = %GIFT_GAVE_ORI_ADJECT:CHARA:回数%
	ELSE
		LOCALS:1 = %GIFT_GOT_ORI_ADJECT:CHARA:回数%
	ENDIF
ELSE
	CALL GIFTDATA_ADJECT(GIFT_ADJECT)
	LOCALS:1 = %RESULTS:0%
ENDIF
IF GIFT_MAIN == 999
	IF FROM
		LOCALS:2 = %GIFT_GAVE_ORI_MAIN:CHARA:回数%
	ELSE
		LOCALS:2 = %GIFT_GOT_ORI_MAIN:CHARA:回数%
	ENDIF
ELSE
	CALL GIFTDATA_MAIN(GIFT_MAIN)
	LOCALS:2 = %RESULTS:0%
ENDIF

RESULTS = %LOCALS:1%%LOCALS:2%
RETURN

;-------------------------------------------------
;引数に地域を羅列するとそこのbitを入れて返す関数Ver.2.0
;"全て"を入れると全て入れた数値(2^11-1)を返す
;"以外"を入れると逆になる
;COMMONに入れてもいいかもしれない（どこで使うか知らん）
;-------------------------------------------------
@AREA_SETBIT(ARGS)
#FUNCTION
VARSET RESULT
SIF STRCOUNT(ARGS, "全て")
	RETURNF 2047

SIF STRCOUNT(ARGS, "神社")
	SETBIT RESULT, 0
SIF STRCOUNT(ARGS, "寺")
	SETBIT RESULT, 1
SIF STRCOUNT(ARGS, "里")
	SETBIT RESULT, 2
SIF STRCOUNT(ARGS, "湖")
	SETBIT RESULT, 3
SIF STRCOUNT(ARGS, "竹")
	SETBIT RESULT, 4
SIF STRCOUNT(ARGS, "森")
	SETBIT RESULT, 5
SIF STRCOUNT(ARGS, "冥界")
	SETBIT RESULT, 6
SIF STRCOUNT(ARGS, "山麓")
	SETBIT RESULT, 7
SIF STRCOUNT(ARGS, "山頂")
	SETBIT RESULT, 8
SIF STRCOUNT(ARGS, "地底")
	SETBIT RESULT, 9
SIF STRCOUNT(ARGS, "月")
	SETBIT RESULT, 10

IF STRCOUNT(ARGS, "以外")
	FOR LOCAL, 0, 11
		INVERTBIT RESULT, LOCAL
	NEXT
ENDIF
RETURNF RESULT

;-------------------------------------------------
;デート帰りの贈り物イベントが発生するかのチェック
;-------------------------------------------------
@GIFT_DATE_CHECK(CHARA)
#FUNCTION
#DIM CHARA
#DIM 上限
#DIM 発生率
;祭日でないなら上限80%
IF FESTIVAL() == ""
	上限 = 80
ELSE
	上限 = 100
ENDIF

;モブ子
SIF TALENT:CHARA:行きずり
	RETURNF 0
;すでに渡している
SIF DAY == GET_GIFTDATA(CFLAG:CHARA:渡した贈り物, "日付")
	RETURNF 0
;前回の贈り物から一週間以上経ってる（祭日を除く）
SIF DAY - GET_GIFTDATA(CFLAG:CHARA:渡した贈り物, "日付") < 7 && FESTIVAL() == ""
	RETURNF 0
;反発刻印,不機嫌,怒り
SIF MARK:CHARA:反発刻印 || TALENT:CHARA:機嫌 < 0 || CFLAG:CHARA:ブチギレ
	RETURNF 0
;ムード1200未満
SIF BASE:CHARA:ムード < 1200
	RETURNF 0

;親密5以降から親密1につき5%上昇
発生率 = (ABL:CHARA:親密 - 4) * 5
;祭日なら1.5倍
SIF FESTIVAL() != ""
	TIMES 発生率, 1.5
;恋人なら1.5倍
SIF TALENT:CHARA:恋人
	TIMES 発生率, 1.5
;発生率%で発生
SIF MIN(発生率, 上限) > RAND:100
	RETURNF 1
RETURNF 0

;-------------------------------------------------
;デート帰りの贈り物イベント
;-------------------------------------------------
@GIFT_DATE_EVENT(CHARA)
#DIM CHARA
#DIM GIFT_ID
#DIMS GIFT_NAME
#DIM 渡した回数
#DIM 点数
#DIM 本人の評価点
#DIMS GIFT_SENSE

PRINTL 
PRINTFORM デートからの帰り道、
SELECTCASE CFLAG:CHARA:性格傾向
;気弱系
CASE 1
	PRINTFORML %CALLNAME:CHARA%はもじもじした様子で懐から何かを取り出し、恐る恐る%CALLNAME:MASTER%に差し出してきた…
;強気系
CASE 2
	PRINTFORML %CALLNAME:CHARA%は%CALLNAME:MASTER%の顔を見て、思い出したかのように何かを取り出すと得意気にそれを渡してきた…
;素直系
CASE 3
	PRINTFORML %CALLNAME:CHARA%はおもむろに何かを取り出すと、%CALLNAME:MASTER%にあげると言ってそれを手渡してきた…
;真面目系
CASE 4
	PRINTFORML %CALLNAME:CHARA%は%CALLNAME:MASTER%に渡したい物があると言って箱を取り出し、丁重に差し出してきた…
;その他
CASEELSE
	PRINTFORML %CALLNAME:CHARA%は渡したい物があると言って何かを取り出し、%CALLNAME:MASTER%に差し出してきた…
ENDSELECT
PRINTW 

;点数は必要ないけど一応
点数 = 0

;贈り物生成
CALL CREATE_GIFT_FROM_CHARA(CHARA, 701, 1000)
GIFT_ID = RESULT
GIFT_NAME = %RESULTS%
GIFT_SENSE = %RESULTS:1%
渡した回数 = GET_GIFTDATA(CFLAG:CHARA:渡した贈り物, "回数") + 1
本人の評価点 = RESULT:1
;IDの設定
GIFT_ID = 渡した回数 * 10000000000000000 + 点数 * 10000000000000 + DAY * 1000000000 + CFLAG:MASTER:デート中 * 10000000 + GIFT_ID
GIFT_GAVE:CHARA:(渡した回数 % 100) = GIFT_ID
CFLAG:CHARA:渡した贈り物 = GIFT_ID
CALL KOJO_MESSAGE_SEND("GIFT", 1, CHARA, GIFT_ID, 本人の評価点, GIFT_NAME, GIFT_SENSE)
PRINTFORMW 【%GIFT_NAME%】を貰った！

;-------------------------------------------------
;土産屋の名前を返す関数
;-------------------------------------------------
@GIFTSHOP_NAME(買った場所, コマンド)
#FUNCTIONS
#DIM 買った場所
#DIM コマンド
SELECTCASE 買った場所
CASE 5
	RETURNF "香霖堂"
CASEELSE
	IF コマンド
		RETURNF "土産屋"
	ELSE
		RETURNF GET_MAPNAME(買った場所)
	ENDIF
ENDSELECT

;-------------------------------------------------
;点数に応じた★を表示する関数
;評価方法が特殊なので表示個数も変則的
;-------------------------------------------------
@REVIEW_STARS(SCORE)
#DIM SCORE
#DIM STAR
#DIM STARCOLOR
SELECTCASE SCORE
CASE IS >= 800
	STAR = 5
CASE IS >= 700
	STAR = 4
CASE IS >= 500
	STAR = 3
CASE IS >= 400
	STAR = 2
CASEELSE
	STAR = 1
ENDSELECT
FOR LOCAL, 1, 6
	IF LOCAL <= STAR
		IF STAR >= 4
			SETCOLOR C_ORANGE
		ELSEIF STAR <= 1
			SETCOLOR C_AQUA
		ELSE
			SETCOLOR C_YELLOW
		ENDIF
		PRINT ★
	ELSE
		SETCOLOR C_GRAY
		PRINT ☆
	ENDIF
NEXT
RESETCOLOR

;-------------------------------------------------
;SHOPから呼ばれる表示関数
;-------------------------------------------------
@SHOW_COLLECTION_GIFT
PRINTFORML ☆貰った贈り物☆
FOR LOCAL, 1, CHARANUM
	IF !CFLAG:LOCAL:出禁 && !TALENT:LOCAL:行きずり
		SIF !GIFT_GAVE:LOCAL:1
			SETCOLOR C_GRAY
		PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL, 15, LEFT%
		RESETCOLOR
	ENDIF
NEXT
PRINTL 
DRAWLINE
PRINTFORML [999] - 戻る
INPUT

IF RESULT == 999
		RETURN
ELSEIF INRANGE(RESULT, 1, CHARANUM) && !CFLAG:RESULT:出禁 && !TALENT:RESULT:行きずり
	CALL SHOW_GIFT_GAVE(RESULT)
ENDIF
RESTART

;-------------------------------------------------
;貰った贈り物を表示する関数
;-------------------------------------------------
@SHOW_GIFT_GAVE(CHARA)
#DIM CHARA
#DIM SCORE
#DIM GIFT_NUM
#DIM LINE
#DIM FAVO
PRINTFORML 　☆%CALLNAME:CHARA%から貰った贈り物　　　　　　　　　　●選択するとレビューできます
FOR LOCAL, 0, 100
	;フラグ0番が100を兼ねているのでこの形に
	SIF GIFT_GAVE:CHARA:((LOCAL + 1) % 100)
		CALL PRINT_GIFTDATA(GIFT_GAVE:CHARA:((LOCAL + 1) % 100), CHARA, "あなたの")
	IF !GIFT_GAVE:CHARA:((LOCAL + 1) % 100)
		LINE = LOCAL
		BREAK
	ENDIF
NEXT
DRAWLINE
CALL GIFTNAME(ITEM:お気に入りアイテム, CHARA, 1)
PRINTFORML ☆現在のお気に入り：%RESULTS%
CALL GIFTNAME(FAVO, CHARA, 1)
PRINTFORML [996] - 前のキャラ
PRINTFORML [997] - 次のキャラ
PRINTFORML [999] - 戻る　　　　　　　　　　　　　　 \@ FAVO ? [998] %RESULTS%をお気に入りにする # \@
INPUT
IF RESULT == 999
	FAVO = 0
	RETURN
ELSEIF RESULT == 998
	ITEM:お気に入りアイテム = FAVO
ELSEIF RESULT == 997
	FOR LOCAL, CHARA + 1, CHARANUM
		IF GIFT_GAVE:LOCAL:1
			CHARA = LOCAL
			CLEARLINE 7 + LINE
			RESTART
		ENDIF
	NEXT
	FOR LOCAL, 1, CHARA + 1
		IF GIFT_GAVE:LOCAL:1
			CHARA = LOCAL
			CLEARLINE 7 + LINE
			RESTART
		ENDIF
	NEXT
ELSEIF RESULT == 996
	FOR LOCAL, 1, CHARA
		LOCAL:1 = CHARA - LOCAL
		IF GIFT_GAVE:(LOCAL:1):1
			CHARA = LOCAL:1
			CLEARLINE 7 + LINE
			RESTART
		ENDIF
	NEXT
	FOR LOCAL, 1, CHARANUM
		LOCAL:1 = CHARANUM - LOCAL
		IF GIFT_GAVE:(LOCAL:1):1
			CHARA = LOCAL:1
			CLEARLINE 7 + LINE
			RESTART
		ENDIF
	NEXT
ELSEIF INRANGE(RESULT, 0, 100) && GIFT_GAVE:CHARA:RESULT
	GIFT_NUM = RESULT
	;星の数が変則的なので
	SELECTCASE GET_GIFTDATA(GIFT_GAVE:CHARA:GIFT_NUM, "得点")
	CASE IS >= 800
		SCORE = 0
	CASE IS >= 700
		SCORE = 800
	CASE IS >= 500
		SCORE = 700
	CASE IS >= 400
		SCORE = 500
	CASEELSE
		SCORE = 400
	ENDSELECT
	GIFT_GAVE:CHARA:GIFT_NUM -= (GET_GIFTDATA(GIFT_GAVE:CHARA:GIFT_NUM, "得点") * 10000000000000)
	GIFT_GAVE:CHARA:GIFT_NUM += (SCORE * 10000000000000)
	FAVO = GIFT_GAVE:CHARA:GIFT_NUM
ENDIF
CLEARLINE 7 + LINE
RESTART

;-------------------------------------------------
;キャラ→あなたの贈り物オーダーメイドする関数
;◆使い方◆
;CHARA    あなたに渡すキャラの番号
;ARGS:1   形容詞名
;ARGS:2   本体名
;
;口上等のイベントで使われることを想定した贈り物をオーダーメイド生成して、あなたの貰った贈り物に登録する関数です
;形容詞と本体名を設定して呼び出すと、指定の贈り物がキャラからあなたへ渡されます
;あなたの貰った贈り物はシステム上では完全にフレーバーなので何を渡しても自由です
;【レトロな＋写真】等、既存のものでも良いですし
;【%CALLNAME:CHARA%モデルの＋オナホール】みたいなオリジナルアイテムも生成可能です
;口上内の専用フラグを使えば連動させることもできます
;ARGSに"ランダム"を入れると、ランダムなものが選ばれます
;関数を呼んだ時点であなたの貰った贈り物として登録されます
;地の文等はありません
;RESULTS:0に贈り物の名前（ARGS:1＋ARGS:2）が入ります
;GIFT_IDに登録される日付は現在のDAY、場所は99（特注品）になります
;-------------------------------------------------
@GIFT_ORDER_MADE_FROM_CHARA(CHARA, ARGS:1, ARGS:2)
#DIM CHARA
#DIM MAIN
#DIM ADJECT
#DIM BASIC_PRICE
#DIM MAGNIF_PRICE
#DIMS GIFTNAME_MAIN
#DIMS GIFTNAME_ADJECT
#DIMS SENSE
#DIMS GIFTNAME
#DIM 渡した回数
#DIM 点数
#DIM GIFT_ID
SENSE = 
;渡した回数の設定
渡した回数 = GET_GIFTDATA(CFLAG:CHARA:渡した贈り物, "回数") + 1

MAIN = 0
IF ARGS:2 == "ランダム"
	CALL DRAW_GIFT_MAIN(99)
	MAIN = RESULT
	BASIC_PRICE = RESULT:1
	GIFTNAME_MAIN = %RESULTS:0%
	SENSE += RESULTS:1
ELSE
	FOR LOCAL, 11, 550
		CALL GIFTDATA_MAIN(LOCAL)
		IF RESULTS == ARGS:2
			MAIN = LOCAL
			BREAK
		ENDIF
	NEXT
	IF MAIN
		CALL GIFTDATA_MAIN(MAIN)
		BASIC_PRICE = RESULT:1
		GIFTNAME_MAIN = %RESULTS:0%
		SENSE += RESULTS:1
	ELSE
		MAIN = 999
		BASIC_PRICE = 10000
		GIFTNAME_MAIN = %ARGS:2%
		SENSE += ARGS:2 + "オリジナル/"
		GIFT_GAVE_ORI_MAIN:CHARA:(渡した回数 % 100) = %GIFTNAME_MAIN%
	ENDIF
ENDIF

ADJECT = 0
IF ARGS:1 == "ランダム"
	CALL DRAW_GIFT_ADJECT(99)
	ADJECT = RESULT
	MAGNIF_PRICE = RESULT:1
	GIFTNAME_ADJECT = %RESULTS:0%
	SENSE += RESULTS:1
ELSE
	FOR LOCAL, 101, 2200
		CALL GIFTDATA_ADJECT(LOCAL)
		IF RESULTS == ARGS:1
			ADJECT = LOCAL
			BREAK
		ENDIF
	NEXT
	IF ADJECT
		CALL GIFTDATA_ADJECT(ADJECT)
		MAGNIF_PRICE = RESULT:1
		GIFTNAME_ADJECT = %RESULTS:0%
		SENSE += RESULTS:1
	ELSE
		ADJECT = 9999
		MAGNIF_PRICE = 300
		GIFTNAME_ADJECT = %ARGS:1%
		SENSE += ARGS:1 + "オリジナル/"
		GIFT_GAVE_ORI_ADJECT:CHARA:(渡した回数 % 100) = %GIFTNAME_ADJECT%
	ENDIF
ENDIF

;値段の設定（意味はない）
RESULT:1 = BASIC_PRICE * MAGNIF_PRICE / 100
;名前の設定
GIFTNAME = %GIFTNAME_ADJECT%%GIFTNAME_MAIN%
;IDの設定
GIFT_ID = ADJECT * 1000 + MAIN
;RESULTS設定
RESULTS:0 = %GIFTNAME%
RESULTS:1 = %SENSE%
;便宜上の点数
点数 = 0
;IDの設定
GIFT_ID = 渡した回数 * 10000000000000000 + 点数 * 10000000000000 + DAY * 1000000000 + 99 * 10000000 + GIFT_ID
;渡す処理
GIFT_GAVE:CHARA:(渡した回数 % 100) = GIFT_ID
CFLAG:CHARA:渡した贈り物 = GIFT_ID
[IF_DEBUG]
	CALL PRINT_GIFTDATA(GIFT_ID, CHARA, "デバッグ")
	PRINTFORMW 【%GIFTNAME%】を貰った！
[ENDIF]

;-------------------------------------------------
;あなた→キャラの贈り物オーダーメイドする関数
;◆使い方◆
;CHARA       あなたが渡すキャラの番号
;RETURN      1=あげた,-1=キャンセルした
;
;口上等のイベントで使われることを想定した贈り物をオーダーメイド生成してプレゼントする関数です
;指定したキャラへあなたがその場で贈り物を特注してプレゼントします
;※その日のうちにすでに土産屋で贈り物を買っていると重複してしまうので、必ず発生条件で"TCVAR:今日の贈り物"を弾いてください
;全ての本体＆形容詞から自由に組み合わせることができます
;さらに【外国産の＋自動車】みたいなオリジナルアイテムも生成可能です
;その場合は各種特徴も同時に設定します
;※オリジナル扱いで既存の名称を使うと偽物認定されます（安価で指輪とか作ったり）
;ただし高得点を取ると携帯するので（自動車等だと）違和感が出ることを留意してください
;呼んだ時点で今日の贈り物として登録され、土産屋と同じ様に一日の終わりで評価点に応じてお気に入りになったりします
;当然ですが所持金を消費します（定価です）
;オリジナル本体は\10000、オリジナル形容詞は300%の倍率がかかります
;地の文等はありません
;RESULTS:0に贈り物の名前（形容詞＋本体名称）が入ります
;RESULT:1に点数（1～999）が入ります
;GIFT_IDに登録される日付は現在のDAY、場所は99（特注品）になります
;オリジナル品はデバッグ表示するとSENSEがズレてたりしますが、評価時にはちゃんと機能してます
;-------------------------------------------------
@GIFT_ORDER_MADE_FROM_MASTER(CHARA)
#DIM CHARA
#DIM START,20
#DIM ページ
#DIM 最後のページ
#DIM 本体
#DIM 形容詞
#DIMS 本体名称
#DIMS 形容詞名称
#DIMS 本体センス
#DIMS 形容詞センス
#DIMS GIFT_NAME
#DIMS SENSE
#DIM 全体価格
#DIM GIFT_ID
#DIM 点数
#DIM 貰った回数
#DIM 渡した回数
IF TCVAR:CHARA:今日の贈り物
	PRINTFORMW ※注意：この関数を呼ぶ際は"TCVAR:今日の贈り物"が有る場合は弾いてください
	RETURN
ENDIF
;▼本体部分
;各ページの頭番を設定する
IF !START
	VARSET LOCAL
	FOR LOCAL, 11, 550
		CALL GIFTDATA_MAIN(LOCAL)
		SIF RESULTS == ""
			CONTINUE
		LOCAL:1 ++
		IF LOCAL:1 == 21
			LOCAL:2 ++
			START:(LOCAL:2) = LOCAL
			LOCAL:1 = 1
		ENDIF
		LOCAL:3 = LOCAL
	NEXT
	START = 1
	ページ = 1
	;最後のページと最後のページの頭番を設定
	最後のページ = LOCAL:2 + 1
	START:最後のページ = LOCAL:3 + 1
ENDIF
VARSET LOCAL
PRINTFORML \@ CHARA ? %CALLNAME:CHARA%に渡す # \@プレゼントを設定しよう（所持金：%金額表示(MONEY)%）
DRAWLINE
FOR LOCAL, START, START:ページ
	CALL GIFTDATA_MAIN(LOCAL)
	SIF RESULTS == ""
		CONTINUE
	PRINTFORML [{LOCAL, 3}] %RESULTS, 18, LEFT%%金額表示(RESULT:1), 9%
	LOCAL:1 ++
NEXT
WHILE LOCAL:1 < 20
	PRINTL 
	LOCAL:1 ++
WEND
DRAWLINE
PRINTL [999] 次のページ
PRINTL [998] 前のページ
PRINTFORML [997] オリジナル（%金額表示(30000)%）
PRINTL [  0] やっぱりやめる
INPUT
;やめる
IF !RESULT
	VARSET START
	RETURN -1
;次のページ
ELSEIF RESULT == 999
	ページ ++
	IF ページ > 最後のページ
		ページ = 1
		START = 1
	ELSE
		START = START:(ページ - 1)
	ENDIF
;前のページ
ELSEIF RESULT == 998
	ページ --
	IF ページ < 1
		ページ = 最後のページ
		START = START:(最後のページ - 1)
	ELSEIF ページ == 1
		START = 1
	ELSE
		START = START:(ページ - 1)
	ENDIF
;オリジナル
ELSEIF RESULT == 997
	PRINTFORML プレゼント（本体部分）の名称を入力してください
	PRINTL [ 0] やっぱりやめる
	INPUTS
	IF RESULTS == "0"
		RESTART
	ELSE
		本体 = 999
		本体名称 = %RESULTS%
		CALL GIFT_ORDER_MADE_ORIGINAL_DESIGN("本体", 本体名称)
		本体センス = %RESULTS%
		;違法チェック
		FOR LOCAL, 11, 550
			CALL GIFTDATA_MAIN(LOCAL)
			IF RESULTS == 本体名称
				本体センス += "粗悪/粗悪/粗悪/"
				BREAK
			ENDIF
		NEXT
	ENDIF
;選択
ELSEIF INRANGE(RESULT, 11, 550)
	LOCAL = RESULT
	CALL GIFTDATA_MAIN(RESULT)
	IF RESULTS != ""
		本体 = LOCAL
		本体名称 = %RESULTS%
		本体センス = %RESULTS:1%
	ENDIF
ENDIF
IF !本体
	CLEARLINE 28
	RESTART
ENDIF
;▼形容詞部分
CALL GIFT_ORDER_MADE_FROM_MASTER_2(CHARA, 本体, 本体名称)
SIF RESULT == -1
	RESTART
VARSET START
形容詞 = RESULT
全体価格 = RESULT:1
形容詞名称 = %RESULTS%
形容詞センス = %RESULTS:1%
GIFT_NAME = %形容詞名称 + 本体名称%
SENSE = %本体センス + 形容詞センス%
GIFT_ID = 形容詞 * 1000 + 本体
DRAWLINE
PRINTFORML 【%GIFT_NAME%】%金額表示(全体価格)%
PRINTL 
IF MONEY < 全体価格
	CALL COLORMESSAGE(@"所持金が足りません！", C_ORANGE, 1)
	CALL COLORMESSAGE(@"最初に戻ります", C_ORANGE, 1)
	FORCEWAIT
	RESTART
ELSE
	PRINTFORML これで決定します。よろしいですか？
	PRINTL [1] 決定
	PRINTL [0] やり直す
	INPUT
	SIF !RESULT
		RESTART
ENDIF
MONEY -= 全体価格
貰った回数 = GET_GIFTDATA(CFLAG:CHARA:贈り物ストック, "回数") + 1
渡した回数 = CFLAG:MASTER:渡した贈り物 / 1000 + 1
CALL REVIEW_GIFT(CHARA, GIFT_ID, SENSE, 全体価格)
点数 = RESULT
GIFT_ID = 貰った回数 * 10000000000000000 + 点数 * 10000000000000 + DAY * 1000000000 + 99 * 10000000 + GIFT_ID
;ストックにGIFT_IDを保存
CFLAG:CHARA:贈り物ストック = GIFT_ID
;ストレージにも保存
GIFT_GOT:CHARA:(貰った回数 % 100) = CFLAG:CHARA:贈り物ストック
;CHARAはID,MASTERは渡したキャラを保存
TCVAR:CHARA:今日の贈り物 = CFLAG:CHARA:贈り物ストック
TCVAR:MASTER:今日の贈り物 = CHARA
;MASTERは渡した回数と渡したキャラを保存
CFLAG:MASTER:渡した贈り物 = 渡した回数 * 1000 + CHARA
;オリジナル名称を設定
GIFT_GOT_ORI_ADJECT:CHARA:(貰った回数 % 100) = %形容詞名称%
GIFT_GOT_ORI_MAIN:CHARA:(貰った回数 % 100) = %本体名称%
;点数
RESULT:1 = 点数
;名前
RESULTS = %GIFT_NAME%
[IF_DEBUG]
	CALL PRINT_GIFTDATA(GIFT_ID, CHARA, "デバッグ")
	PRINTFORMW 【%GIFT_NAME%】をプレゼントした
[ENDIF]
RETURN 1

;-------------------------------------------------
;あなた→キャラの贈り物オーダーメイドする関数の形容詞部分
;@GIFT_ORDER_MADE_FROM_MASTERの内部関数
;-------------------------------------------------
@GIFT_ORDER_MADE_FROM_MASTER_2(CHARA, 本体, 本体名称)
#DIM CHARA
#DIM START,50
#DIM ページ
#DIM 最後のページ
#DIM 本体
#DIM 形容詞
#DIMS 本体名称
#DIMS 形容詞名称
#DIM 本体価格
#DIM 全体価格
#DIMS 形容詞センス
;各ページの頭番を設定する
IF !START
	VARSET LOCAL
	FOR LOCAL, 101, 2200
		CALL GIFTDATA_ADJECT(LOCAL)
		SIF RESULTS == ""
			CONTINUE
		LOCAL:1 ++
		IF LOCAL:1 == 21
			LOCAL:2 ++
			START:(LOCAL:2) = LOCAL
			LOCAL:1 = 1
		ENDIF
		LOCAL:3 = LOCAL
	NEXT
	START = 1
	ページ = 1
	;最後のページと最後のページの頭番を設定
	最後のページ = LOCAL:2 + 1
	START:最後のページ = LOCAL:3 + 1
ENDIF
VARSET LOCAL
PRINTFORML \@ CHARA ? %CALLNAME:CHARA%に渡す # \@プレゼントを設定しよう（所持金：%金額表示(MONEY)%）
CALL GIFTDATA_MAIN(本体)
本体価格 = RESULT:1
本体名称 = \@ 本体 == 999 ? %本体名称% # %RESULTS% \@
DRAWLINE
FOR LOCAL, START, START:ページ
	CALL GIFTDATA_ADJECT(LOCAL)
	SIF RESULTS == ""
		CONTINUE
	PRINTFORML [{LOCAL, 4}] %RESULTS + 本体名称, 38, LEFT%%金額表示(本体価格 * RESULT:1 / 100), 12%
	LOCAL:1 ++
NEXT
WHILE LOCAL:1 < 20
	PRINTL 
	LOCAL:1 ++
WEND
DRAWLINE
PRINTL [ 999] 次のページ
PRINTL [ 998] 前のページ
PRINTFORML [ 997] オリジナル（%金額表示(本体価格 * 300 / 100)%）
PRINTL [   0] 戻る
INPUT
;戻る
IF !RESULT
	VARSET START
	RETURN -1
;次のページ
ELSEIF RESULT == 999
	ページ ++
	IF ページ > 最後のページ
		ページ = 1
		START = 1
	ELSE
		START = START:(ページ - 1)
	ENDIF
;前のページ
ELSEIF RESULT == 998
	ページ --
	IF ページ < 1
		ページ = 最後のページ
		START = START:(最後のページ - 1)
	ELSEIF ページ == 1
		START = 1
	ELSE
		START = START:(ページ - 1)
	ENDIF
;オリジナル
ELSEIF RESULT == 997
	PRINTFORML プレゼント（形容詞部分）の名称を入力してください
	PRINTL [ 0] やっぱりやめる
	INPUTS
	IF RESULTS == "0"
		RESTART
	ELSE
		形容詞 = 9999
		形容詞名称 = %RESULTS%
		CALL GIFT_ORDER_MADE_ORIGINAL_DESIGN("形容詞", 形容詞名称 + 本体名称)
		形容詞センス = %RESULTS%
		全体価格 = 本体価格 * 300 / 100
		;違法チェック
		FOR LOCAL, 101, 2200
			CALL GIFTDATA_ADJECT(LOCAL)
			IF RESULTS == 形容詞名称
				形容詞センス += "粗悪/粗悪/粗悪/"
				BREAK
			ENDIF
		NEXT
	ENDIF
;選択
ELSEIF INRANGE(RESULT, 101, 2200)
	LOCAL = RESULT
	CALL GIFTDATA_ADJECT(RESULT)
	IF RESULTS != ""
		形容詞 = LOCAL
		形容詞名称 = %RESULTS%
		形容詞センス = %RESULTS:1%
		全体価格 = 本体価格 * RESULT:1 / 100
	ENDIF
ENDIF
IF !形容詞
	CLEARLINE 28
	RESTART
ENDIF
VARSET START
RESULT:1 = 全体価格
RESULTS = %形容詞名称%
RESULTS:1 = %形容詞センス%
RETURN 形容詞

;-------------------------------------------------
;贈り物オーダーメイドのSENSEを決める関数
;@GIFT_ORDER_MADE_FROM_MASTERの内部関数
;-------------------------------------------------
@GIFT_ORDER_MADE_ORIGINAL_DESIGN(ARGS, 名称)
#DIMS 名称
VARSET LOCALS
IF ARGS == "本体"
	PRINTFORML ◆【%名称%】に思いを込めるならどうするもの？
	PRINTL [ 1] 眺めるもの
	PRINTL [ 2] 握るもの
	PRINTL [ 3] 抱くもの
	PRINTL [ 4] 身につけるもの
	INPUT
	IF RESULT == 2
		LOCALS:1 = 握る/
	ELSEIF RESULT == 3
		LOCALS:1 = 抱く/
	ELSEIF RESULT == 4
		LOCALS:1 = 身につける/
	ELSE
		LOCALS:1 = 眺める/
	ENDIF
	PRINTFORML ◆【%名称%】はどんなもの？
	PRINTL [ 1] 道具　　　　　　[ 2] 置物　　　　　　[ 3] 装飾品
	PRINTL [ 4] 人形　　　　　　[ 5] 書物　　　　　　[ 6] 食器
	PRINTL [ 7] 楽器　　　　　　[ 8] 刃物　　　　　　[ 9] 弾
	PRINTL [10] 美術品　　　　　[11] 生体品　　　
	INPUT
	IF RESULT == 2
		LOCALS:2 = 置物/
	ELSEIF RESULT == 3
		LOCALS:2 = 装飾/
	ELSEIF RESULT == 4
		LOCALS:2 = 人形/
	ELSEIF RESULT == 5
		LOCALS:2 = 書物/
	ELSEIF RESULT == 6
		LOCALS:2 = 食器/
	ELSEIF RESULT == 7
		LOCALS:2 = 楽器/
	ELSEIF RESULT == 8
		LOCALS:2 = 刃物/
	ELSEIF RESULT == 9
		LOCALS:2 = 弾/
	ELSEIF RESULT == 10
		LOCALS:2 = 美術品/
	ELSEIF RESULT == 11
		LOCALS:2 = 生体品/
	ELSE
		LOCALS:2 = 道具/
	ENDIF
	PRINTFORML ◆【%名称%】はもっというとどんなもの？（任意）
	PRINTL [ 1] 護符　　　　　　[ 2] 法具　　　　　　[ 3] 遺物
	PRINTL [ 4] 眼鏡　　　　　　[ 5] 面　　　　　　　[ 6] 時計
	PRINTL [ 7] 文具　　　　　　[ 8] 照明　　　　　　[ 9] 扇
	PRINTL [ 0] どれでもない
	INPUT
	IF RESULT == 1
		LOCALS:3 = 護符/
	ELSEIF RESULT == 2
		LOCALS:3 = 法具/
	ELSEIF RESULT == 3
		LOCALS:3 = 遺物/
	ELSEIF RESULT == 4
		LOCALS:3 = 眼鏡/
	ELSEIF RESULT == 5
		LOCALS:3 = 面/
	ELSEIF RESULT == 6
		LOCALS:3 = 時計/
	ELSEIF RESULT == 7
		LOCALS:3 = 文具/
	ELSEIF RESULT == 8
		LOCALS:3 = 照明/
	ELSEIF RESULT == 9
		LOCALS:3 = 扇/
	ENDIF
	RESULTS = %LOCALS:1 + LOCALS:2 + LOCALS:3%
ELSEIF ARGS == "形容詞"
	PRINTFORML ◆【%名称%】は何色？
	PRINTL [ 1] 白　　　　　　　[ 2] 黒
	PRINTL [ 3] 赤　　　　　　　[ 4] 青　　　　　　　[ 5] 緑
	PRINTL [ 6] 黄色　　　　　　[ 7] 紫　　　　　　　[ 8] 朱色
	PRINTL [ 9] ピンク　　　　　[10] 茶色　　　　　　[11] 灰色
	PRINTL [12] 金色　　　　　　[13] 銀色　　　　　　[14] 虹色
	INPUT
	IF RESULT == 2
		LOCALS:1 = 黒/黒/
	ELSEIF RESULT == 3
		LOCALS:1 = 赤/赤/
	ELSEIF RESULT == 4
		LOCALS:1 = 青/青/
	ELSEIF RESULT == 5
		LOCALS:1 = 緑/緑/
	ELSEIF RESULT == 6
		LOCALS:1 = 黄/黄/
	ELSEIF RESULT == 7
		LOCALS:1 = 紫/紫/
	ELSEIF RESULT == 8
		LOCALS:1 = 朱/朱/
	ELSEIF RESULT == 9
		LOCALS:1 = ピンク/ピンク/
	ELSEIF RESULT == 10
		LOCALS:1 = 茶/茶/
	ELSEIF RESULT == 11
		LOCALS:1 = 灰/灰/
	ELSEIF RESULT == 12
		LOCALS:1 = 金/金/
	ELSEIF RESULT == 13
		LOCALS:1 = 銀/銀/
	ELSEIF RESULT == 14
		LOCALS:1 = 虹/虹/
	ELSE
		LOCALS:1 = 白/白/
	ENDIF
	PRINTFORML ◆【%名称%】はどんな印象？
	PRINTL [ 1] かわいい
	PRINTL [ 2] かっこいい
	PRINTL [ 3] 美しい
	PRINTL [ 4] 怖い
	INPUT
	IF RESULT == 2
		LOCALS:2 = かっこいい/かっこいい/かっこいい/
	ELSEIF RESULT == 3
		LOCALS:2 = 美しい/美しい/美しい/
	ELSEIF RESULT == 4
		LOCALS:2 = 怖い/怖い/怖い/
	ELSE
		LOCALS:2 = かわいい/かわいい/かわいい/
	ENDIF
	PRINTFORML ◆【%名称%】はどんな見た目？（任意）
	PRINTL [ 1] シンプル
	PRINTL [ 2] 奇妙
	PRINTL [ 3] 芸術的
	PRINTL [ 4] オシャレ
	PRINTL [ 0] どれでもない
	INPUT
	IF RESULT == 1
		LOCALS:3 = シンプル/シンプル/
	ELSEIF RESULT == 2
		LOCALS:3 = 奇妙/奇妙/
	ELSEIF RESULT == 3
		LOCALS:3 = 芸術的/芸術的/
	ELSEIF RESULT == 4
		LOCALS:3 = オシャレ/オシャレ/
	ENDIF
	PRINTFORML ◆【%名称%】はどんな雰囲気？（任意）
	PRINTL [ 1] 地味　　　　　　[ 2] 派手
	PRINTL [ 3] 繊細　　　　　　[ 4] 力強い
	PRINTL [ 5] 子供っぽい　　　[ 6] 知的
	PRINTL [ 7] 不思議　　　　　[ 8] 厳つい
	PRINTL [ 9] 壮大　　　　　　[10] マニアック
	PRINTL [ 0] どれでもない
	INPUT
	IF RESULT == 1
		LOCALS:4 = シック/シック/
	ELSEIF RESULT == 2
		LOCALS:4 = 派手/派手/
	ELSEIF RESULT == 3
		LOCALS:4 = 繊細/繊細/
	ELSEIF RESULT == 4
		LOCALS:4 = パワフル/パワフル/
	ELSEIF RESULT == 5
		LOCALS:4 = ロリータ/ロリータ/
	ELSEIF RESULT == 6
		LOCALS:4 = 知的/知的/
	ELSEIF RESULT == 7
		LOCALS:4 = 不思議/ 不思議/
	ELSEIF RESULT == 8
		LOCALS:4 = 威厳/威厳/
	ELSEIF RESULT == 9
		LOCALS:4 = 壮大/壮大/
	ELSEIF RESULT == 10
		LOCALS:4 = マニアック/マニアック/
	ENDIF
	RESULTS = %LOCALS:1 + LOCALS:2 + LOCALS:3 + LOCALS:4%
ENDIF


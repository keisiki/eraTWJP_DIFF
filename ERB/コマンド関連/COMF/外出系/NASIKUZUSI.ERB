@ONABARE(ARG)
#DIM なし崩し必要値
#DIM どうぞそのまま好感度上昇値

CFLAG:ARG:最後に会った日 = DAY

IF !TALENT:ARG:処女 && ABL:ARG:Ａ感覚 >= ABL:ARG:Ｖ感覚
	LOCAL:2 = 1
ELSE
	LOCAL:2 = 0
ENDIF

IF CFLAG:MASTER:311 == CFLAG:ARG:311
	LOCALS = "外から様子を窺う"
ELSE
	LOCALS = "勝手に上げる"
ENDIF

PRINTFORMW 奥からくぐもった声が聞こえる……
CALL ASK_YN("そっとしておく",@"%LOCALS%")
IF !RESULT
	RETURN
ENDIF

;口上チェック,存在しないならRESULT = 2
CALL KOJO_MESSAGE_SEND("ONABARE", 1, ARG, LOCAL:2)
IF RESULT == 2
	CALL PrintOnanismImage(ARG)
	PRINTFORMW 気づかれないように部屋の中を覗くと、%CALLNAME:ARG%がオナニーに耽っていた
	PRINTFORM 服をはだけ下腹を晒した%CALLNAME:ARG%は、
	IF TALENT:ARG:恋慕
		PRINTFORM %CALLNAME:MASTER%の名を呼び
	;気弱系,真面目系
	ELSEIF CFLAG:ARG:性格傾向 == 1 || CFLAG:ARG:性格傾向 == 4
		PRINTFORM 声を押し殺し
	;強気系
	ELSEIF CFLAG:ARG:性格傾向 == 2 || CFLAG:ARG:性格傾向 == 3 || CFLAG:ARG:性格傾向 == 5
		PRINTFORM 甘い声を上げ
	ENDIF
	PRINTFORMW ながら、ゆっくりと指を動かしている

	;デカパイ
	IF TALENT:ARG:バストサイズ > 0
		PRINTFORM ぶるんと揺れる豊満な乳房
	;ちっぱい
	ELSEIF TALENT:ARG:バストサイズ < 0
		PRINTFORM ふるふると震える薄い胸板
	;普通
	ELSE
		PRINTFORM ぷるんと揺れるおっぱい
	ENDIF
	PRINTFORMW をなで回し、足を広げて秘処の方へと指を持って行く%CALLNAME:ARG%
	PRINTFORMW そのまま、つぷりと\@ !LOCAL:2 ? 秘処 # アヌス \@へ中指を差し込む姿に生唾を飲みこむ
	PRINTFORMW 同時、かつんと、ドアに額があたってしまった
	PRINTFORML 来訪者に気付いた%CALLNAME:ARG%は硬直し、体を隠すことすら忘れて%CALLNAME:MASTER%のほうを見つめている……
ENDIF

CALL KOJO_MESSAGE_SEND("EVENT",26,ARG,0,0)
PRINTFORMW 
$LOOP

PRINTFORML どうしよう？
CALL ASK_M("失礼しました",1,"見ててあげるからどうぞそのまま",1,"一人より二人でする方が気持ちいいよ",BASE:MASTER:精力 > MAXBASE:MASTER:精力 / 5)
IF RESULT == 0
	IF CFLAG:MASTER:311 == CFLAG:ARG:311
		PRINTFORML %CALLNAME:MASTER%はそそくさと寝床についた
	ELSE
		PRINTFORML %CALLNAME:MASTER%は目の前の光景を網膜に焼き付けた上で、動揺する%CALLNAME:ARG%が我に返る前に一言詫びて去った
	ENDIF
	SIF !CFLAG:ARG:弱み握られ
		PRINTFORML %CALLNAME:ARG%の【弱味】を握った
	CALL KOJO_MESSAGE_SEND("EVENT",26,ARG,1,0)
	CFLAG:ARG:オナバレ = 1
	CFLAG:ARG:弱み握られ = 1
	TIME += 5
ELSEIF RESULT == 1
	RESULT = 0
	CALL KOJO_MESSAGE_SEND("ONABARE", 4, ARG, 0, 0, "どうぞそのまま")
	SIF RESULT == -1
		GOTO GOOUT
	SIF RESULT == 1
		GOTO SONOMAMA
	なし崩し必要値 = 抱き寄せ必要値(ARG) - 50 + TALENT:ARG:羞恥心 * 50 - ABL:ARG:露出癖 * 30 - CFLAG:ARG:溜まってる度 / 20
	SIF  TALENT:ARG:プライド > 0
		なし崩し必要値 += 100
	SIF CFLAG:ARG:既成事実 & 合意_うふふ
		なし崩し必要値 -= 50
	SIF TALENT:ARG:度胸 < 0
		なし崩し必要値 -= 200
	CALL SHOW_LOVEPOWER(CFLAG:ARG:合意判定,50)
	CALL SHOW_REASONWALL(なし崩し必要値 ,50)
	;失敗
	IF CFLAG:ARG:合意判定 <= なし崩し必要値
		GOTO GOOUT
	;成功
	ELSE
		$SONOMAMA
		CALL KOJO_MESSAGE_SEND("EVENT",26,ARG,2,0)
		;口上チェック,存在しないならRESULT = 2
		CALL KOJO_MESSAGE_SEND("ONABARE", 2, ARG, LOCAL:2)
		IF RESULT == 2
			PRINTFORM しばし
			IF TALENT:ARG:恋慕
				PRINTFORM うれしさと戸惑いをうかべ
			;気弱系,真面目系
			ELSEIF CFLAG:ARG:性格傾向 == 1 || CFLAG:ARG:性格傾向 == 4
				PRINTFORM もじもじと照れくさそうにし
			;強気系,素直系,その他
			ELSEIF CFLAG:ARG:性格傾向 == 2 || CFLAG:ARG:性格傾向 == 3 || CFLAG:ARG:性格傾向 == 5
				PRINTFORM 呆れたような困ったような顔をし
			ENDIF
			PRINTFORML ていた%CALLNAME:ARG%が、小さくため息をついた
			IF TALENT:ARG:恋人
				PRINTFORML 諸肌に服を脱ぎ捨て、%CALLNAME:MASTER%に正対して大きく足を広げた
			;気弱系,真面目系
			ELSEIF CFLAG:ARG:性格傾向 == 1 || CFLAG:ARG:性格傾向 == 4
				PRINTFORML 恥ずかしそうに少しだけ服をはだけただけで、足は閉ざしたままだ
			;強気系,素直系,その他
			ELSEIF CFLAG:ARG:性格傾向 == 2 || CFLAG:ARG:性格傾向 == 3 || CFLAG:ARG:性格傾向 == 5
				PRINTFORML 服を脱ぎながら、それでも肝心な部分は手で隠している
			ENDIF
			PRINTFORML そんな、%CALLNAME:ARG%に、続きを促す%CALLNAME:MASTER%
			PRINTFORMW 小さくため息をついた%CALLNAME:ARG%がゆっくりと、手を動かし始めた

			PRINTFORML ぬちくちと音をたてて\@ !LOCAL:2 ? 膣口 # 菊座 \@へと埋まる中指
			;デカパイ
			IF TALENT:バストサイズ > 0
				PRINTFORML ぐにむにと指が埋まりカタチを変える乳房
			;ちっぱい
			ELSEIF TALENT:バストサイズ < 0
				PRINTFORML くりくりと指の先端がこすり立てる乳首
			;普通
			ELSE
				PRINTFORML ふにふにと手で揺らされているおっぱい
			ENDIF
			PRINTFORMW その扇情的な光景をさらに卑猥にする甘い声が、響き始めた
			IF TALENT:ARG:恋人
				PRINTFORML 臆面もなく、%CALLNAME:MASTER%の名前を呼びながら、激しく指を動かし始めた
				PRINTFORML とろとろと、上と下の口から涎を垂らし、うっとりとした表情を向けてくる%CALLNAME:ARG%
				PRINTFORMW ゴクリとつばを飲み込む%CALLNAME:MASTER%
				PRINTFORML 徐々に胸を揉みしだく手と、\@ !LOCAL:2 ? 膣口 # 菊座 \@に銜え込まれた指の動きが速くなっていく
				PRINTFORMW そして、%CALLNAME:MASTER%の名を呼びながら、全身を震わせて絶頂した
			;気弱系,真面目系
			ELSEIF CFLAG:ARG:性格傾向 == 1 || CFLAG:ARG:性格傾向 == 4
				PRINTFORML 恥ずかしそうに、胸と\@ !LOCAL:2 ? 秘処 # アヌス \@を弄る%CALLNAME:ARG%
				PRINTFORML けれど、ほんのりと血色の良くなる肌に、目に見えてとがる乳首に、悦楽が垣間見える
				PRINTFORMW 徐々に声が大きくなり、すこしづつ足が開いていく
				PRINTFORML %CALLNAME:MASTER%の視線に気づいていないのか、控えめだが隠せない程度に足が開いた
				PRINTFORML とろりと、愛蜜が垂れ、同時に、びくっと太ももが痙攣する
				PRINTFORMW 余りにも唐突に潮を吹きながら、%CALLNAME:ARG%は頂に上り詰めた
			;強気系,素直系,その他
			ELSEIF CFLAG:ARG:性格傾向 == 2 || CFLAG:ARG:性格傾向 == 3 || CFLAG:ARG:性格傾向 == 5
				PRINTFORML 手を出そうかと近寄る%CALLNAME:MASTER%に、見せるだけと釘を刺してくる%CALLNAME:ARG%
				PRINTFORML 胸を弄りながら、ちゅくちゅくとやらしい音を立てて、\@ !LOCAL:2 ? 秘処 # アヌス \@を弄っている
				PRINTFORMW こちらの、不満げな様子に気づいたのか、ため息をついた%CALLNAME:ARG%が大きく足を広げた
				PRINTFORML たらたらと零れる愛液と、ぴんととがりきったクリトリスは扇情的で
				PRINTFORML こちらの視線を感じているのか、%CALLNAME:ARG%の指の動きが早まっていく
				PRINTFORMW 不意に、\@ !LOCAL:2 ? 膣腔 # 肛腔 \@深くに指を突き込んで、%CALLNAME:ARG%が達した
			ENDIF
		ENDIF
	ENDIF
	PRINTFORML ピクピク、ぴくんぴくんと、内ももを痙攣させ、全身を震わせる%CALLNAME:ARG%
	IF CFLAG:MASTER:311 == CFLAG:ARG:311
		PRINTFORML %CALLNAME:MASTER%は脱力しきった彼女に布団をかけてから寝床についた
	ELSE
		PRINTFORML ぐったりとしたその様子に、さすがに手を出すのが憚られた%CALLNAME:MASTER%はそのまま帰ることにした
		IF !CFLAG:ARG:ノーパン && EQUIP:ARG:下半身下着２ && CFLAG:MASTER:デート中 != MAIN_MAP
			PRINTFORML せっかくなので、お土産をもらって帰りますか？
			CALL ASK_YN
			IF !RESULT
				PRINTFORML 帰り際、%CALLNAME:MASTER%は%CALLNAME:ARG%の%PANTSNAME(EQUIP:ARG:下半身下着２, ARG)%を拾って懐に入れた
				CALL PANTS_GET(ARG)
			ENDIF
		ENDIF
	ENDIF
	CFLAG:ARG:オナバレ = 3
	どうぞそのまま好感度上昇値 = ABL:ARG:親密 * CFLAG:ARG:溜まってる度 / 250 * MIN(ABL:ARG:露出癖,10)
	CALL CHANGE_CFLAG(2,ARG,どうぞそのまま好感度上昇値)
	CALL CHANGE_CFLAG(4,ARG,どうぞそのまま好感度上昇値 * -1 / 20)
	CALL JUEL_UP_SPECIAL(ARG,"恥情","自慰中毒",30,1)
	CALL JUEL_UP_SPECIAL(ARG,"欲情","自慰中毒",30,1)
	CALL ADD_EXP_LESNIAN(CFLAG:ARG:溜まってる度 / 100, ARG, "PRINT")
	EXP:ARG:露出快楽経験 += CFLAG:ARG:溜まってる度 / 200 * ABL:ARG:露出癖
	EXP:ARG:自慰経験 +=  CFLAG:ARG:溜まってる度 / 200 * ABL:ARG:露出癖
	TIME += 27
ELSEIF RESULT == 2
	RESULT = 0
	CALL KOJO_MESSAGE_SEND("ONABARE", 4, ARG, 0, 0, "二人で")
	SIF RESULT == -1
		GOTO GOOUT
	SIF RESULT == 1
		GOTO NASI
	なし崩し必要値 = 抱き寄せ必要値(ARG) - 150 + TALENT:ARG:羞恥心 * 30 - CFLAG:ARG:溜まってる度 / 20
	SIF  TALENT:ARG:プライド > 0
		なし崩し必要値 += 50
	SIF CFLAG:ARG:既成事実 & 合意_うふふ
		なし崩し必要値 -= 50
	SIF TALENT:ARG:度胸 < 0
		なし崩し必要値 -= 200
	CALL SHOW_LOVEPOWER(CFLAG:ARG:合意判定,50)
	CALL SHOW_REASONWALL(なし崩し必要値 ,50)
	$ GOOUT
	IF CFLAG:ARG:合意判定 <= なし崩し必要値
		PRINTFORML %CALLNAME:ARG%はわけのわからないことを喚き散らして%CALLNAME:MASTER%を追い出した
		CALL KOJO_MESSAGE_SEND("EVENT",26,ARG,3,0)
		SIF !CFLAG:ARG:弱み握られ
			PRINTFORML %CALLNAME:ARG%の【弱味】を握った
		CALL GET_ANGRY(ARG,1)
		CFLAG:ARG:オナバレ = 1
		CFLAG:ARG:弱み握られ = 1
		TIME += 5
	ELSE
		$NASI
		CALL NASIKUZUSI(ARG,LOCAL:2)
		CFLAG:ARG:ヤラせちゃった = 1
	ENDIF
ENDIF

@NASIKUZUSI(ARG, 本番はどっちか)
#DIM 前戯１
;1=C 2=B 3=M
#DIM 前戯２
#DIM 本番はどっちか
#DIM 注入量
#DIM 基本注入量
#DIM 好感度上昇量
#DIM 信頼低下量
#DIM 回数
#DIM 経験増加数,58
;０ならＶ、１ならＡ
VARSET 経験増加数
CALL KOJO_MESSAGE_SEND("EVENT",26,ARG,4,0)
回数 = MAX(MIN(ABL:ARG:欲望 / 2, 5) + CFLAG:ARG:溜まってる度 / 200,1)
注入量 = 0
IF TALENT:ARG:キス未経験
	前戯１ = 3
	経験増加数:4 += 3 + ABL:ARG:Ｍ感覚 * 2
	経験増加数:25 = 経験増加数:4
	経験増加数:27 = 経験増加数:4
	EXP:MASTER:Ｍ経験 += 経験増加数:4
	EXP:MASTER:キス経験 += 経験増加数:4
	EXP:MASTER:口淫経験 += 経験増加数:4
ELSEIF !RAND:3
	前戯１ = 3
	経験増加数:4 += 3 + ABL:ARG:Ｍ感覚 * 2
	経験増加数:27 = 経験増加数:4
ELSEIF ABL:ARG:Ｃ感覚 >= ABL:ARG:Ｂ感覚
	前戯１ = 1
	経験増加数:0 += RAND:(回数 * 2) 
	経験増加数:5 += 経験増加数:0 / 2
	経験増加数:10 += 経験増加数:0 / 2
ELSE
	前戯１ = 2
	経験増加数:3 += RAND:(回数 * 2) 
	経験増加数:8 += 経験増加数:3 / 2
	経験増加数:10 += 経験増加数:0 / 2
ENDIF
前戯２ = RAND:3

SIF 本番はどっちか == 1
	前戯２ += 10
IF 本番はどっちか
	基本注入量 = (ABL:ARG:アナル + 1) * 20 + BASE:MASTER:精力 /25
	経験増加数:21 += 回数
	経験増加数:17 += 回数
	経験増加数:10 += 回数
	経験増加数:7 += 2 * 回数
	経験増加数:2 += 回数 * 2
ELSE
	基本注入量 = (ABL:ARG:膣 + 1) * 20 + BASE:MASTER:精力 / 25
	SIF TALENT:ARG:処女 == 1
		基本注入量 += 100
	経験増加数:20 += 回数
	経験増加数:16 += 回数
	経験増加数:10 += 2 * 回数
	経験増加数:6 += 2 * 回数
	経験増加数:1 += 2 * 回数
ENDIF
EXP:MASTER:挿入経験 += 回数
EXP:MASTER:射精経験 += 回数
EXP:MASTER:絶頂経験 += 回数

FOR LOCAL,1,回数
	注入量 += 基本注入量 + RAND:(基本注入量)
NEXT

注入量 = MIN(注入量, BASE:MASTER:精力)

SIF !HAS_PENIS(MASTER)
	注入量 = 0
IF !本番はどっちか
	CALL ADD_SAMEN_V(ARG, 注入量)
ELSE
	CALL ADD_SAMEN_A(ARG, 注入量)
ENDIF

;口上チェック,存在しないならRESULT = 2
CALL KOJO_MESSAGE_SEND("ONABARE", 3, ARG, 前戯１, 本番はどっちか, "", "", 回数, 注入量)
IF RESULT == 2
	;以下デフォルト地の文
	PRINTFORML %CALLNAME:MASTER%は動揺する%CALLNAME:ARG%をよそに後ろ手で扉を閉めてずかずかと上がり込むと、
	PRINTFORMW %CALLNAME:ARG%を抱き寄せて何を考えながらシていたのか訊いた
	PRINTFORM そして彼女が望んだとおり、

	SELECTCASE 前戯１
		CASE 1
			PRINTDATA
				DATAFORM クリトリスを摘まみ
				DATAFORM クリトリスを指先で弾き
				DATAFORM 陰核を指先でこね回し
				DATAFORM 陰核を舐めしゃぶり
			ENDDATA
		CASE 2
			PRINTDATA
				DATAFORM 乳首を指で摘まみ
				DATAFORM 胸を愛撫し
				DATAFORM 乳房を手で揉みしだき
				DATAFORM 乳首を舌で転がし
				DATAFORM 乳首を甘噛みし
			ENDDATA
		CASE 3
			PRINTDATA
				DATAFORM 髪を撫でてから
				DATAFORM そっと
				DATAFORM 情熱的に
			ENDDATA
	ENDSELECT
	IF 前戯１ == 3
		IF TALENT:ARG:キス未経験
			PRINTFORM ファーストキスを奪い
		ELSE
			PRINTFORM 口づけ
		ENDIF
	ENDIF
	PRINT 、

	SELECTCASE 前戯２
		CASE 0
			PRINTFORM 陰唇のまわりを撫でまわし
		CASE 1
			PRINTFORM 膣口に浅く指を挿入し
		CASE 2
			PRINTFORM 膣口を舌でほじくり
		CASE 10
			PRINTFORM 尻をじっくりと揉みしだき
		CASE 11
			PRINTFORM 肛門のまわりを撫でまわし
		CASE 12
			PRINTFORM 肛門に浅く指を挿入し
	ENDSELECT
	PRINT 、

	PRINTDATA
		DATAFORM 散々焦らしてやった
		DATAFORM 徹底的にイかせてやった
		DATAFORM 卑猥な言葉で責め立てた
	ENDDATA
	PRINTW
	PRINTFORM それから%TEXTR("四つん這いにさせ背後から獣のように覆いかぶさり/正面から抱き合って/まんぐり返しの姿勢を取らせ/正面から抱き合って")%
	PRINTDATA
		DATAFORM 甘い言葉を囁いて
		DATAFORM 優しく
		DATAFORM 荒々しく
		DATAFORM ねっとりと
	ENDDATA

	IF TALENT:ARG:処女 && !本番はどっちか
		PRINTFORM 純潔を奪い、
	ELSE
		PRINTFORM {回数}度%TEXTR("犯し/交わり/貫き")%、
	ENDIF
	PRINTFORMW \@本番はどっちか ? アナル# 膣\@に{注入量}の精液を注ぎ込んだ……

	;以上デフォルト地の文
ENDIF

IF 前戯１ == 3 
	SIF TALENT:MASTER:キス未経験
		CALL SET_FIRSTKISS(MASTER, ARG, 2)
	SIF TALENT:ARG:キス未経験
		CALL SET_FIRSTKISS(ARG, MASTER, 2, "流されるままに")
ENDIF
IF TALENT:ARG:処女 == -1 && !本番はどっちか
	SETCOLOR 247,171,166
	PRINTFORMW 【無自覚非処女喪失】
	TALENT:ARG:処女 = 0
ENDIF
IF TALENT:ARG:処女 == 1 && !本番はどっちか
	SETCOLOR 247,171,166
	PRINTFORMW 【処女喪失】
	CALL SET_HISTORY_LOST_V(ARG, MASTER, "ペニス", "自室", "流されるままに")
ENDIF
IF !CFLAG:ARG:アナル処女喪失記念日 && 本番はどっちか
	SETCOLOR 247,171,166
	PRINTFORMW 【アナル処女喪失】
	CALL SET_HISTORY_LOST_A(ARG, MASTER, "ペニス", "自室", "流されるままに")
ENDIF
SETCOLOR C_YELLOW
FOR LOCAL,0,58
	IF 経験増加数:LOCAL
		PRINTFORML %EXPNAME:LOCAL% ＋{経験増加数:LOCAL}
		EXP:ARG:LOCAL += 経験増加数:LOCAL
	ENDIF
NEXT
RESETCOLOR
CALL JUEL_UP_SPECIAL(ARG,"欲情","欲望",20,1)
SELECTCASE 前戯１
;C
	CASE 1
		CALL JUEL_UP_SPECIAL(ARG,"快Ｃ","Ｃ感覚",10,1)
;B
	CASE 2
		CALL JUEL_UP_SPECIAL(ARG,"快Ｂ","Ｂ感覚",10,1)
;M
	CASE 3
		CALL JUEL_UP_SPECIAL(ARG,"快Ｍ","Ｍ感覚",20,1)
ENDSELECT
IF 本番はどっちか
	CALL JUEL_UP_SPECIAL(ARG,"快Ａ","Ａ感覚",15,1)
ELSEIF TCVAR:ARG:破瓜
	CALL JUEL_UP_SPECIAL(ARG,"快Ｖ","Ｖ感覚",100,1)
ELSE
	CALL JUEL_UP_SPECIAL(ARG,"快Ｖ","Ｖ感覚",15,1)
ENDIF
CALL ADD_EXP_LESNIAN(回数 * 5, ARG, "PRINT")
PRINTFORMW …
PRINTFORMW ……
PRINTFORMW ………

CALL KOJO_MESSAGE_SEND("EVENT",26,ARG,5,0)
TIME += 回数 * 20
EXP:MASTER:清掃経験 ++
DOWNBASE:MASTER:体力 = 回数 * 100
DOWNBASE:MASTER:気力 = 回数 * 150
好感度上昇量 = 回数 * ABL:ARG:親密 * 5
信頼低下量 = 好感度上昇量 / 5
SIF TALENT:MASTER:謎の魅力 || TALENT:MASTER:魅惑
	信頼低下量 /= 2
IF TALENT:ARG:思慕 || TALENT:ARG:セフレ
	信頼低下量 /= 2
	SIF TALENT:ARG:快感応答 == 1
		信頼低下量 /= 2
ENDIF
IF TALENT:ARG:恋慕 || TALENT:ARG:愛欲
	信頼低下量 /= 4
	SIF TALENT:ARG:快感応答 == 1
		信頼低下量 = 0
ENDIF

CALL CHANGE_CFLAG(2,ARG,好感度上昇量)
SIF 信頼低下量 > 0
	CALL CHANGE_CFLAG(4,ARG,信頼低下量 * -1)
CFLAG:ARG:溜まってる度 = 0
TCVAR:ARG:破瓜 = 0

IF TALENT:ARG:恋慕 && CFLAG:MASTER:デート中 != MAIN_MAP
	PRINTFORML 行為を終えた%CALLNAME:MASTER%は立ち上がろうとしたが、%CALLNAME:ARG%はまだ一緒にいて欲しそうだ……
	CALL ASK_YN("それでも帰る","泊まっていくか")
	IF !RESULT
		GOTO BYEBYE
	ELSE
		CALL KOJO_MESSAGE_SEND("EVENT",26,ARG,6)
		;お招きフラグにARGを代入
		CFLAG:ARG:お招き = ARG
		CFLAG:MASTER:お招き = ARG
		;初期位置をOMANEKIBEYA()=98にする
		CFLAG:ARG:初期位置 = OMANEKIBEYA()
		;移動させる
		CFLAG:ARG:現在位置 = OMANEKIBEYA()
		CFLAG:MASTER:現在位置 = OMANEKIBEYA()
		IF NEMUKE() < 720 && BASE:ARG:体力 > 500
			PRINTFORML %CALLNAME:MASTER%は今日一日%CALLNAME:ARG%のところで過ごすことにした
			PRINTFORML しばらく一緒に眠って、起きて、掃除をして、イチャイチャして、食事を作って食べて、
			PRINTFORMW またイチャイチャして、セックスして寝た
			VARSET 経験増加数
			IF 本番はどっちか
				注入量 = ABL:ARG:アナル * MIN(ABL:ARG:親密,15) * 10
				経験増加数:21 += MIN(ABL:ARG:親密,15)
				経験増加数:17 += MIN(ABL:ARG:親密,15)
				経験増加数:7 += MIN(ABL:ARG:親密,15) * 2
				経験増加数:2 += MIN(ABL:ARG:親密,15) * 2
			ELSE
				注入量 = ABL:ARG:膣 * MIN(ABL:ARG:親密,15) * 10
				経験増加数:20 += MIN(ABL:ARG:親密,15)
				経験増加数:16 += MIN(ABL:ARG:親密,15)
				経験増加数:6 += 2 * MIN(ABL:ARG:親密,15)
				経験増加数:1 += 2 * MIN(ABL:ARG:親密,15)
			ENDIF
			経験増加数:41 =  MIN(ABL:ARG:親密,15) * 2
			注入量 = MIN(注入量, BASE:MASTER:精力)
			CALL JUEL_UP_SPECIAL(LOCAL,"欲情","欲望",15)
			IF !本番はどっちか
				CALL ADD_SAMEN_V(ARG, 注入量)
				CALL JUEL_UP_SPECIAL(ARG,"快Ｖ","Ｖ感覚",10,1)
			ELSE
				CALL ADD_SAMEN_A(ARG, 注入量)
				CALL JUEL_UP_SPECIAL(ARG,"快Ａ","Ａ感覚",10,1)
			ENDIF
			FOR LOCAL,0,58
				IF 経験増加数:LOCAL
					PRINTFORML %EXPNAME:LOCAL% ＋{経験増加数:LOCAL}
					EXP:ARG:LOCAL += 経験増加数:LOCAL
				ENDIF
			NEXT
			CALL ADD_EXP_LESNIAN(ABL:ARG:親密 * 2,ARG, "PRINT")
			好感度上昇量 = ABL:ARG:親密 * 10
			EXP:MASTER:清掃経験 ++
			EXP:MASTER:料理経験 ++
			TIME += 720 - NEMUKE()
		ELSE
			PRINTFORML 眠くなってきたので、そのまま%CALLNAME:ARG%と一緒に寝た
			CALL ADD_EXP_LESNIAN(ABL:ARG:親密,ARG, "PRINT")
			好感度上昇量 = ABL:ARG:親密 * 5
		ENDIF
		CALL CHANGE_CFLAG(2,ARG,好感度上昇量)

		SIF GETBIT(TALENT:MASTER:性別, 1)
			DOWNBASE:MASTER:精力 = 注入量

		CFLAG:MASTER:就寝時間 = DAY * 1440 + TIME
		BEGIN AFTERTRAIN
		RETURN 1
	ENDIF
ENDIF

;TWあなたは片づけせずにはいられない
$BYEBYE

SIF GETBIT(TALENT:MASTER:性別, 1)
	DOWNBASE:MASTER:精力 = 注入量
PRINTFORM 後始末をした%CALLNAME:MASTER%は、\@ 本番はどっちか? 尻穴# 秘裂\@から\@TCVAR:ARG:破瓜 ? 破瓜血混じりの# \@白濁を溢れさせて
PRINTFORML \@ TALENT:ARG:回復速度? こちらを上気した表情で見つめている#呆ける \@%CALLNAME:ARG%に、

IF ABL:ARG:精液中毒 >= (3 + 本番はどっちか * 2)
	PRINTFORML 掃除してあげたから代わりにこっちを綺麗にしてよと言って口元に\@TCVAR:ARG:破瓜 ?彼女の純潔を散らしたばかりの#汁まみれの \@ペニスを突き付けた
	PRINTFORMW %CALLNAME:ARG%は\@ TALENT:ARG:回復速度 > 0 ? 蕩けた表情で丹念に#ぼんやりした表情で \@精液を舐めとっている……
	SETCOLOR C_YELLOW
	PRINTFORML 精液経験 ＋{ABL:ARG:精液中毒}
	EXP:ARG:精液経験 += ABL:ARG:精液中毒
	RESETCOLOR
	CALL JUEL_UP_SPECIAL(ARG,"屈服","精液中毒",5,1)
	PRINTFORM すっかり満足した%CALLNAME:MASTER%は、
ENDIF

PRINTFORMW \@ CFLAG:MASTER:デート中 == MAIN_MAP? おやすみ# すごくよかった、次は%GET_MAPNAME(MAIN_MAP)%でしよう\@と言って\@ CFLAG:MASTER:311 == CFLAG:ARG:311 ? 眠りに就いた# 立ち去った\@
CFLAG:ARG:オナバレ = 2
IF !CFLAG:ARG:ノーパン && CFLAG:MASTER:311 != CFLAG:ARG:311 && CFLAG:MASTER:デート中 != MAIN_MAP && PANTSNAME(EQUIP:ARG:下半身下着２, ARG) != "ノーパン"
	PRINTFORML せっかくなので、お土産をもらって帰りますか？
	CALL ASK_YN
	IF !RESULT
		PRINTFORML 帰り際、%CALLNAME:MASTER%は%CALLNAME:ARG%の%PANTSNAME(EQUIP:ARG:下半身下着２, ARG)%を拾って懐に入れた
		CALL PANTS_GET(ARG)
	ENDIF
ENDIF

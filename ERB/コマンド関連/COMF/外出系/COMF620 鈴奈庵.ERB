;貸本屋
@COM620
#DIM CHARISMA
#DIM ItemId

$C_LOOP
DRAWLINE
IF !CHARISMA
	PRINTFORML 所持金:%金額表示(MONEY)%
	PRINTL 「借り賃はどれも\3000です」
ELSE
	PRINTFORML 所持カリスマ:{MONEY:2}
	PRINTL 「借り賃はどれも10カリスマです」
ENDIF

DRAWLINE
FOR LOCAL,1, BookTitles + 1
	ItemId = LOCAL + 100
	PRINTFORM [{LOCAL}] %ITEMNAME:ItemId%
	IF ITEM:ItemId
		PRINTFORML （%CALLNAME:MASTER%が借りている 返却まで{FLAG:貸本返却期限-1}日)
	ELSEIF GETBIT (FLAG:1011,LOCAL)
		PRINTL （貸し出し中）
	ELSE
		PRINTL 　
	ENDIF
NEXT

IF !CHARISMA
	PRINTFORML [100] カリスマ払い
ELSE
	PRINTFORML [100] 現金払い
ENDIF
DRAWLINE
PRINTL [0] やめる

$LOOP
INPUT

IF RESULT == 100
	IF !CHARISMA
		LOCALS = 「カリスマで払う？？？？」/「えっと……じゃあ10カリスマ？でいいです……」
		CALL BOOK_RENTAL_SALESTALK(LOCALS)
		IF MONEY:2 < 10
			PRINTFORMW カリスマが足りない
			GOTO C_LOOP
		ENDIF
	ELSE
		LOCALS = 「あ、お金で払ってくれるんですね」
		CALL BOOK_RENTAL_SALESTALK(LOCALS)
	ENDIF
	PRINTFORML 
	CHARISMA = !CHARISMA
	GOTO C_LOOP
ELSE
	IF !CHARISMA && MONEY < 3000
		PRINTW お金が足りなかった…
		RETURN -1
	ENDIF
	IF INRANGE(RESULT, 1, BookTitles)
		RESULT:1 = RESULT
		IF GETBIT (FLAG:1011,RESULT:1)
			ItemId = RESULT:1 + 100
			IF ITEM:ItemId
				IF FLAG:貸本返却期限 == 8
					LOCALS = 「その本は今日あなたが借りましたよ……？」
					CALL BOOK_RENTAL_SALESTALK(LOCALS)
					RETURN -1
				ENDIF
				
				LOCALS = 「その本は今あなたが借りていますけど……」/「借り賃を払って、貸出期間を一週間に延長しますか？」
				CALL BOOK_RENTAL_SALESTALK(LOCALS)
				CALL ASK_YN
				IF !RESULT
					IF !CHARISMA
						MONEY -= 3000
					ELSE
						MONEY:2 -= 10
					ENDIF
					FLAG:貸本返却期限 = 8
					PRINTFORML %ITEMNAME:(100 + RESULT:1)%の貸出期間を一週間に延長した
					LOCALS = 「ありがとうございました」/「その本がよほど気に入ったんですね」
					CALL BOOK_RENTAL_SALESTALK(LOCALS)
				ELSE
					RETURN -1
				ENDIF
			ELSE
				LOCALS = 「貸し出し中です」
				CALL BOOK_RENTAL_SALESTALK(LOCALS)
			ENDIF
			RETURN -1
		ELSE
			FOR LOCAL,1, BookTitles + 1
				ItemId = LOCAL + 100
				IF ITEM:ItemId
					LOCALS = 「二冊以上の貸し出しはできません」/「また借り賃を貰いますが、今借りている本を返却してその本を借りますか？」
					CALL BOOK_RENTAL_SALESTALK(LOCALS)
					CALL ASK_YN
					IF !RESULT
						ITEM:ItemId = 0
						CLEARBIT FLAG:1011,LOCAL
					ELSE
						RETURN -1
					ENDIF
				ENDIF
			NEXT
			SETBIT FLAG:1011,RESULT:1
			IF !CHARISMA
				MONEY -= 3000
			ELSE
				MONEY:2 -= 10
			ENDIF
			ITEM:(100 + RESULT:1) = 1
			FLAG:貸本返却期限 = 8
			TFLAG:193 = 1
			PRINTFORML %ITEMNAME:(100 + RESULT:1)%を借りた
			LOCALS = 「ありがとうございました」/「貸出期間は一週間です」 
			CALL BOOK_RENTAL_SALESTALK(LOCALS)
			RETURN 1
		ENDIF
	ELSEIF RESULT == 0
		RETURN -1
	ELSE
		GOTO LOOP
	ENDIF
ENDIF
TIME += 30

;貸本屋
@COM_ABLE620
;一括管理
SIF GLOBAL_COMABLE(620)
	RETURN RESULT
;貸本屋ではない場所
SIF !BOOK_RENTAL(CFLAG:MASTER:現在位置)
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;時間停止中
SIF FLAG:70
	RETURN 0
RETURN 1

;貸本屋を判定する関数
@BOOK_RENTAL(ARG)
#FUNCTION
;鈴奈庵
SIF ARG == GET_MAP_REPLACEMENT(213)
	RETURNF 1
RETURNF 0

@BOOKLEND
FOR LOCAL,1,BookTitles + 1
	SIF LOCAL == 6	;[紙芝居]をスルー
		CONTINUE
	SIF !ITEM:(100 + LOCAL) && !RAND:3
		INVERTBIT FLAG:1011,LOCAL
NEXT
SIF FLAG:貸本返却期限
	FLAG:貸本返却期限 -= 1
IF FLAG:貸本返却期限 == 1
	FOR LOCAL,1,BookTitles + 1
		ITEM:(100 + LOCAL) = 0
	NEXT
	PRINTL 返却期限が来たので、借りていた本を返しました。
ENDIF

@BOOK_RENTAL_SALESTALK(ARGS)
IF CFLAG:[[小鈴]]:現在位置 == CFLAG:PLAYER:現在位置
	CALL SPTALK, [[小鈴]], "笑顔", 0, @"%ARGS%", 1
	SIF TARGET != [[小鈴]]
		RESETCOLOR
ELSE
	CALL SPTALK, 0, "笑顔", 12, @"%ARGS%", 1
ENDIF
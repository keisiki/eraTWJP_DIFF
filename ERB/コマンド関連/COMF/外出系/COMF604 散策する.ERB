@COM604
#DIM LPCOUNT
#DIM 現在地点
#DIM P_ID
#DIM 所要時間
#DIM 時間補正
#DIM 時間停止ジャンプ
#DIM 特殊ワープ
#DIM ANIMATION_SEED
#DIM PREV_REDRAW
;散策する
時間停止ジャンプ = FLAG:時間停止 || (GETBIT(FLAG:瞬間移動, 0) && !FLAG:デート相手)

ANIMATION_SEED = 0
PREV_REDRAW = CURRENTREDRAW()
;Map Animation Code
$MAPANIMATION
LINESTART = LINECOUNT
SIF ANIMATERECOLOREDMAPS && !FLAG:70
	ANIMATION_SEED ++

VARSET LOCAL
DRAWLINEFORM ～

IF RECOLOREDMAPS
	CALL DRAW_COLOREDMAP(ANIMATION_SEED, "ODEKAKE")
ELSE
	CALL DRAW_MAP("ODEKAKE")
ENDIF

DRAWLINEFORM ～
PRINTL どこに向かいますか？
現在地点 = CFLAG:MASTER:現在位置 % 100 / 10
FOR LPCOUNT, 1, 10
	P_ID = CFLAG:MASTER:デート中 * 100 + LPCOUNT * 10
	SIF NAME_FROM_PLACE(P_ID) == ""
		CONTINUE
	IF LPCOUNT == 現在地点
		PRINTPLAINFORM [  0] - %NAME_FROM_PLACE(P_ID), 18, LEFT%
		CALL COLORMESSAGE(@"現在地　", C_GREEN, 3)
	ELSE
		時間補正 = 0
		IF 時間停止ジャンプ
			LOCALS = TSP%" "%
		ELSE
			;小町の能力
			SIF FLAG:デート相手 == [[小町]]
				時間補正 += -50
			;わかさぎ姫の負担
			SIF FLAG:デート相手 == [[わかさぎ姫]] && !GETBIT (FLAG:プール, 0)
				時間補正 += 100
			;地点ごとのショートカット効果
			SIF SHORTCUT(CFLAG:MASTER:デート中, -1)
				時間補正 += -50
			LOCALS = 分　
		ENDIF
		所要時間 = 散策所要時間(CFLAG:MASTER:現在位置, P_ID, 時間補正)
		PRINTFORM [{LPCOUNT, 3}] - %NAME_FROM_PLACE(P_ID), 18, LEFT%
		IF 時間補正 < 0
			SETCOLOR C_L_GREEN
		ELSEIF 時間補正 > 0
			SETCOLOR C_ROYAL_BLUE
		ENDIF
		PRINTFORM {所要時間, 3} %LOCALS%
		RESETCOLOR
	ENDIF
	LOCAL ++
	SIF LOCAL % 2 == 0
		PRINTL
NEXT
SIF LOCAL % 2 == 1
	PRINTL

;特殊ワープ
特殊ワープ = ODEKAKEMAP_SETTING(CFLAG:MASTER:現在位置, "特殊ワープ")
IF 特殊ワープ
	所要時間 = 時間停止ジャンプ ? 30 # 10
	PRINTFORML [{特殊ワープ, 3}] - %NAME_FROM_PLACE(特殊ワープ), 18, LEFT%{所要時間, 3} %LOCALS%
ENDIF

PRINTL [100] - 戻る　　　　　　　　　　　[ 99] - 他の地域に移動
$INPUT_LOOP
;Map Animation Code
IF ANIMATERECOLOREDMAPS && !FLAG:70
	REDRAW PREV_REDRAW
	TINPUT ANIMATERECOLOREDMAPS,1234,0,""
ELSE
	INPUT
ENDIF
IF INRANGE(RESULT, 1, 9)
	P_ID = CFLAG:MASTER:デート中 * 100 + RESULT * 10
	;所要時間は消費TSPも込みになっているので注意
	所要時間 = 散策所要時間(CFLAG:MASTER:現在位置, P_ID, 時間補正)
ENDIF
;戻る
IF RESULT == 100 || RESULT == 0
	RETURN -1
;他の地域に移動
ELSEIF RESULT == 99
	DRAWLINE
	CALL OTHERREGIONS("散策する")
	IF RESULT == 100
		RESTART
	ELSE
		RETURN 1
	ENDIF
;特殊ワープ,10分/30TSP固定
ELSEIF 特殊ワープ && RESULT == 特殊ワープ
	IF FLAG:時間停止 && BASE:MASTER:TSP < 30
		PRINTW TSPが足りません
		RESTART
	ELSEIF 時間停止ジャンプ
		BASE:MASTER:TSP -= 30
	ELSE
		TIME += 10
	ENDIF
	PRINTFORML %CALLNAME:MASTER%は空間の歪みに足を踏み入れると
	PRINTFORMW いつの間にか%NAME_FROM_PLACE(特殊ワープ)%に立っていた…
	P_ID = 特殊ワープ
	CFLAG:MASTER:現在位置 = P_ID
	CFLAG:MASTER:デート中 = GET_MAPID(P_ID)
	IF FLAG:デート相手
		CFLAG:(FLAG:デート相手):現在位置 = P_ID
		CFLAG:(FLAG:デート相手):デート中 = GET_MAPID(P_ID)
	ENDIF
	RETURN 1
ELSEIF RESULT == 1234
	PREV_REDRAW = CURRENTREDRAW()
	REDRAW 0
	CLEARLINE LINECOUNT - LINESTART
	GOTO MAPANIMATION
;散策移動
ELSEIF INRANGE(RESULT, 1, 9) && NAME_FROM_PLACE(P_ID) != ""
	;移動できるかチェック
	CALL SANSAKU_UNIQUEPLACE(P_ID)
	IF RESULT
		;P_IDへ移動
		P_ID = RESULT
		CFLAG:MASTER:現在位置 = P_ID
		IF FLAG:デート相手
			CFLAG:(FLAG:デート相手):現在位置 = P_ID
			TIME += 所要時間
		ELSEIF FLAG:時間停止 && BASE:MASTER:TSP < 所要時間
			PRINTW TSPが足りません
			RESTART
		ELSEIF 時間停止ジャンプ
			BASE:MASTER:TSP -= 所要時間
		ELSE
			TIME += 所要時間
		ENDIF
		RETURN 1
	ENDIF
ENDIF
RESTART

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;散策する
@COM_ABLE604
SIF TFLAG:妄想中
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(604)
	RETURN RESULT
;デート中以外
SIF AT_HOME(MASTER)
	RETURN 0
SIF CFLAG:MASTER:現在位置 == OMANEKIBEYA()
	RETURN 0
SIF CFLAG:うふふ
	RETURN 0
;潜伏モード中は不可
SIF GETBIT(FLAG:潜伏,0)
	RETURN 0
;デート道中
SIF TFLAG:デート道中
	RETURN 0
;妄想中
SIF TFLAG:妄想中 == 1
	RETURN 0
RETURN 1


;-----------------------------------------------------------
;現在地→移動先の散策にかかる所要時間
;時間停止中は消費TSP
;二次元座標間の距離を算出してる,例えば12→64だったら5+2=7
;-----------------------------------------------------------
@散策所要時間(現在地ID, 移動先ID, 時間補正)
#FUNCTION
#DIM 現在地ID
#DIM 移動先ID
#DIM 時間補正
#DIM 現在地座標
#DIM 移動先座標
現在地座標 = ODEKAKEMAP_SETTING(現在地ID, "座標")
移動先座標 = ODEKAKEMAP_SETTING(移動先ID, "座標")
LOCAL = (ABS(現在地座標 / 10 - 移動先座標 / 10) + ABS(現在地座標 % 10 - 移動先座標 % 10)) * 2
LOCAL = LOCAL * (100 + MAX(時間補正, -50)) / 100
SIF FLAG:時間停止 || (GETBIT(FLAG:瞬間移動, 0) && !FLAG:デート相手)
	LOCAL *= 3
RETURNF LOCAL

;-----------------------------------------------------------
;散策時に移動できるかを返す関数
;散策版MAP_CAN_MOVE, メッセージもここで表示する
;ARG     移動先の場所ID
;RETRUN  0=移動できない, 0以外=移動先
;-----------------------------------------------------------
@SANSAKU_UNIQUEPLACE(ARG)
SELECTCASE ARG
	CASE 妖精の大樹
		IF ABL:MASTER:教養 < 1
			PRINTFORMW 未熟な%CALLNAME:MASTER%には見つけることができない…
			RETURN 0
		ENDIF
	CASE 彼岸, 白玉楼庭, 白玉楼, 天界, 地獄, 畜生界, 霊長園
		IF ABL:MASTER:教養 < 1 && ABL:(FLAG:デート相手):教養 < 1
			PRINTFORMW もう少し教養を付けないと危険だ
			RETURN 0
		ENDIF
	CASE 月の都, 綿月亭, 玉兎の街, 桃林～砂浜, 静かの海
		IF ABL:MASTER:教養 < 2 && ABL:(FLAG:デート相手):教養 < 2
			PRINTFORMW もう少し教養を付けないと危険だ
			RETURN 0
		ENDIF
	CASE 正邪の隠れ家前
		;本人は入れる;恋慕があれば入れてもらえる;前世で正邪が恋慕or恋人なら場所は分かる
		IF FLAG:なりきり == [[正邪]] || TALENT:[[正邪]]:恋慕 || GETBIT (CFLAG:[[正邪]]:引き継ぎ, 0) || GETBIT (CFLAG:[[正邪]]:引き継ぎ, 1)
		ELSE
			PRINTFORMW どこにあるかわからない…
			RETURN 0
		ENDIF
	CASE 仙人の屋敷
		;華扇が恋慕;本人は入れる
		IF TALENT:[[華扇]]:恋慕 || FLAG:なりきり == [[華扇]]
		;デート相手が天子か自分が天子
		ELSEIF FLAG:デート相手 == [[天子]] || FLAG:なりきり == [[天子]]
		;教養が足りないとたどり着けない
		ELSEIF ABL:MASTER:教養 < 3
			PRINTFORMW さっきから屋敷との距離が一向に縮まらない……
			PRINTFORMW 同じところをぐるぐる回っているかのようだ
			RETURN 0
		;それ以外は戦闘力3以上必要
		ELSEIF ABL:MASTER:戦闘能力 < 3 && !FLAG:時間停止
			PRINTFORMW 庭にうずくまっていた剣呑な虎が、%CALLNAME:MASTER%の姿を認めて唸り声を上げた
			PRINTFORMW 近づかない方がよさそうだ
			RETURN 0
		ENDIF
	CASE 永遠亭奥ノ院
		IF FLAG:時間停止
			PRINTFORMW 無断で入った
		ELSEIF !永遠亭侵入許可()
			PRINTL 奥へ入ろうとしたが、入り口で兎たちに止められた
			RETURN 永遠亭
		ENDIF
	CASE 連れ込み宿, ラブホ
		IF !FLAG:デート相手
			PRINTW 一人で行っても仕方ない
			RETURN 0
		ELSEIF MONEY < 3000
			PRINTW お金が足りない
			RETURN 0
		ENDIF
		SIF !GETBIT(CFLAG:(FLAG:デート相手):既成事実 , 1)
			CALL 抱き寄せ(FLAG:デート相手)
		IF TCVAR:(FLAG:デート相手):抱き寄せ初回 >= 6 && !TCVAR:(FLAG:デート相手):泥酔 && !CFLAG:(FLAG:デート相手):負け犬
				PRINTFORMW %CALLNAME:(FLAG:デート相手)%に入るのを拒否されてしまった
				SIF TCVAR:(FLAG:デート相手):抱き寄せ初回 == 6
					PRINTFORMW しかし%CALLNAME:(FLAG:デート相手)%はまんざらでもなさそうだ
			RETURN 0
		ELSE
			PRINTFORML %金額表示(3000)%かかります。宜しいですか？
			CALL ASK_YN
			CFLAG:(FLAG:デート相手):うふふ = 0
			CFLAG:MASTER:うふふ = 0
			IF RESULT == 0
				MONEY -= 3000
				IF TCVAR:(FLAG:デート相手):泥酔
					PRINTFORMW %CALLNAME:(FLAG:デート相手)%が朦朧としているのをいいことに、%CALLNAME:MASTER%は彼女を%NAME_FROM_PLACE(ARG)%へと連れ込んだ
				ELSEIF CFLAG:(FLAG:デート相手):負け犬
					PRINTFORMW %CALLNAME:MASTER%は嫌がる%CALLNAME:(FLAG:デート相手)%を%NAME_FROM_PLACE(ARG)%に引っ張り込んだ
				ELSE
					PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:(FLAG:デート相手)%と%NAME_FROM_PLACE(ARG)%に入った
				ENDIF
			ELSE
				RETURN 0
			ENDIF
		ENDIF
ENDSELECT
RETURN ARG

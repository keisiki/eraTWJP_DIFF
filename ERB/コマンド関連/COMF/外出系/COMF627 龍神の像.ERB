@COM627
#DIMS 低気圧位置

低気圧位置 = %GET_LOCATION_LOW()%

DRAWLINE
PRINTW 龍神の像を眺めてみた
PRINTL
PRINTL ……

IF (IS_COLD_LOW && 低気圧位置 != "寒冷渦通過" && DAY:2 != 2) || (IS_COLD_LOW && DAY:2 == 2) || (IS_SoJ_LOW == 2 && 低気圧位置 != "二つ玉低気圧通過") || CAN_CUMULUS:0 == 5 || CAN_CUMULUS:2 == 2
	PRINTW 目の色が赤色だ！！異常気象が迫っている…
	PRINTL
ELSEIF GET_LOCATION_AREA_TYPHOON() || 低気圧位置 == "台風勢力圏" || (低気圧位置 == "台風遠方" && TIME_ENTER_TERRITORY_TYPHOON:1 == DAY:3)
	PRINTW 目の色が紫色になっている…野分が近づいている
	PRINTL
ELSEIF (IS_SoJ_LOW == 1 && 低気圧位置 != "日本海低気圧通過") || (IS_SOUTHERN_LOW && 低気圧位置 != "南岸低気圧通過") || IS_RAINY_SEASON1 || IS_RAINY_SEASON2 || 低気圧位置 == "温暖前線接近" || 低気圧位置 == "暖域" || 低気圧位置 == "寒冷前線通過"
	PRINTW 目の色が青色になっている…雨が近い
	PRINTL
ELSE
	PRINTW 目の色に変化がない
	PRINTL
ENDIF
TIME += 1
RETURN -1

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;龍神の像
@COM_ABLE627
;一括管理
SIF GLOBAL_COMABLE(627)
	RETURN RESULT
;龍神の像ではない場所
SIF !RYUJIN_STATUE(CFLAG:MASTER:現在位置)
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
RETURN 1

;龍神の像を判定する関数
@RYUJIN_STATUE(ARG)
#FUNCTION
;龍神の像
SIF ARG == GET_MAP_REPLACEMENT(207)
	RETURNF 1
RETURNF 0

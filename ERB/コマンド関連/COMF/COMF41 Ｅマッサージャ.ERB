;-------------------------------------------------
;Ｅマッサージャ
;道具使用コマンド、レベル2
;-------------------------------------------------
@COM41
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;奉仕快楽フラグ
TCVAR:PLAYER:108 = 1

;TARGETがふたなりの場合には射精可能に
CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:TARGET:2 = 10
;強度
TCVAR:TARGET:4 = 1000

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 150
	SOURCE:快Ｃ = 500

ELSE

	DOWNBASE:体力 += 30
	DOWNBASE:気力 += 120

	SOURCE:快Ｃ = 500
	SOURCE:露出 = 40
	SOURCE:逸脱 = 60
	SOURCE:反感 = 60

	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;抑圧、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 60
ENDIF
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ELSE
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 1
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ENDIF
EXP:PLAYER:道具使用経験 ++

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE41
;Ｅマッサージャ実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(41)
	RETURN RESULT
;アイテムを持っているか、アイテムなし設定でないとダメ
SIF ITEM:1 == 0 && NOITEM == 0
	RETURN 0
;ASSIの場合、技巧3以上かサドじゃないと使えない。
IF ASSIPLAY
	SIF ABL:ASSI:技巧 < 3 && TALENT:ASSI:サド == 0
		RETURN 0
ENDIF
;シャワー中はダメ
SIF TEQUIP:32
	RETURN 0
;室内でないとダメ
SIF !INROOM(CFLAG:PLAYER:現在位置)
	RETURN 0
RETURN 1


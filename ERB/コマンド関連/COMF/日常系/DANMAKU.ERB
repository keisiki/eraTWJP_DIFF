@DANMAKU_OPPONENT_STATUS(ARG, ARG:1)
;特殊能力表示
LOCAL:1 = 0
FOR LOCAL, 0, 10
	SIF GETBIT(CFLAG:ARG:弾幕特能, LOCAL)
		LOCAL:1 ++
NEXT
IF LOCAL:1 == 10
	CALL DANMAKU_SA_OUTPUT(ARG, LOCAL:1)
ELSEIF LOCAL:1 == 0
	PRINTFORML 弾幕スキル未収得
ELSE
	FOR LOCAL, 0, 10
		CALL DANMAKU_SA_OUTPUT(ARG, LOCAL)
	NEXT
ENDIF

SIF ARG:1
	RETURN
;戦闘能力の差でメッセージ分岐
SELECTCASE ABL:ARG:戦闘能力 - ABL:MASTER:戦闘能力
	CASE -6 TO -4
		PRINTFORML 目隠しして座っていても勝てる。
	CASE -4 TO -2
		PRINTFORML 目隠ししていても勝てそうだ。
	CASE -1
		PRINTFORML 負ける気はしない。
	CASE 0
		PRINTFORML たぶん勝てそうだ。
	CASE 1
		PRINTFORML 勝てない相手ではない。
	CASE 2
		PRINTFORML 相手はかなり強そうだ。
	CASE 3
		PRINTFORML 少なくとも、あなたの倍は強そうだ。
	CASE 4
		PRINTFORML 奇跡が起きなければ殺されるだろう。
	CASE 5,6
		PRINTFORML 相手が巨人だとすれば、あなたは蟻のフン以下だ。
	CASEELSE
		
ENDSELECT

@DANMAKU_SA_OUTPUT(OPPONENT, ARG:0)
#DIM OPPONENT

SIF ARG:0 < 10
	LOCAL = GETBIT(CFLAG:OPPONENT:弾幕特能, ARG:0)
	IF ARG:0 == 10
		SETCOLOR C_RED
		PRINTFORML 【百載無窮】: 全ての特殊能力をあわせ持つ。最強
	ELSEIF LOCAL && ARG:0 == 0
		SETCOLOR C_P_BLUE
		PRINTFORML 【言笑自若】: 相手のダイス数Down
	ELSEIF LOCAL && ARG:0 == 1
		SETCOLOR C_P_BLUE
		PRINTFORML 【温柔敦厚】: 相手のダイス面Down
	ELSEIF LOCAL && ARG:0 == 2
		SETCOLOR C_RED
		PRINTFORML 【勇気凛々】: %CALLNAME:OPPONENT%のダイス数Up
	ELSEIF LOCAL && ARG:0 == 3
		SETCOLOR C_RED
		PRINTFORML 【豪放磊落】: %CALLNAME:OPPONENT%のダイス面Up
	ELSEIF LOCAL && ARG:0 == 4
		SETCOLOR C_P_PURPLE
		PRINTFORML 【怪力乱神】: %CALLNAME:OPPONENT%のダイス値が相手のダイス値より小さくても、その差が一定数以下なら、残機が減らない
	ELSEIF LOCAL && ARG:0 == 5
		SETCOLOR C_P_PURPLE
		PRINTFORML 【洽覧深識】: %CALLNAME:OPPONENT%の初期残機が4になる
	ELSEIF LOCAL && ARG:0 == 6
		SETCOLOR C_P_PURPLE
		PRINTFORML 【残忍酷薄】: 相手の被弾からの復帰時のボム数が-1される
	ELSEIF LOCAL && ARG:0 == 7
		SETCOLOR C_P_PURPLE
		PRINTFORML 【乾坤一擲】: %CALLNAME:OPPONENT%が一定確率でボムを使うようになる
	ELSEIF LOCAL && ARG:0 == 8
		SETCOLOR C_RED
		PRINTFORML 【狷介孤高】: %CALLNAME:OPPONENT%のダイス修正値Up
	ELSEIF LOCAL && ARG:0 == 9
		SETCOLOR C_P_BLUE
		PRINTFORML 【幽愁暗恨】: 相手のダイス修正値Down
	ENDIF
RESETCOLOR


;-------------------------------------------------
;戦闘中の処理
;-------------------------------------------------
@DANMAKU_BATTLE(OPPONENT, ENEMY = -1)
#DIM プレイヤー残機
#DIM 相手残機
#DIM 残りHP
#DIM 残りボム回数
#DIM ターン数
#DIM プレイヤーダイス面
#DIM 相手ダイス面
#DIM プレイヤーダイス数
#DIM 相手ダイス数
#DIM プレイヤーダイス値
#DIM 相手ダイス値
#DIM プレイヤー修正値
#DIM 相手修正値
#DIM 相手強さ
#DIM 霊力充填フラグ
#DIM 慢心フラグ
#DIM OPPONENT
#DIM ENEMY
#DIM BombPower
#DIM B_Min
#DIM B_Max
#DIM 持ち込みアイテム
#DIM DamageThreshold
#DIM Pichun
#DIM Charge
プレイヤー残機 = 3
相手残機 = 3
相手強さ = ABL:OPPONENT:戦闘能力
残りHP = MAXBASE:MASTER:体力
残りボム回数 = 2
ターン数 = 1
霊力充填フラグ = 0
慢心フラグ = 0
Pichun = 0
Charge = 0
B_min = ABL:MASTER:戦闘能力 * 7 * (ABL:MASTER:戦闘能力 / 2 + 1)
B_Max = ABL:MASTER:戦闘能力 * 11 * (ABL:MASTER:戦闘能力 / 2 + 1) + 6
SIF TALENT:OPPONENT:プライド > 0 && ABL:MASTER:戦闘能力 < 相手強さ && 相手強さ > 2 && TALENT:OPPONENT:行きずり == 1
	慢心フラグ = 1
;【洽覧深識】の効果
SIF GETBIT(CFLAG:OPPONENT:弾幕特能, 5)
	相手残機 ++
;【エクステンド】の効果
SIF TALENT:MASTER:エクステンド == 1
	プレイヤー残機 ++
;【ルナシューター】の効果
SIF TALENT:MASTER:ルナシューター == 1
	残りボム回数 ++
;弾幕勝負補助アイテムの持ち込み
CALL DANMAKU_ITEM_USE
持ち込みアイテム = RESULT

;プレイヤーのダイス数は2.5n + 期待値2 + 期待値1
;ダイス面は(n+1)*期待値10*期待値7*n/2

WHILE プレイヤー残機 > 0 && 相手残機 > 0
	;ダイス数計算
	プレイヤーダイス数 = (ABL:MASTER:戦闘能力 + RAND:5) * 2 + (ABL:MASTER:戦闘能力 + RAND:3) / 2
	相手ダイス数 = (相手強さ + RAND:4 + 1) * 2 + 相手強さ / 2 * RAND:3
	SELECTCASE 相手残機
		CASE IS > 2
			IF 慢心フラグ && 相手残機 > 2
				CALL COLORMESSAGE(@"%CALLNAME:OPPONENT%は%CALLNAME:MASTER%のことを見くびっている",C_YELLOW,1)
				相手ダイス数 -= 相手強さ / 2
			ENDIF
		CASE 2
			相手ダイス数 += 相手強さ / 2
		CASE 1
			相手ダイス数 += 相手強さ / 2 + 1
	ENDSELECT

	;ダイス面計算
	プレイヤーダイス面 = (ABL:MASTER:戦闘能力 + 1) * (6 + RAND:8) + RAND:(15 + TFLAG:幸運補正) * ABL:MASTER:戦闘能力 / 2
	相手ダイス面 = (相手強さ + 1) * (9 + RAND:7) + RAND:10 - (相手残機 - 1) * 相手強さ
	;修正値計算
	IF (相手強さ - ABL:MASTER:戦闘能力) < 0
		プレイヤー修正値 = (ABL:MASTER:戦闘能力 - 相手強さ + 3) * (-RAND:4 - 8) * ABL:MASTER:戦闘能力 / 2
		相手修正値 = (ABL:MASTER:戦闘能力 - 相手強さ + 3) * (RAND:4 + 8) * 相手強さ / 2
	ELSEIF (相手強さ - ABL:MASTER:戦闘能力) > 0
		プレイヤー修正値 = (相手強さ - ABL:MASTER:戦闘能力 + 3) * (RAND:4 + 8) * ABL:MASTER:戦闘能力 / 2
		相手修正値 = (相手強さ - ABL:MASTER:戦闘能力 + 3) * (-RAND:4 - 8) * 相手強さ / 2
	ELSE
		プレイヤー修正値 = 0
		相手修正値 = 0
	ENDIF
	
	;イベント用
	プレイヤー修正値 -= (HANDICAP_FIXED + TFLAG:GipsLV)
	プレイヤー修正値 -= RAND:(HANDICAP_RAND + TFLAG:GipsLV * ABL:MASTER:戦闘能力 + 1)
	;【言笑自若】の効果
	SIF GETBIT(CFLAG:OPPONENT:弾幕特能, 0)
		プレイヤーダイス数 -= (相手強さ / 2 + RAND:3 + 1) * RAND:2
	;【温柔敦厚】の効果
	SIF GETBIT(CFLAG:OPPONENT:弾幕特能, 1)
		プレイヤーダイス面 -= (相手強さ / 2 + 1) * RAND:4 + RAND:5
	;【勇気凛々】の効果
	SIF GETBIT(CFLAG:OPPONENT:弾幕特能, 2)
		相手ダイス数 += (相手強さ / 2 + RAND:3 + 2) * RAND:2
	;【豪放磊落】の効果
	SIF GETBIT(CFLAG:OPPONENT:弾幕特能, 3)
		相手ダイス面 += 相手強さ * 2 + 6 / (RAND:3 + 1)
	;【狷介孤高】の効果
	SIF GETBIT(CFLAG:OPPONENT:弾幕特能, 8)
		相手修正値 += 相手強さ * (RAND:5 + 5) + RAND:5
	;【幽愁暗恨】の効果
	SIF GETBIT(CFLAG:OPPONENT:弾幕特能, 9)
		プレイヤー修正値 -= 相手強さ * (RAND:4 + 4) + RAND:9
		
	;【霊力充填】の効果
	IF 霊力充填フラグ
		Charge = 相手強さ / 2 + 1
		プレイヤーダイス数 += Charge
		CALL COLORMESSAGE(@"【霊力充填】により、ダイス数が{Charge}アップ！",C_RED,1)
		Charge = (相手強さ / 2 + 1) * 2
		プレイヤーダイス面 += Charge
		CALL COLORMESSAGE(@"【霊力充填】により、ダイス面が{Charge}アップ！",C_RED,1)
		Charge = 相手強さ * (RAND:3 + 2)
		プレイヤー修正値 += Charge
		CALL COLORMESSAGE(@"【霊力充填】により、修正値が{Charge}アップ！",C_RED,1)
		霊力充填フラグ = 0
	ENDIF

	;【国士無双の薬】使用中　残りHP増大・ダイス面に補正
	SIF TCVAR:MASTER:国士無双の薬
		プレイヤーダイス面 += MAX(RAND:(MAX((TCVAR:MASTER:国士無双の薬), 1) * 2), 1)
	
	;戦闘バフの効果（プレイヤーと対戦相手両方）
	SIF TCVAR:MASTER:エンチャント
		プレイヤー修正値 += ABL:MASTER:戦闘能力 * RAND:3 + RAND:3
	SIF TCVAR:OPPONENT:エンチャント
		相手修正値 += 相手強さ * RAND:3 + RAND:3
	SIF TCVAR:MASTER:加護
		相手修正値 -= ABL:MASTER:戦闘能力 * RAND:3 + RAND:3
	SIF TCVAR:OPPONENT:加護
		プレイヤー修正値 -= 相手強さ * RAND:3 + RAND:3

	;弾幕勝負補助アイテムの効果
	CALL DANMAKU_ITEM_EFFECT(持ち込みアイテム)
	プレイヤーダイス面 += RESULT:0
	プレイヤーダイス数 += RESULT:1
	;念のためリセット
	VARSET RESULT
	プレイヤーダイス数 = MAX(プレイヤーダイス数, 1)
	相手ダイス数 = MAX(相手ダイス数, 1)
	プレイヤーダイス面 = MAX(プレイヤーダイス面, 1)
	相手ダイス面 = MAX(相手ダイス面, 1)
		
	PRINTL 
	PRINTFORML -----------------------------------------------------
	PRINTFORML TURN:{ターン数}
	PRINTFORML -----------------------------------------------------
	PRINTL 
	
	CALL DICE_PRINT(プレイヤーダイス数, プレイヤーダイス面, プレイヤー修正値, 相手ダイス数, 相手ダイス面, 相手修正値, "確定前")
	PRINTFORMW 運命のダイスロール！！
	;イベント用
	IF (HANDICAP_FIXED || HANDICAP_RAND)
		CALL KOJO_MESSAGE_SEND("DANMAKU", 相手残機, OPPONENT, ENEMY, -1, "ハンデ")
	ENDIF
	;ダイス値計算
	プレイヤーダイス値 = DICE_VALUE(プレイヤーダイス数, プレイヤーダイス面, プレイヤー修正値)
	相手ダイス値 = DICE_VALUE(相手ダイス数, 相手ダイス面, 相手修正値)
	LOCAL = 相手ダイス値 - プレイヤーダイス値
	
	;持ち込みアイテム【バレットゴーレム】の効果　基本ダイス差を0.8倍に
	SIF LOCAL && 持ち込みアイテム == 854
		TIMES LOCAL, 0.8
	
	CALL DICE_PRINT(プレイヤーダイス値, 0, 0, 相手ダイス数, 相手ダイス面, 相手修正値, "確定後")
	$BEFORE_BATTLE
	PRINTL 
	PRINTFORML 残りHP: {残りHP}   残りボム使用回数: {残りボム回数}   ボム威力: Min:{B_Min}, Max:{B_Max}   残り残機: {プレイヤー残機}
	
	IF 持ち込みアイテム
		SETCOLOR 0x666666
		PRINTFORML   （持ち込みアイテム: %ITEMNAME:持ち込みアイテム% …%DANMAKU_ITEM_EXPLANATION(持ち込みアイテム)%）
	ENDIF

	IF TCVAR:MASTER:国士無双の薬
		SETCOLOR 0x666666
		PRINTFORML   （効果発揮中: 国士無双の薬　　…ダイス面がほんのり良くなる）
	ENDIF
	
	IF TCVAR:MASTER:エンチャント
		SETCOLOR 0x666666
		PRINTFORML   （バフ効果: エンチャント　　…ダイス修正値Up）
	ENDIF
	IF TCVAR:MASTER:加護
		SETCOLOR 0x666666
		PRINTFORML   （バフ効果: 加護　　…相手のダイス修正値Down）
	ENDIF
	IF TCVAR:OPPONENT:エンチャント
		SETCOLOR 0xc86666
		PRINTFORML   （相手のバフ効果: エンチャント　　…相手のダイス修正値Up）
	ENDIF
	IF TCVAR:OPPONENT:加護
		SETCOLOR 0xc86666
		PRINTFORML   （相手のバフ効果: 加護　　…ダイス修正値Down）
	ENDIF
	RESETCOLOR
	
	CALL ASK_M("回避に専念する", 1, "勝負する", 1)
	SELECTCASE RESULT
		CASE 0
			;【霊力充填】の効果
			IF TALENT:MASTER:霊力充填 == 1
				霊力充填フラグ = 1
				CALL COLORMESSAGE(@"%CALLNAME:MASTER%の【霊力充填】発動！",C_RED,2)
			ENDIF
			PRINTFORML 弾幕の回避に専念することにした
			PRINTFORMW ・・・
			PRINTL 
			CALL DICE_PRINT(プレイヤーダイス値, 相手ダイス値, 0, 0, 0, 0, "勝負")
			PRINTW 
			;【バレットゴーレム】の効果  回避選択時はさらに0.9倍（トータル0.72倍程度に）
			IF LOCAL && 持ち込みアイテム == 855
				TIMES LOCAL, 0.9
				PRINTFORML ゴーレムが盾になって弾幕を防ぐ…！
			ENDIF
			$UseEpitaph
			;【グレイズ上手】の効果
			IF TALENT:MASTER:グレイズ上手 == 1
				IF ABL:MASTER:戦闘能力 < 3
					LOCAL = (LOCAL * 2) / (ABL:MASTER:戦闘能力 + 2)
				ELSE
					LOCAL = (LOCAL * 3) / (ABL:MASTER:戦闘能力 + 3)
				ENDIF
			ENDIF
			DamageThreshold = MAX(ABL:MASTER:戦闘能力 / 2, 1)
			IF LOCAL <= 20 * DamageThreshold
				PRINTFORML ほとんどダメージを受けなかった
				SIF LOCAL > 0
					残りHP -= LOCAL * 4
			ELSEIF LOCAL <= 50 * DamageThreshold
				PRINTFORML 少しダメージを受けた
				残りHP -= LOCAL * 5
			ELSEIF LOCAL <= 150 * DamageThreshold
				PRINTFORML 結構ダメージを受けた
				残りHP -= LOCAL * 6
			ELSE
				PRINTFORML かなりのダメージを受けた
				残りHP = 残りHP - MAXBASE:MASTER:体力 / 2 + 1
			ENDIF
			PRINTFORMW 残りHP: {残りHP}
			IF 残りHP <= 0
				Pichun = 1
			ENDIF
		CASE 1
			;【乾坤一擲】の効果
			IF GETBIT(CFLAG:OPPONENT:弾幕特能, 7)
				IF RAND:100 + 1 < 10 + 相手強さ * 2
					LOCAL:1 = 相手強さ * (7 + RAND:5) * (相手強さ / 2 + 1) + RAND:7
					相手ダイス値 += LOCAL:1
					SETCOLOR C_RED
						PRINTFORML %CALLNAME:OPPONENT%の【乾坤一擲】発動！
						PRINTFORMW %CALLNAME:OPPONENT%のダイス値が+{LOCAL:1}された！
					RESETCOLOR
					;口上呼び出し
					CALL KOJO_MESSAGE_SEND("DANMAKU", 相手残機, OPPONENT, ENEMY, -1, "乾坤一擲")
					LOCAL = 相手ダイス値 - プレイヤーダイス値
				ENDIF
			ENDIF
			IF 残りボム回数 >= 1
				PRINTFORML ボムを使う？
				CALL ASK_M("使う", 1, "使わない", 1)
				SELECTCASE RESULT
					CASE 0
						CALL UseBomb
						BombPower = RESULT
						プレイヤーダイス値 += BombPower
						PRINTFORMW ダイス値が{プレイヤーダイス値}になった！
						残りボム回数 --
						LOCAL = 相手ダイス値 - プレイヤーダイス値
					CASE 1
						
				ENDSELECT
			ENDIF
			PRINTFORML 勝負することにした！
			PRINTFORMW ・・・
			PRINTL 
			CALL DICE_PRINT(プレイヤーダイス値, 相手ダイス値, 0, 0, 0, 0, "勝負")
			IF (持ち込みアイテム == 870 ||持ち込みアイテム == 871) && LOCAL > 0 && BASE:MASTER:TSP >= 500
				SETBGCOLOR 1, 24, 60
				{
				CALL COLORMESSAGE(@"%TEXTR("見えるッ！　こいつの動きが！　何をしようとしているのか、完全にッ！ 
				/『読める』………動きの『軌跡』が読める……『未来への動きの軌跡』が…！")%", 0xFF1493, 2, 1)
				}
				PRINTFORML %CALLNAME:MASTER%は『エピタフ』により己の被弾を予見した！
				CALL RECOVER(MASTER, -500, "TSP")
				CALL COLORMESSAGE(@"残りTSP：{BASE:MASTER:TSP}", C_RED, 2)
				CALL ASK_M("回避に専念",1,"『キング・クリムゾン』！(TSP1500)",持ち込みアイテム == 871 && BASE:MASTER:TSP >= 1500)
				SELECTCASE RESULT
					CASE 0
						PRINTFORML %CALLNAME:MASTER%は咄嗟に回避に切り替えた！
						RESETBGCOLOR
						GOTO UseEpitaph
					CASE 1
						CLEARLINE 1
						PRINTL 
						SETCOLOR 0xFF1493
						{
						PRINTFORML %TEXTR("『キング・クリムゾン』の能力の中ではこの世の時間は消し飛び……そして全ての人間はこの時間の中で動いた足跡を覚えていないッ！
						/『空の雲はちぎれ飛んだ事に気づかず！』『消えた炎は消えた瞬間を炎自身さえ認識しない！』/『結果』だけだ！！この世には『結果』だけが残る！！
						/時間の消し飛んだ世界では『動き』は全て無意味となるのだッ！！")%
						}
						PRINTFORMW これが我が『キング・クリムゾン』の能力！『時間を消し去って』飛び越えさせた……！！
						RESETCOLOR
						RESETBGCOLOR
						CALL RECOVER(MASTER, -1500, "TSP")
						PRINTFORML %CALLNAME:MASTER%の後方へ、弾幕が飛び去っていく――
						IF RAND:3 + 4 < 相手強さ
							PRINTFORMW 続く%CALLNAME:MASTER%のあり得ない角度からの反撃を、%CALLNAME:OPPONENT%は垂らした血の滴の数で察知して回避した！
							LOCAL = 0
						ELSE
							PRINTFORMW 驚愕に目を見開く間もなく、%CALLNAME:OPPONENT%は死角から不可解な反撃を受けた！
							LOCAL = -100
						ENDIF
				ENDSELECT
			ENDIF
			PRINTW 
			IF LOCAL > 0
				IF 残りボム回数 >= 1
					PRINTFORML 追い詰められた%CALLNAME:MASTER%はボムを使った！
					CALL UseBomb
					BombPower = RESULT

					SETCOLOR C_RED
					PRINTFORM ダイス値の差:{LOCAL} 
					IF LOCAL <= BombPower
						PRINTFORML ≦ {BombPower}
						PRINTFORMW %CALLNAME:MASTER%はボムで%CALLNAME:OPPONENT%の弾幕を打ち消した！
						LOCAL = 0
					ELSE
						PRINTFORMW ＞ {BombPower}
					ENDIF
					RESETCOLOR
					残りボム回数 --
				ENDIF
			ELSEIF LOCAL < 0
				;【怪力乱神】の効果
				IF GETBIT(CFLAG:OPPONENT:弾幕特能, 4) && (-LOCAL) < 相手強さ * 10
					SETCOLOR C_RED
						PRINTFORML %CALLNAME:OPPONENT%の【怪力乱神】発動！
						PRINTFORMW %CALLNAME:MASTER%の放った弾幕は、%CALLNAME:OPPONENT%を捉える寸前で消滅した！
						;口上呼び出し
						CALL KOJO_MESSAGE_SEND("DANMAKU", 相手残機, OPPONENT, ENEMY, -1, "怪力乱神")
						LOCAL = 0
					RESETCOLOR
				ENDIF
			ENDIF
			IF LOCAL < 0
				CALL COLORMESSAGE(@"撃破した！　やったぜ！",C_YELLOW,1)
				相手残機 --
				;口上呼び出し
				CALL KOJO_MESSAGE_SEND("DANMAKU", 相手残機, OPPONENT, ENEMY, -1, "被弾")
				PRINTFORMW 相手残機: {相手残機}
			ELSEIF LOCAL == 0
				PRINTFORMW お互い決定打を与えられなかったので、仕切り直しだ
			ELSE
				Pichun = 1
			ENDIF
		CASE 2

	ENDSELECT
	IF Pichun
		PRINTFORML ＼ピチューン／
		PRINTFORML 被弾してしまった……
		プレイヤー残機 --
		残りHP = MAXBASE:MASTER:体力
		残りボム回数 = 2
		PRINTFORMW プレイヤー残機: {プレイヤー残機}
		;【ルナシューター】の効果
		SIF TALENT:MASTER:ルナシューター == 1
			残りボム回数 ++
		;【残忍酷薄】の効果
		IF GETBIT(CFLAG:OPPONENT:弾幕特能, 6) && プレイヤー残機 > 0
			CALL COLORMESSAGE(@"%CALLNAME:OPPONENT%の【残忍酷薄】発動！",C_RED,2)
			;【三粒の天滴】の効果
			IF ITEM:三粒の天滴 && !RAND:2
				PRINTFORMW 蛙の神様の加護により【残忍酷薄】を無効化した！
			ELSE
				;口上呼び出し
				CALL KOJO_MESSAGE_SEND("DANMAKU", 相手残機, OPPONENT, ENEMY, -1, "残忍酷薄")
				残りボム回数 --
			ENDIF
		ENDIF
	ENDIF
	Pichun = 0
	ターン数 ++
WEND

PRINTL 
IF プレイヤー残機 <= 0
	PRINTFORMW %CALLNAME:MASTER%は負けてしまった……
	CALL COLORMESSAGE(@"負け犬！",C_RED,2,1)
ELSEIF 相手残機 <= 0
	PRINTFORML %CALLNAME:MASTER%は%CALLNAME:OPPONENT%との勝負に勝った！
	CALL COLORMESSAGE(@"人生の勝利者！",C_GREEN,2,1)
	CALL DANMAKU_GIPS_REWARD
ENDIF
TFLAG:GipsLV = 0
PRINTL 

;楽譜入手処理
IF (RAND:(100) < 30)
	RESULT = 楽譜入手_弾幕勝負(OPPONENT)
	SIF RESULT != -1
		PRINTL
ENDIF

RETURN 相手残機


@DICE_PRINT(ARG:0, ARG:1, ARG:2, ARG:3, ARG:4, ARG:5, ARGS)
IF ARGS == "確定前"
	PRINTFORM %CALLNAME:MASTER%: 
	SETCOLOR C_GREEN
	PRINTFORM {ARG:0}d{ARG:1}
	IF ARG:2 > 0
		SETCOLOR C_RED
		PRINTFORM +{ARG:2}
	ELSEIF ARG:2 < 0
		SETCOLOR C_BLUE
		PRINTFORM {ARG:2}
	ENDIF
	RESETCOLOR
	PRINTFORM (Min:{ARG:0 + ARG:2}, Max:{ARG:0 * ARG:1 + ARG:2})  VS  
	SETCOLOR C_GREEN
	PRINTFORM {ARG:3}d{ARG:4}
	IF ARG:5 > 0
		SETCOLOR C_RED
		PRINTFORM +{ARG:5}
	ELSEIF ARG:5 < 0
		SETCOLOR C_BLUE
		PRINTFORM {ARG:5}
	ENDIF
	RESETCOLOR
	PRINTFORM (Min:{ARG:3 + ARG:5}, Max:{ARG:3 * ARG:4 + ARG:5}) 
	PRINTFORML :%CALLNAME:TARGET%
;ARG:1,2は使わない
ELSEIF ARGS == "確定後"
	PRINTFORM %CALLNAME:MASTER%: 
	SETCOLOR C_GREEN
	PRINTFORM {ARG:0}  
	RESETCOLOR
	PRINTFORM VS  
	SETCOLOR C_GREEN
	PRINTFORM {ARG:3}d{ARG:4}
	IF ARG:5 > 0
		SETCOLOR C_RED
		PRINTFORM +{ARG:5}
	ELSEIF ARG:5 < 0
		SETCOLOR C_BLUE
		PRINTFORM {ARG:5}
	ENDIF
	RESETCOLOR
	PRINTFORM (Min:{ARG:3 + ARG:5}, Max:{ARG:3 * ARG:4 + ARG:5}) 
	PRINTFORML :%CALLNAME:TARGET%
;ARG:2,3,4,5は使わない
ELSEIF ARGS == "勝負"
	PRINTFORM %CALLNAME:MASTER%: 
	SETCOLOR C_GREEN
	PRINTFORM {ARG:0}  
	RESETCOLOR
	PRINTFORM VS  
	SETCOLOR C_GREEN
	PRINTFORM {ARG:1} 
	RESETCOLOR
	PRINTFORML :%CALLNAME:TARGET%
ENDIF


@DICE_VALUE(ARG:0, ARG:1, ARG:2)
#FUNCTION
;初期化しないとダメ
LOCAL:1 = 0
FOR LOCAL, 0, ARG:0
	LOCAL:1 += RAND:(ARG:1) + 1
NEXT

RETURNF LOCAL:1 + ARG:2


@SHOW_DANMAKU_RULE
PRINTFORML 互いにダイスを振り、出た目の大きさを競うというのが基本のルールです。
PRINTFORMW 最初は互いに残機を3つ所持していて、戦闘能力が大きいほどダイスの数と面が増えます。
PRINTFORMW プレイヤーのみ、自分の出目を見た後、回避に専念するか勝負するかを選ぶことができます（このとき、相手の出目は見えません）。
PRINTFORMW 回避に専念することを選んだ場合は、残機が減らない代わりに、出目の差に応じてHPにダメージを受けます。
PRINTFORMW HPが0になると、プレイヤーの残機が1減ります。残機が0になれば、プレイヤーの敗北です。
PRINTFORMW 勝負することを選び、相手より小さい出目だった場合は、無条件でプレイヤーの残機が1減ります。
PRINTFORMW 勝負することを選び、相手より大きい出目を出したときに限り、相手の残機を1減らすことが出来ます。
;残機0ならまだプレイできるじゃんとか言わない
PRINTFORMW プレイヤーの残機が0になる前に、相手の残機を0にすることができれば、プレイヤーの勝ちとなります。
PRINTFORMW また、プレイヤーはボムを使ってダイスの出目をプラスできるので、ここぞという場面で使いましょう。
PRINTFORMW 更に、ダイス勝負に負けてプレイヤーの残機が減る時、ボムを使用して残機が減るのを防ぐことがあります。
PRINTFORMW ただし、残機が減るとボム使用回数が強制的に2になるので、使わないまま残機を減らすのは損になります。


@UseBomb
#DIM BombPower
BombPower = ABL:MASTER:戦闘能力 * (7 + RAND:5) * (ABL:MASTER:戦闘能力 / 2 + 1) + RAND:7

CALL COLORMESSAGE(@"ボム威力 {BombPower}",C_YELLOW,1)
;【ルナシューター】の効果
IF TALENT:MASTER:ルナシューター == 1
	BombPower = BombPower * (RAND:3 + 2)
	CALL COLORMESSAGE(@"【ルナシューター】の効果で、ボム威力が {BombPower}にアップ！",C_YELLOW,2)
ENDIF
;【三粒の天滴】の効果
IF ITEM:三粒の天滴
	;LOCAL = MAX(RAND:750, 400)
	LOCAL = BombPower / 10
	BombPower += LOCAL
	CALL COLORMESSAGE(@"%CALLNAME:MASTER%の懐から、おもむろに無数のカエルが飛び出した！",C_YELLOW,1)
	CALL COLORMESSAGE(@"ボム威力が{LOCAL}アップ！",C_YELLOW,1)
ENDIF
RETURN BombPower

@DANMAKU_ITEM_USE
#DIM 持ち込みアイテム
#DIMS 効果テキスト
持ち込みアイテム = 0
DRAWLINE
PRINTFORML 弾幕勝負補助アイテムを使用しますか？
DRAWLINE
FOR LOCAL,850, 900
	SIF !DANMAKU_ITEM_AVAILABLE(LOCAL)
		CONTINUE
	PRINTFORML [{LOCAL}] - %ITEMNAME:LOCAL% 
NEXT
DRAWLINE
PRINTFORML [999] - 使用しない
INPUT
IF !DANMAKU_ITEM_AVAILABLE(RESULT)
	RETURN 0
ENDIF

持ち込みアイテム = RESULT
PRINTFORML %ITEMNAME:持ち込みアイテム%を持ち込みますか？
PRINTFORML %DANMAKU_ITEM_EXPLANATION(持ち込みアイテム)%
CALL ASK_YN
IF !RESULT
	;先に進む
ELSE
	RESTART
ENDIF
ITEM:持ち込みアイテム -= 1
RETURN 持ち込みアイテム

@DANMAKU_ITEM_EXPLANATION(ARG)
#FUNCTIONS
#DIMS 効果テキスト
SELECTCASE ARG
	CASE 850
		効果テキスト = ダイス面がほんのり良くなる
	CASE 851
		効果テキスト = ダイス数がほんのり増える
	CASE 852
		効果テキスト = ダイス面が少し良くなる
	CASE 853
		効果テキスト = ダイス数が少し増える
	CASE 854
		効果テキスト = ダイス面が少し良くなり、ダイス数が少し増える
	CASE 855
		効果テキスト = 受けるダメージを少し軽減する
	CASE 870
		効果テキスト = ダイスで負けても回避行動を選んだことになる　発動ごとにTSP500消費
	CASE 871
		効果テキスト = 被弾を無効化して相手の残機を減らすチート能力だがTSP消費がべらぼうに激しい
	CASEELSE
		効果テキスト = 
ENDSELECT
RETURNF 効果テキスト

@DANMAKU_ITEM_EFFECT(ARG)
#DIM Dnum
#DIM Dface
Dnum = 0
Dface = 0
SELECTCASE ARG
	CASE 850	;パワフルドラッグ
		Dface = MAX(RAND:((ABL:MASTER:戦闘能力) + 3), 2)
	CASE 851	;アイシクルボム
		Dnum = MAX(RAND:((ABL:MASTER:戦闘能力) + 1), 1)
	CASE 852	;太乙の火種
		Dface = MAX(RAND:((ABL:MASTER:戦闘能力 + 1) * 2), 4)
	CASE 853	;魅惑のエサ
		Dnum = MAX(RAND:((ABL:MASTER:戦闘能力 + 1) * 2), 3)
	CASE 854	;賢者の石
		Dface = MAX(RAND:((ABL:MASTER:戦闘能力 + 1) * 3), 6)
		Dnum = MAX(RAND:((ABL:MASTER:戦闘能力 + 1) * 2), 5)
	CASE 855	;バレットゴーレム
	CASE 870	;エピタフ
	CASE 871	;キンクリ
ENDSELECT
RETURN Dface, Dnum

@DANMAKU_ITEM_AVAILABLE(ARG)
#FUNCTION
SIF !INRANGE(ARG, 850, 900)
	RETURNF 0
SIF DANMAKU_ITEM_EXPLANATION(ARG) == ""
	RETURNF 0
SIF !ITEM:ARG
	RETURNF 0
RETURNF 1

@DANMAKU_GipsEquip(ARGS)
IF ARGS == "椛口上" || ITEM:ルナシューター養成ギプス
	PRINTFORML %CALLNAME:MASTER%はルナシューター養成ギプスを装備し、バックルのツマミに手を掛けた。
	CALL ASK_M("やっぱやめる", 1, "Easy",1, "Normal", 1, "Hard", 1, "Lunatic" , 1, "『危険・手動モード』", 1 )
ELSE
	PRINTFORML %CALLNAME:MASTER%はシューターギプスを装備し、バックルのツマミに手を掛けた。
	CALL ASK_M("やっぱやめる", 1, "Easy",1, "Normal", 1, "Hard", 1)
ENDIF
SELECTCASE RESULT
	CASE 0
		RETURN
	CASE 1
		TFLAG:GipsLV = 1
	CASE 2
		TFLAG:GipsLV = 3
	CASE 3
		TFLAG:GipsLV = 5
	CASE 4
		TFLAG:GipsLV = 10
	CASE 5
		PRINTFORML 負荷レベルを設定してください
		PRINTL 参考負荷レベル　Easy:1　Normal:3　Hard:5　Lunatic:10
		PRINTL [0] [1] [3] [5] [10] [15] [30] [50] [100]
		INPUT
		TFLAG:GipsLV = MAX(RESULT, 0)
ENDSELECT

@DANMAKU_GIPS_REWARD
#DIM Reward
SIF TFLAG:GipsLV <= 0
	RETURN
PRINTFORML ギプスの効果で%CALLNAME:MASTER%は多めに経験値をもらった
Reward = MIN(15, TFLAG:GipsLV)
;SELECTCASE GipsLV
;	CASE IS >= 10
;		Reward = MIN(15, GipsLV)
;	CASE IS >= 5
;		Reward = 4
;	CASE IS >= 3
;		Reward = 2
;	CASEELSE
;		Reward = 1
;ENDSELECT
IF ABL:MASTER:戦闘能力 <= ABL:TARGET:戦闘能力
	IF TFLAG:GipsLV >= 10
		Reward *= 3
		CALL COLORMESSAGE(@"相手が同格以上のため追加経験値が3倍",C_YELLOW,1)
	ELSE
		Reward *= 2
		CALL COLORMESSAGE(@"相手が同格以上のため追加経験値が2倍",C_YELLOW,1)
	ENDIF
ENDIF
CALL AddEXP("戦闘経験" , MASTER, Reward)
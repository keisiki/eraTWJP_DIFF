;-------------------------------------------------
;就寝
;日常系コマンド、レベル1
;-------------------------------------------------
@COM402
#DIM 労働量
#DIM 収益

IF FLAG:内職 && BASE:MASTER:気力 > 300 && BASE:MASTER:TSP > 0
	PRINTFORML %CALLNAME:MASTER%は寝る前に残ったTSPを使って内職をした
	労働量 = MIN(BASE:MASTER:気力, BASE:MASTER:TSP /2)
	収益 = 労働量 * (5 + RAND:5) / 10
	MONEY += 収益
	PRINTFORMW \\{収益}稼いだ
ENDIF
IF FLAG:自動縁結び && !FLAG:願掛け内容 && !CFLAG:添い寝中
	PRINTFORM 寝る前に
	IF MONEY >= 1000
		CALL GANKAKE_0(FLAG:自動縁結び)
		MONEY -= 1000
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:(FLAG:自動縁結び)%と親しくなれるよう神に祈った
	ELSE
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:(FLAG:自動縁結び)%と親しくなれるよう神に祈ろうとしたが賽銭がなかった
	ENDIF
ENDIF

IF 子供の睡眠時間(TARGET) && CFLAG:出産経過日 >= CHILD_よちよち && FLAG:子供描写 && !INRANGE(TIME, 540, 840)
	PRINTFORMDL 家族三人で川の字になって寝る事にした…
	CALL KOJO_MESSAGE_SEND("EVENT", 0, TARGET, 0, 0, "CHILD_RAISING_OYASUMI")
	TFLAG:193 = 1
	RESETCOLOR
ENDIF

CALL OYASUMI

RETURN 1

;就寝
@COM_ABLE402
;移動実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(402)
	RETURN RESULT
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;私室設定
SIF CFLAG:MASTER:現在位置 != CFLAG:MASTER:311 && CFLAG:MASTER:現在位置 != OMANEKIBEYA() && CFLAG:添い寝中 == 0
	RETURN 0
;起床後12時間以上経過してないとダメ（デバッグ中）
IF FLAG:1005 == 0
SIF NEMUKE() < 720
	RETURN 0
SIF CFLAG:うふふ == 2
	RETURN 0
ENDIF
RETURN 1

@OYASUMI
IF TARGET > 0
	CFLAG:同行 = 0
	CFLAG:添い寝中 = 0
	CFLAG:MASTER:同行 = 0
	CFLAG:MASTER:添い寝中 = 0
ENDIF

IF CFLAG:MASTER:現在位置 == CFLAG:MASTER:311
	CALL SET_SLEEP(1,MASTER,0)
ELSE
	CALL SET_SLEEP(1,TARGET,1)
	SIF CFLAG:TARGET:衰弱
		CFLAG:TARGET:衰弱 = 0
ENDIF

CALL SET_SNEAK(0,TARGET,1)
;CALL SET_TOGETHER(0,TARGET,1)

CFLAG:MASTER:就寝時間 = DAY * 1440 + TIME
BEGIN AFTERTRAIN
;-------------------------------------------------
;女の子を物色
;日常系コマンド、レベル1
;-------------------------------------------------

@COM444
#DIMS YUKIZURI_RACE  ;生成するMOB娘の種類

IF FLAG:デート相手
	PRINTFORMW 物凄く可愛い娘がすぐ隣にいる
	RETURN -1
ENDIF

;潜伏モード中は不可
IF GETBIT(FLAG:潜伏,0)
	PRINTFORMW 他の子に目移りしている場合ではない
	RETURN -1
ENDIF

$BUSSYOKU
IF !FLAG:70
	TIME += 3
ELSE
	BASE:MASTER:TSP -= 10
ENDIF
PRINTFORML %CALLNAME:MASTER%は可愛い娘がいないか物色した
PRINTFORMW ……あの子はどうだろう？
SETBIT TFLAG:一日一回, 9
SELECTCASE CAN_NANPA(CFLAG:MASTER:現在位置)
	CASE 1
		YUKIZURI_RACE '= TEXTR("町娘/人妻")
	CASE 2
		YUKIZURI_RACE = 妖精
	CASE 3
		YUKIZURI_RACE = メイド妖精
	CASE 4
		;３日に一度販売員が出没
		IF ((GETTIME()/1000000000) % 100) % 3 == 1 && !RAND:7
			YUKIZURI_RACE = 販売員
		ELSE
			YUKIZURI_RACE = イナバ
		ENDIF
	CASE 5
		YUKIZURI_RACE = ゾンビフェアリー
	CASE 6
		YUKIZURI_RACE '= TEXTR("化け狐/化け猫")
	CASE 7
		YUKIZURI_RACE = 河童
	CASE 8
		YUKIZURI_RACE = 白狼天狗
	CASE 9
		IF !RAND:10
			YUKIZURI_RACE = 鬼
		ELSE
			YUKIZURI_RACE = ゾンビフェアリー
		ENDIF
	CASE 10
		YUKIZURI_RACE = 玉兎
	CASE 11
		YUKIZURI_RACE '= TEXTR("化け狸/茨たぬき")
ENDSELECT



;PRINTFORMW %YUKIZURI_RACE%
CALL MAKE_YUKIZURI(RANDOM_CHARANUM, YUKIZURI_RACE)
TEQUIP:RANDOM_CHARANUM:上半身着衣状況 = 1
$CHOICE
CALL CHARA_STATE(RANDOM_CHARANUM)
IF CSTR:RANDOM_CHARANUM:処女喪失履歴 != ""
	DRAWLINE
	CALL COLORMESSAGE(@"処女喪失履歴：%CSTR:RANDOM_CHARANUM:処女喪失履歴%",C_PINK,1)
	PRINTFORML 
ENDIF
DEBUGPRINTFORML %CSTR:RANDOM_CHARANUM:モブ子画像文字列_服_通常%
CALL ASK_M("気になる",1,"別の娘を探す",1,"直前の娘を探す", TFLAG:直前モブ子マップ == CFLAG:MASTER:現在位置 /100,"以前知り合った娘を探す",1,"物色中止",1)
IF RESULT == 4
	CFLAG:RANDOM_CHARANUM:現在位置 = SUKIMA()
	CFLAG:RANDOM_CHARANUM:着替え実行 = 0
	CFLAG:RANDOM_CHARANUM:出禁 = 2
	SIF TARGET == RANDOM_CHARANUM
		TARGET = 0
	RETURN -1
ENDIF
IF RESULT == 1
	TFLAG:直前モブ子マップ = CFLAG:MASTER:現在位置 /100
	SAVECHARA "TEMP_MOB", "直前に会った子", RANDOM_CHARANUM
	GOTO BUSSYOKU
ELSEIF RESULT == 3
	CALL LOAD_MOBGIRL(0)
	IF RESULT == -1
		;念のためまっさらにしておく
		CALL モブ子画像リセット(RANDOM_CHARANUM)
		DELCHARA RANDOM_CHARANUM
		ADDCHARA RANDOM_CHARANUM
		RETURN -1
	ENDIF
	CFLAG:RANDOM_CHARANUM:出禁 = 0
ELSEIF RESULT == 2
	CALL モブ子画像リセット(RANDOM_CHARANUM)
	DELCHARA RANDOM_CHARANUM
	LOADCHARA "TEMP_MOB"
	TFLAG:直前モブ子マップ = 0
	GOTO CHOICE
ENDIF
CFLAG:RANDOM_CHARANUM:現在位置 = CFLAG:MASTER:現在位置
CFLAG:RANDOM_CHARANUM:デート中 = MAIN_MAP

IF CFLAG:MASTER:現在位置 / 100 == MAIN_MAP
	;拠点内で時間停止せずに物色した場合は、同行扱い
	IF !FLAG:70
		CFLAG:RANDOM_CHARANUM:同行 = 180
		CFLAG:MASTER:同行 = 180
	ENDIF
	CFLAG:RANDOM_CHARANUM:拠点来訪 = 1
	CFLAG:RANDOM_CHARANUM:遭遇位置 = 0
	;CFLAG:RANDOM_CHARANUM:自宅位置 = 0
	CFLAG:RANDOM_CHARANUM:来訪時間 = CSVCFLAG(RANDOM_CHARANUM,352)
	CFLAG:RANDOM_CHARANUM:帰宅時間 = CSVCFLAG(RANDOM_CHARANUM,353)
	CFLAG:RANDOM_CHARANUM:就寝時間 = CSVCFLAG(RANDOM_CHARANUM,354)
	CFLAG:RANDOM_CHARANUM:起床時間 = CSVCFLAG(RANDOM_CHARANUM,355)
ELSE
	;引っ越ししたりして定義しなおす場合もあるので、毎回確認
	CFLAG:RANDOM_CHARANUM:拠点来訪 = 0
	CFLAG:RANDOM_CHARANUM:遭遇位置 = CFLAG:MASTER:現在位置
	;CFLAG:RANDOM_CHARANUM:自宅位置 = CFLAG:MASTER:現在位置
	CFLAG:RANDOM_CHARANUM:来訪時間 = 0
	CFLAG:RANDOM_CHARANUM:帰宅時間 = 0
	CFLAG:RANDOM_CHARANUM:就寝時間 = 0
	CFLAG:RANDOM_CHARANUM:起床時間 = 0
ENDIF
CFLAG:RANDOM_CHARANUM:ディレイ = 30
TARGET = RANDOM_CHARANUM
RETURN 1


@COM_ABLE444
;一括管理
SIF GLOBAL_COMABLE(622)
	RETURN RESULT
SIF !CAN_NANPA(CFLAG:MASTER:現在位置)
	RETURN 0
SIF !INRANGE(TIME,540,1140)
	RETURN 0
;SIF !CFLAG:RANDOM_CHARANUM:出禁
;	RETURN 0
RETURN 1

@CAN_NANPA(ARG)
#FUNCTION

SIF ARG == GET_MAP_REPLACEMENT(202)
	RETURNF 1
SIF ARG == GET_MAP_REPLACEMENT(438)
	RETURNF 2
SIF ARG == GET_MAP_REPLACEMENT(309)
	RETURNF 3
SIF ARG == GET_MAP_REPLACEMENT(415)
	RETURNF 4
SIF ARG == GET_MAP_REPLACEMENT(416)
	RETURNF 4
SIF ARG == GET_MAP_REPLACEMENT(431)
	RETURNF 4
SIF ARG == GET_MAP_REPLACEMENT(434)
	RETURNF 4
SIF ARG == GET_MAP_REPLACEMENT(927)
	RETURNF 5
SIF ARG == GET_MAP_REPLACEMENT(928)
	RETURNF 5
SIF ARG == GET_MAP_REPLACEMENT(601)
	RETURNF 6
SIF ARG == GET_MAP_REPLACEMENT(704)
	RETURNF 7
SIF ARG == GET_MAP_REPLACEMENT(806)
	RETURNF 8
SIF ARG == GET_MAP_REPLACEMENT(938)
	RETURNF 9
SIF ARG == GET_MAP_REPLACEMENT(1003)
	RETURNF 10
SIF ARG == GET_MAP_REPLACEMENT(540)
	RETURNF 11

RETURNF 0


@PRINT_SAVED_GIRL_LIST
DRAWLINE
FOR LOCAL,0,MAXMOB
	IF FIND_CHARADATA(TOSTR(LOCAL))
		CHKCHARADATA RESULTS:0
		SIF MOBGIRL_PLACE:LOCAL == CFLAG:MASTER:現在位置 / 100
			SETCOLOR C_P_GREEN
		SIF MOBGIRL_PLACE:LOCAL == 90
			SETCOLOR C_PINK
		PRINTBUTTON @"[%RESULTS:0%]", LOCAL
		PRINTFORML 
	ELSE
		PRINTBUTTON "[なし]", LOCAL
		PRINTFORML 
	ENDIF
	RESETCOLOR
NEXT
DRAWLINE
PRINTBUTTON "[やめる]", 99

@LOAD_MOBGIRL(ARG)
CALL PRINT_SAVED_GIRL_LIST
INPUT
LOCAL = RESULT
SIF !INRANGE(LOCAL, 0, MAXMOB -1)
	RETURN -1
IF !ARG && MOBGIRL_PLACE:LOCAL != CFLAG:MASTER:現在位置 / 100
	IF MOBGIRL_PLACE:LOCAL == 90
		PRINTFORMW 店の外では会ってくれない…
	ELSE
		PRINTFORML {MOBGIRL_PLACE:LOCAL}
		PRINTFORMW ここにはいないだろう
	ENDIF
	RETURN -1
ELSEIF ARG && MOBGIRL_PLACE:LOCAL != 90
	PRINTFORMW 「はぁ！？　しょんなメスっこ、うちにゃーいねーぎゃ！」
	PRINTFORML 
	RETURN -1
ENDIF
IF FIND_CHARADATA(TOSTR(LOCAL))
	CALL モブ子画像リセット(RANDOM_CHARANUM)
	DELCHARA RANDOM_CHARANUM
	LOADCHARA TOSTR(LOCAL)
	PRINTFORML %CALLNAME:RANDOM_CHARANUM%を\@ ARG ? 指名した # 見つけた \@
	IF CSTR:RANDOM_CHARANUM:モブ子画像文字列_服_通常 == ""
		PRINTFORML 外見が未設定です。タイプを選んでください。
		PRINTBUTTON "[町娘] ", "町娘"
		PRINTBUTTON "[人妻] ", "人妻"
		PRINTBUTTON "[妖精] ", "妖精"
		PRINTBUTTON "[メイド妖精] ", "メイド妖精"
		PRINTBUTTON "[ゾンビフェアリー] ", "ゾンビフェアリー"
		PRINTBUTTON "[河童] ", "河童"
		PRINTBUTTON "[白狼天狗] ", "白狼天狗"
		PRINTBUTTON "[イナバ] ", "イナバ"
		PRINTBUTTON "[玉兎] ", "玉兎"
		PRINTBUTTON "[鬼] ", "鬼"
		PRINTBUTTON "[化け狸] ", "化け狸"
		PRINTBUTTON "[化け狐] ", "化け狐"
		PRINTBUTTON "[化け猫] ", "化け猫"
		PRINTBUTTON "[ランダム] ", "ランダム"
		INPUTS
		CALL SET_MOB_IMAGE_STR(RANDOM_CHARANUM, RESULTS)
	ENDIF
	;過去に合成されて裸画像の笑顔に[装飾_照れ]が乗ってない場合に追記
	SIF STRFIND(CSTR:RANDOM_CHARANUM:モブ子画像文字列_裸_笑顔, "装飾_照れ") == -1
		CSTR:RANDOM_CHARANUM:モブ子画像文字列_裸_笑顔 += "[装飾9:装飾_照れ][乗色:朱色]"

	;過去（ver4.760以前）に保存されて種族のTALENT設定が適合しない場合に修正を行う
	SELECTCASE CAN_NANPA(CFLAG:MASTER:現在位置)
		;里
		CASE 1
			;以前の194 人間が現varでは194 幽霊になっている
			IF TALENT:RANDOM_CHARANUM:幽霊
				TALENT:RANDOM_CHARANUM:幽霊 = 0
				TALENT:RANDOM_CHARANUM:人間 = 1
			ENDIF
		;霧の湖、紅魔館、および地底の妖精たち
		CASE 2, 3, 5
			;以前の190 妖精が現verでは190 人間になっている
			IF TALENT:RANDOM_CHARANUM:人間
				TALENT:RANDOM_CHARANUM:人間 = 0
				TALENT:RANDOM_CHARANUM:妖精 = 1
			ENDIF
		;竹林
		CASE 4
			;イナバ…妖獣が新設された
			SIF TALENT:RANDOM_CHARANUM:妖怪 != 6
				TALENT:RANDOM_CHARANUM:妖怪 = 6
		;冥界
		CASE 6
			;化け狸、狐、猫…妖獣が新設された
			SIF TALENT:RANDOM_CHARANUM:妖怪 != 6
				TALENT:RANDOM_CHARANUM:妖怪 = 6
		;妖怪の山山麓
		CASE 7
			;河童…河童が新設された
			SIF TALENT:RANDOM_CHARANUM:妖怪 != 4
				TALENT:RANDOM_CHARANUM:妖怪 = 4
		;妖怪の山山頂
		CASE 8
			;白狼天狗…以前の197 天狗が現verでは197 追加種族（巫女）になっている
			IF TALENT:RANDOM_CHARANUM:追加種族
				TALENT:RANDOM_CHARANUM:追加種族 = 0
				TALENT:RANDOM_CHARANUM:妖怪 = 5
			ENDIF
		;地底　鬼か妖精
		CASE 9
			;鬼…以前の195 鬼が現verでは195　付喪神になっている
			IF TALENT:RANDOM_CHARANUM:付喪神
				TALENT:RANDOM_CHARANUM:付喪神 = 0
				TALENT:RANDOM_CHARANUM:妖怪 = 2
			;妖精は同上
			ELSEIF TALENT:RANDOM_CHARANUM:人間
				TALENT:RANDOM_CHARANUM:人間 = 0
				TALENT:RANDOM_CHARANUM:妖精 = 1
			ENDIF
		;月　一応分けておく
		CASE 10
			;玉兎…妖獣が新設された
			SIF TALENT:RANDOM_CHARANUM:妖怪 != 6
				TALENT:RANDOM_CHARANUM:妖怪 = 6
	ENDSELECT

	IF FLAG:70	;時間停止中と風俗嬢は反応なし
	ELSEIF TALENT:RANDOM_CHARANUM:風俗嬢
	ELSEIF CFLAG:RANDOM_CHARANUM:負け犬 && (TALENT:RANDOM_CHARANUM:セフレ || TALENT:RANDOM_CHARANUM:思慕)
		PRINTFORMW %CALLNAME:MASTER%の姿を認めた%CALLNAME:RANDOM_CHARANUM%は悔しげな、それでいて期待するような表情でもじもじしている…
	ELSEIF CFLAG:RANDOM_CHARANUM:負け犬 && !TALENT:RANDOM_CHARANUM:恋慕 && !TALENT:RANDOM_CHARANUM:思慕 && !TALENT:RANDOM_CHARANUM:愛欲
		PRINTFORMW %CALLNAME:MASTER%の姿を認めた%CALLNAME:RANDOM_CHARANUM%は表情を強ばらせた
	ELSEIF TALENT:RANDOM_CHARANUM:恋慕
		PRINTFORMW %CALLNAME:MASTER%の姿を認めた%CALLNAME:RANDOM_CHARANUM%は嬉しそうにこちらに駆け寄ってきた
	ELSEIF TALENT:RANDOM_CHARANUM:愛欲
		PRINTFORMW %CALLNAME:MASTER%の姿を認めた%CALLNAME:RANDOM_CHARANUM%はこちらにやって来るなり、腕にしなだれかかってきた…
	ELSEIF ABL:RANDOM_CHARANUM:親密 >= 5
		PRINTFORMW %CALLNAME:MASTER%の姿を認めた%CALLNAME:RANDOM_CHARANUM%は手を振ってきた
	ENDIF
ELSE
	RETURN -1
ENDIF

@SAVE_MOBGIRL
#DIM CHOSEN_MOB
#DIMS MOBNAME

$LOOP
CALL PRINT_SAVED_GIRL_LIST
INPUT
SIF RESULT == 99
	RETURN -1
SIF !INRANGE(RESULT, 0, MAXMOB - 1)
	GOTO LOOP

CHOSEN_MOB = RESULT
IF FIND_CHARADATA(TOSTR(CHOSEN_MOB))
	CHKCHARADATA RESULTS:0
	STRFIND RESULTS:0, "：" 
	SUBSTRING RESULTS:0, 0, RESULT
	MOBNAME = %RESULTS:0%
	PRINTFORML %MOBNAME%の事を忘れて、代わりに%CALLNAME:RANDOM_CHARANUM%のことを覚えますか？
	CALL ASK_YN
	IF !RESULT
		PRINTFORM %CALLNAME:MASTER%は薄情にも%MOBNAME%のことを奇麗さっぱり忘れ、
	ELSE
		GOTO LOOP
	ENDIF
ENDIF
MOBGIRL_PLACE:CHOSEN_MOB = CFLAG:RANDOM_CHARANUM:よく行く地域
SIF TALENT:RANDOM_CHARANUM:風俗嬢
	MOBGIRL_PLACE:CHOSEN_MOB = 90
TALENT:RANDOM_CHARANUM:行きずり = 2
CFLAG:RANDOM_CHARANUM:モブ子セーブスロット = CHOSEN_MOB
SAVECHARA TOSTR(CHOSEN_MOB), CSTR:RANDOM_CHARANUM:モブ子プロフィール１, RANDOM_CHARANUM
PRINTFORMW %CALLNAME:RANDOM_CHARANUM%のことを\@ TALENT:RANDOM_CHARANUM:風俗嬢 ? お気にとしてキープした # 覚えた \@


;-------------------------------------------------
;モブ子プロフィール２を更新する
;負け犬でなければ、その日の過ごし方の様子でプロフィール２を追記
;負け犬の場合は、陥落状態に合わせて更新
;-------------------------------------------------
@MOBGIRL_PROFILE2()
#DIM 回数

回数 = EXP:RANDOM_CHARANUM:Ｖ性交経験 + EXP:RANDOM_CHARANUM:Ａ性交経験

;負け犬でない場合は、性的関係の進展でテキストを構成
IF CFLAG:RANDOM_CHARANUM:負け犬 == 0 
	;出会って日が浅い
	IF DAY:0 - PRINTDATEF_TO_DAY(CSTR:RANDOM_CHARANUM:モブ子プロフィール１) <= 30
		CSTR:RANDOM_CHARANUM:モブ子プロフィール２ = 出会ったばかりの%CALLNAME:MASTER%
	ELSE
		CSTR:RANDOM_CHARANUM:モブ子プロフィール２ = %CALLNAME:MASTER%
	ENDIF
	;にゅーぷらん
	IF TALENT:RANDOM_CHARANUM:風俗嬢
		SIF GROUPMATCH(TALENT:RANDOM_CHARANUM:風俗嬢, 1, 2) && CFLAG:RANDOM_CHARANUM:1001 == 3
			TALENT:RANDOM_CHARANUM:風俗嬢 += 2
		IF CFLAG:RANDOM_CHARANUM:1001  > 6
			LOCALS = にすっかり情が移ってしまい、もはや仕事か本気か区別がつかなくなってしまった
		ELSEIF CFLAG:RANDOM_CHARANUM:1001  > 3
			LOCALS = に単なる客以上の感情を持ち、奉仕が出来ることに悦びを覚えている
		ELSE
			LOCALS = を上客と見なしリピートの指名を嬉しく思っている
		ENDIF
	;生解禁
	ELSEIF CFLAG:RANDOM_CHARANUM:生解禁
		IF TALENT:RANDOM_CHARANUM:人妻 == 1
			LOCALS = に夢中になってしまい、夫や家庭を顧みない、爛れた間柄となっている
		ELSE
			LOCALS = を雌として本能的に受け入れ、膣内射精で子を孕んでもいいとすら感じている
		ENDIF
	;合意ックス
	ELSEIF GETBIT(CFLAG:RANDOM_CHARANUM:既成事実 , 1)
		IF TALENT:RANDOM_CHARANUM:人妻 == 1
			LOCALS = を持て余した情欲のはけ口にしているようだ
		ELSE
			LOCALS = に惹かれ、体を許してしまった
		ENDIF
	;睡眠姦(一応)
	ELSEIF EXP:RANDOM_CHARANUM:睡眠姦経験
		LOCALS = の策に嵌り、眠っている間に体を好きにされてしまった
	;合意なしックス
	ELSEIF CFLAG:RANDOM_CHARANUM:ヤラせちゃった
		IF TALENT:RANDOM_CHARANUM:人妻 == 1
			LOCALS = の夫にはない魅力に抗えないでいるようだ
		ELSE
			LOCALS = と流されるがままに淫行に及んでしまった
		ENDIF
	;不埒刻印
	ELSEIF MARK:RANDOM_CHARANUM:不埒刻印
		IF TALENT:RANDOM_CHARANUM:人妻 == 1
			LOCALS = にいけないと思いながらも性的な目線を向けている
		ELSE
			LOCALS = に性的な興味を持ち始めたようだ
		ENDIF
	;時姦刻印
	ELSEIF MARK:RANDOM_CHARANUM:時姦刻印
		LOCALS = の目に留まり、止まった時の中で体を開発されてしまった
	ELSE
		LOCALS = の印象はまずまずのようだ
	ENDIF
	CSTR:RANDOM_CHARANUM:モブ子プロフィール２ += LOCALS

;負け犬の場合は、陥落強度に応じてテキストを構成
ELSE
	IF TALENT:RANDOM_CHARANUM:恋慕
		CSTR:RANDOM_CHARANUM:モブ子プロフィール２ = 弾幕ごっこの勝敗などどうでもよく、今や%CALLNAME:MASTER%に心の底から惚れこんでいるようだ
	ELSEIF TALENT:RANDOM_CHARANUM:愛欲
		IF GETBIT(TALENT:MASTER:性別, 1)
			IF 回数 >= 75
				CSTR:RANDOM_CHARANUM:モブ子プロフィール２ = %CALLNAME:MASTER%ちんぽに完全に屈服し、再び犯されるのを今か今かと心待ちにしている
			ELSE
				CSTR:RANDOM_CHARANUM:モブ子プロフィール２ = %CALLNAME:MASTER%に完全に屈服し、再び愛されるのを今か今かと心待ちにしている
			ENDIF
		ELSE
			CSTR:RANDOM_CHARANUM:モブ子プロフィール２ = かつての威勢はどこへやら、心もカラダも%CALLNAME:MASTER%色にどっぷり染められてしまっている
		ENDIF
	ELSEIF TALENT:RANDOM_CHARANUM:思慕
		CSTR:RANDOM_CHARANUM:モブ子プロフィール２ = %CALLNAME:MASTER%の腕っぷしだけでなく、人となりが気になり始めたようだ
	ELSEIF TALENT:RANDOM_CHARANUM:セフレ
		CSTR:RANDOM_CHARANUM:モブ子プロフィール２ = %CALLNAME:MASTER%に散々に鳴かされ、肉欲の虜に堕ちてしまっている
	ELSEIF MARK:RANDOM_CHARANUM:快楽刻印
		CSTR:RANDOM_CHARANUM:モブ子プロフィール２ = 口では否定しつつも、体は正直に快楽に反応してしまっているようだ
	ENDIF
ENDIF

;-------------------------------------------------
;PRINT_DATE_Fの●年目 ■の★日の文字列を
;DAY:0（総日数）と同じ日数に読み替える関数
;ARGS CSTR:RANDOM_CHARANUM:モブ子プロフィール１を
;代入して、出会った日付を割り出す使用前提
;-------------------------------------------------
@PRINTDATEF_TO_DAY(ARGS)
#FUNCTION
#DIM YEAR
#DIM MOUTH
#DIMS 季
#DIM DAYS
#DIMS カット
#DIM TOTAL

SPLIT ARGS, "：", LOCALS
カット = %LOCALS:1%
SPLIT カット, "年目 ", LOCALS
カット = %LOCALS:1%
YEAR = TOINT(LOCALS:0)
SPLIT カット, "の月の", LOCALS
季 = %LOCALS:0%
カット = %LOCALS:1%
SPLIT カット, "日", LOCALS
DAYS = TOINT(LOCALS:0)
SELECTCASE 季
	CASE "春"
		MOUTH = 0
	CASE "夏"
		MOUTH = 31
	CASE "秋"
		MOUTH = 62
	CASE "冬"
		MOUTH = 93
ENDSELECT
TOTAL = (YEAR - 1) * 124 + MOUTH + DAYS
RETURNF TOTAL


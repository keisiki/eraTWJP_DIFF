;ムシバトル
@COM483
#DIM CHARA
#DIM 同室人数
#DIM 参加可能人数
#DIM 順位
VARSET MUSHI_BATTLER
VARSET MUSHI_TEAM
VARSET MUSHI_POWER
;TARGETがメイン
IF TCVAR:TARGET:ムシバトル参加可否 == -1
	PRINTFORML %CALLNAME:TARGET%のムシたちは疲れている。バトルはやめておこう。
	RETURN -1
ENDIF

MUSHI_BATTLER:1 = MASTER
;参加可能人数チェック
参加可能人数 = 0
同室人数 = GET_TARGETNUM()
FOR LOCAL, 1, 同室人数 + 1
	CHARA = TARGET:LOCAL
	SIF !SHIRAHU(CHARA)
		CONTINUE
	SIF CFLAG:CHARA:行動 == 5
		CONTINUE
	SIF BASE:CHARA:気力 < 500 || BASE:CHARA:体力 < 500
		CONTINUE
	SIF TCVAR:CHARA:ムシバトル参加可否 == -1
		CONTINUE
	参加可能人数 ++
NEXT
;TARGETは実行判定で可能なはずなのでエラーチェック
IF 参加可能人数 == 0
	THROW
;参加者一人（TARGETのみ）
ELSEIF 参加可能人数 == 1
	;一人の時は確認をとる
	MUSHI_CHARANUM = 2
	CHARA = TARGET
	PRINTFORML %CALLNAME:CHARA%と対戦します　\@ CFLAG:CHARA:ムシバトル直接勝利 ? ★直接勝利済み # \@
	PRINTFORM %CALLNAME:CHARA%のムシバトルパワー：
	CALL COLOR_STAMP(MBP_RANK(CHARA) + 1, 0, "★", RANKCOLOR(MBP_RANK(CHARA) + 1), 1)
	PRINTL 
	;＠試合前口上
	CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 0, CHARA, 0, 0, "口上色")
	CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 0, CHARA, 0, 0, "試合前")
	PRINTFORML %RESULTS%
	RESETCOLOR
	PRINTL [1] 対戦開始！
	PRINTL [0] やっぱりやめる
	INPUT
	SIF !RESULT
		RETURN -1
	;1はMASTER
	MUSHI_BATTLER:2 = CHARA
	TCVAR:CHARA:ムシバトル参加可否 = 2
ELSE
	MUSHI_CHARANUM = 1
	CALL MUSHIBATTLE_CHARASELECT(同室人数, 参加可能人数)
	;キャンセル
	SIF RESULT == -1
		RETURN -1
ENDIF

;セットアップ１
CALL MUSHI_BATTLE_SETUP_1
;見せ合い＆ムシ選出
CALL MUSHI_BATTLE_SELECTION
;セットアップ２
CALL MUSHI_BATTLE_SETUP_2
;TARGETを変更しておく
IF MUSHI_TEAM
	TARGET = MUSHI_BATTLER:3
ELSE
	TARGET = MUSHI_BATTLER:2
ENDIF
;試合開始！
CALL MUSHI_BATTLE
;結果発表
CALL MUSHI_BATTLE_RESULT

;ソース獲得
DOWNBASE:MASTER:気力 += 400
DOWNBASE:MASTER:体力 += 200
TFLAG:194 = TCVAR:MASTER:ムシバトル参加可否 % 10
FOR LOCAL, 2, MUSHI_CHARANUM + 1
	CHARA = MUSHI_BATTLER:LOCAL
	DOWNBASE:CHARA:気力 += 400
	DOWNBASE:CHARA:体力 += 200
	順位 = TCVAR:CHARA:ムシバトル参加可否 % 10
	SELECTCASE TFLAG:193
	;タイマン
	CASE 1
		IF 順位 == 1
			SOURCE:CHARA:歓楽 = 2000 + ABL:CHARA:親密 * 50
			SOURCE:CHARA:征服 = 400 + 120 * ABL:CHARA:サドっ気
		ELSE
			SOURCE:CHARA:歓楽 = 1000 + ABL:CHARA:親密 * 100
			SOURCE:CHARA:受動 = 400 + 120 * ABL:CHARA:従順
			TFLAG:信頼度管理 = 1
			;初勝利
			IF !CFLAG:CHARA:ムシバトル直接勝利
				CFLAG:CHARA:ムシバトル直接勝利 = 1
				TFLAG:好感度ボーナス = 100
				TFLAG:信頼度管理  += 1
			ENDIF
		ENDIF
	;バトルロイヤル
	CASE 2
		IF 順位 == 1
			SOURCE:CHARA:歓楽 = 2500 + ABL:CHARA:親密 * 60
			SOURCE:CHARA:征服 = 600 + 150 * ABL:CHARA:サドっ気
		ELSEIF 順位 == 2
			SOURCE:CHARA:歓楽 = 1500 + ABL:CHARA:親密 * 80
			SOURCE:CHARA:受動 = 200 + 80 * ABL:CHARA:従順
		ELSEIF 順位 == 3
			SOURCE:CHARA:歓楽 = 1200 + ABL:CHARA:親密 * 90
			SOURCE:CHARA:受動 = 300 + 100 * ABL:CHARA:従順
		ELSE
			SOURCE:CHARA:歓楽 = 900 + ABL:CHARA:親密 * 100
			SOURCE:CHARA:受動 = 400 + 120 * ABL:CHARA:従順
		ENDIF
	;チーム戦
	CASE 3
		IF 順位 == 1
			SOURCE:CHARA:歓楽 = 1600 + ABL:CHARA:親密 * 50
			SOURCE:CHARA:征服 = 200 + 80 * ABL:CHARA:サドっ気
		ELSE
			SOURCE:CHARA:歓楽 = 1200 + ABL:CHARA:親密 * 80
			SOURCE:CHARA:受動 = 200 + 80 * ABL:CHARA:従順
		ENDIF
		IF CHARA == TARGET
			TIMES SOURCE:CHARA:歓楽, 2.00
			TIMES SOURCE:CHARA:受動, 2.00
			TIMES SOURCE:CHARA:征服, 2.00
			TFLAG:信頼度管理 = 1
		ENDIF
	ENDSELECT
NEXT

;今日はもう戦えない
FOR LOCAL, 1, 同室人数 + 1
	CHARA = TARGET:LOCAL
	SIF TCVAR:CHARA:ムシバトル参加可否 > 1
		TCVAR:CHARA:ムシバトル参加可否 = -1
NEXT

;時間経過
TIME += 60

RETURN 1

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;ムシバトル
@COM_ABLE483
;時間停止中
SIF FLAG:時間停止
	RETURN 0
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(483)
	RETURN RESULT
;昆虫採集セットがない
SIF !ITEM:昆虫採集セット
	RETURN 0
;昆虫採集できる場所である
SIF !MUSHITORI_SPOT(CFLAG:MASTER:現在位置)
	RETURN 0
;冬の間はおやすみ
SIF GET_MONTH() == "冬の月"
	RETURN 0
;虫を持ってない
SIF !MUSHI_CAGE:1
	RETURN 0
;対象がいない
SIF TARGET <= 0
	RETURN 0
;意識がない
SIF !SHIRAHU(TARGET)
	RETURN 0
;添い寝中
SIF CFLAG:MASTER:添い寝中
	RETURN 0
;うふふ中
SIF CFLAG:MASTER:うふふ
	RETURN 0
;仕事中
SIF CFLAG:行動 == 5
	RETURN 0
;体力・気力不足
SIF BASE:MASTER:気力 < 500 || BASE:MASTER:体力 < 500
	RETURN 0
;体力・気力不足
SIF BASE:TARGET:気力 < 500 || BASE:TARGET:体力 < 500
	RETURN 0
RETURN 1

;-------------------------------------------------
;ムシバトル：キャラクターセレクト関数
;ARG 同室人数
;TCVAR:ムシバトル参加可否 =-1,参加不可, =1,参加可能, =2～4,参加エントリー
;-------------------------------------------------
@MUSHIBATTLE_CHARASELECT(ARG, 参加可能人数)
#DIM CHARA
#DIM 参加可能人数
#DIM LINE
LINE = 7
PRINTFORML ＜＜参加者を選べ！！（最大%TOFULL(TOSTR(MUSHI_MAXBATTLE))%人）＞＞
SETCOLOR C_AQUA
PRINTPLAINFORM [  0] %CALLNAME:MASTER, 16, LEFT%★？Ｐ１
PRINTL 
RESETCOLOR
FOR LOCAL, 1, ARG + 1
	CHARA = TARGET:LOCAL
	SIF !SHIRAHU(CHARA)
		CONTINUE
	SIF TCVAR:CHARA:ムシバトル参加可否 == -1
		CONTINUE
	IF TCVAR:CHARA:ムシバトル参加可否 > 1
		SETCOLOR C_AQUA
		PRINTFORM [{CHARA, 3}] %CALLNAME:CHARA, 16, LEFT%
		CALL PRINT_MBP_RANK(CHARA)
		PRINTFORM Ｐ%TOFULL(TOSTR(TCVAR:CHARA:ムシバトル参加可否))%
		SIF MUSHI_TEAM && TCVAR:CHARA:ムシバトル参加可否 == 3
			PRINTFORM (仲間)
		;＠試合前口上
		CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 0, CHARA, 0, 0, "口上色")
		CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 0, CHARA, 0, 0, "試合前")
		PRINTFORML %RESULTS%
		RESETCOLOR
	ELSE
		TCVAR:CHARA:ムシバトル参加可否 = 1
		PRINTFORM [{CHARA, 3}] %CALLNAME:CHARA, 16, LEFT%
		CALL PRINT_MBP_RANK(CHARA)
		PRINTL 
	ENDIF
	LINE ++
NEXT
DRAWLINE
SIF MUSHI_TEAM
	SETCOLOR C_AQUA
SIF 参加可能人数 < 3
	SETCOLOR C_GRAY
PRINTL [901] チーム戦（Ｐ１＆Ｐ３ VS Ｐ２＆Ｐ４）
RESETCOLOR
IF MUSHI_BATTLER:2
	PRINTL [900] 対戦開始！
ELSE
	PRINTL 
ENDIF
PRINTL [999] やっぱりやめる
INPUT

;やめる
IF RESULT == 999
	;参加可否をリセットする（-1以外）
	FOR LOCAL, 1, ARG + 1
		CHARA = TARGET:LOCAL
		SIF TCVAR:CHARA:ムシバトル参加可否 > 0
			TCVAR:CHARA:ムシバトル参加可否 = 0
	NEXT
	RETURN -1
;対戦開始
ELSEIF RESULT == 900
	IF MUSHI_TEAM && MUSHI_CHARANUM != 4
		PRINTW 参加者が４人いないとチーム戦はできません！
		LINE ++
	ELSE
		;特殊補正（口上用？,数値は任意）
		FOR LOCAL, 1, MUSHI_CHARANUM + 1
			LOCAL:1 = 0
			MUSHI_POWER:LOCAL = LOCAL:1
		NEXT
		RETURN 1
	ENDIF
;チーム戦切り替え
ELSEIF RESULT == 901 && 参加可能人数 > 3
	MUSHI_TEAM = !MUSHI_TEAM
;参加キャンセル
ELSEIF INRANGE(RESULT, 1, CHARANUM) && TCVAR:RESULT:ムシバトル参加可否 > 1
	FOR LOCAL, TCVAR:RESULT:ムシバトル参加可否, MUSHI_MAXBATTLE + 1
		;参加者はキャンセルする
		IF MUSHI_BATTLER:LOCAL == RESULT
			MUSHI_BATTLER:LOCAL = 0
		;番号が後の人がいるなら登録番号を一つ下げる
		ELSEIF MUSHI_BATTLER:LOCAL
			TCVAR:(MUSHI_BATTLER:LOCAL):ムシバトル参加可否 --
		ENDIF
	NEXT
	;その後シフトする
	FOR LOCAL, TCVAR:RESULT:ムシバトル参加可否, MUSHI_MAXBATTLE + 1
		SIF !MUSHI_BATTLER:LOCAL
			SWAP MUSHI_BATTLER:LOCAL, MUSHI_BATTLER:(LOCAL + 1)
	NEXT
	;参加状態を戻す
	TCVAR:RESULT:ムシバトル参加可否 = 1
	MUSHI_CHARANUM --
;参加エントリー
ELSEIF INRANGE(RESULT, 1, CHARANUM) && TCVAR:RESULT:ムシバトル参加可否 == 1 && MUSHI_CHARANUM < MUSHI_MAXBATTLE
	FOR LOCAL, 2, MUSHI_MAXBATTLE + 1
		;空きに入れる
		IF !MUSHI_BATTLER:LOCAL
			MUSHI_BATTLER:LOCAL = RESULT
			TCVAR:RESULT:ムシバトル参加可否 = LOCAL
			BREAK
		ENDIF
	NEXT
	MUSHI_CHARANUM ++
ENDIF
CLEARLINE LINE
RESTART

;-------------------------------------------------
;ムシバトル：セットアップ１：見せ合い前
;-------------------------------------------------
@MUSHI_BATTLE_SETUP_1
#DIM CHARA
#DIM LPCOUNT
;1=MASTERはセット済み
FOR LPCOUNT, 2, MUSHI_MAXBATTLE + 1
	CHARA = MUSHI_BATTLER:LPCOUNT
	IF !CHARA
		BREAK
	ELSE
		CALL CHARA_MUSHICAGE_SETTING(CHARA, LPCOUNT)
	ENDIF
NEXT

;-------------------------------------------------
;ムシバトル：見せ合い
;-------------------------------------------------
@MUSHI_BATTLE_SELECTION
#DIM CHARA
#DIM LPCOUNT
VARSET MUSHI_SELECT
VARSET MUSHI_REST
PRINTFORML ◆対戦相手の虫カゴ◆
FOR LPCOUNT, -2, MUSHI_MAXCAGE + 2
	FOR LOCAL, 2, MUSHI_CHARANUM + 1
		CHARA = MUSHI_BATTLER:LOCAL
		IF LOCAL == 2
			IF LPCOUNT == -2
				PRINTFORM ┌
			ELSEIF LPCOUNT == 0
				PRINTFORM ├
			ELSEIF LPCOUNT == MUSHI_MAXCAGE + 1
				PRINTFORM └
			ELSE
				PRINTFORM │
			ENDIF
		ENDIF
		IF LPCOUNT == -2
			IF LOCAL == MUSHI_CHARANUM
				PRINTFORM %"─" * 20%┐
			ELSE
				PRINTFORM %"─" * 20%┬
			ENDIF
		ELSEIF LPCOUNT == -1
			LOCALS = %CALLNAME:CHARA%
			SIF MUSHI_TEAM && LOCAL == 3
				LOCALS += "(仲間)"
			PRINTFORM %LOCALS, 36, LEFT%
			CALL PRINT_MBP_RANK(CHARA)
			PRINTFORM │
		ELSEIF LPCOUNT == MUSHI_MAXCAGE + 1
			IF LOCAL == MUSHI_CHARANUM
				PRINTFORM %"─" * 20%┘
			ELSE
				PRINTFORM %"─" * 20%┴
			ENDIF
		ELSEIF LPCOUNT == 0
			IF LOCAL == MUSHI_CHARANUM
				PRINTFORM %"─" * 20%┤
			ELSE
				PRINTFORM %"─" * 20%┼
			ENDIF
		ELSE
			CALL PRINT_MUSHINAME(MUSHI_NAME:(LOCAL * 10 + LPCOUNT), MUSHI_CAGE:(LOCAL * 10 + LPCOUNT), 40)
			PRINTFORM │
		ENDIF
	NEXT
	PRINTL 
NEXT
;フィールドタイプ設定
MUSHI_FIELD:99 = 0
WHILE !MUSHI_FIELD:99
	SETBIT MUSHI_FIELD:99, RAND:8
	SIF !(MUSHITORI_SPOT(CFLAG:MASTER:現在位置) &  MUSHI_FIELD:99)
		MUSHI_FIELD:99 = 0
WEND
PRINTFORM ＜フィールドタイプ：
CALL PRINT_FIELDTYPE(MUSHI_FIELD:99)
PRINTL ＞
PRINTFORML ◆%CALLNAME:MASTER%の虫カゴ◆　バトル参加させるムシを１～３匹選出してください
DRAWLINE
;MASTERの選出に引き渡し
MUSHI_REST:1 = 0
MUSHI_MENTAL:1 = 1000
CALL MUSHI_BATTLE_SELECTION_MASTER

;-------------------------------------------------
;ムシバトル：MASTERのムシ選出
;-------------------------------------------------
@MUSHI_BATTLE_SELECTION_MASTER
#DIM LINE
#DIM LPCOUNT
LINE = 9
;虫カゴ表示
FOR LPCOUNT, 1, MUSHI_MAXCAGE + 1
	IF MUSHI_CAGE:LPCOUNT
		SIF MATCH(MUSHI_SELECT, MUSHI_CAGE:LPCOUNT)
			SETCOLOR C_AQUA
		PRINTFORM [{LPCOUNT, 2}]
		LOCAL:1 = 0
		;選出済み
		FOR LOCAL, 1, MUSHI_MAXSELECT + 1
			IF MUSHI_SELECT:LOCAL == MUSHI_CAGE:LPCOUNT
				PRINTFORM 【%TOFULL(TOSTR(LOCAL))%】
				LOCAL:1 = 1
				BREAK
			ENDIF
		NEXT
		;未選出
		SIF !LOCAL:1
			PRINT 　　　
		PRINTFORM 
		CALL SHOW_MUSHI_DATA(MUSHI_CAGE:LPCOUNT, "一行")
		PRINTL 
		RESETCOLOR
	ELSE
		PRINTPLAINFORM [{LPCOUNT, 2}] からっぽ
		PRINTL 
	ENDIF
NEXT
DRAWLINE
IF !MUSHI_SELECT:1
	SETCOLOR C_GRAY
ELSE
	SETCOLOR C_CREAM
ENDIF
PRINTL [99] ＜＜READY to FIGHT！＞＞
RESETCOLOR
INPUT
;試合開始！
IF RESULT == 99 && MUSHI_SELECT:1
	;RESTを入れる
	FOR LOCAL, 1, MUSHI_MAXSELECT + 1
		SIF MUSHI_SELECT:LOCAL
			SETBIT MUSHI_REST:1, LOCAL
	NEXT
	RETURN
;ムシ選択
ELSEIF INRANGE(RESULT, 1, MUSHI_MAXCAGE) && MUSHI_CAGE:RESULT
	;参加キャンセル
	IF MATCH(MUSHI_SELECT, MUSHI_CAGE:RESULT)
		FOR LOCAL, 1, MUSHI_MAXSELECT + 1
			;参加をキャンセルする
			IF MUSHI_SELECT:LOCAL == MUSHI_CAGE:RESULT
				MUSHI_SELECT:LOCAL = 0
				BREAK
			ENDIF
		NEXT
		;その後シフトする
		FOR LOCAL, 1, MUSHI_MAXSELECT + 1
			SIF !MUSHI_SELECT:LOCAL
				SWAP MUSHI_SELECT:LOCAL, MUSHI_SELECT:(LOCAL + 1)
		NEXT
	;参加エントリー
	ELSE
		FOR LOCAL, 1, MUSHI_MAXSELECT + 1
			;空きに入れる
			IF !MUSHI_SELECT:LOCAL
				MUSHI_SELECT:LOCAL = MUSHI_CAGE:RESULT
				BREAK
			ENDIF
		NEXT
	ENDIF
ENDIF
CLEARLINE LINE
RESTART

;-------------------------------------------------
;ムシバトル：セットアップ２：選出後
;-------------------------------------------------
@MUSHI_BATTLE_SETUP_2
#DIM CHARA
#DIM LPCOUNT
#DIM SETNUM
#DIM KIRIHUDA
VARSET MUSHI_STATUS
VARSET MUSHI_MENTAL
;MASTERの設定
FOR LPCOUNT, 1, MUSHI_MAXSELECT + 1
	MUSHI_SELECT:(10 + LPCOUNT) = MUSHI_SELECT:LPCOUNT
	CALL MUSHI_PALAM(MUSHI_SELECT:LPCOUNT, MUSHI_POWER:1)
	CALL MUSHI_FLAGSETTING(MUSHI_SELECT:(10 + LPCOUNT), 10 + LPCOUNT)
NEXT
;戦闘初期設定
;MASTERのMENTALは1000固定
MUSHI_MENTAL:1 = 1000
MUSHI_STAND:1 = MUSHI_SELECT:11
MUSHI_STANDNUM:1 = 11
GETCOLOR
MUSHI_COLOR:1 = RESULT
;キャラの選出を決める
FOR LPCOUNT, 2, MUSHI_CHARANUM + 1
	CHARA = MUSHI_BATTLER:LPCOUNT
	SETNUM = LPCOUNT * 10
	KIRIHUDA = 0
	;切り札がいるなら切り札は確定（番号はランダム）
	RESULT = 0
	TRYCALLFORM MUSHI_KIRIHUDA_{CHARA}
	IF RESULT
		KIRIHUDA = 1
		LOCAL = RAND:MUSHI_MAXSELECT + 1
		MUSHI_SELECT:(SETNUM + LOCAL) = MUSHI_CAGE:(SETNUM + 1)
		MUSHI_NAME:(SETNUM + LOCAL) = MUSHI_NAME:(SETNUM + 1)
		CALL MUSHI_PALAM(MUSHI_SELECT:(SETNUM + LOCAL), MUSHI_POWER:LPCOUNT)
		CALL MUSHI_FLAGSETTING(MUSHI_SELECT:(SETNUM + LOCAL), SETNUM + LOCAL)
	ENDIF
	;虫カゴからランダムで空いてる番号に入れていく,ただし切り札がいるなら1番は除外
	FOR LOCAL, 1, MUSHI_MAXSELECT + 1
		IF !MUSHI_SELECT:(SETNUM + LOCAL)
			WHILE !MUSHI_SELECT:(SETNUM + LOCAL)
				LOCAL:1 = SETNUM + RAND:(MUSHI_MAXCAGE - KIRIHUDA) + 1 + KIRIHUDA
				LOCAL:2 = MUSHI_CAGE:(LOCAL:1)
				IF !MATCH(MUSHI_SELECT, LOCAL:2)
					MUSHI_SELECT:(SETNUM + LOCAL) = LOCAL:2
					MUSHI_NAME:(SETNUM + LOCAL) = MUSHI_NAME:(LOCAL:1)
				ENDIF
			WEND
			CALL MUSHI_PALAM(MUSHI_SELECT:(SETNUM + LOCAL), MUSHI_POWER:LPCOUNT)
			CALL MUSHI_FLAGSETTING(MUSHI_SELECT:(SETNUM + LOCAL), SETNUM + LOCAL)
		ENDIF
	NEXT
	;戦闘初期設定
	FOR LOCAL, 1, MUSHI_MAXSELECT + 1
		SETBIT MUSHI_REST:LPCOUNT, LOCAL
	NEXT
	MUSHI_MENTAL:LPCOUNT = MBP(CHARA)
	MUSHI_STAND:LPCOUNT = MUSHI_SELECT:(SETNUM + 1)
	MUSHI_STANDNUM:LPCOUNT = SETNUM + 1
	;口上色の設定
	RESULT = 0
	GETCOLOR
	LOCAL = RESULT
	CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 0, CHARA, 0, 0, "口上色")
	GETCOLOR
	;色が変わってたら登録する
	IF LOCAL != RESULT
		MUSHI_COLOR:LPCOUNT = RESULT
	ELSE
		MUSHI_COLOR:LPCOUNT = LOCAL
	ENDIF
	RESETCOLOR
NEXT
[IF_DEBUG]
	;選出チェック
	FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
		CHARA = MUSHI_BATTLER:LPCOUNT
		SETNUM = LPCOUNT * 10
		DRAWLINE
		PRINTFORML %CALLNAME:CHARA%
		FOR LOCAL, 1, MUSHI_MAXSELECT + 1
			SIF MUSHI_SELECT:(SETNUM + LOCAL)
				CALL PRINT_MUSHINAME(MUSHI_NAME:(SETNUM + LOCAL), MUSHI_CAGE:(SETNUM + LOCAL), 40)
				PRINTFORML HP:{MUSHI_HP:(SETNUM + LOCAL), 5, LEFT} ATK:{MUSHI_ATK:(SETNUM + LOCAL), 4, LEFT} SPD:{MUSHI_SPD:(SETNUM + LOCAL), 4, LEFT}
		NEXT
	NEXT
	DRAWLINE
	WAIT
[ENDIF]

;-------------------------------------------------
;ムシバトル：結果発表関数
;-------------------------------------------------
@MUSHI_BATTLE_RESULT
#DIM LPCOUNT
#DIM CHARA
PRINTFORML ■試合結果■
;チーム戦
IF MUSHI_TEAM
	TFLAG:193 = 3
	PRINTFORML ┌%"─" * 42%┐
	PRINT │
	CALL COLORMESSAGE(@"＜人生の勝ち組！＞", 0xff7878)
	PRINTFORML %" " * 66%│
	FOR LOCAL, 1, MUSHI_CHARANUM / 2 + 1
		CHARA = MUSHI_BATTLER:(MUSHI_FINISH:LOCAL)
		TCVAR:CHARA:ムシバトル参加可否 = 11
		PRINTFORM │
		SIF MUSHI_COLOR:(MUSHI_FINISH:LOCAL) != MUSHI_COLOR:1
			SETCOLOR MUSHI_COLOR:(MUSHI_FINISH:LOCAL)
		LOCALS = %CALLNAME:CHARA%
		;＠試合後一言口上
		CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 1, CHARA, MUSHI_FINISH:LOCAL, 0, "チーム戦：勝ち")
		LOCALS += RESULTS
		PRINTFORM %LOCALS, 84, LEFT%
		RESETCOLOR
		PRINTL │
		PRINT │　 ＆ 
		CALL PRINT_MUSHINAME(MUSHI_NAME:(MUSHI_STANDNUM:(MUSHI_FINISH:LOCAL)), MUSHI_STAND:(MUSHI_FINISH:LOCAL), 78)
		PRINTL │
	NEXT
	PRINTFORML ├%"─" * 42%┤
	PRINT │
	CALL COLORMESSAGE(@"＜負け組！＞", 0x6464ff)
	PRINTFORML %" " * 72%│
	FOR LOCAL, 1, MUSHI_CHARANUM + 1
		IF !MATCH(MUSHI_FINISH, LOCAL)
			CHARA = MUSHI_BATTLER:LOCAL
			TCVAR:CHARA:ムシバトル参加可否 = 12
			PRINTFORM │
			SIF MUSHI_COLOR:LOCAL != MUSHI_COLOR:1
				SETCOLOR MUSHI_COLOR:LOCAL
			LOCALS = %CALLNAME:CHARA%
			;＠試合後一言口上
			CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 2, CHARA, LOCAL, 0, "チーム戦：負け")
			LOCALS += RESULTS
			PRINTFORM %LOCALS, 84, LEFT%
			RESETCOLOR
			PRINTL │
			PRINT │　 ＆ 
			CALL PRINT_MUSHINAME(MUSHI_NAME:(MUSHI_STANDNUM:LOCAL), MUSHI_STAND:LOCAL, 78)
			PRINTL │
		ENDIF
	NEXT
	PRINTFORML └%"─" * 42%┘
;タイマン
ELSEIF MUSHI_CHARANUM == 2
	TFLAG:193 = 1
	FOR LPCOUNT, -3, MUSHI_MAXSELECT + 2
		FOR LOCAL, 1, MUSHI_CHARANUM + 1
			CHARA = MUSHI_BATTLER:LOCAL
			IF LOCAL == 1
				IF LPCOUNT == -3
					PRINTFORM ┌
				ELSEIF LPCOUNT == 0
					PRINTFORM ├
				ELSEIF LPCOUNT == MUSHI_MAXSELECT + 1
					PRINTFORM └
				ELSE
					PRINTFORM │
				ENDIF
			ENDIF
			IF LPCOUNT == -3
				IF LOCAL == MUSHI_CHARANUM
					PRINTFORM %"─" * 20%┐
				ELSE
					PRINTFORM %"─" * 20%┬
				ENDIF
			ELSEIF LPCOUNT == -2
				IF MUSHI_FINISH:1 == LOCAL
					LOCALS = %CALLNAME:CHARA%＜人生の勝利者！＞
					SETCOLOR 0xff7878
					TCVAR:CHARA:ムシバトル参加可否 = 11
				ELSE
					LOCALS = %CALLNAME:CHARA%＜負け犬！＞
					SETCOLOR 0x6464ff
					TCVAR:CHARA:ムシバトル参加可否 = 12
				ENDIF
				PRINTFORM %LOCALS, 36, LEFT%
				RESETCOLOR
				CALL PRINT_MBP_RANK(CHARA)
				PRINTFORM │
			ELSEIF LPCOUNT == -1
				;＠試合後一言口上
				IF MUSHI_FINISH:1 == LOCAL
					CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 1, CHARA, LOCAL, 0, "タイマン：勝ち")
				ELSE
					CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 2, CHARA, LOCAL, 0, "タイマン：負け")
				ENDIF
				SIF MUSHI_COLOR:LOCAL != MUSHI_COLOR:1
					SETCOLOR MUSHI_COLOR:LOCAL
				PRINTFORM %RESULTS, 40, LEFT%
				RESETCOLOR
				PRINTFORM │
			ELSEIF LPCOUNT == MUSHI_MAXSELECT + 1
				IF LOCAL == MUSHI_CHARANUM
					PRINTFORM %"─" * 20%┘
				ELSE
					PRINTFORM %"─" * 20%┴
				ENDIF
			ELSEIF LPCOUNT == 0
				IF LOCAL == MUSHI_CHARANUM
					PRINTFORM %"─" * 20%┤
				ELSE
					PRINTFORM %"─" * 20%┼
				ENDIF
			ELSE
				SIF !GETBIT(MUSHI_REST:LOCAL, LPCOUNT)
					SETCOLOR C_GRAY
				CALL PRINT_MUSHINAME(MUSHI_NAME:(LOCAL * 10 + LPCOUNT), MUSHI_CAGE:(LOCAL * 10 + LPCOUNT), 40)
				RESETCOLOR
				PRINTFORM │
			ENDIF
		NEXT
		PRINTL 
	NEXT
	SIF MUSHI_FINISH:1 == 1
		CALL COLORMESSAGE(@"☆%CALLNAME:(MUSHI_BATTLER:(MUSHI_FINISH:2))%に初めて直接対決で勝利した！", C_YELLOW, 1)
;バトルロイヤル
ELSE
	TFLAG:193 = 2
	PRINTFORML ┌%"─" * 44%┐
	FOR LOCAL, 1, MUSHI_CHARANUM + 1
		CHARA = MUSHI_BATTLER:(MUSHI_FINISH:LOCAL)
		PRINT │
		IF LOCAL == 1
			CALL COLORMESSAGE(@"☆優勝☆", C_YELLOW)
			SIF MUSHI_COLOR:(MUSHI_FINISH:LOCAL) != MUSHI_COLOR:1
				SETCOLOR MUSHI_COLOR:(MUSHI_FINISH:LOCAL)
			LOCALS = %CALLNAME:CHARA%
			;＠試合後一言口上
			CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", LOCAL, CHARA, MUSHI_FINISH:LOCAL, 0, "バトルロイヤル：優勝")
			LOCALS += RESULTS
			PRINTFORM %LOCALS, 80, LEFT%
			RESETCOLOR
			PRINTL │
		ELSE
			PRINTFORM 　%TOFULL(TOSTR(LOCAL))%位　
			SIF MUSHI_COLOR:(MUSHI_FINISH:LOCAL) != MUSHI_COLOR:1
				SETCOLOR MUSHI_COLOR:(MUSHI_FINISH:LOCAL)
			LOCALS = %CALLNAME:CHARA%
			;＠試合後一言口上
			CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", LOCAL, CHARA, MUSHI_FINISH:LOCAL, 0, "バトルロイヤル：２位以下")
			LOCALS += RESULTS
			PRINTFORM %LOCALS, 80, LEFT%
			RESETCOLOR
			PRINTL │
		ENDIF
		TCVAR:CHARA:ムシバトル参加可否 = LOCAL + 10
		PRINT │　　　　 ＆ 
		CALL PRINT_MUSHINAME(MUSHI_NAME:(MUSHI_STANDNUM:(MUSHI_FINISH:LOCAL)), MUSHI_STAND:(MUSHI_FINISH:LOCAL), 76)
		PRINTL │
		IF LOCAL == MUSHI_CHARANUM
			PRINTFORML └%"─" * 44%┘
		ELSE
			PRINTFORML ├%"─" * 44%┤
		ENDIF
	NEXT
ENDIF
WAIT

;-------------------------------------------------
;ムシバトル：キャラクターの虫カゴを設定する関数
;-------------------------------------------------
@CHARA_MUSHICAGE_SETTING(CHARA, BATTLE_NUM)
#DIM CHARA
#DIM BATTLE_NUM
#DIM MUSHI_ID
#DIM MUSHI_NUM
#DIM RARE_MIN
#DIM RARE_MAX
#DIM RANK_MIN
#DIM RANK_MAX
#DIM D_MUTA
#DIM D_FIELD
#DIM D_AREA
#DIMS D_TRIBE
#DIMS D_TYPE
#DIM MBP
RARE_MIN = 0
RARE_MAX = 0
RANK_MIN = 0
RANK_MAX = 0
D_MUTA = 0
D_FIELD = 0
D_AREA = 0
D_TRIBE = 
D_TYPE = 
MBP = MBP(CHARA)
;切り札チェック
RESULT = 0
TRYCALLFORM MUSHI_KIRIHUDA_{CHARA}
IF RESULT
	;切り札がいるなら1番に登録
	MUSHI_ID = RESULT:1 * 1000000 + RESULT:2 * 10000000 + RESULT
	CALL MUSHI_DATA(RESULT)
	MUSHI_ID += MUSHI_RANK * 10000 + MUSHI_RARE * 1000
	MUSHI_CAGE:(1 + BATTLE_NUM * 10) = MUSHI_ID
	MUSHI_NAME:(1 + BATTLE_NUM * 10) = %MUSHI_NAME%
	MUSHI_NUM = 2
ELSE
	MUSHI_NUM = 1
ENDIF
VARSET RESULT
VARSET RESULTS
;キャラデータからのリクエスト受付
TRYCALLFORM MUSHI_CHARACAGE_{CHARA}
IF RESULT
	RARE_MIN = RESULT:1
	RARE_MAX = RESULT:2
	RANK_MIN = RESULT:3
	RANK_MAX = RESULT:4
	D_MUTA = RESULT:5
	D_FIELD = RESULT:6
	D_AREA = RESULT:7
	D_TRIBE = %RESULTS:1%
	D_TYPE = %RESULTS:2%
ENDIF
;レアリティ設定
IF !RARE_MAX
	RARE_MIN = MBP_RANK(CHARA)
	IF MBP < 300
		RARE_MAX = 2
	ELSEIF MBP < 1000
		RARE_MAX = 3
	ELSEIF MBP < 1600
		RARE_MAX = 3
	ELSEIF MBP < 2200
		RARE_MAX = 4
	ELSEIF MBP < 3200
		RARE_MAX = 4
	ELSEIF MBP < 5000
		RARE_MAX = 5
	ELSEIF MBP < 6200
		RARE_MAX = 5
	ELSEIF MBP < 8000
		RARE_MAX = 6
	ELSEIF MBP < 12000
		RARE_MAX = 6
	ELSEIF MBP < 20000
		RARE_MAX = 7
	ELSEIF MBP < 30000
		RARE_MAX = 7
	ELSEIF MBP < 50000
		RARE_MAX = 8
	ELSEIF MBP < 150000
		RARE_MAX = 8
	ELSEIF MBP < 500000
		RARE_MAX = 9
	ELSEIF MBP < 1000000
		RARE_MAX = 9
	ELSE
		RARE_MAX = 9
	ENDIF
ENDIF
;ランク設定
IF !RANK_MAX
	RANK_MIN = LIMIT(MBP / 2000, 0 , 80)
	RANK_MAX = 100
ENDIF
;変異率設定
IF !D_MUTA
	D_MUTA = LIMIT(MBP / 10000, 1, 25)
ENDIF
;ムシを出力して虫カゴに登録していく
FOR LOCAL, MUSHI_NUM, MUSHI_MAXCAGE + 1
	CALL DRAW_MUSHI_DESIGN(CHARA, RARE_MIN, RARE_MAX, RANK_MIN, RANK_MAX, D_MUTA, D_FIELD, D_AREA, D_TRIBE, D_TYPE)
	MUSHI_CAGE:(LOCAL + BATTLE_NUM * 10) = RESULT
	MUSHI_NAME:(LOCAL + BATTLE_NUM * 10) = %MUSHI_NAME%
NEXT
;-------------------------------------------------
;ムシバトル：バトル用の戦闘力を出力する関数
;Mushi Battle Power
;-------------------------------------------------
@MBP(CHARA)
#FUNCTION
#DIM CHARA
;基本値1000
LOCAL = 1000
SELECTCASE TALENT:CHARA:採集Lv
CASE 0
	TIMES LOCAL, 1.00
CASE 1
	TIMES LOCAL, 2.00
CASE 2
	TIMES LOCAL, 4.00
CASE 3
	TIMES LOCAL, 8.00
CASE 4
	TIMES LOCAL, 12.00
CASE 5
	TIMES LOCAL, 18.00
CASEELSE
	TIMES LOCAL, 25.00
ENDSELECT
SELECTCASE ABL:CHARA:教養
CASE 0
	TIMES LOCAL, 1.00
CASE 1
	TIMES LOCAL, 1.20
CASE 2
	TIMES LOCAL, 1.40
CASE 3
	TIMES LOCAL, 1.60
CASE 4
	TIMES LOCAL, 1.80
CASE 5
	TIMES LOCAL, 2.00
CASEELSE
	TIMES LOCAL, 2.40
ENDSELECT
SELECTCASE ABL:CHARA:戦闘能力
CASE 0
	TIMES LOCAL, 1.00
CASE 1
	TIMES LOCAL, 1.10
CASE 2
	TIMES LOCAL, 1.20
CASE 3
	TIMES LOCAL, 1.30
CASE 4
	TIMES LOCAL, 1.50
CASE 5
	TIMES LOCAL, 1.70
CASEELSE
	TIMES LOCAL, 2.00
ENDSELECT
SELECTCASE TALENT:CHARA:汚臭耐性
CASE -2
	TIMES LOCAL, 0.20
CASE -1
	TIMES LOCAL, 0.50
CASE 1
	TIMES LOCAL, 2.00
CASE 2
	TIMES LOCAL, 3.00
ENDSELECT
SIF TALENT:CHARA:調合知識
	TIMES LOCAL, 1.50
SIF TALENT:CHARA:禁断の知識
	TIMES LOCAL, 2.50
SIF TALENT:CHARA:幼稚
	TIMES LOCAL, 15.00
IF GET_TRIBENAME(CHARA, GETNUM(TALENT, "人間")) == "天人"
	TIMES LOCAL, 0.30
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "人間")) == "仙人"
	TIMES LOCAL, 1.50
;地上住みは除く
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "人間")) == "月人" && !GROUPMATCH(CHARA, [[輝夜]], [[永琳]])
	TIMES LOCAL, 0.05
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "人間")) == "魔界人"
	TIMES LOCAL, 1.50
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "人間")) == "外界人"
	TIMES LOCAL, 0.80
ENDIF
IF GET_TRIBENAME(CHARA, GETNUM(TALENT, "妖怪")) == "鬼"
	TIMES LOCAL, 4.00
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "妖怪")) == "吸血鬼"
	TIMES LOCAL, 1.50
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "妖怪")) == "河童"
	TIMES LOCAL, 2.00
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "妖怪")) == "天狗"
	TIMES LOCAL, 1.50
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "妖怪")) == "妖獣"
	TIMES LOCAL, 2.00
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "妖怪")) == "妖鳥"
	TIMES LOCAL, 2.00
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "妖怪")) == "妖蟲"
	TIMES LOCAL, 25.00
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "妖怪")) == "悪魔"
	TIMES LOCAL, 2.50
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "妖怪")) == "小人"
	TIMES LOCAL, 0.05
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "妖怪")) == "山童"
	TIMES LOCAL, 4.00
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "妖怪")) == "妖怪"
	TIMES LOCAL, 1.20
ENDIF
IF GET_TRIBENAME(CHARA, GETNUM(TALENT, "妖精")) == "妖精"
	TIMES LOCAL, 15.00
ENDIF
LOCALS = %GET_STR(, "キャラデータ", CHARA, "料理：好きな味")%
IF STRCOUNT(LOCALS, "グロテスク/") >= 5
	TIMES LOCAL, 4.00
ELSEIF STRCOUNT(LOCALS, "グロテスク/") >= 3
	TIMES LOCAL, 3.00
ELSEIF STRCOUNT(LOCALS, "グロテスク/") >= 1
	TIMES LOCAL, 2.00
ENDIF
LOCALS = %GET_STR(, "キャラデータ", CHARA, "料理：嫌いな味")%
IF STRCOUNT(LOCALS, "グロテスク/") >= 5
	TIMES LOCAL, 0.10
ELSEIF STRCOUNT(LOCALS, "グロテスク/") >= 3
	TIMES LOCAL, 0.20
ELSEIF STRCOUNT(LOCALS, "グロテスク/") == 1
	TIMES LOCAL, 0.50
ENDIF
RETURNF LOCAL

;-------------------------------------------------
;ムシバトル：ムシバトルパワーのランク,レアリティ最低値にもなる
;-------------------------------------------------
@MBP_RANK(CHARA)
#FUNCTION
#DIM CHARA
IF MBP(CHARA) < 300
	RETURNF 0
ELSEIF MBP(CHARA) < 1000
	RETURNF 0
ELSEIF MBP(CHARA) < 1600
	RETURNF 1
ELSEIF MBP(CHARA) < 2200
	RETURNF 1
ELSEIF MBP(CHARA) < 3200
	RETURNF 2
ELSEIF MBP(CHARA) < 5000
	RETURNF 2
ELSEIF MBP(CHARA) < 6200
	RETURNF 3
ELSEIF MBP(CHARA) < 8000
	RETURNF 3
ELSEIF MBP(CHARA) < 12000
	RETURNF 4
ELSEIF MBP(CHARA) < 20000
	RETURNF 4
ELSEIF MBP(CHARA) < 30000
	RETURNF 5
ELSEIF MBP(CHARA) < 50000
	RETURNF 5
ELSEIF MBP(CHARA) < 150000
	RETURNF 6
ELSEIF MBP(CHARA) < 500000
	RETURNF 6
ELSEIF MBP(CHARA) < 1000000
	RETURNF 7
ELSE
	RETURNF 8
ENDIF
;-------------------------------------------------
;ムシバトル：ムシバトルパワーのランクを★Ｘでプリントする関数
;-------------------------------------------------
@PRINT_MBP_RANK(CHARA)
#DIM CHARA
IF CHARA == MASTER
	PRINTFORM ★？
ELSE
	SETCOLOR RANKCOLOR(MBP_RANK(CHARA) + 1)
	PRINTFORM ★%TOFULL(TOSTR(MBP_RANK(CHARA)))%
	RESETCOLOR
ENDIF

;-------------------------------------------------
;ムシバトル：残りムシ数を○×でプリントする関数
;bit,0は使いません
;-------------------------------------------------
@PRINT_RESTMUSHI(ARG)
FOR LOCAL, 1, MUSHI_MAXSELECT + 1
	IF GETBIT(MUSHI_REST:ARG, LOCAL)
		PRINT ○
	ELSE
		PRINT ×
	ENDIF
NEXT

;-------------------------------------------------
;ムシバトル用のフィールドの描写っぽいなにか
;-------------------------------------------------
@PRINT_FIELD_EFFECT(ARG)
#DIM 色
IF GETBIT(ARG, 0)
	LOCALS = vvvv
	色 = 0x78ff78
ELSEIF GETBIT(ARG, 1)
	LOCALS = ＊＊
	色 = C_PINK
ELSEIF GETBIT(ARG, 2)
	LOCALS = 林林
	色 = 0x00c832
ELSEIF GETBIT(ARG, 3)
	LOCALS = ▒▒▒▒
	色 = 0xffc864
ELSEIF GETBIT(ARG, 4)
	LOCALS = ～～
	色 = 0x64c8ff
ELSEIF GETBIT(ARG, 5)
	LOCALS = ▲▼
	色 = 0xb49678
ELSEIF GETBIT(ARG, 6)
	LOCALS = ＃＃
	色 = 0xff7878
ELSEIF GETBIT(ARG, 7)
	LOCALS = _/_/
	色 = 0xc8dcff
ENDIF
CALL COLORMESSAGE(@"%LOCALS * 9% ", 色)
PRINT ＶＳ．
CALL COLORMESSAGE(@" %LOCALS * 9%", 色)


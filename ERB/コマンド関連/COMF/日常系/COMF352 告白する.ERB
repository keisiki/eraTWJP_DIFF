;-------------------------------------------------
;告白する
;日常系コマンド、レベル2
;-------------------------------------------------
@COM352
LOCAL = 0
IF FLAG:甲斐性無
	DRAWLINE
	PRINTFORMW %CALLNAME:MASTER%にはそんな度胸はない
	RETURN -1
ENDIF
;告白対象確認
PRINTFORML %CALLNAME:TARGET%に告白します。【恋人】は恋人は{FLAG:追加恋人枠 + 1}人しか取得出来ません。
PRINTW よろしいですか？
CALL ASK_YN("やめる","告白する")
IF !RESULT
	RETURN -1
ENDIF

;パラメータにより成功基礎値判定

SELECTCASE TFLAG:192
	;通常
	CASE 0
		IF  CFLAG:合意判定 > 350 + TALENT:一線越えない * 100
			TFLAG:193 = 1
		ELSE
			TFLAG:193 = 0
		ENDIF
	;強制成功or大成功
	CASE 1
		TFLAG:193 = 1
	;強制失敗
	CASE -1
		TFLAG:193 = 0
	;コマンド終了
	CASE -2
		TFLAG:193 = 0
		RETURN 0
ENDSELECT

IF TFLAG:193 == 1
	SOURCE:歓楽 = 1000
	SOURCE:受動 = 500 + 100 * ABL:従順
	SOURCE:征服 = 500 + 100 * ABL:サドっ気
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE352
;停止中は不可
SIF FLAG:70
	RETURN 0
;妄想中は告白できない
SIF TFLAG:妄想中 == 1
	RETURN 0
;告白する実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(352)
	RETURN RESULT
;二人きりでないとだめ
SIF GET_TARGETNUM() > 1
	RETURN 0
;馴れ合い強度2
SIF TCVAR:MASTER:馴れ合い < 2
	RETURN 0
;すでに恋人の場合はだめ
SIF TALENT:恋人
	RETURN 0

;恋人の数が上限に達していてはだめ
SIF !CAN_MAKE_恋人()
	RETURN 0
SIF TALENT:TARGET:行きずり
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;添い寝中
SIF CFLAG:添い寝中
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
RETURN 1

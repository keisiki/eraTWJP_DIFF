;-------------------------------------------------
;アイテム画面
;-------------------------------------------------
@COM490
#DIM 対象
#DIM 全員
#DIM CLIENT_ID
#DIM 虚無への回帰
#DIM 使用アイテム
#DIM 表示アイテム種別
#DIMS 食品特徴
VARSET LOCAL
VARSET LOCALS
対象 = MASTER
虚無への回帰 = 0
表示アイテム種別 = 0
PRINTL 何を使いますか？
$LOOP
DRAWLINE
FOR LOCAL,120, 699
	SIF 表示アイテム種別 != 0 && ITEMTYPE(LOCAL) != 表示アイテム種別
		CONTINUE
	IF 対象 != MASTER
		IF 使用可能アイテム(LOCAL) == 2 && ITEM:LOCAL
			CALL PRINT_ITEM_EXPLANATION(LOCAL)
		ENDIF
	ELSE
		IF 使用可能アイテム(LOCAL) && ITEM:LOCAL
			CALL PRINT_ITEM_EXPLANATION(LOCAL)
		ENDIF
	ENDIF
NEXT
PRINTL
DRAWLINE
CALL PRINTBUTTON_EX("[全表示]", 2000, 表示アイテム種別 != 0, 表示アイテム種別 == 0)
CALL PRINTBUTTON_EX("[回復薬]", 2001, 表示アイテム種別 != 1, 表示アイテム種別 == 1)
CALL PRINTBUTTON_EX("[バフ薬]", 2002, 表示アイテム種別 != 2, 表示アイテム種別 == 2)
CALL PRINTBUTTON_EX("[食　品]", 2003, 表示アイテム種別 != 3, 表示アイテム種別 == 3)
CALL PRINTBUTTON_EX("[お菓子]", 2004, 表示アイテム種別 != 4, 表示アイテム種別 == 4)
CALL PRINTBUTTON_EX("[果　物]", 2005, 表示アイテム種別 != 5, 表示アイテム種別 == 5)
CALL PRINTBUTTON_EX("[その他]", 2006, 表示アイテム種別 != 6, 表示アイテム種別 == 6)
RESETCOLOR
PRINTL
DRAWLINE
SIF ITEM:昆虫採集セット
	PRINTFORML [901] 虫カゴを確認する
PRINTFORML [900] コレクションのスキマ自動仕様切り替え（現在\@ FLAG:Cスキマ自動仕様 ? ON# OFF\@）
IF TARGET && SHIRAHU(TARGET)
	SIF 対象 != MASTER
		SETCOLOR C_YELLOW
	PRINTFORML [998] %CALLNAME:TARGET%に使う
	RESETCOLOR
ENDIF

PRINTL [999] やめる
INPUT
使用アイテム = RESULT
;アイテム以外の処理
SELECTCASE RESULT
	CASE 999
		RETURN -1
	CASE 900
		FLAG:Cスキマ自動仕様 = !FLAG:Cスキマ自動仕様
		PRINTFORMW 自動仕様を\@ FLAG:Cスキマ自動仕様 ? ON# OFF\@にしました
		GOTO LOOP
	CASE 901
		;[虫捕りをする]へ転送
		CALL SHOW_MUSHI_CAGE
		GOTO LOOP
	CASE 998
		IF 対象 != MASTER
			対象 = MASTER
		ELSEIF SHIRAHU(TARGET) && TARGET
			対象 = TARGET
		ENDIF
		GOTO LOOP
	CASE 2000 TO 2006
		表示アイテム種別 = RESULT % 2000
		GOTO LOOP
ENDSELECT

;使用可能アイテム以外の入力、またはアイテム未所持の場合は再入力
IF 使用可能アイテム(使用アイテム) == 0 || ITEM:使用アイテム <= 0
	GOTO LOOP
ENDIF
;相手使用時にMASTERのみアイテムの選択は再入力
IF 対象 != MASTER && 使用可能アイテム(使用アイテム) == 1
	GOTO LOOP
ENDIF
;IF ITEMTYPE_DRUG(使用アイテム,対象) && TCVAR:(ARG:1):お薬
;	PRINTL 既にお薬を服用済みなので今日はもうやめておこう
;	GOTO LOOP
;ENDIF
IF ITEMTYPE_FOOD(使用アイテム) && !TIME_PROGRESS(TCVAR:対象:空腹時刻)
	PRINTFORML %CALLNAME:対象%はまだ空腹になってない
	GOTO LOOP
ENDIF
IF ITEMTYPE_SWEETS(使用アイテム) && !TIME_PROGRESS(TCVAR:対象:デザート空腹時刻)
	PRINTL おやつはまだ食べたくない
	GOTO LOOP
ENDIF
TFLAG:193 = 使用アイテム
TFLAG:194 = 対象 == MASTER ? MASTER # 1
SELECTCASE 使用アイテム
	CASE 120	;ブラックサンダー
		PRINTFORML %ITEMNAME:使用アイテム%を食べた
		SIF 対象 == MASTER
			PRINTL ……チープな味がした
		食品特徴 = %ITEMNAME:使用アイテム%/甘い/ジャンキー/サクサク/
		CALL RECOVER(対象, ITEM_TASTE(対象, 50, 食品特徴), "体力", "チョコによる回復")
		CALL RECOVER(対象, ITEM_TASTE(対象, 50, 食品特徴), "気力", "チョコによる回復")
		CALL 満腹度上昇(対象, "デザート", 60)
		LOCAL:1 ++
	CASE 121	;チョコレート
		IF 対象 == MASTER
			PRINTFORML %ITEMNAME:使用アイテム%を食べた
			食品特徴 = %ITEMNAME:使用アイテム%/甘い/
			CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 300, 食品特徴), "体力", "チョコによる回復")
			CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 300, 食品特徴), "気力", "チョコによる回復")
			CALL 満腹度上昇(対象, "デザート")
			LOCAL:1 ++
		ENDIF
	CASE 122	;高級チョコ
		IF 対象 == MASTER
			PRINTFORML %ITEMNAME:使用アイテム%を食べた
			PRINTFORML うまい！
			食品特徴 = %ITEMNAME:使用アイテム%/甘い/上品/豪華/高級食材/
			CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 500, 食品特徴), "体力", "チョコによる回復")
			CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 500, 食品特徴), "気力", "チョコによる回復")
			CALL 満腹度上昇(対象, "デザート")
			LOCAL:1 ++
		ENDIF
	CASE 123	;本命チョコ
		IF 対象 == MASTER
			PRINTFORML %ITEMNAME:使用アイテム%を食べた
			PRINTFORML 愛を感じる！
			食品特徴 = %ITEMNAME:使用アイテム%/愛情/甘い/
			CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 1000, 食品特徴), "体力", "チョコによる回復")
			CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 1000, 食品特徴), "気力", "チョコによる回復")
			CALL 満腹度上昇(対象, "デザート")
			LOCAL:1 ++
		ENDIF
	CASE 212	;かしわもち
		PRINTFORML %ITEMNAME:使用アイテム%を食べた
		LOCAL:1 ++
		;端午の節句は夏の季語だそうなので春物じゃなかった
		食品特徴 = %ITEMNAME:使用アイテム%/甘い/
		CALL RECOVER(対象, ITEM_TASTE(対象, 100, 食品特徴), "体力", "かしわもちによる回復")
		CALL RECOVER(対象, ITEM_TASTE(対象,   0, 食品特徴), "気力", "かしわもちによる回復")
		CALL 満腹度上昇(対象, "デザート")
	CASE 213	;アイス
		SIF TARGET && SHIRAHU(TARGET)
			PRINT 二人並んで
		PRINTDATA
			DATAFORM 『アイス』
			DATAFORM 『冷凍ミカン』
			DATAFORM 『柄が二つ付いたソーダアイス』
			DATAFORM 『二つで一組のたまごアイス』
			DATAFORM 『チューペット』
			DATAFORM 『パピコ』
			DATAFORM 『練乳の詰まったミルクバー』
			DATAFORM 『アズキバー』
			DATAFORM 『スイカバー』
			DATAFORM 『ガツン、とみかん』
			DATAFORM 『ブラックサンダーアイス』
			DATAFORM 『雪見大福』
		ENDDATA
		PRINTFORML を食べた
		食品特徴 = %ITEMNAME:使用アイテム%/冷たい/甘い/
		CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 150, 食品特徴), "気力", "アイスによる回復")
		CALL 満腹度上昇(MASTER, "デザート")
		IF  TARGET && SHIRAHU(TARGET)
			CALL RECOVER(TARGET, ITEM_TASTE(TARGET, 150, 食品特徴), "気力", "アイスによる回復")
			CALL 満腹度上昇(TARGET, "デザート")
		ENDIF
		LOCAL:1 ++
	CASE 215	;紅茶
		PRINTFORML %ITEMNAME:使用アイテム%を使った
		SETBIT TCVAR:MASTER:特別なお茶, 0
		LOCAL:1 ++
	CASE 352	;イチゴ
		PRINTFORML %ITEMNAME:使用アイテム%を食べた
		食品特徴 = %ITEMNAME:使用アイテム%/野菜/果物/甘い/
		CALL RECOVER(対象, ITEM_TASTE(対象, 50, 食品特徴), "気力", "イチゴによる回復")
		LOCAL:1 ++
	CASE 364	;スイカ
		食品特徴 = %ITEMNAME:使用アイテム%/野菜/果物/甘い/
		IF FLAG:70 == 1
			PRINTFORML %ITEMNAME:使用アイテム%を食べた
			CALL RECOVER(対象, ITEM_TASTE(対象, 100, 食品特徴), "気力", "スイカによる回復")
		ELSE
			IF TARGET != MASTER
				IF GET_TARGETNUM() > 1
					PRINTFORM 皆で
				ELSEIF SHIRAHU(TARGET)
					PRINTFORM %CALLNAME:TARGET%と
				ENDIF
			ENDIF
			PRINTFORML %ITEMNAME:使用アイテム%を食べた
			CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 100, 食品特徴), "気力", "スイカによる回復")
			FOR 全員, 1, GET_TARGETNUM() + 1
				SIF TARGET:全員 <= 0
					CONTINUE
				SIF !SHIRAHU(TARGET:全員)
					CONTINUE
				CALL RECOVER(TARGET:全員, ITEM_TASTE(TARGET:全員, 100, 食品特徴), "気力", "スイカによる回復")
			NEXT
		ENDIF
		LOCAL:1 ++
	;CASE 400
	;	PRINTFORML %CALLNAME:MASTER%は%ITEMNAME:使用アイテム%を開いた
	;	CALL COM401
	;	LOCAL:1 ++
	CASE 500	;青汁
		PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
		PRINTL マズイ！もう一杯！
		食品特徴 = %ITEMNAME:使用アイテム%/苦い/苦い/苦い/ヘルシー/野菜/葉物/野趣/
		CALL RECOVER(対象,100, "体力", "青汁による回復")
		CALL RECOVER(対象, ITEM_TASTE(対象, 200, 食品特徴), "気力", "青汁による回復")
		LOCAL:1 ++
	CASE 501	;栄養ドリンク
		PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
		CALL RECOVER(対象, 薬の知識(200), "体力", "栄養ドリンクによる回復")
		CALL RECOVER(対象, 薬の知識(100), "気力", "栄養ドリンクによる回復")
		CFLAG:対象:眠気覚まし = MAX(薬の知識(20), CFLAG:対象:眠気覚まし)
		TCVAR:対象:お薬 = 1
		LOCAL:1 ++
	CASE 502	;精力剤
		IF HAS_PENIS(対象)
			PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
			CALL RECOVER(対象, 薬の知識(300), "精力", "精力剤による回復")
			TCVAR:対象:お薬 = 1
			LOCAL:1 ++
		ELSE
			PRINTFORML 精力剤の使用対象外のようだ
			GOTO LOOP
		ENDIF
	CASE 503	;ギンギンＺ
		PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
		PRINTL ふぁいといっぱつ、まんたーん！
		CALL RECOVER(対象, 薬の知識(500), "体力", "ギンギンＺによる回復")
		CALL RECOVER(対象, 薬の知識(300), "気力", "ギンギンＺによる回復")
		CFLAG:対象:眠気覚まし = MAX(薬の知識(60), CFLAG:対象:眠気覚まし)
		TCVAR:対象:お薬 = 1
		LOCAL:1 ++
	;CASE 504
	;		IF FLAG:70
	;			PRINTFORML 時間停止を解除してから使おう
	;			GOTO LOOP
	;		ENDIF
	;		IF CFLAG:MASTER:現在位置 != CFLAG:MASTER:初期位置
	;			PRINTFORML これは自室で飲もう
	;			GOTO LOOP
	;		ENDIF
	;		PRINTFORML ストロング虚無を口にした途端、虚無の力で思考はぼやけ、すべてがどうでもよくなってきた
	;		PRINTFORML %CALLNAME:MASTER%は缶を空けながら一日を無為に過ごした…
	;		虚無への回帰 = 1
	CASE 505	;ギンギラギンＸ
		PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
		PRINTL ２４時間戦えそうな気がする…！
		CALL RECOVER(対象, 薬の知識(800), "体力", "ギンギラギンＸによる回復")
		CALL RECOVER(対象, 薬の知識(600), "気力", "ギンギラギンＸによる回復")
		CFLAG:対象:眠気覚まし = MAX(薬の知識(180), CFLAG:対象:眠気覚まし)
		CFLAG:対象:徹夜 = 1
		TCVAR:対象:お薬 = 1
		LOCAL:1 ++
	CASE 506	;精力剤『天元凸破』
		PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
		PRINTL おまえの×××で天を衝け！
		CALL RECOVER(対象, 薬の知識(400), "体力", "精力剤『天元凸破』による回復")
		CALL RECOVER(対象, 薬の知識(300), "気力", "精力剤『天元凸破』による回復")
		CALL RECOVER(対象, 薬の知識(800), "精力", "精力剤『天元凸破』による回復")
		TCVAR:対象:お薬 = 1
		LOCAL:1 ++
	CASE 507	;ポーション
		PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
		CALL RECOVER(対象, 薬の知識(150), "気力", "ポーションによる回復")
		CALL RECOVER(対象, 薬の知識(150), "TSP", "ポーションによる回復")
		TCVAR:対象:お薬 = 1
		LOCAL:1 ++
	CASE 508	;マジックポーション
		PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
		PRINTL 魔力がみなぎってくる…
		CALL RECOVER(対象, 薬の知識(300), "気力", "マジックポーションによる回復")
		CALL RECOVER(対象, 薬の知識(600), "TSP", "マジックポーションによる回復")
		TCVAR:対象:お薬 = 1
		LOCAL:1 ++
	CASE 509	;破眠ポーション
		PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
		CALL RECOVER(対象, 薬の知識(200), "気力", "破眠ポーションによる回復")
		CFLAG:対象:眠気覚まし = MAX(薬の知識(240), CFLAG:対象:眠気覚まし)
		CFLAG:対象:徹夜 = 1
		TCVAR:対象:お薬 = 1
		LOCAL:1 ++
	CASE 510	;ウコンドリンク
		PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
		PRINTL 肝機能が高まり、酔いにくくなった
		TFLAG:酔い止め = 1
		LOCAL:1 ++
	CASE 511	;エリクサー
		PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
		PRINTL 疲れが彼方へと吹き飛んだ…！
		CALL RECOVER(対象, MAX(9999, MAXBASE:対象:体力), "体力", "エリクサーによる回復")
		CALL RECOVER(対象, MAX(9999, MAXBASE:対象:気力), "気力", "エリクサーによる回復")
		CALL RECOVER(対象, MAX(9999, MAXBASE:対象:精力), "精力", "エリクサーによる回復")
		CALL RECOVER(対象, MAX(9999, MAXBASE:対象:TSP),  "TSP",  "エリクサーによる回復")
		CFLAG:対象:眠気覚まし = MAX(300, CFLAG:対象:眠気覚まし)
		TCVAR:対象:お薬 = 1
		LOCAL:1 ++
	CASE 512	;仙丹
		PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
		PRINTL 尋常でない力がみなぎる……！
		CALL BUFF_BASE(対象, BASE_体力, MAX(MAXBASE:対象:体力 / 5, 500))
		CALL BUFF_BASE(対象, BASE_気力, MAX(MAXBASE:対象:気力 / 5, 500))
		CALL BUFF_BASE(対象, BASE_精力, MAX(MAXBASE:対象:精力 / 5, 500))
		LOCAL:1 ++
	CASE 514	;仙香玉兎
		IF TFLAG:仙香玉兎 > 0
			PRINTFORML もうすでに使用している
			GOTO LOOP
		ELSE
			PRINTFORML %ITEMNAME:使用アイテム%を香炉にくべた
			PRINTL 幻惑的な薫香が周囲に漂う……
			TFLAG:仙香玉兎 = 240
			LOCAL:1 ++
		ENDIF
	CASE 515	;国士無双の薬　３回目まではバフ効果
		PRINTFORML %ITEMNAME:使用アイテム%を飲んだ
		IF TCVAR:対象:国士無双の薬 < 3
			PRINTL 力があふれてくる……！
			CALL BUFF_BASE(対象, BASE_体力, MAX(MAXBASE:対象:体力 / 15, 150))
			CALL BUFF_BASE(対象, BASE_精力, MAX(MAXBASE:対象:精力 / 10, 150))
			TCVAR:対象:国士無双の薬 ++
			LOCAL:1 ++
		ELSE
			;四回目で爆発、デバフに
			SETCOLOR C_RED
			PRINTFORMW ！！
			PRINTFORML ……爆発してアフロなことになった……飲みすぎたようだ……
			RESETCOLOR
			CALL BUFF_BASE(対象, BASE_体力, MIN((-1 * (MAXBASE:対象:体力 / 5)), -450))
			CALL BUFF_BASE(対象, BASE_精力, MIN((-1 * (MAXBASE:対象: 精力 / 3)), -450))
			TCVAR:対象:国士無双の薬 = 0
			LOCAL:1 ++
		ENDIF
	CASE 516	;ガシングガーデン
		IF TFLAG:GG設置位置 == CFLAG:MASTER:現在位置
			PRINTFORMW %TEXTR(@"「私が育てました」（%GET_MAPNAME(MAIN_MAP)%在住　%CALLNAME:MASTER%氏）/本品種は安全性に配慮していますが、万が一ご気分の悪くなられた方は十分に距離をとって休息してください。")%
		ELSEIF TFLAG:GG設置位置
			PRINTFORML ガシングガーデンは一日限りの儚い命
			PRINTFORMW 一株を最期まで見届けてあげよう
			GOTO LOOP
		ELSEIF INROOM(CFLAG:MASTER:現在位置)
			PRINTFORMW 野外でやろう
			GOTO LOOP
		;目撃者がいると駄目
		ELSEIF (TARGET > 0 || DATE_HITOGOMI(CFLAG:MASTER:現在位置) > 0) && !FLAG:70
			PRINTFORMW ここでは人目がある……こっそりと仕掛けるべきだろう
			GOTO LOOP
		ELSE
			PRINTFORML %CALLNAME:MASTER%は魔法の種子を撒いた
			PRINTFORML みるみるうちに魔法植物が花開き、周囲に情欲を掻き立てる芳香が満ちる……
			TFLAG:GG設置位置 = CFLAG:MASTER:現在位置
			LOCAL:1 ++
		ENDIF
	CASE 517	;ポイズンボディ
		IF TFLAG:ポイズンボディ > 0
			PRINTFORML これ以上摂取したら肝機能障害になる
			GOTO LOOP
		ELSEIF HAS_PENIS(対象)
			PRINTFORML %ITEMNAME:使用アイテム%を服用した
			PRINTFORML ぐるぐると体内に渦巻く何かの感覚とともに、アソコが熱くなってきた……
			PRINTFORML （%CALLNAME:MASTER%の精液に発情作用が付与　効果発揮中は射精時の体力・気力消費増加）
			PRINTFORML %CALLNAME:MASTER%は頭痛・眩暈・吐き気・関節痛に襲われたが、健康より大事なものもあると心を強く持って耐えた
			TFLAG:ポイズンボディ = 240
			LOCAL:1 ++
		ELSE
			PRINTFORML このお薬は男性専用のようだ
			GOTO LOOP
		ENDIF
	CASE 520	;包帯
		IF !TFLAG:キスマーク
			PRINTFORML 今使っても意味がない
			GOTO LOOP
		ELSE
			PRINTFORML %CALLNAME:MASTER%は%ITEMNAME:使用アイテム%を巻いてキスマークを隠した
			TFLAG:包帯 = 1
			LOCAL:1 ++
		ENDIF
	CASE 521	;易書
		IF FLAG:70
			PRINTFORML 時間停止を解除してから使おう
			GOTO LOOP
		ENDIF
		PRINTFORML %CALLNAME:MASTER%は易書を開いた
		PRINTFORML 易者が出てきた
		PRINTFORMW 
		IF CFLAG:[[霊夢]]:現在位置 == CFLAG:MASTER:現在位置
			PRINTFORML すかさず霊夢は易者の頭目掛けてお祓い棒を振り下ろした
			PRINTFORML 哀れ易者は２つに割られてしまった
			FOR LOCAL,1,CHARANUM
				IF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
					BASE:LOCAL:理性 = MAXBASE:LOCAL:理性
					BASE:LOCAL:ムード = 0
				ENDIF
			NEXT
			LOCAL:1 ++
		ELSE
			PRINTFORML 「占術を通じて進むべき道を見てやろう」
			$EKISYA
			CALL ASK_M("今日の運勢", !GETBIT(FLAG:999, 1), "困っている人を探す", 1, "爪切り", BASE:MASTER:TSP >= 20, "ミラダの石の解呪(100カリスマ)", MONEY:2 >= 100, "サムズアップ", 1, "供物にする", 1)
			SELECTCASE RESULT
				CASE 0
					SELECTCASE RAND:10
						CASE 0
							TFLAG:幸運補正 = 3
							PRINTFORML 「絶好調だ、くじでも引きに行ったらどうだ？」
						CASE 1 TO 3
							TFLAG:幸運補正 = 1
							PRINTFORML 「良好だ、溜まっていた作業があればやるといい」
						CASE 4,5
							PRINTFORML 「よくもなし悪くもなし」
						CASE 6 TO 8
							TFLAG:幸運補正 = -1
							PRINTFORML 「低調だ、無理はするな」
						CASE 9
							TFLAG:幸運補正 = -3
							PRINTFORML 「……てんで駄目だな、大人しくしていろ」
					ENDSELECT
				CASE 1
					CLIENT_ID = SEARCH_UNIQUE_COM_人助け()
					IF !CLIENT_ID
						PRINTFORML 「うーむ、特に何も見えんな」
						PRINTFORML 「強いて言えば私だ、巫女に狙われている」
						PRINTFORML それはどうにもならなさそうだ
					ELSE
						PRINTFORML 「%CALLNAME:CLIENT_ID%が手助けを求めているようだぞ」
					ENDIF
				CASE 2
					PRINTFORMW %CALLNAME:MASTER%は易者を見ていると苛ついてきた
					PRINTFORMW ……なんだその爪は。
					PRINTFORML 人間やめましたアピールなのか知らないが、いくらなんでも伸ばしすぎだ。
					PRINTFORML 日常生活で不便じゃないのか？他人をうっかり引っ掻いちゃったらどうしようとは思わないのか？
					PRINTFORML 自分はいつデリケートな部分に触ってもいいように毎日チェックしているぞ。
					PRINTFORML それ以前に何かに引っ掛けて生爪剥がすことになったら痛いじゃないか。
					PRINTFORMW 第一汚れが溜まって不潔だ。
					PRINTFORMW 我慢できなくなった%CALLNAME:MASTER%は時間を止めて易者の爪を丁寧に切り揃えた
					PRINTFORMW %CALLNAME:MASTER%は大きな達成感を味わった……
					PRINTFORML 一方突然爪が短くなった易者は困惑している……
					CALL RECOVER_PERMIL(対象,40,"気力")
					BASE:MASTER:TSP -= 20
				CASE 3
					PRINTFORML 誰のを解呪する？
					FOR LOCAL,1,CHARANUM
						SIF CFLAG:LOCAL:体目当て
							PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
					NEXT
					PRINTFORML 
					DRAWLINE
					PRINTFORML [999] やめる
					INPUT
					IF RESULT == 999
						GOTO EKISYA
					ELSEIF CFLAG:RESULT:体目当て
						PRINTFORML 「こういう道具はよくないと思うぞ」
						PRINTFORMW 易者が何やら複雑な儀式を執り行うと、ミラダの石は徐々に光を失い崩れ去った
						TIME += 25
						CFLAG:RESULT:体目当て = 0
						MONEY:2 -= 100
					ENDIF
				CASE 4
					PRINTFORML %CALLNAME:MASTER%は易者と視線を交わし無言でサムズアップをした
					PRINTFORML 易者はしばし躊躇った後、力強いサムズアップを返してきた
					PRINTFORMW ふたりは友情を育んだ
					GOTO EKISYA
				CASE 5
					PRINTFORML %CALLNAME:MASTER%は時間を止め易者の服を脱がせ、その下着を神への供物とした
					CALL BUFF_BASE(MASTER,BASE_TSP,100,1)
					PRINTFORMW 知らぬ間に下着を脱がされた易者は落ち着かなさそうだ
			ENDSELECT
			PRINTFORMW 
			DRAWLINE
			$EKISYA_DIVIDE
			PRINTFORML 霊夢が現れ、易者の頭目掛けてお祓い棒を振り下ろした
			PRINTFORML 哀れ易者は２つに割られてしまった
			PRINTFORMW 霊夢は去っていった
			FOR LOCAL,1,CHARANUM
				IF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
					BASE:LOCAL:理性 = MAXBASE:LOCAL:理性
					BASE:LOCAL:ムード = 0
				ENDIF
			NEXT
			LOCAL:1 ++
		ENDIF
	CASE 530	;ミラダの石
		CALL SET_MIRADA
		IF RESULT == -1
			RETURN -1
		ELSE
			LOCAL:1 ++
		ENDIF
	CASE 550	;ヤツメ串
		PRINTFORML %ITEMNAME:使用アイテム%を食べた
		CALL 満腹度上昇(対象,"軽食")
		食品特徴 = %ITEMNAME:使用アイテム%/魚料理/おつまみ/
		CALL RECOVER_PERMIL(対象, ITEM_TASTE(対象, 40, 食品特徴), "体力", 0, "ヤツメ串による回復")
		CALL RECOVER_PERMIL(対象, ITEM_TASTE(対象, 80, 食品特徴), "気力", 0, "ヤツメ串による回復")
		CALL RECOVER_PERMIL(対象, ITEM_TASTE(対象, 50, 食品特徴), "精力", 0, "ヤツメ串による回復")
		LOCAL:1 ++
	CASE 576	;(酒造パッチ)あくまき
		PRINTFORML %ITEMNAME:使用アイテム%を食べた
		CALL 満腹度上昇(対象,"主食")
		食品特徴 = %ITEMNAME:使用アイテム%/和食/米/団子/甘い/もちもち/家庭的/春物/
		CALL RECOVER(対象, ITEM_TASTE(対象, 150, 食品特徴), "体力", "あくまきによる回復")
		CALL RECOVER(対象, ITEM_TASTE(対象, 150, 食品特徴), "気力", "あくまきによる回復")
		LOCAL:1 ++
	CASE 626	;キイチゴ
		PRINTFORML %ITEMNAME:使用アイテム%を食べた
		食品特徴 = %ITEMNAME:使用アイテム%/果物/甘い/すっぱい/野趣/
		CALL RECOVER(対象, ITEM_TASTE(対象, 50, 食品特徴), "気力", "キイチゴによる回復")
		LOCAL:1 ++
	CASE 628	;アケビ
		PRINTFORML %ITEMNAME:使用アイテム%を食べた
		食品特徴 = %ITEMNAME:使用アイテム%/果物/甘い/野趣/
		CALL RECOVER(対象,80, "体力", "アケビによる回復")
		CALL RECOVER(対象, ITEM_TASTE(対象, 80, 食品特徴), "気力", "アケビによる回復")
		LOCAL:1 ++
	CASE 630	;仙桃
		PRINTFORML %ITEMNAME:使用アイテム%を食べた
		PRINTL 力がみなぎる……！
		CALL BUFF_BASE(対象, BASE_体力, MAX(MAXBASE:対象:体力 / 10, 500))
		CALL BUFF_BASE(対象, BASE_気力, MAX(MAXBASE:対象:気力 / 10, 500))
		CALL BUFF_BASE(対象, BASE_精力, MAX(MAXBASE:対象:精力 / 10, 500))
		LOCAL:1 ++
	CASE 665	;(酒造パッチ)ヤマブドウ
		PRINTFORML %ITEMNAME:使用アイテム%を食べた
		食品特徴 = %ITEMNAME:使用アイテム%/果物/甘い/すっぱい/野趣/
		CALL RECOVER(対象, ITEM_TASTE(対象, 50, 食品特徴), "気力", "ヤマブドウによる回復")
		LOCAL:1 ++
	CASE 669	;(酒造パッチ)携帯タコス
		PRINTFORML %ITEMNAME:使用アイテム%を食べた
		CALL 満腹度上昇(対象,"軽食")
		食品特徴 = タマネギ/トマト/キャベツ/洋食/野菜/獣肉/スパイス/チーズ/辛い/ガッツリ/濃厚/機能的/野趣/鮮やか/サクサク/
		CALL RECOVER(対象, ITEM_TASTE(対象, 120, 食品特徴), "体力", "タコスによる回復")
		CALL RECOVER(対象, ITEM_TASTE(対象, 120, 食品特徴), "気力", "タコスによる回復")
		LOCAL:1 ++
	CASEELSE
		GOTO LOOP
ENDSELECT
IF LOCAL:1
	IF !FLAG:70
		TIME += 5
	ELSE
		BASE:MASTER:TSP -= 5
	ENDIF
	ITEM:使用アイテム -= 1
	SIF 虚無への回帰
		CALL OYASUMI
	RETURN 0
ENDIF


@SET_MIRADA
PRINTFORML 対象を選んでください。
PRINTFORML 黄色は既に有効、ピンクは既に陥落素質がついているキャラです。
SIF FLAG:並び替え
	CALL CHARASORT_EX
FOR LOCAL:1, 1, EX_CHARANUM + 1
	IF FLAG:並び替え
		LOCAL = RESULT:(LOCAL:1)
		SIF LOCAL < 0
			CONTINUE
	ELSE
		LOCAL = LOCAL:1
		SIF LOCAL >= CHARANUM
			BREAK
	ENDIF
	;SIF TALENT:LOCAL:行きずり
	;	CONTINUE
	SIF CFLAG:LOCAL:出禁
		CONTINUE
	SIF CFLAG:LOCAL:体目当て
		SETCOLOR C_YELLOW
	SIF TALENT:LOCAL:セフレ || TALENT:LOCAL:恋慕
		SETCOLOR C_PINK
	PRINTFORMLC [{LOCAL, 3}] %CALLNAME:LOCAL%
	RESETCOLOR
NEXT
PRINTFORML 
DRAWLINE
PRINTFORML [999] やめる
INPUT
SIF !INRANGE(RESULT,1,CHARANUM)
	RETURN -1
SIF CFLAG:RESULT:体目当て
	RETURN -1
IF TALENT:RESULT:セフレ || TALENT:RESULT:恋慕
	TCVAR:RESULT:発情 = 1
ELSE
	CFLAG:RESULT:体目当て = 1
ENDIF
PRINTFORMW %CALLNAME:RESULT%のことを思い浮かべながらミラダの石を強く握ると、石はうっすらと光を帯びた
RETURN 1

@使用可能アイテム(ARG)
;1だとMASTERのみ、2だとあなた以外にも使える
#FUNCTION

SIF CFLAG:MASTER:うふふ && !ITEMTYPE_DRUG(ARG)
	RETURNF 0
;(酒造パッチ)あくまきとヤマブドウ、タコス
;SELECTCASE ARG
;	CASE 121 TO 123, 215, 364, 500, 510, 514, 515, 516, 517, 520, 521, 530
;		RETURNF 1
;	CASE 120, 212, 213, 352, 501, 503, 505 TO 509, 511, 512, 550, 576, 626, 628, 630, 665, 669
;		RETURNF 2
;	CASE 502
;		SIF HAS_PENIS(TARGET)
;			RETURNF 2
;		RETURNF 1
;	CASEELSE
;		RETURNF 0
;ENDSELECT
SELECTCASE ITEMTYPE(ARG)
	CASE 1, 2, 3, 4, 5
		IF GROUPMATCH(ITEMNAME:ARG, "国士無双の薬", "チョコレート", "高級チョコ", "本命チョコ", "青汁")
			RETURNF 1
		ENDIF
		RETURNF 2
	CASE 6
		RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT

;本来@ITEMDATAを使うべきだが#FUNCTIONじゃないのがなんかやだ
@ITEMTYPE(ARG)
#FUNCTION
IF ITEMTYPE_DRUG(ARG)
	RETURNF 1
ELSEIF ITEMTYPE_BUFF(ARG)
	RETURNF 2
ELSEIF ITEMTYPE_FOOD(ARG)
	RETURNF 3
ELSEIF ITEMTYPE_SWEETS(ARG)
	RETURNF 4
ELSEIF ITEMTYPE_FRUIT(ARG)
	RETURNF 5
ELSEIF ITEMTYPE_ETCETERA(ARG)
	RETURNF 6
ENDIF
RETURNF 0

@ITEMTYPE_DRUG(ARG)
#FUNCTION
;SIF GROUPMATCH(ARG, 501, 502, 503, 505, 506, 507, 508, 509, 511)
SIF GROUPMATCH(ITEMNAME:ARG, "栄養ドリンク", "精力剤", "ギンギンＺ", "ギンギラギンＸ", "精力剤『天元凸破』", "ポーション", "マジックポーション", "破眠ポーション", "エリクサー")
	RETURNF 1

;BUFF_BASEを行うもの
@ITEMTYPE_BUFF(ARG)
#FUNCTION
;SIF GROUPMATCH(ARG, 512, 515, 630)
SIF GROUPMATCH(ITEMNAME:ARG, "仙丹", "国士無双の薬", "仙桃")
	RETURNF 1

;満腹度が増えるもの
@ITEMTYPE_FOOD(ARG)
#FUNCTION
;SIF GROUPMATCH(ARG, 550, 576, 669)
SIF GROUPMATCH(ITEMNAME:ARG, "ヤツメ串", "あくまき", "携帯タコス")
	RETURNF 1

;デザート満腹度が増えるもの
@ITEMTYPE_SWEETS(ARG)
#FUNCTION
;SIF GROUPMATCH(ARG, 120, 121, 122, 123, 212, 213)
SIF GROUPMATCH(ITEMNAME:ARG, "ブラックサンダー", "チョコレート", "高級チョコ", "本命チョコ", "かしわもち", "アイス")
	RETURNF 1

;満腹度が増えないもの
@ITEMTYPE_FRUIT(ARG)
#FUNCTION
;SIF GROUPMATCH(ARG, 352, 364, 500, 626, 628, 665)
SIF GROUPMATCH(ITEMNAME:ARG, "イチゴ", "スイカ", "青汁", "キイチゴ", "アケビ", "ヤマブドウ")
	RETURNF 1

;その他のもの
@ITEMTYPE_ETCETERA(ARG)
#FUNCTION
;SIF GROUPMATCH(ARG, 215, 510, 514, 516, 517, 520, 521, 530)
SIF GROUPMATCH(ITEMNAME:ARG, "紅茶", "ウコンドリンク", "仙香玉兎", "ガシングガーデン", "ポイズンボディ", "包帯", "易書", "ミラダの石")
	RETURNF 1

@PRINT_ITEM_EXPLANATION(ARG)
PRINTFORM [{ARG, 3, RIGHT}] %ITEMNAME:ARG, 18, LEFT% {ITEM:ARG, 3, RIGHT}個　
SETCOLOR C_L_GRAY
	PRINTFORML %ITEM_EXPLANATION(ARG)%
RESETCOLOR

@薬の知識(ARG)
#FUNCTION
#DIM 回復量
#DIM 強化回数
回復量 = ARG
;回復量 = 基準値 * (1.1 ^ (調合Lv + 薬に関係しそうな知識))
強化回数 = TALENT:MASTER:調合Lv + TALENT:MASTER:禁断の知識 + TALENT:MASTER:調合知識
WHILE 強化回数 > 0
	TIMES 回復量, 1.1
	強化回数 --
WEND
RETURNF 回復量

;味覚による回復補正
;満腹度が上がらない食品は気力のみ補正
@ITEM_TASTE(CHARA, 基準値, 食品特徴)
#FUNCTION
#DIM CHARA
#DIM 基準値
#DIMS 食品特徴
#DIMS TASTE_CHARA_GOOD
#DIMS TASTE_CHARA_BAD
#DIM 回復量
TASTE_CHARA_GOOD = %GET_STR(, "キャラデータ", CHARA, "料理：好きな味")%
TASTE_CHARA_BAD = %GET_STR(, "キャラデータ", CHARA, "料理：嫌いな味")%

回復量 = 基準値 + (STR_MULTI_COUNT(TASTE_CHARA_GOOD, 食品特徴) - STR_MULTI_COUNT(TASTE_CHARA_BAD, 食品特徴)) * 10

;旬など季節に応じた補正
SELECTCASE GET_MONTH()
	CASE "春の月"
		IF STRCOUNT(食品特徴, "春物/") && !STRCOUNT(TASTE_CHARA_BAD, "春物/")
			TIMES 回復量, 1.3
		ENDIF
	CASE "夏の月"
		;夏はアイスが2.0倍（嫌いじゃなければ）
		IF STRCOUNT(食品特徴, "アイス/") && !STRCOUNT(TASTE_CHARA_BAD, "アイス/")
			TIMES 回復量, 2.0
		ELSEIF STRCOUNT(食品特徴, "スイカ/")
			TIMES 回復量, 1.3
		ENDIF
	CASE "秋の月"
		IF STRCOUNT(食品特徴, "アケビ/")
			TIMES 回復量, 1.3
		ENDIF
	CASE "冬の月"
		;冬はアイスが0.5倍（好きじゃなければ）
		IF STRCOUNT(食品特徴, "アイス/") && !STRCOUNT(TASTE_CHARA_GOOD, "アイス/")
			TIMES 回復量, 0.5
		ENDIF
ENDSELECT

RETURNF 回復量

;アイテム
@COM_ABLE490
;移動実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(490)
	RETURN RESULT
RETURN 1

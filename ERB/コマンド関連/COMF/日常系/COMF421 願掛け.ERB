;-------------------------------------------------
;願掛け
;日常系コマンド、レベル1
;-------------------------------------------------
@COM421
PRINTL 何を願いますか？
$INPUT_LOOP
PRINTL [0] - 縁結び(\1000)
SIF TALENT:MASTER:2 & 2
	PRINTL [1] - 精力絶倫(\3000)
PRINTL [2] - 心の平安(\3000)
PRINTL [3] - おっぱいおっぱい(\3000)
PRINTL [10] - ギャルのパンティおくれーっ！！！！(\5000)
PRINTL [15] - みんながＨな気分になりますように(\10000)
SIF TARGET && ABL:サドっ気 >= 3
	PRINTFORML [16] - \@ TALENT:恋人? 幻想郷一かわいい# \@%CALLNAME:TARGET%が激しすぎるので何とかして
SIF TARGET
	PRINTFORML [20] - \@ TALENT:恋人? 幻想郷一かわいい# \@%CALLNAME:TARGET%がもっとＨになりますように
SIF TALENT:TARGET:恋慕 && !TALENT:妊娠
	PRINTFORML [21] - 子宝祈願(現在の子宝補正:{CFLAG:子宝補正})
PRINTFORML [30] - 女の子に嫌われてしまったのを何とかしたい
;21/08/27 反発履歴削除追加
PRINTFORML [35] - 女の子のわだかまりをなくしたい
SIF CFLAG:MASTER:魔力回収 && CFLAG:MASTER:小槌 < 6
	PRINTFORML [40] - 小槌の代償を取り除いてほしい
SIF FLAG:開運大紋購入回数 > 10
	PRINTFORML [50] - 悪徳商人に天罰を
SIF CAN_DOWSING()
	PRINTFORML [60] - 探しものが見つからない
PRINTL [999] - やっぱりやめる
INPUT
SIF RESULT == 999
	RETURN -1

IF RESULT == 0
	IF MONEY < 1000
		PRINTL お金が足りない
		GOTO INPUT_LOOP
	ENDIF
	PRINTL 誰の事を思い浮かべますか？
	CALL SHOW_CHALALIST
	PRINTL 
	PRINTL [999] - やっぱりやめる
	INPUT
	IF !INRANGE(RESULT,1,CHARANUM)
		RETURN -1
	ELSE
		CALL GANKAKE_0(RESULT)
		MONEY -= 1000
	ENDIF
;精力絶倫
ELSEIF RESULT == 1
	IF MONEY < 3000
		PRINTL お金が足りない
		GOTO INPUT_LOOP
	ENDIF
	MONEY -= 3000
	FLAG:願掛け内容 = 200
	IF EXP:MASTER:11 > (100 + FLAG:561 * 100) && FLAG:561 < 11
		LOCAL += 100
		FLAG:561 += 1
	ENDIF
	CALL BUFF_BASE(MASTER,BASE_精力,500)
	FLAG:71 ++
;心の平安
ELSEIF RESULT == 2
	IF MONEY < 3000
		PRINTL お金が足りない
		GOTO INPUT_LOOP
	ENDIF
	MONEY -= 3000
	CALL BUFF_BASE(MASTER,BASE_気力,500)
	FLAG:願掛け内容 = 201
	FLAG:71 = 0
ELSEIF RESULT == 3
	IF MONEY < 3000
		PRINTL お金が足りない
		GOTO INPUT_LOOP
	ENDIF
	MONEY -= 3000
	PRINTFORML %CALLNAME:MASTER%は手を合わせながら一心不乱におっぱいのことを考えた
	TFLAG:OPPAI = 1
ELSEIF RESULT == 10
	IF MONEY < 5000
		PRINTL お金が足りない
		GOTO INPUT_LOOP
	ELSE
		MONEY -= 5000
		FLAG:願掛け内容 = 202
	ENDIF
ELSEIF RESULT == 15
	IF MONEY < 10000
		PRINTL お金が足りない
		GOTO INPUT_LOOP
	ELSE
		MONEY -= 10000
		FLAG:願掛け内容 = 203
	ENDIF
ELSEIF RESULT == 16 && ABL:サドっ気
		CALL IKENIE(TARGET,7)
ELSEIF RESULT == 20 && TARGET != 0
		CALL IKENIE(TARGET,2)
ELSEIF RESULT == 21 && TALENT:TARGET:恋慕
		CALL IKENIE(TARGET,6)
ELSEIF RESULT == 30
	PRINTL 誰を？
	DRAWLINE
	FOR LOCAL,1,CHARANUM
		SIF MARK:LOCAL:反発刻印 && !CFLAG:LOCAL:出禁
			PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL,15,LEFT%
	NEXT
	PRINTL 
	DRAWLINE
	PRINTL [999] - やっぱりやめる
	INPUT
	IF RESULT >= 1 && RESULT <= CHARANUM && MARK:RESULT:反発刻印
		CALL IKENIE(RESULT,1)
	ELSE
		RETURN -1
	ENDIF
ELSEIF RESULT == 35
	PRINTL 誰の？
	DRAWLINE
	FOR LOCAL,1,CHARANUM
		SIF !MARK:LOCAL:反発刻印 && MARK:LOCAL:反発取得履歴 && !CFLAG:LOCAL:出禁
			PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL,15,LEFT%
	NEXT
	PRINTL 
	DRAWLINE
	PRINTL [999] - やっぱりやめる
	INPUT
	IF RESULT >= 1 && RESULT <= CHARANUM && !MARK:RESULT:反発刻印 && MARK:RESULT:反発取得履歴
		CALL IKENIE(RESULT,8)
	ELSE
		RETURN -1
	ENDIF

ELSEIF RESULT == 40 && CFLAG:MASTER:魔力回収 && CFLAG:MASTER:小槌 < 6
	CALL IKENIE(71,3)
ELSEIF RESULT == 50 && FLAG:開運大紋購入回数 > 10
	CALL IKENIE(53,5)
ELSEIF RESULT == 60 && CAN_DOWSING()
	CALL DOWSING_PANTS
ENDIF
DOWNBASE:MASTER:気力 = 100
TIME += 10
RETURN 1


;願掛け
@COM_ABLE421
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;掃除実行判定
SIF !TFLAG:100
	RETURN 0
;すでに今日願掛けしてたら弾く
SIF FLAG:願掛け内容 > 0
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(421)
	RETURN RESULT
;賽銭箱
SIF !天の声可(CFLAG:MASTER:現在位置) && CFLAG:MASTER:現在位置 != 守矢神社本殿
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
SIF CFLAG:うふふ == 2
	RETURN 0
RETURN 1

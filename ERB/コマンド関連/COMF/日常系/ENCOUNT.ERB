;21/09/01 口上/口上色対応,LOOTアイテム整理
@ENEMY_ENCOUNT(ARG)
#DIM ENEMY
#DIMS ENEMY_RACE
#DIM 勝敗
#DIM LOOT
#DIM LOOT_CANDIDATE, 17 = 51, 52, 53, 55, 56, 58, 59, 81, 82, 83, 84, 121, 131, 500, 501, 502, 510
#DIM CONST CANDIDATE_N = 17
#DIM REWARD
#DIM 販売員遭遇

;デート中は発生しない
SIF FLAG:デート相手
	RETURN
	
;その日物色済みだと無理
SIF GETBIT(TFLAG:一日一回, 9)
	RETURN

;とりあえず50%固定で
SIF RAND:100 > 50
	RETURN

;まれにイベント
IF 販売員遭遇 != DAY && !(FLAG:日替わりイベント % 8)
	販売員遭遇 = DAY
	CALL イナバの流し売り
	RETURN
ENDIF

;相手が変わるとかやりたい
SELECTCASE ARG
	;湖
	CASE 3
		ENEMY_RACE = 妖精
	;竹林
	CASE 4
		IF !RAND:3
			ENEMY_RACE = イナバ
		ELSE
			ENEMY_RACE = 妖精
		ENDIF
	;山麓
	CASE 7
		ENEMY_RACE = 河童
	;山頂
	CASE 8
		ENEMY_RACE = 白狼天狗
	CASE 9
		ENEMY_RACE = 鬼
	CASE 10
		ENEMY_RACE = 玉兎
	CASEELSE
		RETURN
ENDSELECT


HANDICAP_FIXED = RAND:(ABL:ENEMY:戦闘能力 * 5 + 1) + 10
HANDICAP_RAND = 0
PRINTFORMW 道中、不意に%ENEMY_RACE%が襲い掛かってきた！
SETBIT TFLAG:一日一回, 9
CALL MAKE_YUKIZURI(RANDOM_CHARANUM, ENEMY_RACE, 1)

CALL KOJO_MESSAGE_SEND("DANMAKU", 0, RANDOM_CHARANUM, 1, -1, "戦闘前")
TEQUIP:RANDOM_CHARANUM:上半身着衣状況 = 1

CALL CHARA_STATE(RANDOM_CHARANUM)
	DRAWLINE
	CALL COLORMESSAGE(@"処女喪失履歴：%CSTR:RANDOM_CHARANUM:処女喪失履歴%",C_PINK,1)
	PRINTFORML 
CALL DANMAKU_OPPONENT_STATUS(RANDOM_CHARANUM)
IF HANDICAP_FIXED
	CALL COLORMESSAGE(@"地形効果：%CALLNAME:MASTER%のダイス値が{HANDICAP_FIXED}低下",C_YELLOW,1)
	;【三粒の天滴】の効果
	IF ITEM:三粒の天滴
		HANDICAP_FIXED /= 2
		CALL COLORMESSAGE(@"土着神の呪いにより、地形効果が{HANDICAP_FIXED}に半減！",C_YELLOW,1)
	ENDIF
ENDIF
$LOOP
CALL ASK_M("返り討ちにしてやる",1,"逃げる",1,"ルール説明",1)
SELECTCASE RESULT
	CASE 0
		IF BASE:MASTER:気力 < 500 || BASE:MASTER:体力 < 500
			PRINTFORML 今は疲れすぎている
			GOTO LOOP
		ENDIF
	CASE 1
		PRINTFORML %CALLNAME:MASTER%はすたこら逃げ出した
		RETURN
	CASE 2
		CALL SHOW_DANMAKU_RULE
		GOTO LOOP
ENDSELECT
ENEMY = RANDOM_CHARANUM
TARGET = ENEMY
REWARD = MAX(ABL:ENEMY:戦闘能力 * 10 + RAND:5, 1)
;米とか酒とか
LOOT = LOOT_CANDIDATE:(RAND:CANDIDATE_N)
TIME += 60
CFLAG:ENEMY:面識 = 1
PRINTFORMW 勝負開始！！
CALL DANMAKU_BATTLE(TARGET, 1)

勝敗 = RESULT
CALL RECOVER(MASTER, -500, "体力")
CALL RECOVER(MASTER, -500, "気力")
CALL KOJO_MESSAGE_SEND("DANMAKU", 勝敗, TARGET, 1, -1, "戦闘後")
SIF 勝敗 < 3
	EXP:MASTER:戦闘経験 += ABL:ENEMY:戦闘能力 * 3
SIF 勝敗 < 1
	EXP:MASTER:戦闘経験 += ABL:ENEMY:戦闘能力
IF 勝敗 > 0
	;CALL KOJO_MESSAGE_SEND("DANMAKU", 勝敗, TARGET, 1, -1, "逆レ開始")
	SETCOLOR C_RED
	IF TCVAR:ENEMY:発情 && HAS_PENIS(MASTER)
		PRINTFORML %CALLNAME:ENEMY%は動けなくなった%CALLNAME:MASTER%を組み敷いた
		PRINTFORML %CALLNAME:MASTER%の喉笛を、生暖かい吐息が吹き降りていく…
		PRINTFORML もはやこれまでと思った次の瞬間、性器が熱い感触に包まれた
		PRINTFORML 恐る恐る下腹を見下ろすと、ニヤニヤと笑みを浮かべる%CALLNAME:ENEMY%のワレメに%CALLNAME:MASTER%のペニスが根元まで収まっている
		PRINTFORML 満月の夜に発情期を迎えた%CALLNAME:ENEMY%は獣性の渇きを癒すため、%CALLNAME:MASTER%を滅茶苦茶に犯し、嬲り、蹂躙していった…
		PRINTFORMW ………
		PRINTFORMW ……
		PRINTFORMW …
		PRINTFORMW 気が済むまで%CALLNAME:MASTER%を貪り尽くした%CALLNAME:ENEMY%は、ぐったりした%CALLNAME:MASTER%を打ち捨てて去って行った
		FLAG:気絶中断 = 100 + CFLAG:MASTER:現在位置 / 100
		CFLAG:MASTER:デート中 = CFLAG:MASTER:現在位置 / 100
		CALL LOST_GYAKURE(ENEMY)
	ELSEIF ITEM:LOOT
		PRINTFORMW 負けた%CALLNAME:MASTER%は%ITEMNAME:LOOT%を取られてしまった…
		ITEM:LOOT --
	ELSEIF MONEY:2 >= REWARD
		PRINTFORMW 負けた%CALLNAME:MASTER%は{REWARD}カリスマ取られてしまった…
		MONEY:2 -= REWARD
	ELSE
		REWARD *= 300
		PRINTFORMW 負けた%CALLNAME:MASTER%は\@ MONEY > REWARD ? \\{REWARD}# 有り金すべて\@取られてしまった…
		MONEY = MAX(MONEY - REWARD, 0)
	ENDIF
	RESETCOLOR
	RETURN
ENDIF

;source操作他
TCVAR:ENEMY:弾幕勝負不能 = 1
CFLAG:ENEMY:弾幕勝負勝利 = 1
CFLAG:ENEMY:負け犬 = 1
CFLAG:ENEMY:よく行く地域 = ARG

PRINTFORML %CALLNAME:MASTER%は地べたに膝をつく%CALLNAME:ENEMY%に…
CALL ASK_M("供物にする",1,"カツアゲ",1,"体で償わせる",1, "ミラダの石", ITEM:ミラダの石)
SELECTCASE RESULT
	CASE 0
		PRINTFORML %CALLNAME:MASTER%が%CALLNAME:ENEMY%に手をかざして祈りの言葉を唱えると、服を透かして%CALLNAME:ENEMY%の下着が淡く輝き始めた
		PRINTFORML やがて超自然の炎が熾り、%CALLNAME:ENEMY%も衣服も害することなく下着だけが燃え尽きた
		PRINTFORML 神威を目の当たりにした%CALLNAME:ENEMY%は恐れをなして逃げて行った
		CALL BUFF_BASE(MASTER,BASE_TSP,500, 1)
		CALL BUFF_BASE(MASTER,BASE_精力,500, 1)
		FLAG:篤信 += 10
		;DELCHARA RANDOM_CHARANUM
		;ADDCHARA RANDOM_CHARANUM
		TARGET = 0
		RETURN
	CASE 1
		PRINTFORMW オラッ！ジャンプしろジャンプ！
		MONEY:2 += REWARD
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:ENEMY%から{REWARD}カリスマまきあげた
		IF RAND:5 < ABL:ENEMY:戦闘能力
			PRINTFORMW さらに%ITEMNAME:LOOT%もまきあげた
			ITEM:LOOT ++
		ENDIF
		TARGET = 0
		;DELCHARA RANDOM_CHARANUM
		;ADDCHARA RANDOM_CHARANUM
		RETURN
	CASE 2
		MARK:ENEMY:反発刻印 = 1
		PRINTFORML %CALLNAME:ENEMY%に、巫女に突き出されたくなければ今日一日%CALLNAME:MASTER%に従うように命じた
		CALL COLORMESSAGE(@"%CALLNAME:ENEMY%に反発刻印がついたが、人前でなければいつでも押し倒せるようになった",C_YELLOW,1)
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:ENEMY%を連れて意気揚々と帰路についた
	CASE 3
		PRINTFORML 睨みつけてくる%CALLNAME:ENEMY%を見返しながら、%CALLNAME:MASTER%はミラダの石を握りしめた
		PRINTFORML すると%CALLNAME:ENEMY%は視線を逸らして俯いてしまった
		CFLAG:ENEMY:体目当て = 1
		ITEM:ミラダの石 --
		ABL:ENEMY:親密 = MAX(3, ABL:ENEMY:親密)
		ABL:ENEMY:欲望 = MAX(3, ABL:ENEMY:欲望)
		CFLAG:ENEMY:溜まってる度 = 1000
		PRINTFORML %CALLNAME:MASTER%は%CALLNAME:ENEMY%の肩に手を置いて、今日一日従うように命じた
		PRINTFORML %CALLNAME:ENEMY%はやけに素直に受け入れた
		CALL COLORMESSAGE(@"%CALLNAME:ENEMY%を人前でなければいつでも押し倒せるようになった",C_YELLOW,1)
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:ENEMY%を連れて意気揚々と帰路についた
ENDSELECT

FLAG:デート相手 = TARGET
CFLAG:TARGET:同行 = 180
CFLAG:MASTER:同行 = 180
CFLAG:ENEMY:眠気覚まし = 600
CFLAG:拠点来訪 = 1
CSTR:ENEMY:モブ子プロフィール２ = 無様にも返り討ちにされ今はもう大人しい

RETURN 1

@LOST_GYAKURE(ARG)
BASE:MASTER:体力 = 0
BASE:MASTER:気力 = 0
BASE:MASTER:精力 = -500
CFLAG:ARG:溜まってる度 = 0
CALL SET_SLEEP(1,MASTER,0)
CFLAG:MASTER:就寝時間 = DAY * 1440 + TIME + 120 + RAND:120
TFLAG:106 = 0
BEGIN AFTERTRAIN

;IF TIME:2 >= 5 && TIME:2 <= 7 && TIME:5 < 2
;	CALL COLORMESSAGE(@"満月の光が%CALLNAME:TARGET%の本能をかき立てる…！",C_YELLOW,1)
;	CALL COLORMESSAGE(@"%CALLNAME:TARGET%の戦闘力がアップ！",C_YELLOW,1)
;ENDIF

;21/09/20 イベント販売員追加
;固有コマンド:薬品販売とデイリー販売員イナバからの流用
;デイリーイナバが愛されキャラなのでこっちは腹黒うざキャラで。販売価格もぼったくり
@イナバの流し売り
#DIM 取引
#DIM 割引
#DIM 値段
#DIM 石販売中
石販売中 = (GETBIT(FLAG_DAILY_EV23, 0) && !ITEM:(GETNUM(ITEM, "ミラダの石")))
割引 = -50
値段 = ITEMPRICE:(GETNUM(ITEM, "ミラダの石")) / 10
SETCOLOR 0XFFFFFF
PRINTFORMDW 道中、不意にイナバが襲い掛かってきた…！？
PRINTFORML
PRINTFORML 「あああ！！いけません！！お客さま！！！ああっ！困りますっ！！」
PRINTFORMW 「あーっ！！お客さま！！私は弾幕勝負なんてやりませんから！！どうか待ってください！！！」
PRINTFORML 「ああっ！いけません！！どうか落ち着いて！！話を聞いてくださいっ！！あーっ！！！」
PRINTFORMDW 大きな荷物を抱えたイナバが一人で勝手に騒いでいる… 
CALL PRINT_FACE, 0, "通常", "服", "18"
PRINTFORML 「えー、お見苦しいところをすみません」
PRINTFORMW 「私少々慌ててしまいまして…」
PRINTFORML 「でも、ここで出会ったのもなにかのご縁」
PRINTFORM 「お客さまはわたくしどもの
CALL COLORMESSAGE("素晴らしい", C_YELLOW, 0, 1)
PRINTFORMW 商品に興味はありませんか？」
RESETCOLOR
CALL ASK_M("無視して帰る", 1, "ちょっと見てみる", 1)
SETCOLOR 0XFFFFFF
IF !RESULT
	PRINTFORMDL うさんくさいセールスはお断りだ
	PRINTFORML 「そんなー、せめてお話だけでもー」
	PRINTFORMDL なおも食い下がる販売員を無視して%CALLNAME:MASTER%は帰路についた
	PRINTFORMW
	RESETCOLOR
	RETURN
ENDIF
CALL PRINT_FACE, 0, "笑顔", "服", "18"
PRINTFORMW 「ありがとうございます！」
PRINTFORM 「では、他所では見られない
CALL COLORMESSAGE("秘蔵", C_AQUA, 0, 1)
PRINTFORML の一品！」
PRINTFORM 「多少
CALL COLORMESSAGE("お値段", C_M_GREEN, 0, 1)
PRINTFORM は張りますが、その効果は
CALL COLORMESSAGE("折り紙", C_RED, 0, 1)
PRINTFORMW 付き！！」
PRINTFORM 「お客さまのような
CALL COLORMESSAGE("特別", C_YELLOW, 0, 1)
PRINTFORM な方にふさわしい
CALL COLORMESSAGE("特別", C_YELLOW, 0, 1)
PRINTFORML な商品の数々！！」
PRINTFORMW 「さあこちらをご覧ください！！！」
RESETCOLOR
DRAWLINE
$INPUT_LOOP
RESETCOLOR
CALL ITEM_SHOPPING(GETNUM(ITEM, "仙香玉兎"), "", 割引)
CALL ITEM_SHOPPING(GETNUM(ITEM, "国士無双の薬"), "", 割引)
CALL ITEM_SHOPPING(GETNUM(ITEM, "ガシングガーデン"), "", 割引)
CALL ITEM_SHOPPING(GETNUM(ITEM, "ポイズンボディ"), "", 割引)
IF 石販売中
	PRINTFORM [{GETNUM(ITEM, "ミラダの石")}] 
	CALL COLORMESSAGE("愛欲を独占するミラダの石ミレニアム", C_D_PINK, 1, 1)
	CALL COLORMESSAGE("　　　特別価格90％OFF！", C_RED, 0, 1)
	PRINTFORML %金額表示(値段), 13%
ENDIF
DRAWLINE
SIF 石販売中
	PRINTL [990] 「ミラダの石」って何だっけ？
PRINTFORML [999] やっぱり買わない
PRINTFORML 　　　現在の所持金:%金額表示(MONEY)%
$INPUT_LOOP2
INPUT
取引 = RESULT
SETCOLOR 0XFFFFFF
SELECTCASE 取引
	CASE GETNUM(ITEM, "仙香玉兎") TO GETNUM(ITEM, "ポイズンボディ"), GETNUM(ITEM, "ミラダの石")
		IF 取引 == GETNUM(ITEM, "ミラダの石") && !石販売中
			CLEARLINE 1
			GOTO INPUT_LOOP2
		ENDIF
		LOCAL = 取引 == GETNUM(ITEM, "ミラダの石") ? 90 # 割引
		CALL SHOP_ASK(RESULT, LOCAL)
		IF RESULT
			PRINTFORMW 「えー、では他の品物なんてどうでしょうか？」
			CLEARLINE 12 + RESULT + (石販売中 * 3)
			GOTO INPUT_LOOP
		ENDIF
		CALL GET_ITEM(ITEMNAME:取引, 1, 2)
	;あのセリフたまに聞きたくなるので
	CASE 990
		IF !石販売中
			CLEARLINE 1
			GOTO INPUT_LOOP2
		ENDIF
		PRINTFORMW 「おや？　お客様さまはご存知かと思いましたが…」
		PRINTFORML 「ああでも懐かしいですね」
		PRINTFORML 「私も仕事を始めたての頃、あの商品説明を必死に覚えたものです…」
		PRINTFORML 「では私の確認がてら、お客さまにも改めて聞いて頂きましょう」
		PRINTFORMW 
		CALL DAILY_EV23_SALESTALK(1)
		FONTBOLD
		PRINTFORMW 「なんと！」
		PRINTFORM 「今回、お値段
		CALL COLORMESSAGE(@"\\{値段}", C_RED, 0, 1)
		PRINTFORMW でのご提供が実現いたしました！」
		FONTREGULAR
		PRINTFORML 「いつ手に入るかわからない希少な品！！」
		PRINTFORM 「この
		CALL COLORMESSAGE("特別価格", C_RED, 0, 1)
		PRINTFORML で買わなければ…」
		PRINTFORMW 「もう二度と手に入らないかもしれませんよ！？」
		PRINTFORML 「さあ、お客さま！」
		PRINTFORM 「充実した性生活を約束する
		CALL COLORMESSAGE("愛欲を独占するミラダの石ミレニアム", C_D_PINK, 0, 1)
		PRINTFORMW ！！！！」
		PRINTFORML 「もちろんっ！　買いますよねッ！？」
		FONTREGULAR
		CALL ASK_M("買った！", MONEY >= 値段, "買わない", 1)
		IF !RESULT
			MONEY -= 値段
			CALL GET_ITEM("ミラダの石", 1, 2)
		ELSE
			取引 = 999
		ENDIF
	CASE 999
	CASEELSE
		CLEARLINE 1
		GOTO INPUT_LOOP2
ENDSELECT
IF 取引 < 999
	CALL PRINT_FACE, 0, "笑顔", "服", "18"
	CALL COLORMESSAGE("「お買い上げありがとうございます！」", 0XFFFFFF, 2, 1)
	PRINTFORMW 「いや～お客さまはいいカモ…」
	CLEARLINE 1
	PRINTFORMW 「ぜひまたお買い上げ頂きたいですね！！」
	PRINTFORML 「では今日はこの辺で失礼します！」
ELSE
	CALL PRINT_FACE, 0, "怒り", "服", "18"
	CALL COLORMESSAGE("「なぁんだ、ただの貧乏人かあ…」", 0XFFFFFF, 1, 2)
	CALL COLORMESSAGE("「やだやだ…時間を損しちゃったわー」", 0XFFFFFF, 1, 2)
	PRINTFORMDW %CALLNAME:MASTER%が買わないとわかった途端、販売員の態度が悪くなった…
	CALL COLORMESSAGE(@"「なーんにも買わないヤツ相手するほど%TEXTR("あたい/あたし/ボク/")%も暇じゃないんだよー」", 0XFFFFFF, 1, 2)
	CALL COLORMESSAGE("「じゃあーねー」", 0XFFFFFF, 1, 2)
ENDIF
PRINTFORMDW ウサギの販売員は\@ 取引 < 999 ? # 捨て台詞を残して \@いなくなった
RESETCOLOR
PRINTFORML 
RETURN
	
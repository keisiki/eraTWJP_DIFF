;-------------------------------------------------
;おみくじ
;日常系コマンド、レベル1
;-------------------------------------------------
;LOCAL
@COM434
VARSET LOCAL

IF !GETBIT(FLAG:999, 0)

	PRINTFORML 賽銭箱の隅に張り紙がある
	PRINTFORML ┌─────────┐
	PRINTFORML ｜　　　　　　　　　｜
	PRINTFORML ｜　おみくじ　　　　｜
	PRINTFORML ｜　　はじめました　｜
	PRINTFORML ｜　　　　　　　　　｜
	PRINTFORML ｜　　　　　　　　　｜
	PRINTFORML ｜　　1日1回 200円　｜
	PRINTFORML ｜　　お代はこちら　｜
	PRINTFORML ｜　　　　　　　　　｜
	PRINTFORML └─────────┘
	PRINTFORMW 
	PRINTFORMW …
	PRINTFORMW 賽銭箱に入れればいいらしいが、一体どこから出てくるのだ？

	SETBIT FLAG:999, 0

ENDIF

IF GETBIT(FLAG:999, 1)
	PRINTFORML おみくじは1日1回です。
	RETURN -1
ENDIF

IF MONEY < 200
	PRINTFORML 財布を確認したらお金が足りなかった。
	RETURN -1
ENDIF

PRINTFORML おみくじは1回200円、どうする？
PRINTFORML [1] 引いてみる
PRINTFORML [0] やめておく
PRINTFORML 
INPUT
SIF !RESULT
	RETURN -1

MONEY -= 200
TIME += 2
SETBIT FLAG:999, 1

SELECTCASE RAND:15
	CASE 0 TO 6
		PRINTL パンッ、パンッ
		PRINTL お金を入れて柏手を打つ
		PRINTW ……
		PRINTL ヒラヒラと何かが落ちてきた
		PRINTW 手に取って見たところ、どうやらこれがおみくじらしい
	CASE 7 TO 11
		PRINTL チャリンチャリン
		PRINTW お金を賽銭箱に放り込むと……
		PRINTW 賽銭箱からおみくじが飛び出してきた
	CASE 12 TO 13
		PRINTW ……
		PRINTW …
		PRINTW あ…ありのまま 今 起こった事を話すぜ！
		PRINTW 『おみくじのためにお賽銭を入れたらその手におみくじが握られていた』
		PRINTW な… 何を言ってるのか わからねーと思うがおれも何をされたのかわからなかった…
		PRINTW 頭がどうにかなりそうだった…
		PRINTW 催眠術だとか超スピードだとか時間停止だとか、そんなチャチなもんじゃあ断じてねえ
		PRINTW もっと恐ろしいものの片鱗を味わったぜ…
	CASE 14
		PRINTW （ゴンッ）
		PRINTL 後頭部に何かがぶつかった
		PRINTL 振り返すと、足元に何かを包んだ紙が落ちていた
		PRINTW 開けてみると、紙はおみくじで……
		PRINTW ……おみくじで石を包ないでください
ENDSELECT

PRINTW 受け取った（？）おみくじの中身を見てみた。
PRINTW …
CALL FORTUNE

;当たり、キャッシュバックによる返金処理
IF GETBIT(FLAG:999, 10)
	PRINTL ……
	PRINTW …

	SELECTCASE RAND:15
		CASE 0 TO 4
			PRINTW おみくじの最後に奇妙な文言があった
			PRINTL ┌─・─ｷﾘﾄﾘｾﾝ─・─┐
			PRINTL ｜　　　　　　　　　｜
			PRINTL ｜　幻想郷共通　　　｜
			PRINTL ｜　　　　　商品券　｜
			PRINTL ｜　　　　　　　　　｜
			PRINTL ｜　　　　　 200円　｜
			PRINTL ｜　　　　　　　　　｜
			PRINTL ｜　お釣りもでます　｜
			PRINTL ｜　　使用期限なし　｜
			PRINTL └─────────┘
			PRINTW これはいわゆるひとつの金券と考えていいのだろうか？
		CASE 5 TO 6
			PRINTL ﾋｭｯ
			PRINTW 風切り音を聞いたあなたはとっさに身を伏せた
			PRINTL ﾄｽﾄｽｯ
			PRINTW 何かが刺さったような音がした壁を見ると、そこには先ほど入れたはずのお賽銭が刺さっていた
			PRINTL あれ、気づいてなかったら刺さっていた？
		CASE 7 TO 11
			PRINTL チャリンチャリン♪    ○彡
			PRINTW 賽銭箱からさっき投げ入れたはずのお賽銭が飛び出してきた
		CASE 12 TO 13
			PRINTW ……
			PRINTW …
			PRINTW あ…ありのまま 今 起こった事を話すぜ！
			PRINTW 『おみくじを読み終えたと思ったら さっき入れたはずのお賽銭が手のなかに戻っていた』
			PRINTW な… 何を言ってるのか わからねーと思うがおれも何をされたのかわからなかった…
			PRINTW 頭がどうにかなりそうだった…
			PRINTW 瞬間移動だとか因果逆転だとか時間停止だとか、そんなチャチなもんじゃあ断じてねえ
			PRINTW もっと恐ろしいものの片鱗を味わったぜ…
		CASE 14
			PRINTW （コツコツッ）
			PRINTL 後頭部に何かがぶつかった
			PRINTL 振り返すと、足元にさっき入れたはずのお賽銭が落ちていた
			PRINTW このお金で明日も引きに来いということなのだろうか？
	ENDSELECT
	
	MONEY += 200
	CLEARBIT FLAG:999, 10
ENDIF


RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
;おみくじ
@COM_ABLE434
;一括管理
SIF GLOBAL_COMABLE(434)
	RETURN RESULT
SIF !TFLAG:100
	RETURN 0
;気力0では不可
SIF BASE:MASTER:気力 <= 0
	RETURN 0
;賽銭箱（おみくじの代金入れも兼ねている）でないと引けない
SIF !OMIKUJI_SPOT(CFLAG:MASTER:現在位置)
	RETURN 0
SIF CFLAG:MASTER:現在位置 != 賽銭箱
	RETURN 0
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
RETURN 1

@OMIKUJI_SPOT(ARG)
#FUNCTION
;博麗神社の賽銭箱
SIF ARG == GET_MAP_REPLACEMENT(3)
	RETURNF 1
RETURNF 0

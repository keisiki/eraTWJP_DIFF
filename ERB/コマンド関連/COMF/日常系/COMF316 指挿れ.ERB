;-------------------------------------------------
;指挿入れ
;日常系コマンド　レベル3
;-------------------------------------------------
@COM316
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER

;破瓜抑制
TFLAG:2 = 1
TFLAG:理性削り = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 40
	IF !TALENT:処女 || TALENT:処女 -1
		SOURCE:快Ｖ = 120
	ELSE
		SOURCE:快Ｖ = 60
	ENDIF
ELSE
	IF FLAG:甲斐性無 && !CFLAG:うふふ
		DRAWLINE
		PRINTFORMW %CALLNAME:MASTER%にはそんな度胸はない
		RETURN -1
	ENDIF
	;口上による強制失敗
	IF TFLAG:192 == -1
		RETURN -1
	ENDIF
	DOWNBASE:体力 += 30
	DOWNBASE:気力 += 80

	IF !TALENT:処女 || TALENT:処女 -1
		SOURCE:快Ｖ = 120
		SOURCE:情愛 = 150
		SOURCE:苦痛 = 250
		SOURCE:欲情 = 200
		SOURCE:露出 = 250
		SOURCE:逸脱 = 150
		SOURCE:反感 = 800
	;V経験が0の時はV経験を伴わない物に派生
	;Vの珠は入らないが、快Vが高いと慣らしてあるため処女喪失時に影響
	ELSE
		SOURCE:快Ｖ = 60
		SOURCE:情愛 = 150
		SOURCE:苦痛 = 250
		SOURCE:欲情 = 200
		SOURCE:露出 = 350
		SOURCE:逸脱 = 300
		SOURCE:反感 = 1000
	ENDIF

	;信頼
	TFLAG:98 = -7

	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;抑圧、抵抗持ちには、抑鬱ソース追加
	IF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 20
		;処女
		SIF TALENT:処女
			SOURCE:鬱屈 += 10
	ENDIF
ENDIF
;Vへの苦痛
CALL PAIN_CHECK_V(TARGET)
;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の膣内⇔調教者の指の汚れが移動
STAIN:6 |= STAIN:PLAYER:1
STAIN:PLAYER:1 |= STAIN:6
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:PLAYER:手淫経験 ++
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
	ENDIF
ELSE
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 4
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
	ENDIF
	TIME += 10
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE316
;指挿入れ実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(316)
	RETURN RESULT
;時間停止中はスキップ
IF !FLAG:70
	;馴れ合い強度2
	SIF (TCVAR:MASTER:馴れ合い < 2) && CFLAG:睡眠 == 0
		RETURN 0
	;着衣
	SIF TEQUIP:0 & 4
		RETURN 0
	;睡眠中
	SIF CFLAG:睡眠 && CFLAG:イタズラ < 2
		RETURN 0
	;仕事中
	SIF CFLAG:行動 == 5
		RETURN 0
	;デート中
	SIF CHK_DATENOW(CFLAG:デート中) && CFLAG:現在位置 != OMANEKIBEYA()
		RETURN 0
	;添い寝中
	SIF CFLAG:添い寝中
		RETURN 0
	SIF TEQUIP:50 >= 0
		RETURN 0
ENDIF
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
;腕使用中
SIF TFLAG:61 && MASTER_POSE(SET_C,3) != TARGET && MASTER_POSE(SET_P,SET_YUBI) != TARGET && MASTER_POSE(SET_V,SET_YUBI) != TARGET && MASTER_POSE(SET_A,SET_YUBI) != TARGET
	RETURN 0
SIF GETBIT (TCVAR:26,5)
	RETURN 0
RETURN 1


;-------------------------------------------------
;オナニー
;日常系コマンド、レベル1
;-------------------------------------------------
@COM451
#DIM C_ID
#DIM AMT
IF TARGET
	PRINTFORMW オナニーの時はね、誰にも邪魔されず 自由でなんというか救われてなきゃあダメなんだ 独りで静かで豊かで……
	RETURN -1
ENDIF
PRINTFORML 誰のことを思い浮かべますか？
DRAWLINE
FOR LOCAL,1,CHARANUM
	IF !IS_NAMED(LOCAL) || !CFLAG:LOCAL:面識
		CONTINUE
	ENDIF
	PRINTFORMLC [{LOCAL, 3}] %CALLNAME:LOCAL%
NEXT
PRINTL
DRAWLINE
PRINTL [999] 誰も思い浮かべない [1000] やめる
INPUT
C_ID = RESULT
SELECTCASE C_ID
	CASE 1 TO CHARANUM
		CALL PRINT_FACE, C_ID, "発情", "裸", "_1"
		PRINTFORML %CALLNAME:MASTER%は%CALLNAME:C_ID%のことを考えながら性欲を処理した
	CASE 999
		PRINTFORML %CALLNAME:MASTER%は%TEXTR("天体の運行/山川草木/生老病死")%に思いを馳せながら性欲を処理した
	CASEELSE
		RETURN -1
ENDSELECT
AMT = 50 + RAND:50
IF TALENT:MASTER:濃厚精液
	AMT *= 2
	PRINTFORMW ……濃いのが{AMT}出た
ELSE
	PRINTFORMW ……{AMT}出た
ENDIF
BASE:MASTER:精力 -= AMT
DOWNBASE:MASTER:気力 = 100

TIME += 20
RETURN 1

;待つ
@COM_ABLE451
;ショップ画面では不可
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(451)
	RETURN RESULT
;気力0では不可
SIF BASE:MASTER:気力 <= 0
	RETURN 0
;精力0では不可
SIF BASE:MASTER:精力 <= 0
	RETURN 0
;ウフフ中は不可
SIF CFLAG:うふふ
	RETURN 0
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;私室以外では不可
SIF CFLAG:MASTER:現在位置 != CFLAG:MASTER:初期位置
	RETURN 0
RETURN 1
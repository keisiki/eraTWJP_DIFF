;『にゅーぷらん』を実行する関数
@INVITE_NEWPLAN
#DIM チェンジ回数
#DIM 指名回数
#DIM 希望
#DIM 幅 = 3, 5, 5, 13
#DIM 位置 = 0, 3, 8, 0
{
#DIMS AWAHIME_RACE = "人妻", "町娘", "泡姫",
					 "化け狸", "化け狐", "イナバ", "白狼天狗", "化け猫",
					 "鬼", "河童", "メイド妖精", "ゾンビフェアリー", "妖精"
}
チェンジ回数 = 0
指名回数 = 0
TARGET = 0
RESULT = 0
VARSET LOCAL
;３日に一度販売員がお勤め
SIF ((GETTIME()/1000000000) % 100) % 3 == 2
	AWAHIME_RACE:5 = 販売員
;２日に一度タヌキが変わる
SIF DAY % 2
	AWAHIME_RACE:3 = 茨たぬき
IF !STRCOUNT(CSTR:0:0, "泡入浴/")
	CSTR:0:0 = %CSTR:0:0%泡入浴/
	PRINTL 「んーぎゃ、いい判断だぎゃ」
	PRINTW 「ほしたら、『にゅーぷらん』のしぇつめーをしてやるんだぎゃ！」
	PRINTL 「みゃず、ぎゃーの見しぇてやる似顔絵から、好みのメスっこを選ぶぎゃ」
	PRINTL 「そのあと、おみゃーしゃんを貸し切りの風呂に案内したるぎゃ！」
	PRINTW 「ほんでちょーっと待ったら、メスっこがやってきて一緒に風呂ひゃーってくれるぎゃ……」
	PRINTL 
	PRINTL 「しょのあとは知らん！　ぎゃーはいーっしゃい関係にゃーぎゃ」
	PRINTL 「おみゃーしゃんとメスっこぎゃ、仲よくしょーぎゃ、あれこれしょーぎゃ、好きにすればいいぎゃ！」
	PRINTW 「時間はてゃーっぷり90分！　足りんけりゃ延長もおーけーぎゃ」
	PRINTL
	PRINTL 「ほれっ、ほれっ！　やる気が出たんでねーぎゃ？」
	PRINTW 番台の妖怪は、げへげへとさらに厭らしい笑みを浮かべている
ELSE
	PRINTL 「んーぎゃ、おみゃーしゃんは、やーっぱり好きものだぎゃ！」
	PRINTL 番台の妖怪は、げっへっへと相変わらず厭らしい笑みを浮かべている
ENDIF
PRINTL
$LOOP_SOAP0
PRINTFORML 「\@ !RESULT ? ほったら # もーいっきゃー \@、好みのメスっこのてゃーぷ、ぎゃーにおしえるだぎゃー」
PRINTL [1] 人間
PRINTL [2] ケモミミっ娘
PRINTL [3] 人外娘
PRINTL [4] おまかせ
PRINTL [5] お気にの子を指名
PRINTL [0] やっぱりやめる
$LOOP_SOAP1
INPUT
SELECTCASE RESULT
	CASE 0
		SIF チェンジ回数 > 0 || 指名回数 > 0
			CALL SOAP_RESET
		IF TFLAG:211 >= 2
			PRINTL 「んがー！！　んがー！！　んがー！！」
			PRINTW 「おみゃーはっ！　ぎゃーの金もーけの邪魔ぎゃ！」
			PRINTL 「おーい！！　おみゃーら！　でてこいっ！」
			PRINTW 「こいつをほーりだすぎゃ！！」
			CALL KOWAI_ONISAN
			RETURN 0
		ELSE
			PRINTL 「はぁ！？　やめちみゃうんぎゃ！？」
			PRINTL 「よーぎゃねーなら、とっとと帰るぎゃ！」
			TFLAG:211 = 2
			RETURN -1
		ENDIF
	CASE 1 TO 5
		希望 = RESULT -1
	CASEELSE
		CLEARLINE 1
		REUSELASTLINE 「好みははっきり言うんだぎゃ！！」
		GOTO LOOP_SOAP1
ENDSELECT
$LOOP_SOAP2
SELECTCASE チェンジ回数 
	CASE 0
		IF 希望 == 3
			PRINTL 「おうおう、安心してみゃかすんぎゃ」
			PRINTW 「うちゃー、きれーどころが揃ってるぎゃ！」
		ELSEIF 希望 == 4
			PRINTL 「お気にぃーりのメスっこ、見つかったぎゃ？」
			PRINTW 「ほしたら、てゃーくさんうちに来て、たーんまり金落っことすといーぎゃ！」
		ELSE
			PRINTL 「おうおう、わかったぎゃ」
			PRINTW 「おみゃーしゃん好みなメスっこはと…」
		ENDIF
	CASE 1 TO 9, 11 TO 17
		PRINTFORML 「\@ チェンジ回数 < 10 ? これでだめぎゃ？ # まーだ決みゃらんぎゃ… \@」
		PRINTW 「ほんなら、べつのメスっこはと…」
	CASE 10
		PRINTL 「しっかし、ちゅーもんの多い客だぎゃ」
		PRINTW 「メスっこにゃー、おっぴゃーと穴がありゃ別にいいんだぎゃ…」
	CASE 18
		PRINTL 「いーかげんにするぎゃ！」
		PRINTW 「もうメスっこがおらんくなるぎゃ！！」
	CASE 19
		PRINTL 「んがー！」
		PRINTW 「こいつぎゃっ！　しゃーごのっ！　メスっこだぎゃーっ！！」
ENDSELECT
;リピート指名
IF 希望 == 4
	指名回数 ++
	CALL LOAD_MOBGIRL(1)
	SIF RESULT == -1
		GOTO LOOP_SOAP0
	CFLAG:RANDOM_CHARANUM:出禁 = 0
	;なぜか再登場は裸なので無理やりバスタオル着せる。衣装周りは複雑でよくわからない
	EQUIP:RANDOM_CHARANUM:ワンピース = 13
	CFLAG:RANDOM_CHARANUM:ノーパン = 1
	CFLAG:RANDOM_CHARANUM:上半身下着確認 = 1
	CFLAG:RANDOM_CHARANUM:下半身下着確認 = 1
	;攻守交代時すぐ萎えないように溜まってる度を補充
	CFLAG:RANDOM_CHARANUM:溜まってる度 = LIMIT(CFLAG:RANDOM_CHARANUM:溜まってる度 + RAND:200, 500, 1000)
	TALENT:RANDOM_CHARANUM:機嫌 = 0 + RAND:2
	TCVAR:RANDOM_CHARANUM:ピル = 1
ELSE
	チェンジ回数 ++
	LOCAL = RAND(幅:希望) + 位置:希望
	CALL MAKE_YUKIZURI(RANDOM_CHARANUM, AWAHIME_RACE:LOCAL, 2)
ENDIF
TEQUIP:RANDOM_CHARANUM:上半身着衣状況 = 1
CALL CHARA_STATE(RANDOM_CHARANUM)
DRAWLINE
CALL COLORMESSAGE(@"処女喪失履歴：%CSTR:RANDOM_CHARANUM:処女喪失履歴%", C_PINK, 1)
PRINTFORML 
PRINTFORML 「このメスっこ\@ 希望 == 4 ? で、いー # は、どー \@ぎゃ？」
PRINTL [1] この子にする
PRINTL [2] チェンジで！
PRINTL [0] やっぱりやめる
$LOOP_SOAP3
INPUT
SELECTCASE RESULT
	CASE 0
		CALL SOAP_RESET
		IF TFLAG:211 >= 2
			PRINTL 「んがー！！　んがー！！　んがー！！」
			PRINTW 「おみゃーはぎゃーの金もーけの邪魔ぎゃ！」
			PRINTL 「おーい！！　おみゃーら！　出番だぎゃっ！」
			PRINTW 「こいつをほーりだすぎゃ！！」
			CALL KOWAI_ONISAN
			RETURN 0
		ELSE
			PRINTL 「はぁ！？　やめちみゃうんぎゃ！？」
			PRINTL 「よーぎゃねーなら！　とっとと帰るぎゃ！」
			TFLAG:211 = 2
			RETURN -1
		ENDIF
	CASE 1
		IF MONEY < 10000
			CALL SOAP_RESET
			PRINTW 「…………」
			;番台ブチギレ
			IF TFLAG:211 >= 2
				PRINTL 「んがー！！　んがー！！　んがー！！」
				PRINTW 「おみゃー！　金もねーのに遊ぼーとは、ずーずーしぃーぎゃ！」
				PRINTL 「ぎゃーの金もーけっ！　邪魔するんじゃねーぎゃ！！」
				PRINTW 「おーい、おみゃーら！　このびんぼー人をつみゃみだすぎゃ！！」
				CALL KOWAI_ONISAN
				RETURN 0
			ELSE
				PRINTW 「金、足らんぎゃ…」
				PRINTL 「びんぼー人にゃー用はねーぎゃ！」
				PRINTW 「しゃっしゃときゃーれきゃーれ！！」
				TFLAG:211 = 3
				RETURN -1
			ENDIF
		ELSE
			MONEY -= 10000
			PRINTFORML 「\@ チェンジ回数 < 10 ? よっしゃ、決みゃり # よーやく決みゃった \@ぎゃ！」
			PRINTL 「ほれっ！　お客しゃま！　しゃっしゃとこっちにくるぎゃ！」
			PRINTFORML 番台の妖怪に案内され、%CALLNAME:PLAYER%は貸し切り風呂へ移動する
			IF HAS_PENIS(PLAYER) && !CFLAG:RANDOM_CHARANUM:1001
				PRINTFORML %CALLNAME:PLAYER%の胸も股間も、初めて見る娘への期待でパンパンに膨らんできた…
				BASE:PLAYER:勃起 = MAX(BASE:PLAYER:勃起, 1000)
			ENDIF
			PRINTW 
			PRINTFORML しばらく待つとバスタオル一枚の%CALLNAME:RANDOM_CHARANUM%が入ってきた
			;初対面の二人になぜか恋愛感情が生まれる
			IF 希望 != 4
				PRINTL 出会った瞬間二人は恋に落ちた！！
				TALENT:RANDOM_CHARANUM:恋慕 = 1
				CFLAG:RANDOM_CHARANUM:好感度 = REQUIRED_FAVOR_恋慕
				CFLAG:RANDOM_CHARANUM:信頼度 = REQUIRED_TRUST_恋慕
				CFLAG:RANDOM_CHARANUM:既成事実 = 3
				CFLAG:RANDOM_CHARANUM:生解禁 = 2
				ABL:RANDOM_CHARANUM:親密 = REQUIRED_AFFECTION_恋慕
				ABL:RANDOM_CHARANUM:従順 = RAND:4 + TALENT:RANDOM_CHARANUM:風俗嬢 * 3
				ABL:RANDOM_CHARANUM:奉仕精神 = RAND:4 + TALENT:RANDOM_CHARANUM:風俗嬢 * 3
				;モブ子相手のAct_COMフィルタをまとめてOFF、オート喘ぎをON
				VARSET CFLAG:RANDOM_CHARANUM:0, 1, 591, 593
				VARSET CFLAG:RANDOM_CHARANUM:0, 1, 595, 598
				CFLAG:RANDOM_CHARANUM:オート喘ぎ = 1
			;馴染みの客なら臨戦態勢にしておく
			ELSEIF CFLAG:RANDOM_CHARANUM:1001 > 1
				PALAM:RANDOM_CHARANUM:潤滑 = PALAMLV:MIN(1 + CFLAG:RANDOM_CHARANUM:1001, 6)
				PALAM:RANDOM_CHARANUM:欲情 = PALAMLV:MIN(3 + CFLAG:RANDOM_CHARANUM:1001, 8)
			ENDIF
			PRINTW 
			CALL TALK_MOBGIRL("泡姫挨拶")
			SETBIT TFLAG:一日一回, 9
			;交渉成立ならうふふ
			IF INRANGE(RESULT, 1, 3)
				LOCAL:0 = TIME / 60 * 60
				LOCAL:1 = LOCAL:0 + 600 > 1440 ? LOCAL:0 - 840 # LOCAL:0 + 600
				LOCAL:2 = RESULT
				IF MAIN_MAP == 9
					CFLAG:RANDOM_CHARANUM:拠点来訪 = 1
					CFLAG:RANDOM_CHARANUM:遭遇位置 = 934
				ELSE
					CFLAG:RANDOM_CHARANUM:拠点来訪 = 0
					CFLAG:RANDOM_CHARANUM:遭遇位置 = 940
				ENDIF
				;眠気で部屋追い出し避けの為、今から活動に設定
				CFLAG:RANDOM_CHARANUM:来訪時間 = LOCAL:0
				CFLAG:RANDOM_CHARANUM:帰宅時間 = LOCAL:1
				CFLAG:RANDOM_CHARANUM:就寝時間 = LOCAL:1
				CFLAG:RANDOM_CHARANUM:起床時間 = LOCAL:0
				;デート中処理、@OSASOIを流用してお風呂(OMANEKIBEYA)へ移動
				CFLAG:RANDOM_CHARANUM:同室 = 1
				CFLAG:RANDOM_CHARANUM:デート中 = 9
				CFLAG:PLAYER:デート中 = 9
				FLAG:デート相手 = RANDOM_CHARANUM
				CFLAG:PLAYER:同行 = 60
				CFLAG:RANDOM_CHARANUM:同行 = 60
				CALL OMANEKI_ENTER(RANDOM_CHARANUM)
				CALL TARGETSET_CHACK
				YOGORE:(OMANEKIBEYA()) = 1
				TCVAR:RANDOM_CHARANUM:連泊禁止 = 1
				;専用フラグ立ててうふふに移行
				TCVAR:PLAYER:馴れ合い = 3
				TCVAR:RANDOM_CHARANUM:馴れ合い = 3
				CFLAG:RANDOM_CHARANUM:ディレイ = 30
				BASE:RANDOM_CHARANUM:ムード = 700 + (500 * (TALENT:RANDOM_CHARANUM:風俗嬢 > 2))
				BASE:RANDOM_CHARANUM:理性 = 400 - (400 * (TALENT:RANDOM_CHARANUM:風俗嬢 > 2))
				TFLAG:泡風呂 = DAY * 1440 + TIME + 90
				IF !ITEM:20
					TFLAG:マット貸し出し = 1
					ITEM:20 ++
				ENDIF
				CFLAG:RANDOM_CHARANUM:うふふ = 1
				CFLAG:PLAYER:うふふ = 1
				UFUFU_START_TIME = TIME
				UFUFU_START_DAY = DAY
				;指名回数をカウント
				CFLAG:RANDOM_CHARANUM:1001 ++
				;会話で状態の変更
				SELECTCASE LOCAL:2
					CASE 2
						BASE:MASTER:勃起 = MAX(BASE:MASTER:勃起, 1000)
					CASE 3
						BASE:MASTER:勃起 = MIN(BASE:MASTER:勃起 + 500, 1500)
						CFLAG:MASTER:うふふ = 2
						CFLAG:RANDOM_CHARANUM:うふふ = 2
						TFLAG:COMABLE管理 = 3
						TCVAR:RANDOM_CHARANUM:26 = 0
						CALL CCOMF51(RANDOM_CHARANUM)
						SELECTCCOM:0:1 = 51
						TCVAR:RANDOM_CHARANUM:次回カウンター = 51
				ENDSELECT
				RETURN 1
			ENDIF
			;交渉決裂で通常の温泉処理
			RETURN RESULT
		ENDIF
	CASE 2
		;チェンジしすぎで怖いお兄さんやってくる
		IF チェンジ回数 >= 20 || 指名回数 >= 5
			PRINTL 「んがー！！　んがー！！　んがー！！」
			PRINTL 「おみゃー！　いーきゃげんにしとけぎゃ！」
			PRINTW 「しょんなに気に入らんならっ！　おみゃーにぴったりの相手呼んだるぎゃ！！」
			PRINTW 「おーい、おみゃーら！　こいつの相手！　任せるぎゃ！！」
			CALL SOAP_RESET
			CALL KOWAI_ONISAN
			RETURN 0
		ELSEIF 希望 == 4
			PRINTL 「はぁ！？　なんぎゃそりゃあ」
			PRINTL 
			GOTO LOOP_SOAP0
		ELSE
			GOTO LOOP_SOAP2
		ENDIF
	CASEELSE
		CLEARLINE 1
		REUSELASTLINE 「よけーな事しぇんと、しゃっしゃと選ぶぎゃ！！」
		GOTO LOOP_SOAP3
ENDSELECT

;お風呂サービス終了判定関数
;立てっぱなしだとマズいフラグあるので処理集約してフラグ折っておく
@泡風呂終了判定(ARGS)
#DIM CHR
#DIM 回復回数
#DIM 延長回数
#DIM 一日一回
#DIM 終了処理
終了処理 = 1
CHR = RANDOM_CHARANUM
IF 一日一回 != DAY
	延長回数 = 0
	回復回数 = 0
	一日一回 = DAY
ENDIF
SELECTCASE ARGS
	CASE "タイムアップ"
		CALL TALK_MOBGIRL("泡姫タイムアップ", 延長回数)
		RESETCOLOR
		延長回数 ++
		SIF RESULT
			RETURN
	CASE "コマンドから終了"
		CALL TALK_MOBGIRL("泡姫コマンドから終了", 延長回数)
	CASE "攻守交代時終了"
		CALL TALK_MOBGIRL("泡姫攻守交代時終了")
	CASE "体力限界"
		CALL TALK_MOBGIRL("泡姫体力限界", 回復回数)
		RESETCOLOR
		IF !回復回数
			回復回数 ++
			BASE:CHR:体力 += MAXBASE:CHR:体力 / 3 * 2
			BASE:CHR:気力 += MAXBASE:CHR:気力 / 2
			RETURN
		ENDIF
	CASE "あなた限界"
		CALL TALK_MOBGIRL("あなた限界")
	;恋慕＋ＮＧ行為無しなので普通は怒られない。保険的な処理
	CASE "女の子フキゲン"
		PRINTFORMW %CALLNAME:MASTER%はお風呂から\@ TALENT:CHR:度胸 < 0 ? 締め # 追い \@出された…
ENDSELECT
;うふふを終了する処理
FOR COUNT, 0, 2
	CHR = COUNT ? RANDOM_CHARANUM # 0
	CALL ENDUFUFU(CHR)
	CALL SET_PRANK(0, CHR, 1)
	;接触/はだけをリセット
	CALL TOUCH_SET(0,0,0,1)
	TEQUIP:CHR:下半身ずらし状態 = 0
	TEQUIP:CHR:上半身はだけ状態 = 0
	CALL CLOTHES_SETTING_TRAIN(CHR)
	TEQUIP:CHR:上着脱衣済み = 0
	TEQUIP:CHR:お風呂場プレイ = 0
	CFLAG:CHR:同行 = 0
	CFLAG:CHR:お招き = 0
NEXT
TFLAG:MASTERのＰ挿入中 = 0
;あなたの現在位置を戻してサービス終了
FLAG:デート相手 = 0
CFLAG:MASTER:デート中 = 9
CFLAG:MASTER:現在位置 = CFLAG:RANDOM_CHARANUM:遭遇位置
TFLAG:泡風呂 = 0
ITEM:20 = TFLAG:マット貸し出し ? 0 # ITEM:20
CALL SOAP_STAND_BY_SUKIMA
RETURN

;こわいオニいさんがやってくる関数
@KOWAI_ONISAN
TFLAG:211 = 4
RESETCOLOR
PRINTL
CALL COLORMESSAGE("「おいっ！！　ごらぁ！！　われぇ！！　なめとったらあかんぞぉ！！！」", C_RED, 1, 3)
PRINTW 突然いかつい鬼たちが乗り込んできた！！
PRINTFORML %CALLNAME:PLAYER%は時止めもする間もなくボッコボコに殴られる…
PRINTFORMW 鬼たちは%CALLNAME:PLAYER%を散々痛めつけてから店の外に放り出した…
CALL RECOVER(PLAYER, -1500, "体力" , "ボコボコにされた")
CALL RECOVER(PLAYER, -1500, "気力" , "ぼろぼろになった")
WAIT
RETURN

;出会った嬢を消す関数
@SOAP_RESET
CFLAG:RANDOM_CHARANUM:初期位置 = SUKIMA()
CFLAG:RANDOM_CHARANUM:現在位置 = SUKIMA()
CFLAG:RANDOM_CHARANUM:デート中 = MAIN_MAP
CALL モブ子画像リセット(RANDOM_CHARANUM)
FORGET_MOBGIRL = 1
DELCHARA RANDOM_CHARANUM
ADDCHARA RANDOM_CHARANUM
CALL TARGETSET_CHACK
RESETCOLOR
RETURN

;出会った嬢をSUKIMAに退避させる関数
@SOAP_STAND_BY_SUKIMA
CFLAG:RANDOM_CHARANUM:初期位置 = SUKIMA()
CFLAG:RANDOM_CHARANUM:現在位置 = SUKIMA()
CFLAG:RANDOM_CHARANUM:デート中 = MAIN_MAP
CFLAG:RANDOM_CHARANUM:出禁 = 2
CALL TARGETSET_CHACK
RESETCOLOR
RETURN
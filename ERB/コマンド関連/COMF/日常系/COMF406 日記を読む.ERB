;-------------------------------------------------
;日記を読む
;日常系コマンド
;-------------------------------------------------
@COM406
#DIM 同棲
#DIM OWNER

IF TARGET > 0 && CFLAG:TARGET:初期位置 == CFLAG:MASTER:初期位置
	同棲 = 1
ELSE
	同棲 = 0
ENDIF
OWNER = COM406_CHECK_OWNER()

LOCAL = 50 + ABL:MASTER:話術技能 * 5 + ABL:TARGET:親密 * 2
SIF BASE:TARGET:酒気 > MAXBASE:TARGET:酒気 / 2
	LOCAL -= 15

SELECTCASE TFLAG:192
	;通常
	CASE 0
		SELECTCASE RAND:LOCAL
			CASE IS < 20
				TFLAG:193 = 1
			CASE 20 TO 50
				TFLAG:193 = 2
			CASEELSE
				TFLAG:193 = 3
		ENDSELECT
	;強制成功or大成功
	CASE 1
		SELECTCASE RAND:LOCAL
			CASE IS < 50
				TFLAG:193 = 2
			CASEELSE
				TFLAG:193 = 3
		ENDSELECT
	;強制失敗
	CASE -1
		TFLAG:193 = 1
	;コマンド終了
	CASE -2
		TFLAG:193 = -1
		RETURN 0
ENDSELECT

TFLAG:194 = OWNER

IF CFLAG:MASTER:現在位置 == CFLAG:MASTER:初期位置
	PRINTFORML 机の上には日記帳が\@ 同棲 ? 二冊# \@置いてある。
	PRINTFORML 何をする？
	VARSET LOCALS, "？？？？"
	IF TARGET > 0
		LOCALS:0 = %CALLNAME:TARGET%に日記を読んでもらう
		LOCALS:1 = %CALLNAME:TARGET%の日記を読む
	ENDIF
	{
	CALL ASK_M("日記をつける",1,"日記を読む",1,@"%LOCALS:0%", TARGET > 0,
	@"%LOCALS:1%", 同棲 && (!SHIRAHU(TARGET) || TALENT:TARGET:恋慕 ),
	"何もしない",1)
	}
	SELECTCASE RESULT
		CASE 0
			PRINTFORML 日記の内容を直接入力してください。<br>を入力すれば改行されます。
			PRINTFORML システム側で自動記録されるので日付の記入はいりません。
			PRINTL
			INPUTS
			CALL MASTER_DIARY_WRITE,RESULTS
			PRINTL
		CASE 1
			CALL READ_MASTER_DIARY
		CASE 2
			IF ABL:TARGET:親密 >= 5
				PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%の日記を手に取った…
				CALL SHARE_MASTER_DIARY
			ELSE
				PRINTFORMW %CALLNAME:TARGET%は興味なさそうだ…
			ENDIF
		CASE 3
			CALL CHARA_DIARY_COM(TARGET)
		CASE 4
	ENDSELECT
	GOTO SKIP
ELSEIF TALENT:OWNER:恋慕 && CFLAG:MASTER:現在位置 != CFLAG:MASTER:初期位置
	PRINTFORML 枕の隣に日記帳が置いてある。
	PRINTFORML %CALLNAME:OWNER%は日記の内容をシェアする気があるようだ。
	PRINTFORML どうしますか？
	CALL ASK_YN("日記を読む","読まない")
	IF !RESULT
		CALL CHARA_DIARY_COM(OWNER)
	ENDIF
ELSEIF CFLAG:OWNER:現在位置 != CFLAG:MASTER:現在位置 && TARGET && !(FLAG:70 || !SHIRAHU(TARGET))
	PRINTFORML 枕の下に%CALLNAME:OWNER%の日記帳が置いてあるようだ。
	PRINTFORML しかし…すぐ傍には%CALLNAME:TARGET%がいる…
	PRINTFORML どうしますか？
	CALL ASK_YN("一緒に日記を盗み読む","読まない")
	IF !RESULT && TFLAG:193 > 0
		CALL CHARA_DIARY_COM(OWNER)
	ELSE
		PRINTFORML やっぱりやめよう…
	ENDIF
ELSE
	PRINTFORML 枕の下に日記帳が置いてあるようだ。
	PRINTFORML どうする？
	CALL ASK_YN("盗み読む","読まない")
	IF !RESULT
		CALL CHARA_DIARY_COM(OWNER)
	ENDIF
ENDIF

$SKIP
IF !FLAG:70
	TIME += 5
ELSE
	BASE:MASTER:TSP -= 10
ENDIF

IF SHIRAHU(TARGET) && TARGET > 0
	;固定で獲得するソース
	SOURCE:歓楽 = 200

	;ABL:親密をみる
	IF ABL:親密 <= 1
		SOURCE:歓楽 += (ABL:親密 * 30)
	ELSEIF ABL:親密 <= 3
		SOURCE:歓楽 += 200 + (ABL:親密 * 30)
	ELSEIF ABL:親密 <= 5
		SOURCE:歓楽 += 500 + (ABL:親密 * 30)
	ELSEIF ABL:親密 <= 8
		SOURCE:歓楽 += 750 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 10
		SOURCE:歓楽 += 1000 + (ABL:親密 * 50)
	ELSE
		SOURCE:歓楽 += 1600 + (ABL:親密 * 30)
	ENDIF


	SOURCE:歓楽 = LIMIT(SOURCE:歓楽,0,5000)


	SOURCE:受動 = 100 + 100 * ABL:従順
	SOURCE:征服 = 100 + 100 * ABL:サドっ気

	IF TFLAG:193 == -1 || TFLAG:193 == -2
		TIMES SOURCE:歓楽 , 0.10
		TIMES SOURCE:征服 , 0.50
		TIMES SOURCE:受動 , 0.50
	ELSEIF TFLAG:193 == 0
		TIMES SOURCE:歓楽 , 1.00
		TIMES SOURCE:征服 , 1.00
		TIMES SOURCE:受動 , 1.00
	ELSE
		TIMES SOURCE:歓楽 , 2.00
		TIMES SOURCE:征服 , 2.00
		TIMES SOURCE:受動 , 2.00
	ENDIF
	SELECTCASE ABL:MASTER:話術技能
		CASE 0
			TIMES SOURCE:歓楽 , 0.20
			TIMES SOURCE:征服 , 0.20
			TIMES SOURCE:受動 , 0.20
		CASE 1
			TIMES SOURCE:歓楽 , 0.40
			TIMES SOURCE:征服 , 0.40
			TIMES SOURCE:受動 , 0.40
		CASE 2
			TIMES SOURCE:歓楽 , 0.70
			TIMES SOURCE:征服 , 0.70
			TIMES SOURCE:受動 , 0.70
		CASE 3
			TIMES SOURCE:歓楽 , 1.00
			TIMES SOURCE:征服 , 1.00
			TIMES SOURCE:受動 , 1.00
		CASE 4
			TIMES SOURCE:歓楽 , 1.20
			TIMES SOURCE:征服 , 1.20
			TIMES SOURCE:受動 , 1.20
		CASEELSE
			TIMES SOURCE:歓楽 , 1.50
			TIMES SOURCE:征服 , 1.50
			TIMES SOURCE:受動 , 1.50
	ENDSELECT
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE406
#DIM OWNER
OWNER = COM406_CHECK_OWNER()

;移動実行判定
SIF !TFLAG:100
	RETURN 0
	
;イタズラかウフフ中
SIF CFLAG:MASTER:イタズラ
	RETURN 0

SIF CFLAG:MASTER:うふふ
	RETURN 0

;拠点にいる時かお招き部屋のみ
SIF !AT_HOME(MASTER) && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
	RETURN 0

SIF CFLAG:MASTER:現在位置 != CFLAG:MASTER:初期位置 && !OWNER
	RETURN 0

SIF !TALENT:OWNER:恋慕 && ABL:MASTER:教養 < ABL:OWNER:教養 && OWNER
	RETURN 0

SIF !TALENT:OWNER:恋慕 && CFLAG:OWNER:現在位置 == CFLAG:OWNER:初期位置 && !(FLAG:70 || !SHIRAHU(OWNER)) && OWNER
	RETURN 0

SIF !TALENT:TARGET:恋慕 && !(FLAG:70 || !SHIRAHU(TARGET)) && TARGET
	RETURN 0

RETURN 1

@MESSAGE_COM406
#DIM OWNER
OWNER = COM406_CHECK_OWNER()

IF (!TALENT:OWNER:恋慕 && !(FLAG:70 || !SHIRAHU(OWNER))) && !(CFLAG:MASTER:現在位置 == CFLAG:MASTER:初期位置)
	PRINTFORML 読み終わった後、日記帳をそっと元の場所に戻した。
ELSE
ENDIF

;日記の著者チェッカー
@COM406_CHECK_OWNER
#FUNCTION

IF TARGET > 0 && CFLAG:TARGET:初期位置 == CFLAG:MASTER:初期位置 && CFLAG:MASTER:現在位置 == CFLAG:MASTER:初期位置
	RETURNF TARGET
ELSEIF TARGET == CFLAG:MASTER:お招き && CFLAG:MASTER:現在位置 == OMANEKIBEYA()
	RETURNF CFLAG:MASTER:お招き
ELSE
	RETURNF PRIVATEROOM:(CFLAG:MASTER:現在位置 % 100)
ENDIF
;採集する
@COM445
#DIM 採集物
#DIM 採集パワー
#DIM 採集パワーM
#DIM 採集パワーT
#DIM 土地の恵み
#DIM CHARA
#DIM 採集位置
;あからさまな人の敷地での無断採集はやめる
SELECTCASE CFLAG:MASTER:現在位置
	CASE 太陽の畑, 438
		IF ABL:[[幽香]]:親密 < 5
			DRAWLINE
			PRINTFORML ここで無断で採集をするのは非常に危険な気がする…
			RETURN -1
		ENDIF
	;天界、桃園
	CASE 天界, 833
		IF ABL:[[天子]]:親密 < 5
			DRAWLINE
			PRINTFORML ここで無断で採集をするのはマズい気がする…
			RETURN -1
		ENDIF
ENDSELECT

採集物 = 0
採集パワーM = 0
採集パワーT = 0
LOCAL:4 = 0

;参加者判定
IF FLAG:デート相手 && !FLAG:70
	CHARA = FLAG:デート相手
ELSEIF TARGET && ABL:TARGET:親密 >= 5 && SHIRAHU(TARGET) && CFLAG:TARGET:行動 != 5
	CHARA = TARGET
ELSE
	CHARA = 0
ENDIF

;土地の恵み。日替わりで-25～+25の間をランダムで浮動。釣りを参考に。
土地の恵み = CFLAG:MASTER:今日の運勢 / 20 - 25

SIF ITEM:三粒の天滴
	土地の恵み += 5

;採集パワー設定
採集パワーM = 10 + TALENT:MASTER:採集Lv * 10 + ABL:MASTER:教養 * 5 + TFLAG:幸運補正 * 10
SIF ITEM:108
	採集パワーM += 10

SIF CHARA && !FLAG:70
	採集パワーT = 10 + TALENT:CHARA:採集Lv * 10 + ABL:CHARA:教養 * 5
;参加者で最もパワーが高いものを基準に、1/2したもう１人のパワーを足す
IF 採集パワーM >= 採集パワーT
	採集パワー = 採集パワーM + (採集パワーT / 2)
ELSE
	採集パワー = 採集パワーT + (採集パワーM / 2)
ENDIF
採集位置 = GATHERING_SPOT(CFLAG:MASTER:現在位置)
REPEAT 5
	REUSELASTLINE  \@ CHARA ? %CALLNAME:CHARA%と一緒に #  \@採集中.%"." * COUNT%
	TWAIT 150, 1
REND
;デバッグ用
;PRINTFORML 採集位置{採集位置}
;PRINTFORML 採集パワー{採集パワー}

$TREASURSARCH

CALL Gathering_Drawing(採集位置, 採集パワー, 土地の恵み)

採集物 = RESULT:0
TFLAG:194 = RESULT:1
VARSET RESULT
;試行回数を１回増やす
LOCAL:4 ++
;採集物が気に入らない場合は引き直し
IF 採集物 != 0
	IF !FLAG:採集再抽選
		CLEARLINE 3
	ELSE
		SETCOLOR 0x00FF00
		PRINTFORM %ITEMNAME:(採集物)%
		RESETCOLOR
		PRINTFORML (所持数：{ITEM:(採集物)})が見つかった。採集しますか？
		CALL ASK_YN("採集する","探し直す")
		IF !RESULT
			CLEARLINE 4
		ELSE
			CLEARLINE 4
			採集物 = 0
			TFLAG:194 = 0
			REPEAT 5
				REUSELASTLINE  \@ CHARA ? %CALLNAME:CHARA%と一緒に #  \@採集中.%"." * COUNT%
				TWAIT 150, 1
			REND
		ENDIF
	ENDIF
ENDIF

;試行回数３回以下で採集物が確保されていない場合はループ
SIF LOCAL:4 <= 3 && 採集物 == 0
	GOTO TREASURSARCH

;アイテム入手処理 99個以上にならないようにする
IF 採集物 != 0
	LOCALS = %ITEMNAME:採集物%
	CALL GET_ITEM(LOCALS, TFLAG:194 , 2)
ENDIF

;基本の経験
EXP:MASTER:採集経験 ++
SIF !採集物
	EXP:MASTER:採集経験 += 2
SIF CHARA
	EXP:CHARA:採集経験 ++

SIF ITEM:108 && !RAND:2
	EXP:MASTER:採集経験 ++

SIF ABL:MASTER:教養 <= 2
	EXP:MASTER:学習経験 ++

;能力差で教えてもらう・教えてあげる
IF 採集パワーM >= 採集パワーT && CHARA
	EXP:CHARA:採集経験 ++
ELSEIF CHARA
	EXP:MASTER:採集経験 ++
ENDIF

;デート経験
IF CHK_DATENOW(CFLAG:CHARA:デート中) && CHARA > 0
	EXP:MASTER:デート経験 ++
	EXP:CHARA:デート経験 ++
ENDIF

;時間停止時はTSP減
IF FLAG:70
	BASE:MASTER:TSP -= 120
ELSE
	;時間経過
	TIME += 60
	;体力減少は戦闘能力によって減少抑制
	DOWNBASE:MASTER:体力 = 180 - (ABL:MASTER:戦闘能力 * 15)
	DOWNBASE:MASTER:気力 = 150 
ENDIF

;参加者のソース,同一コマンド制限を受けないので控えめに,見つからないと半減
IF CHARA > 0
	;固定で獲得するソース
	SOURCE:CHARA:歓楽 = 300
	SOURCE:CHARA:情愛 = 50
	
	;ABL:従順をみる
	IF ABL:従順 <= 1
		SOURCE:CHARA:歓楽 += (ABL:従順 * 40)
		SOURCE:CHARA:情愛 += (ABL:親密 * 20)
	ELSEIF ABL:従順 <= 3
		SOURCE:CHARA:歓楽 += 200 + (ABL:従順 * 40)
		SOURCE:CHARA:情愛 += 50 + (ABL:親密 * 30)
	ELSEIF ABL:従順 <= 5
		SOURCE:CHARA:歓楽 += 500 + (ABL:従順 * 40)
		SOURCE:CHARA:情愛 += 200 + (ABL:親密 * 30)
	ELSEIF ABL:従順 <= 8
		SOURCE:CHARA:歓楽 += 750 + (ABL:従順 * 60)
		SOURCE:CHARA:情愛 += 300 + (ABL:親密 * 40)
	ELSEIF ABL:従順 <= 11
		SOURCE:CHARA:歓楽 += 1000 + (ABL:従順 * 60)
		SOURCE:CHARA:情愛 += 600 + (ABL:親密 * 40)
	ELSE
		SOURCE:CHARA:歓楽 += 1600 + (ABL:従順 * 30)
		SOURCE:CHARA:情愛 += 1000 + (ABL:親密 * 20)
	ENDIF
	SIF CHARA != FLAG:デート相手 
		SOURCE:CHARA:情愛 = 0
	
	;好感度はみない
	
	SOURCE:CHARA:受動 = 200 + 100 * ABL:従順
	SOURCE:CHARA:征服 = 200 + 100 * ABL:サドっ気
	
	;採集成果をみる、アイテム価格に応じてとりあえず設定
	IF 採集物
		SELECTCASE ITEMPRICE:(採集物)
			CASE 1 TO 499
				TIMES SOURCE:CHARA:歓楽 , 1.00
				TIMES SOURCE:CHARA:受動 , 1.00
				TIMES SOURCE:CHARA:征服 , 1.00
				SOURCE:CHARA:達成 = 80
			CASE 500 TO 999
				TIMES SOURCE:CHARA:歓楽 , 1.10
				TIMES SOURCE:CHARA:受動 , 1.10
				TIMES SOURCE:CHARA:征服 , 1.10
				SOURCE:CHARA:達成 = 115
			CASE 1000 TO 2999
				TIMES SOURCE:CHARA:歓楽 , 1.25
				TIMES SOURCE:CHARA:受動 , 1.25
				TIMES SOURCE:CHARA:征服 , 1.15
				SOURCE:CHARA:達成 = 125
			CASE 3000 TO 5999
				TIMES SOURCE:CHARA:歓楽 , 1.50
				TIMES SOURCE:CHARA:受動 , 1.50
				TIMES SOURCE:CHARA:征服 , 1.25
				SOURCE:CHARA:達成 = 150
			CASE 6000 TO 9999
				TIMES SOURCE:CHARA:歓楽 , 1.75
				TIMES SOURCE:CHARA:受動 , 1.75
				TIMES SOURCE:CHARA:征服 , 1.50
				SOURCE:CHARA:達成 = 200
			CASE 10000 TO 14999
				TIMES SOURCE:CHARA:歓楽 , 2.00
				TIMES SOURCE:CHARA:受動 , 2.00
				TIMES SOURCE:CHARA:征服 , 1.75
				SOURCE:CHARA:達成 = 300
			CASEELSE
				TIMES SOURCE:CHARA:歓楽 , 2.25
				TIMES SOURCE:CHARA:受動 , 2.25
				TIMES SOURCE:CHARA:征服 , 2.00
				SOURCE:CHARA:達成 = 350
		ENDSELECT
	;見つからない
	ELSE
		TIMES SOURCE:CHARA:歓楽 , 0.50
		TIMES SOURCE:CHARA:受動 , 0.50
		TIMES SOURCE:CHARA:征服 , 0.50
	ENDIF
	;採集物のキャラに応じた固有反応（上書き）
	SELECTCASE 採集物
		;カエル
		CASE 706
		;チルノ
			IF CHARA == [[チルノ]]
				TIMES SOURCE:CHARA:歓楽 , 2.00
				TIMES SOURCE:CHARA:受動 , 2.00
				TIMES SOURCE:CHARA:征服 , 2.00
			;諏訪子
			ELSEIF CHARA == [[諏訪子]]
				TIMES SOURCE:CHARA:歓楽 , 0.50
				TIMES SOURCE:CHARA:受動 , 0.50
				TIMES SOURCE:CHARA:征服 , 0.50
			ELSE
				TIMES SOURCE:CHARA:歓楽 , 1.25
				TIMES SOURCE:CHARA:受動 , 1.00
				TIMES SOURCE:CHARA:征服 , 1.00
			ENDIF
			SOURCE:CHARA:達成 = 110
	ENDSELECT
ENDIF

;フラグ引き渡し
TFLAG:SELECTCOMの分岐 = 採集物

RETURN 1

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;採集する
@COM_ABLE445
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(445)
	RETURN RESULT
;採集できる場所である
SIF !GATHERING_SPOT(CFLAG:MASTER:現在位置)
	RETURN 0
;デート中はデート相手とだけ
SIF FLAG:デート相手 && TARGET != FLAG:デート相手
	RETURN 0
;睡眠中
SIF CFLAG:MASTER:睡眠
	RETURN 0
SIF CFLAG:うふふ
	RETURN 0
;体力・気力不足
SIF BASE:MASTER:気力 < 650 || BASE:MASTER:体力 < 680
	RETURN 0
;時間停止中も可能（仮）
;SIF FLAG:70
;	RETURN 0
RETURN 1

;-----------------------------------------------------------
;現在位置から採集スポットを割り出す関数
;ARG		現在位置
;ARG:1		@ShowGatheringSpotsからの参照用
;RETURN		0 該当なし	1～30 森、草原	31～60 水系		61～ 特殊
;COMMENT	場所を追加した場合、CASEを変更の事
;-----------------------------------------------------------
@GATHERING_SPOT(ARG, ARG:1)
#FUNCTION
IF !IN_HOME(ARG) && !ARG:1
;サブマップなどでの判定
	SELECTCASE ARG
		CASE 50				;妖精の大樹
			SIF MAIN_MAP != 0
				RETURNF 1
		CASE 斜角の竹林		;430 メインマップでは「地下室」を指定するため除外
			SIF MAIN_MAP != 4
				RETURNF 2
		CASE 無名の丘
			RETURNF 3
		CASE 太陽の畑
			RETURNF 4
		CASE 再思の道
			RETURNF 5
		CASE 古木の大樹		;530
			SIF MAIN_MAP != 5
				RETURNF 6
		CASE 630	;三途の川～冥界におけるマヨヒガ
			SIF MAIN_MAP != 6
				RETURNF 7
		CASE 彼岸
			RETURNF 8
		CASE 麓の樹海
			SIF MAIN_MAP != 7
				RETURNF 11
		CASE 絶景の丘
			SIF MAIN_MAP != 8
				RETURNF 12
		CASE 天界
			RETURNF 13
		CASE 桃林～砂浜
			RETURNF 14
		CASE 霧の湖
			SIF MAIN_MAP != 3
				RETURNF 31
		CASE 三途の川
			RETURNF 32
		CASE 玄武の沢
			SIF MAIN_MAP != 7
				RETURNF 33
		CASE 大蝦蟇の池
			SIF MAIN_MAP != 7
				RETURNF 34
		CASE 九天の滝
			SIF MAIN_MAP != 8
				RETURNF 35
		CASE 山の湖
			SIF MAIN_MAP != 8
				RETURNF 36
		CASE 地獄の深道
			SIF MAIN_MAP != 9
				RETURNF 37
		CASE 墓地
			RETURNF 61
		CASE 無縁塚
			RETURNF 62
		CASE 間欠泉地下センター
			RETURNF 64
		CASE 灼熱地獄跡
			RETURNF 65
		CASE 幻想風穴
			SIF MAIN_MAP != 9
				RETURNF 66
		CASE 静かの海
			RETURNF 67
	ENDSELECT
ELSE
	;メインマップでの判定
	SELECTCASE ARG
		;博霊神社：鎮守の森
		CASE 25
			RETURNF 1
		;命蓮寺：墓地
		CASE 121
			RETURNF 61
		;紅魔館：湖畔
		CASE 343
			RETURNF 31
		;永遠亭：斜角の竹林、無名の丘、太陽の畑
		CASE 433
			RETURNF 2
		CASE 436
			RETURNF 3
		CASE 438
			RETURNF 4
		;魔法の森：無縁塚、キノコ群生地、再思の道、古木の大樹
		CASE 506
			RETURNF 62
		CASE 507
			RETURNF 63
		CASE 509
			RETURNF 5
		CASE 540
			RETURNF 6
		;冥界：彼岸、八雲邸玄関
		CASE 604
			RETURNF 8
		CASE 630
			RETURNF 7
		;妖怪の山山麓：沢へ続く道、玄武の沢、中腹、大蝦蟇の池、地下間欠泉センター
		CASE 702
			RETURNF 9
		CASE 703
			RETURNF 33
		CASE 705
			RETURNF 10
		CASE 708
			RETURNF 34
		CASE 712
			RETURNF 64
		;妖怪の山山頂：頂上、滝の裏、湖畔、桃園
		CASE 805
			RETURNF 12
		CASE 806
			RETURNF 35
		CASE 814
			RETURNF 36
		CASE 833
			RETURNF 13
		;地底：灼熱地獄跡、川岸、縦穴の底
		CASE 928
			RETURNF 65
		CASE 945
			RETURNF 37
		CASE 948
			RETURNF 66
		;月:桃林～砂浜、入り江
		CASE 1014, 1015
			RETURNF 14
		CASE 1016
			RETURNF 67
	ENDSELECT
ENDIF
RETURNF 0

;-------------------------------------------------
;その採集スポットでの採集物の出現判定をする関数
;アイテム番号ARG:0
;GATHERING_SPOT(ARG)の返り値ARG:1（採集スポット）  の
;出現可否を数字で返す 出現==1 非出現==0
;アイテムを追加した場合は、GROUPMATCH内にそれぞれ追記
;-------------------------------------------------
@GATHERING_PRODUCT(ARG:0, ARG:1)
#FUNCTION

SELECTCASE ARG:1
	;鎮守の森、妖精の大樹
	CASE 1
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 607, 608, 611, 612, 616, 619, 621, 622, 626, 627, 628, 636)
			RETURNF 1
	;斜角の竹林
	CASE 2
		SIF GROUPMATCH(ARG:0, 607, 608, 611, 612, 613, 618, 619, 629, 635, 640)
			RETURNF 1
	;無名の丘
	CASE 3
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 603, 612, 616, 620, 626, 627, 631, 636, 638)
			RETURNF 1
	;太陽の畑
	CASE 4
		SIF GROUPMATCH(ARG:0, 612, 613, 614, 615, 617, 618, 619, 620, 623, 624, 626, 627)
			RETURNF 1
	;再思の道
	CASE 5
		SIF GROUPMATCH(ARG:0, 612, 614, 619, 624, 631, 632, 638)
			RETURNF 1
	;古木の大樹
	CASE 6
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 603, 604, 605, 607, 608, 611, 612, 616, 618, 621, 622, 623, 625, 626, 627, 628)
			RETURNF 1
	;マヨヒガ
	CASE 7
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 603, 607, 608, 611, 612, 616, 617, 619, 621, 622, 626, 627, 628)
			RETURNF 1
	;彼岸
	CASE 8
		SIF GROUPMATCH(ARG:0, 600, 601, 607, 608, 612, 614, 620, 624, 638)
			RETURNF 1
	;山道・沢へ続く道
	CASE 9
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 607, 608, 611, 612, 616, 617, 621, 622, 626, 627, 628, 636)
			RETURNF 1
	;山道・中腹
	CASE 10
		SIF GROUPMATCH(ARG:0, 611, 613, 616, 619, 621, 622, 623, 633)
			RETURNF 1
	;麓の樹海
	CASE 11
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 607, 608, 611, 612, 613, 616, 617, 619, 621, 622, 623, 626, 627, 628, 633)
			RETURNF 1
	;絶景の丘、頂上
	CASE 12
		SIF GROUPMATCH(ARG:0, 602, 603, 607, 608, 611, 612, 615, 616, 619, 621, 622, 625, 626, 627, 628)
			RETURNF 1
	;天界、桃園
	CASE 13
		SIF GROUPMATCH(ARG:0, 617, 623, 630)
			RETURNF 1
	;桃林～砂浜
	CASE 14
		SIF GROUPMATCH(ARG:0, 607, 608, 613, 617, 618, 624, 625, 630)
			RETURNF 1
	;霧の湖、湖畔(紅魔館)
	CASE 31
		SIF GROUPMATCH(ARG:0, 606, 607, 608, 609, 611, 612, 617, 624, 633, 706)
			RETURNF 1
	;三途の川
	CASE 32
		SIF GROUPMATCH(ARG:0, 607, 608, 612, 614, 631, 638, 706)
			RETURNF 1
	;玄武の沢
	CASE 33
		SIF GROUPMATCH(ARG:0, 606, 607, 609, 611, 612, 618, 635, 706)
			RETURNF 1
	;大蝦蟇の池
	CASE 34
		SIF GROUPMATCH(ARG:0, 604, 612, 620, 631, 636, 706)
			RETURNF 1
	;九天の滝、滝の裏
	CASE 35
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 606, 609, 611, 613, 631, 634, 706)
			RETURNF 1
	;山の湖、湖畔（妖怪の山山頂）
	CASE 36
		SIF GROUPMATCH(ARG:0, 606, 607, 609, 611, 612, 618, 632, 706)
			RETURNF 1
	;地獄の深道、川岸
	CASE 37
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 604, 606, 609, 620, 631, 633, 636, 706)
			RETURNF 1
	;墓地
	CASE 61
		SIF GROUPMATCH(ARG:0, 601, 602, 604, 614, 620, 631, 636, 638)
			RETURNF 1
	;無縁塚
	CASE 62
		SIF GROUPMATCH(ARG:0, 604, 606, 609, 631, 634, 635, 636, 638)
			RETURNF 1
	;キノコ群生地
	CASE 63
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 603, 604, 605, 605, 629)
			RETURNF 1
	;間欠泉地下センター
	CASE 64
		SIF GROUPMATCH(ARG:0, 632, 634, 637)
			RETURNF 1
	;灼熱地獄跡
	CASE 65
		SIF GROUPMATCH(ARG:0, 631, 634, 635, 637, 638)
			RETURNF 1
	;幻想風穴、縦穴の底
	CASE 66
		SIF GROUPMATCH(ARG:0, 602, 604, 631, 632)
			RETURNF 1
	;静かの海
	CASE 67
		SIF GROUPMATCH(ARG:0, 639)
			RETURNF 1
ENDSELECT
RETURNF 0

;-------------------------------------------------
;採集物の季節ごとの出現判定
;アイテム番号ARG:0の季節ごとの
;出現可否を数字で返す関数 出現==1 非出現==0
;アイテムを追加した場合は、CASEに追記
;ARG:1 に採集地点を入れると、以下のリスト用の式に変換 省略可
;-------------------------------------------------
@GATHERING_SEASON(ARG:0, ARG:1 = 0)
#FUNCTION

LOCAL = DAY:2
SIF GatheringSeason
	LOCAL = GatheringSeason

SELECTCASE LOCAL
	;春:
	CASE 1
		SELECTCASE ARG:0
			CASE 600 TO 602, 604 TO 608, 610 TO 613, 616, 618 TO 627, 629, 631 TO 640, 706
				RETURNF 1
		ENDSELECT
	;夏
	CASE 2
		SELECTCASE ARG:0
			CASE 600 TO 601, 603, 606 TO 608, 612 TO 613, 618 TO 627, 630 TO 640, 706
				RETURNF 1
		ENDSELECT
	;秋
	CASE 3
		SELECTCASE ARG:0
			CASE 600 TO 608, 611 TO 614, 616 TO 623, 625 TO 628, 631 TO 640, 706
				RETURNF 1
		ENDSELECT
	;冬
	CASE 4
		;天界、地底、月とキノコ群生地は夏とみなして採集可能
		IF ARG:1
			LOCAL:1 =ARG:1
		ELSE
			LOCAL:1 = GATHERING_SPOT(CFLAG:MASTER:現在位置)
		ENDIF

		IF GROUPMATCH(LOCAL:1, 13, 14, 37, 63, 64, 65, 66, 67)
			SELECTCASE ARG:0
				CASE 600 TO 601, 603, 606 TO 609, 612 TO 613, 618 TO 627, 630 TO 640, 706
					RETURNF 1
			ENDSELECT
		ELSE
			SELECTCASE ARG:0
				CASE 606, 609, 615, 620, 631 TO 635, 637 TO 640
					RETURNF 1
			ENDSELECT
		ENDIF
ENDSELECT
RETURNF 0

@SHOW_GATHERING_LIST
#DIM PLACE
PRINTFORML 採集場所一覧
CALL COLORMESSAGE(@"中級素材　",C_YELLOW)
CALL COLORMESSAGE(@"高級素材　",C_P_GREEN)
CALL COLORMESSAGE(@"最高級素材",C_AQUA)
PRINTFORML 
DRAWLINE
FOR LOCAL,1, 90
	IF ForagePlaceName(LOCAL) != ""
		PRINTFORMLC [{LOCAL}] %ForagePlaceName(LOCAL)%
	ENDIF
NEXT
PRINTFORML 
DRAWLINE
PRINTFORML [99] やめる
INPUT
PLACE = RESULT
IF ForagePlaceName(PLACE) != ""
	CALL SHOW_GATHERING_ITEM(PLACE)
	PRINTFORMW 
	CLEARLINE 1
	RESTART
ELSE	
	RETURN -1
ENDIF

@SHOW_GATHERING_LIST_SELECT(PLACE)
#DIM PLACE
DRAWLINE
PRINTFORML 採集可能物一覧
CALL COLORMESSAGE(@"中級素材　",C_YELLOW)
CALL COLORMESSAGE(@"高級素材　",C_P_GREEN)
CALL COLORMESSAGE(@"最高級素材",C_AQUA)
PRINTFORML 
DRAWLINE

SELECTCASE PLACE
	CASE 99
		RETURN -1
	CASEELSE
		CALL SHOW_GATHERING_ITEM(PLACE)
		PRINTFORML
		DRAWLINE
		PRINTFORMW
ENDSELECT

@SHOW_GATHERING_ITEM(PLACE)
#DIM PLACE

FOR LOCAL,600,710
	IF GATHERING_PRODUCT(LOCAL, PLACE)
		LOCALS = %ITEMNAME:LOCAL%
		IF ITEMPRICE:LOCAL >= 1000
			SETCOLOR C_YELLOW
			SIF ABL:MASTER:教養 < 1
				LOCALS = ？？？
		ENDIF
		IF ITEMPRICE:LOCAL >= 3000
			SETCOLOR C_P_GREEN
			SIF ABL:MASTER:教養 < 2
				LOCALS = ？？？
		ENDIF
		IF ITEMPRICE:LOCAL >= 10000
			SETCOLOR C_AQUA
			SIF ABL:MASTER:教養 < 3
				LOCALS = ？？？
		ENDIF
		;季節条件を満たしていない場合は灰色表記に
		SIF !GATHERING_SEASON(LOCAL, PLACE)
			SETCOLOR 0x606060

		PRINTFORM %LOCALS%　
	ENDIF
	RESETCOLOR
NEXT

;-----------------------------
;引数で指定した価格範囲内の素材の中からランダムに入手
;-----------------------------
@GetRandMaterial(QYT, PriceRange:0, PriceRange:1)
#DIM PriceRange, 2
#DIM cArray, 100
#DIM cNumber
#DIM Chosen
#DIM QYT
cNumber = 0
FOR LOCAL, 600, 700
	SIF ITEMNAME:LOCAL == ""
		CONTINUE
	;アイテム価格上限
	SIF ITEMPRICE:LOCAL > PriceRange:0
		CONTINUE
	;下限は省略可
	SIF ITEMPRICE:LOCAL < PriceRange:1
		CONTINUE
	SIF ITEM:LOCAL >= 99
		CONTINUE
	cArray:cNumber = LOCAL
	cNumber ++
NEXT
SIF !cNumber
	RETURN

FOR LOCAL,0,QYT
	Chosen = RAND:cNumber
	ITEM:(cArray:Chosen) ++
	CALL COLORMESSAGE(@"%ITEMNAME:(cArray:Chosen)%を手に入れた",C_YELLOW,1)
NEXT

@ForagePlaceName(ARG)
#FUNCTIONS
SELECTCASE ARG
CASE 1
	IF MAIN_MAP == 0
		LOCALS = 鎮守の森
	ELSE
		LOCALS = 妖精の大樹
	ENDIF
CASE 2
	LOCALS = 斜角の竹林
CASE 3
	LOCALS = 無名の丘
CASE 4
	LOCALS = 太陽の畑
CASE 5
	LOCALS = 再思の道
CASE 6
	LOCALS = 古木の大樹
CASE 7
	IF MAIN_MAP == 6
		LOCALS = 八雲邸玄関
	ELSE
		LOCALS = マヨヒガ
	ENDIF
CASE 8
	LOCALS = 彼岸
CASE 9
	LOCALS = 山道・沢へ続く道
CASE 10
	LOCALS = 山道・中腹
CASE 11
	LOCALS = 麓の樹海
CASE 12
	IF MAIN_MAP == 8
		LOCALS = 頂上
	ELSE
		LOCALS = 絶景の丘
	ENDIF
CASE 13
	IF MAIN_MAP == 8
		LOCALS = 桃園
	ELSE
		LOCALS = 天界
	ENDIF
CASE 14
	LOCALS = 桃林～砂浜
CASE 31
	IF MAIN_MAP == 3
		LOCALS = 湖畔(紅魔館)
	ELSE
		LOCALS = 霧の湖
	ENDIF
CASE 32
	LOCALS = 三途の川
CASE 33
	LOCALS = 玄武の沢
CASE 34
	LOCALS = 大蝦蟇の池
CASE 35
	IF MAIN_MAP == 8
		LOCALS = 滝の裏
	ELSE
		LOCALS = 九天の滝
	ENDIF
CASE 36
	IF MAIN_MAP == 8
		LOCALS = 湖畔（妖怪の山山頂）
	ELSE
		LOCALS = 山の湖
	ENDIF
CASE 37
	IF MAIN_MAP == 9
		LOCALS = 川岸（地底）
	ELSE
		LOCALS = 地獄の深道
	ENDIF
CASE 61
	LOCALS = 墓地
CASE 62
	LOCALS = 無縁塚
CASE 63
	LOCALS = キノコ群生地
CASE 64
	LOCALS = 間欠泉地下センター
CASE 65
	LOCALS = 灼熱地獄跡
CASE 66
	IF MAIN_MAP == 9
		LOCALS = 縦穴の底
	ELSE
		LOCALS = 幻想風穴
	ENDIF
CASE 67
	LOCALS = 静かの海
CASEELSE
	LOCALS =
ENDSELECT

RETURNF LOCALS

@Gathering_Drawing(採集位置, 採集パワー, Modification)
;RESULT:0	採集アイテム
;RESULT:1	採集量
#DIM 採集位置
#DIM 採集パワー
#DIM Chosen
#DIM QTY
#DIM Modification
#DIM start			;抽選開始値
#DIM end			;抽選終了値
#DIM difference		;加算or減算
Chosen = 0
QTY = 0
;採集物の選出
;ランダムで検索スタート位置をずらしたり、検索の向きを変えたりする
SELECTCASE RAND:5
	CASE 0
		start = 600 + RAND:40
		end = 707
		difference = 1
	CASE 1
		start = 706
		end = 599
		difference = -1
	CASE 2
		start = 640 - RAND:40
		end = 599
		difference = -1
	CASEELSE
		start = 600 + (5 * (RAND:9))
		end = 707
		difference = 1
ENDSELECT

FOR LOCAL:0, start, end, difference
	;アイテムが無名でなく、出現条件を満たし、所有数上限でない
	SIF ITEMNAME:(LOCAL:0) == ""
		CONTINUE
	IF ITEM:(LOCAL:0) == 99
		DEBUGPRINTFORML 所持上限のため%ITEMNAME:(LOCAL:0)%をスキップ
		CONTINUE
	ENDIF
	IF !GATHERING_SEASON(LOCAL:0)
		DEBUGPRINTFORML 季節外れのため%ITEMNAME:(LOCAL:0)%をスキップ
		CONTINUE
	ENDIF
	IF  GATHERING_PRODUCT(LOCAL:0, 採集位置) < 1
		DEBUGPRINTFORML 位置が不適切なため%ITEMNAME:(LOCAL:0)%をスキップ
		CONTINUE
	ENDIF
	LOCAL:5 = ITEMPRICE:(LOCAL:0) + 1000
	;採集パワー＋補正値 が √アイテム費用+1000 を上回ったら入手
	IF 採集パワー + Modification < SQRT(LOCAL:5)
		DEBUGPRINTFORML 採集パワー不足なため%ITEMNAME:(LOCAL:0)%をスキップ
		CONTINUE
	ENDIF
	DEBUGPRINTFORML %ITEMNAME:(LOCAL:0)%に決定
	Chosen = LOCAL:0
	;価格10000を超える希少アイテムは１つしか手に入らない、それ以下の価格帯は採集パワーに応じて複数入手が可能に
	SELECTCASE LOCAL:5
		CASE 0 TO 2000
			QTY = MAX((RAND:(SQRT(採集パワー)) - 1), 1)
		CASE 2001 TO 4000
			QTY = MAX((RAND:(SQRT(採集パワー)) - 2), 1)
		CASE 4001 TO 7000
			QTY = MAX((RAND:(SQRT(採集パワー)) - 3), 1)
		CASE 7001 TO 9999
			QTY = MAX((RAND:(SQRT(採集パワー)) - 4), 1)
		CASEELSE
			QTY = 1
	ENDSELECT
	BREAK
NEXT
RETURN Chosen, QTY
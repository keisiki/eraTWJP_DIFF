;-------------------------------------------------
;演奏する
;日常系コマンド、レベル1
;-------------------------------------------------
@COM416
#DIM DYNAMIC NOW_TARGET
#DIM DYNAMIC NUM_TARGET
#DIM DYNAMIC MUSIC_BONUS
#DIM DYNAMIC BAND_BONUS
#DIM DYNAMIC DANCE_BONUS
#DIM OHINERI
#DIM INTERRUPTED
#DIM OMAKE, 20 = 40,120,195,212,213,214,215,216,500,510,520,521,600,601,610,611,612,636,642,706
#DIM CHOSEN_OMAKE

TFLAG:使用楽器 = 0
;IF 宴会開催中判定() && CFLAG:MASTER:現在位置 == TFLAG:宴会場 && MASTER != TARGET
IF CFLAG:MASTER:現在位置 == TFLAG:宴会場 && MASTER != TARGET && DAY >= FLAG:開始日 && TIME >= FLAG:33 && FLAG:参加人数
	IF ABL:MASTER:音楽技能 < 3
		PRINTL 今の音楽技能では宴会に相応しい演奏をする自信がない……
		RETURN -1
	ELSEIF ABL:MASTER:音楽技能 <= ABL:TARGET:戦闘能力 - TALENT:TARGET:機嫌
		PRINTFORML 上手く演奏しなければ%CALLNAME:TARGET%は石を投げてきそう\@ TALENT:TARGET:機嫌 < 0 ? な機嫌の悪さだ # だ \@
	ELSEIF ABL:MASTER:音楽技能 < 5
		PRINTL 下手な演奏をしようものなら石が飛んでくるかもしれない
	ELSE
		PRINTL 宴会をどの楽器で盛り上げようか
	ENDIF
ELSE
	PRINTL 何を使おうか……
ENDIF
{
	CALL ASK_M(
		"戻る", 1,
		ITEMNAME:30, MUSIC_ABLE(30),
		ITEMNAME:31, MUSIC_ABLE(31),
		ITEMNAME:32, MUSIC_ABLE(32),
		ITEMNAME:33, MUSIC_ABLE(33),
		ITEMNAME:34, MUSIC_ABLE(34)
	)
}
TFLAG:使用楽器 = RESULT
SIF ITEM:演奏指南書 && !RAND:2
	EXP:MASTER:演奏経験 ++
SIF !TFLAG:使用楽器
	RETURN -1
INTERRUPTED = 0
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:現在位置 != CFLAG:MASTER:現在位置
		CONTINUE
	SIF CFLAG:LOCAL:行動 != 5
		CONTINUE
	IF GROUPMATCH(CFLAG:LOCAL:職種, 40, 41, 42, 43, 44, 47, 48, 50)
		PRINTFORML 「うるさい！」
		CALL THROW_STONE(LOCAL)
	ELSEIF CFLAG:LOCAL:職種 == ENKAI_SANKA && !ACCOMPANY_ABLE(LOCAL)
		EXP:MASTER:演奏経験 ++
		SIF ABL:LOCAL:戦闘能力 >= 4
			EXP:MASTER:演奏経験 ++
		IF ABL:MASTER:音楽技能 + TALENT:LOCAL:機嫌 >= ABL:LOCAL:戦闘能力
			PRINTFORML %CALLNAME:LOCAL%は%TEXTR("うっとりした/目を輝かせた/あなたの演奏を褒めたたえた")%
			OHINERI = MAX(RAND:(ABL:LOCAL:戦闘能力 + 1), 1)
			MONEY:2 += OHINERI
			PRINTPLAIN *ﾁｬﾘﾝ*
			CALL COLORMESSAGE(@"{OHINERI}カリスマのおひねりが飛んできた！",C_YELLOW,1)
			IF !RAND:7
				IF !RAND:2
					;はずれくじ
					CHOSEN_OMAKE = 131
				ELSE
					CHOSEN_OMAKE = OMAKE:(RAND:20)
				ENDIF
				CALL COLORMESSAGE(@"%ITEMNAME:(CHOSEN_OMAKE)%が飛んできた！",C_YELLOW,1)
				ITEM:CHOSEN_OMAKE ++
			ENDIF
		ELSE
			PRINTFORML 「%TEXTR("うるさい！/引っ込め！/下手くそ！")%」
			CALL THROW_STONE(LOCAL)
		ENDIF
	ENDIF
	SIF RESULT == -1
		INTERRUPTED ++
NEXT
IF INTERRUPTED
	PRINTFORML 「厳しい客だぜ」
	CALL COLORMESSAGE(@"%CALLNAME:MASTER%は演奏を中断した",C_YELLOW,1)
	RETURN -1
ENDIF

SELECTCASE TFLAG:192
	;通常
	CASE 0
		LOCAL = RAND:100
		LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
		SIF LOCAL:1 > 99
			LOCAL:1 = 99
		IF LOCAL <= (9 + ABL:MASTER:音楽技能)
			;10％で大成功
			EXP:MASTER:演奏経験 ++
			EXP:MASTER:演奏経験 += 5
			TFLAG:193 = 1
		ELSEIF LOCAL >= LOCAL:1
			;10～1％で失敗
			TFLAG:193 = -1
		ELSE
			;残りは成功
			EXP:MASTER:演奏経験 ++
			TFLAG:193 = 0
		ENDIF
	;強制成功or大成功
	CASE 1
		IF RAND:100 <= (9 + ABL:MASTER:音楽技能)
			;10％で大成功
			EXP:MASTER:演奏経験 ++
			EXP:MASTER:演奏経験 += 5
			TFLAG:193 = 1
		ELSE
			;残りは成功
			EXP:MASTER:演奏経験 ++
			TFLAG:193 = 0
		ENDIF
	;強制失敗
	CASE -1
		TFLAG:193 = -1
	;コマンド終了
	CASE -2
		TFLAG:193 = -1
		RETURN 0
ENDSELECT


DOWNBASE:MASTER:体力 = 300
DOWNBASE:MASTER:気力 = 300

;その場に居るTARGET全員で回す
NOW_TARGET = TARGET
NUM_TARGET = GET_TARGETNUM()
CVARSET TCVAR, GETNUM(TCVAR, "演奏参加"), 0

FOR LOCAL, 1, NUM_TARGET + 1
	SIF !SHIRAHU(TARGET:LOCAL)
		CONTINUE
	TARGET = TARGET:LOCAL
	CALL COM416_1
	SELECTCASE TCVAR:演奏参加
	CASE 1, 2
		MUSIC_BONUS += ABL:音楽技能 + 2 * TALENT:音感
	CASE 3
		DANCE_BONUS += ABL:音楽技能
	ENDSELECT
NEXT

BAND_BONUS = CMATCH(TCVAR:演奏参加, 1, 1) + CMATCH(TCVAR:演奏参加, 2, 1)
IF TFLAG:193 < 0
	FOR LOCAL, 1, NUM_TARGET + 1
		SOURCE:(TARGET:LOCAL):反感 += MUSIC_BONUS * ABL:(TARGET:LOCAL):音楽技能
		SIF TCVAR:(TARGET:LOCAL):演奏参加
			SOURCE:(TARGET:LOCAL):鬱屈 += 3 * MUSIC_BONUS * ABL:音楽技能
		SOURCE:(TARGET:LOCAL):反感 = SOURCE:(TARGET:LOCAL):反感 * (2 + BAND_BONUS + DANCE_BONUS) / 2
		SOURCE:(TARGET:LOCAL):鬱屈 = SOURCE:(TARGET:LOCAL):鬱屈 * (2 + BAND_BONUS + DANCE_BONUS) / 2
		SOURCE:(TARGET:LOCAL):歓楽 = SOURCE:(TARGET:LOCAL):歓楽 * 2 / (2 + BAND_BONUS + DANCE_BONUS)
		SOURCE:(TARGET:LOCAL):征服 = SOURCE:(TARGET:LOCAL):征服 * 2 / (2 + BAND_BONUS + DANCE_BONUS)
		;この手の個別処理は無いほうが良いが思いついてしまったものは仕方がない
		;ルナサ
		SIF TCVAR:[[ルナサ]]:演奏参加
			TIMES SOURCE:(TARGET:LOCAL):鬱屈, 1.1
		SIF TCVAR:[[舞]]:演奏参加
			TIMES SOURCE:(TARGET:LOCAL):鬱屈, 0.9
		SIF TCVAR:[[里乃]]:演奏参加
			TIMES SOURCE:(TARGET:LOCAL):反感, 0.9
	NEXT
ELSE
	BAND_BONUS += TFLAG:193
	BAND_BONUS += MAX(SQRT(MUSIC_BONUS) - 1, 0)
	FOR LOCAL, 1, NUM_TARGET + 1
		SIF !SHIRAHU(TARGET:LOCAL)
			CONTINUE
		SOURCE:(TARGET:LOCAL):歓楽 += MUSIC_BONUS * 15
		SOURCE:(TARGET:LOCAL):征服 += MUSIC_BONUS * 10
		SOURCE:(TARGET:LOCAL):歓楽 = MAX(0, SOURCE:(TARGET:LOCAL):歓楽 * (2 + BAND_BONUS + DANCE_BONUS) / 2)
		SOURCE:(TARGET:LOCAL):征服 = MAX(0, SOURCE:(TARGET:LOCAL):征服 * (2 + BAND_BONUS + DANCE_BONUS) / 2)
		;メルラン
		SIF TCVAR:[[メルラン]]:演奏参加
			TIMES SOURCE:(TARGET:LOCAL):歓楽, 1.1
		;雷鼓
		SIF TCVAR:[[雷鼓]]:演奏参加
			TIMES SOURCE:(TARGET:LOCAL):征服, 1.1
		;こころ
		SIF TCVAR:[[こころ]]:演奏参加
			TIMES SOURCE:(TARGET:LOCAL):歓楽, 1.2
		SIF TCVAR:[[こころ]]:演奏参加
			TIMES SOURCE:(TARGET:LOCAL):征服, 1.2
	NEXT
ENDIF

TFLAG:好感度ボーナス = BAND_BONUS * 50
TARGET = NOW_TARGET

TIME += 30
RETURN 1


@COM416_1

DOWNBASE:気力 += 100

;歓楽強度
SOURCE:歓楽 = 700 + ABL:MASTER:音楽技能 * 300 + ABL:親密 * 50
;征服強度
SOURCE:征服 = 100 * (ABL:MASTER:音楽技能 + 1) + ABL:親密 * 30

IF TFLAG:193 == -1
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:征服 , 0.50
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:歓楽 , 1.00
	TIMES SOURCE:征服 , 1.00
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:征服 , 2.00
ENDIF
;音楽技能で歓楽がさらに増減
IF ABL:MASTER:音楽技能 >= 5
	TIMES SOURCE:歓楽 , 2.00
ELSEIF ABL:MASTER:音楽技能 == 4
	TIMES SOURCE:歓楽 , 1.50
ELSEIF ABL:MASTER:音楽技能 == 3
	TIMES SOURCE:歓楽 , 1.00
ELSEIF ABL:MASTER:音楽技能 == 2
	TIMES SOURCE:歓楽 , 0.50
ELSE
	TIMES SOURCE:歓楽 , 0.10
ENDIF
;[音楽知識]を持っている場合、歓楽増加
SIF TALENT:MASTER:音楽知識
	TIMES SOURCE:歓楽 , 1.50
;音感のあるなしでもう少し増減
IF TALENT:MASTER:音感 == 2
	TIMES SOURCE:歓楽 , 2.00
ELSEIF TALENT:MASTER:音感 == 1
	TIMES SOURCE:歓楽 , 1.50
ELSEIF TALENT:MASTER:音感 == -1
	TIMES SOURCE:歓楽, 0.50
ENDIF

SIF TARGET <= 0
	RETURN 1
SELECTCASE TALENT:TARGET:音楽知識
;楽器持ちキャラ、プリリバ三姉妹と九十九姉妹と雷鼓がTARGET
CASE 1
	EXP:MASTER:演奏経験 ++
	IF ACCOMPANY_ABLE(TARGET)
		TCVAR:演奏日時 = TIME
		DOWNBASE:気力 += 200
		DOWNBASE:体力 += 100
		TIMES SOURCE:歓楽 , 2.00
		TCVAR:演奏参加 = 1
		EXP:演奏経験 ++
		PRINTFORMW %CALLNAME:TARGET%は楽器を奏で始めた！
		IF ABL:MASTER:音楽技能 >= ABL:TARGET:音楽技能
			EXP:演奏経験 ++
			TIMES SOURCE:征服 , 1.50
		ENDIF
	ENDIF
;歌唱キャラ、ミスティアと響子、エレンがTARGET
CASE 2
	EXP:MASTER:演奏経験 ++
	IF ACCOMPANY_ABLE(TARGET)
		TCVAR:演奏日時 = TIME
		DOWNBASE:気力 += 200
		DOWNBASE:体力 += 100
		TIMES SOURCE:歓楽 , 2.00
		TCVAR:演奏参加 = 2
		EXP:歌唱経験 ++
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%の旋律に合わせて歌い始めた！
		IF ABL:MASTER:音楽技能 >= ABL:TARGET:音楽技能
			EXP:歌唱経験 ++
			TIMES SOURCE:征服 , 1.50
		ENDIF
	ENDIF
;舞踏キャラ、こころ、舞、里乃
CASE 3
	EXP:MASTER:演奏経験 ++
	IF ACCOMPANY_ABLE(TARGET)
		TCVAR:演奏日時 = TIME
		DOWNBASE:気力 += 200
		DOWNBASE:体力 += 100
		TIMES SOURCE:歓楽 , 2.00
		TCVAR:演奏参加 = 3
		EXP:舞踏経験 ++
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%の旋律に合わせて踊り出した！
		IF ABL:MASTER:音楽技能 >= ABL:TARGET:音楽技能
			EXP:舞踏経験 ++
			TIMES SOURCE:征服 , 1.50
		ENDIF
	ENDIF
CASEELSE
	SIF ABL:MASTER:音楽技能 <= ABL:TARGET:音楽技能 || TALENT:TARGET:音楽知識
		EXP:MASTER:演奏経験 ++
ENDSELECT
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE416
;演奏実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(416)
	RETURN RESULT
	
;いずれの楽器（30～34）も使えない
LOCAL:1 = 0
FOR LOCAL,30,35
	SIF MUSIC_ABLE(LOCAL)
		LOCAL:1 ++
NEXT
SIF !LOCAL:1
	RETURN 0

;楽器三種のいずれかを持っていても13,24,30風呂と14,40トイレでは演奏できない
;SIF CFLAG:MASTER:現在位置 == 13 || CFLAG:MASTER:現在位置 == 14 || CFLAG:MASTER:現在位置 == 24 || CFLAG:MASTER:現在位置 == 30 || CFLAG:MASTER:現在位置 == 39 || CFLAG:MASTER:現在位置 == 40
SIF (IN_TOILET(CFLAG:MASTER:現在位置) || BATHCHECK(CFLAG:MASTER:現在位置)) && GET_MAPID(CFLAG:MASTER:現在位置) == MAIN_MAP
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
SIF CFLAG:うふふ == 2
	RETURN 0
;睡眠中
;いっそ無理やり起床させるという案もあったり
SIF CFLAG:睡眠 && !CFLAG:添い寝中 || CFLAG:添い寝中
	RETURN 0
;仕事中、かつ、宴会参加者ではない
SIF CFLAG:行動 == 5 && CFLAG:職種 != ENKAI_SANKA
	RETURN 0
;停止中は不可
SIF FLAG:70
	RETURN 0
RETURN 1

@MUSIC_ABLE(ARG)
#FUNCTION
SIF !ITEM:ARG
	RETURNF 0
SELECTCASE ARG
;キーボード、ギター、ペット
	CASE 30,32,33
		RETURNF 1
;ピアノ
;博麗神社居間、紅魔館の広場と廃洋館、地霊殿の広場、寺子屋に設置しているという形です。
	CASE 31
		SIF GROUPMATCH(CFLAG:MASTER:現在位置,GET_MAP_REPLACEMENT(9),GET_MAP_REPLACEMENT(222),GET_MAP_REPLACEMENT(309),GET_MAP_REPLACEMENT(335))
			RETURNF 1
;バイオリン
	CASE 34
		SIF ABL:MASTER:音楽技能 >= 2
			RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT
RETURNF 0

@ACCOMPANY_ABLE(ARG)
#FUNCTION
SIF BASE:ARG:気力 < 200
	RETURNF 0
SIF TIME_PROGRESS(TCVAR:ARG:演奏日時) < 90
	RETURNF 0
SIF ABL:MASTER:音楽技能 < 3
	RETURNF 0
SIF !SHIRAHU(ARG)
	RETURNF 0
SIF CFLAG:ARG:行動 == 5
	RETURNF 0

RETURNF 1

@THROW_STONE(ARG)
#DIM LISTENER_P
CALL COLORMESSAGE(@"%CALLNAME:ARG%は石を投げた",C_YELLOW,1)
LISTENER_P = ABL:ARG:戦闘能力
IF RAND:(ABL:MASTER:戦闘能力 * (2 + CFLAG:ARG:弾幕勝負勝利) + 1) > LISTENER_P
	CALL COLORMESSAGE(@"%CALLNAME:MASTER%は華麗に攻撃を回避した",C_YELLOW,1)
ELSE
	CALL RECOVER(MASTER, (LISTENER_P + 1) * (-150), "体力", @"%CALLNAME:ARG%の投石")
	RETURN -1
ENDIF
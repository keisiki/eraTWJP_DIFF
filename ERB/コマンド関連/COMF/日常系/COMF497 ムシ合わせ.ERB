;ムシ合わせ
;NPCとのタイマンを前提として既存のを簡素化したのを増築したが、
;いっそ既存のにガッツリ手を突っ込んでNPCも扱える作りにした方がいいか？
;好きなキャラと一緒にNPC相手のタッグマッチとかできたらいいよね。

@COM497
#DIM 順位
VARSET MUSHI_BATTLER
VARSET MUSHI_TEAM
VARSET MUSHI_POWER
VARSET BATTLERNAME
MUSHI_BATTLER:1 = MASTER
BATTLERNAME:1 '= @"%CALLNAME:MASTER%"
;参加者は一人
MUSHI_CHARANUM = 2

NPC_KIRIFUDA_ID = 0
NPC_KIRIFUDA_X = 0
NPC_KIRIFUDA_G = 0

PRINTFORML 対戦相手を選んでください
CALL ASK_M("ムシバトラー1号",1,"ムシバトラー2号",1,"天野川リュウセイ",1)
SELECTCASE RESULT
	CASE 0
		BATTLERNAME:2 = ムシバトラー1号
		NPC_LV = 1
	CASE 1
		BATTLERNAME:2 = ムシバトラー2号
		NPC_LV = 3
	CASE 2
		BATTLERNAME:2 = 天野川リュウセイ
		NPC_LV = 6
		NPC_KIRIFUDA_ID = 322
		NPC_KIRIFUDA_X = 1
		NPC_KIRIFUDA_G = 1
ENDSELECT
;仕様上、ランク4以上は強さに目茶苦茶幅がある
SELECTCASE NPC_LV
	CASE 1
		NPC_MBP = 1600 + RAND:1600
	CASE 2
		NPC_MBP = 3200 + RAND:2800
	CASE 3
		NPC_MBP = 6200 + RAND:6000
	CASE 4
		NPC_MBP = 12000 + RAND:18000
	CASE 5
		NPC_MBP = 30000 + RAND:120000
	CASE 6
		NPC_MBP = 150000 + RAND:350000
	CASEELSE
		NPC_MBP = 500000 + RAND:500000
ENDSELECT


PRINTFORML %BATTLERNAME:2%と対戦します　
PRINTFORM %BATTLERNAME:2%のムシバトルパワー：
CALL COLOR_STAMP(NPC_LV + 1, 0, "★", RANKCOLOR(NPC_LV + 1), 1)
PRINTL 
RESETCOLOR
PRINTL [1] 対戦開始！
PRINTL [0] やっぱりやめる
INPUT
SIF !RESULT
	RETURN -1
;1はMASTER

;本来キャラ番号が入るところ
;MUSHI_BATTLER:2 = 999

;セットアップ１
CALL CHARA_MUSHICAGE_SETTING(999, 2, 1)

;見せ合い＆ムシ選出
CALL NPC_MUSHI_BATTLE_SELECTION
;セットアップ２
CALL NPC_MUSHI_BATTLE_SETUP_2

;試合開始！
CALL MUSHI_BATTLE
;結果発表
CALL MUSHI_BATTLE_RESULT(1)

;時間経過
TIME += 30

RETURN 1

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;ムシバトル
@COM_ABLE497
;時間停止中
SIF FLAG:時間停止
	RETURN 0
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(497)
	RETURN RESULT
;昆虫採集セットがない
SIF !ITEM:昆虫採集セット
	RETURN 0
;;昆虫採集できる場所である
SIF !MUSHITORI_SPOT(CFLAG:MASTER:現在位置)
	RETURN 0
;;冬の間はおやすみ
;SIF GET_MONTH() == "冬の月"
;	RETURN 0
;虫を持ってない
SIF !MUSHI_CAGE:1
	RETURN 0
;添い寝中
SIF CFLAG:MASTER:添い寝中
	RETURN 0
;うふふ中
SIF CFLAG:MASTER:うふふ
	RETURN 0
;体力・気力不足
SIF BASE:MASTER:気力 < 500 || BASE:MASTER:体力 < 500
	RETURN 0
RETURN 1


;-------------------------------------------------
;ムシバトル：見せ合い
;-------------------------------------------------
@NPC_MUSHI_BATTLE_SELECTION

#DIM LPCOUNT
VARSET MUSHI_SELECT
VARSET MUSHI_REST
PRINTFORML ◆対戦相手の虫カゴ◆
FOR LPCOUNT, -2, MUSHI_MAXCAGE + 2
	IF LPCOUNT == -2
		PRINTFORM ┌
	ELSEIF LPCOUNT == 0
		PRINTFORM ├
	ELSEIF LPCOUNT == MUSHI_MAXCAGE + 1
		PRINTFORM └
	ELSE
		PRINTFORM │
	ENDIF
	IF LPCOUNT == -2
		PRINTFORM %"─" * 20%┐
	ELSEIF LPCOUNT == -1
		LOCALS = %BATTLERNAME:2%
		PRINTFORM %LOCALS, 36, LEFT%
		SETCOLOR RANKCOLOR(NPC_LV + 1)
		PRINTFORM ★%TOFULL(TOSTR(NPC_LV))%
		RESETCOLOR
		PRINTFORM │
	ELSEIF LPCOUNT == MUSHI_MAXCAGE + 1
		PRINTFORM %"─" * 20%┘
	ELSEIF LPCOUNT == 0
		PRINTFORM %"─" * 20%┤
	ELSE
		CALL PRINT_MUSHINAME(MUSHI_NAME:(20 + LPCOUNT), MUSHI_CAGE:(20 + LPCOUNT), 40)
		PRINTFORM │
	ENDIF
	PRINTL 
NEXT
;フィールドタイプ設定
MUSHI_FIELD:99 = 0
WHILE !MUSHI_FIELD:99
	SETBIT MUSHI_FIELD:99, RAND:8
	SIF !(MUSHITORI_SPOT(CFLAG:MASTER:現在位置) &  MUSHI_FIELD:99)
		MUSHI_FIELD:99 = 0
WEND
PRINTFORM ＜フィールドタイプ：
CALL PRINT_FIELDTYPE(MUSHI_FIELD:99)
PRINTL ＞
PRINTFORML ◆%CALLNAME:MASTER%の虫カゴ◆　バトル参加させるムシを１～３匹選出してください
DRAWLINE
;MASTERの選出に引き渡し
MUSHI_REST:1 = 0
MUSHI_MENTAL:1 = 1000
CALL MUSHI_BATTLE_SELECTION_MASTER

;-------------------------------------------------
;ムシバトル：セットアップ２：選出後
;-------------------------------------------------
@NPC_MUSHI_BATTLE_SETUP_2
#DIM LPCOUNT
#DIM SETNUM
#DIM KIRIHUDA
VARSET MUSHI_STATUS
VARSET MUSHI_MENTAL
;MASTERの設定
FOR LPCOUNT, 1, MUSHI_MAXSELECT + 1
	MUSHI_SELECT:(10 + LPCOUNT) = MUSHI_SELECT:LPCOUNT
	CALL MUSHI_PALAM(MUSHI_SELECT:LPCOUNT, MUSHI_POWER:1)
	CALL MUSHI_FLAGSETTING(MUSHI_SELECT:(10 + LPCOUNT), 10 + LPCOUNT)
NEXT
;戦闘初期設定
;MASTERのMENTALは1000固定
MUSHI_MENTAL:1 = 1000
MUSHI_STAND:1 = MUSHI_SELECT:11
MUSHI_STANDNUM:1 = 11
GETCOLOR
MUSHI_COLOR:1 = RESULT
;キャラの選出を決める
FOR LPCOUNT, 2, MUSHI_CHARANUM + 1
	SETNUM = LPCOUNT * 10
	IF NPC_KIRIFUDA_ID
	;切り札がいるなら切り札は確定（番号はランダム）
		KIRIHUDA = 1
		LOCAL = RAND:MUSHI_MAXSELECT + 1
		MUSHI_SELECT:(SETNUM + LOCAL) = MUSHI_CAGE:(SETNUM + 1)
		MUSHI_NAME:(SETNUM + LOCAL) = MUSHI_NAME:(SETNUM + 1)
		CALL MUSHI_PALAM(MUSHI_SELECT:(SETNUM + LOCAL), MUSHI_POWER:LPCOUNT)
		CALL MUSHI_FLAGSETTING(MUSHI_SELECT:(SETNUM + LOCAL), SETNUM + LOCAL)
	ELSE
		KIRIHUDA = 0
	ENDIF
	;虫カゴからランダムで空いてる番号に入れていく,ただし切り札がいるなら1番は除外
	FOR LOCAL, 1, MUSHI_MAXSELECT + 1
		IF !MUSHI_SELECT:(SETNUM + LOCAL)
			WHILE !MUSHI_SELECT:(SETNUM + LOCAL)
				LOCAL:1 = SETNUM + RAND:(MUSHI_MAXCAGE - KIRIHUDA) + 1 + KIRIHUDA
				LOCAL:2 = MUSHI_CAGE:(LOCAL:1)
				IF !MATCH(MUSHI_SELECT, LOCAL:2)
					MUSHI_SELECT:(SETNUM + LOCAL) = LOCAL:2
					MUSHI_NAME:(SETNUM + LOCAL) = MUSHI_NAME:(LOCAL:1)
				ENDIF
			WEND
			CALL MUSHI_PALAM(MUSHI_SELECT:(SETNUM + LOCAL), MUSHI_POWER:LPCOUNT)
			CALL MUSHI_FLAGSETTING(MUSHI_SELECT:(SETNUM + LOCAL), SETNUM + LOCAL)
		ENDIF
	NEXT
	;戦闘初期設定
	FOR LOCAL, 1, MUSHI_MAXSELECT + 1
		SETBIT MUSHI_REST:LPCOUNT, LOCAL
	NEXT
	MUSHI_MENTAL:LPCOUNT = NPC_MBP
	MUSHI_STAND:LPCOUNT = MUSHI_SELECT:(SETNUM + 1)
	MUSHI_STANDNUM:LPCOUNT = SETNUM + 1
	;口上色の設定
	;色が変わってたら登録する
	IF LOCAL != RESULT
		MUSHI_COLOR:LPCOUNT = RESULT
	ELSE
		MUSHI_COLOR:LPCOUNT = LOCAL
	ENDIF
	RESETCOLOR
NEXT
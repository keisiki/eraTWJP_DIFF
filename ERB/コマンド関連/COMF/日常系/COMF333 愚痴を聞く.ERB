;-------------------------------------------------
;愚痴を聞く
;日常系コマンド、レベル2
;-------------------------------------------------
@COM333
VARSET LOCAL

;固定で獲得するソース
SOURCE:歓楽 = 400

LOCAL = 50 + ABL:MASTER:話術技能 * 5 + ABL:親密 * 2

SIF BASE:酒気 > MAXBASE:酒気 / 2
	LOCAL -= 15

SELECTCASE TFLAG:192
	;通常
	CASE 0
		SELECTCASE RAND:LOCAL
			CASE IS < 20
				TFLAG:193 = 1
			CASE 20 TO 50
				TFLAG:193 = 2
			CASEELSE
				TFLAG:193 = 3
		ENDSELECT
	;強制成功or大成功
	CASE 1
		SELECTCASE RAND:LOCAL
			CASE IS < 50
				TFLAG:193 = 2
			CASEELSE
				TFLAG:193 = 3
		ENDSELECT
	;強制失敗
	CASE -1
		TFLAG:193 = 1
	;コマンド終了
	CASE -2
		TFLAG:193 = -1
		RETURN 0
ENDSELECT
SIF BASE:酒気 > MAXBASE:酒気 / 2
	TFLAG:193 += 3

SELECTCASE TFLAG:193
	CASE 1
		SOURCE:反感 = 100
	CASE 2
		SOURCE:歓楽 += ABL:親密 * 30
		TALENT:機嫌 ++
		BASE:怒り /= 2
		CFLAG:不機嫌 = 0
	CASE 3
		SOURCE:歓楽 += ABL:親密 * 50
		SOURCE:情愛 = 200
		TALENT:機嫌 ++
		BASE:怒り /= 2
		CFLAG:不機嫌 = 0
	CASE 4
		SOURCE:反感 = 1000
		SOURCE:歓楽 += ABL:親密 * 30
	CASE 5
		SOURCE:歓楽 += ABL:親密 * 50
	CASE 6
		SOURCE:歓楽 += ABL:親密 * 75
		SOURCE:情愛 = 300
		TALENT:機嫌 = 1
		BASE:怒り /= 2
		CFLAG:不機嫌 = 0
ENDSELECT

TIME += 20

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE333
;愚痴を聞く実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(333)
	RETURN RESULT
SIF FLAG:70
	RETURN 0
SIF ABL:親密 < 3
	RETURN 0
SIF MARK:反発刻印
	RETURN 0
SIF CFLAG:うふふ
	RETURN 0
SIF TALENT:機嫌 >= 0
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
RETURN 1

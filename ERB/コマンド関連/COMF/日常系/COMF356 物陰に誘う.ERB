;-------------------------------------------------
;物陰に誘う
;日常系コマンド、レベル1
;-------------------------------------------------
@COM356
;物陰に誘う
;LOCAL:1 潜伏場所

IF FLAG:甲斐性無
	DRAWLINE
	PRINTFORMW %CALLNAME:MASTER%にはそんな度胸はない
	RETURN -1
ENDIF
;口上処理による強制成功
SIF TFLAG:192 == 1
	GOTO SENPUKU
;口上処理による強制失敗
SIF TFLAG:192 == -1
	RETURN -1
;無知でない場合、露出癖3以上(羞恥心で増減)必須
IF !TALENT:無知 && ABL:露出癖 + TALENT:羞恥心 < 3
	DRAWLINE
	PRINTFORMW %CALLNAME:TARGET%はまだ外ですることに抵抗があるようだ
	TFLAG:193 = -1
	RETURN -1
ENDIF

$SENPUKU
CALL 潜伏場所決定処理


IF TALENT:無知
	DRAWLINE
	PRINTFORMW %CALLNAME:MASTER%は何も知らない%CALLNAME:TARGET%を言葉巧みに%NAME_HIDING_PLACE()%に誘い出した……
ELSE
	DRAWLINE
	PRINTFORMW %CALLNAME:MASTER%は頬を赤くする%CALLNAME:TARGET%を%NAME_HIDING_PLACE()%に誘った……
ENDIF

CFLAG:MASTER:うふふ = 1
CFLAG:TARGET:うふふ = 1

SETBIT CFLAG:TARGET:潜伏状態,0
SETBIT FLAG:潜伏,0

;開始時に潜伏率を100%に
BASE:MASTER:潜伏率 = 100




;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE356
SIF !TFLAG:100
	RETURN 0
;時間停止中は不可
SIF FLAG:時間停止
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(356)
	RETURN RESULT
;自室不可
;21/06/25,注記どおり動作するよう変更しました
;SIF CFLAG:MASTER:現在位置 == (CFLAG:MASTER:初期位置 || CFLAG:TARGET:初期位置)
SIF GROUPMATCH(CFLAG:MASTER:現在位置, CFLAG:MASTER:初期位置, CFLAG:TARGET:初期位置)
	RETURN 0
SIF CFLAG:MASTER:現在位置 == OMANEKIBEYA()
	RETURN 0
SIF CFLAG:うふふ
	RETURN 0
SIF !(CFLAG:既成事実 & 合意_うふふ)
	RETURN 0
;睡眠中
SIF CFLAG:睡眠 && !CFLAG:添い寝中
	RETURN 0
;潜伏モード中は不可
SIF GETBIT(FLAG:潜伏,0)
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
SIF TARGET < 1
	RETURN 0
RETURN 1

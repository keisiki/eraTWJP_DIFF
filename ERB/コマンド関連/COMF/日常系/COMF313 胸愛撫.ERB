;-------------------------------------------------
;胸愛撫
;日常系コマンド　レベル3
;-------------------------------------------------
@COM313
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
TFLAG:理性削り = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 20
	SOURCE:快Ｂ = 80
	IF ABL:PLAYER:指 >= 4
		SOURCE:快Ｂ += 50
	ELSE
		SOURCE:快Ｂ += ABL:PLAYER:指 * 10
	ENDIF
	;着衣補正
	IF TEQUIP:1 > 1
		TIMES SOURCE:快Ｂ , 0.80
		TIMES SOURCE:欲情 , 0.80
		TIMES SOURCE:露出 , 0.80
	ENDIF
ELSE
	IF FLAG:甲斐性無 && !CFLAG:うふふ
		DRAWLINE
		PRINTFORMW %CALLNAME:MASTER%にはそんな度胸はない
		RETURN -1
	ENDIF
	;口上による強制失敗
	IF TFLAG:192 == -1
		RETURN -1
	ENDIF
	DOWNBASE:体力 += 5
	DOWNBASE:気力 += 50
	SOURCE:快Ｂ = 80
	SOURCE:歓楽 = 100
	SOURCE:欲情 = 50
	SOURCE:露出 = 150
	SOURCE:反感 = 700
	SOURCE:受動 = 120 + 240 * ABL:従順

	;好感度をみる
	SELECTCASE ABL:親密
		CASE IS < 5
			SOURCE:反感 -= (ABL:親密 * 10)
		CASE IS < 10
			SOURCE:情愛 += ABL:親密 * 5
			SOURCE:欲情 += ABL:親密 * 10
			SOURCE:反感 -= (ABL:親密 * 20)
		CASEELSE
			SOURCE:情愛 += ABL:親密 * 15
			IF !TALENT:恋慕 && !TALENT:セフレ
				SOURCE:情愛 += ABL:親密 * 5
				SOURCE:欲情 += (ABL:親密 * 20)
				SOURCE:反感 -= (ABL:親密 * 50)
			ELSE
				SOURCE:欲情 += (ABL:親密 * 15)
				SOURCE:反感 -= (ABL:親密 * 30)
			ENDIF
	ENDSELECT
	SIF SOURCE:反感 < 0
		SOURCE:反感 = 0
	SOURCE:欲情 = MIN(SOURCE:欲情,500)
	SOURCE:情愛 = MIN(SOURCE:情愛,300)

	IF ABL:PLAYER:指 >= 4
		SOURCE:快Ｂ += 50
	ELSE
		SOURCE:快Ｂ += ABL:PLAYER:指 * 10
	ENDIF
	;着衣補正
	IF TEQUIP:1 > 1
		TIMES SOURCE:快Ｂ , 0.80
		TIMES SOURCE:欲情 , 0.80
		TIMES SOURCE:露出 , 0.80
	ENDIF
	IF HITOKE(1)
		TIMES SOURCE:露出 , 2.00
		TIMES SOURCE:反感 , 2.00
	ENDIF
	;信頼
	TFLAG:98 = -5

	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;抑圧、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 20
ENDIF
;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のＢ⇔調教者の指の汚れが移動
STAIN:5 |= STAIN:PLAYER:1
STAIN:PLAYER:1 |= STAIN:5
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 3
	ENDIF
ELSE
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 3
		EXP:MASTER:レズ経験 += 3
	ENDIF
	TIME += 10
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE313
;胸愛撫実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(313)
	RETURN RESULT
;時間停止中はスキップ
IF !FLAG:70
	;馴れ合い強度2
	SIF (TCVAR:MASTER:馴れ合い < 2) && !CFLAG:睡眠
		RETURN 0
	;睡眠中
	SIF CFLAG:睡眠 && !CFLAG:イタズラ
		RETURN 0
	;添い寝中
	SIF CFLAG:添い寝中
		RETURN 0
	;仕事中
	SIF CFLAG:行動 == 5
		RETURN 0
	;デート中
	SIF CHK_DATENOW(CFLAG:デート中) && CFLAG:現在位置 != OMANEKIBEYA()
		RETURN 0
ENDIF
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
;腕使用中
SIF TFLAG:61 && MASTER_POSE(SET_B,SET_YUBI) != TARGET
	RETURN 0
SIF GETBIT (TCVAR:26,3)
	RETURN 0
RETURN 1


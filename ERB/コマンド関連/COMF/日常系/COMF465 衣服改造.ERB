@COM370
#DIM 改造部位
PRINTFORML %CALLNAME:TARGET%の何を改造しますか？

FOR LOCAL,1,23
	SIF EQUIP:TARGET:LOCAL
	PRINTFORML [{LOCAL}]- %CLOTHNAME(LOCAL,EQUIP:TARGET:LOCAL)%
NEXT
PRINTFORML [999]- やめる
$LOOP
INPUT
SELECTCASE RESULT
	CASE 1 TO 22
		IF !EQUIP:TARGET:RESULT
			GOTO LOOP
		ELSE
			改造部位 = RESULT
		ENDIF
	CASE 999
		RETURN -1
	CASEELSE
		GOTO LOOP
ENDSELECT
PRINTFORML %CLOTHNAME(改造部位,EQUIP:TARGET:改造部位)%をどうしますか？
CALL ASK_YN("元に戻す","仕立て直す")
IF !RESULT
	CSTR:TARGET:(50 + 改造部位) =
	PRINTFORML 元に戻しました。
ELSE
	PRINTFORML どう仕立てますか？（前につける修飾語を入力）
	INPUTS
	IF RESULTS == ""
		GOTO LOOP
	ELSE
		CSTR:TARGET:(50 + 改造部位) = %RESULTS%%CLOTHNAME(改造部位,EQUIP:TARGET:改造部位)%
	ENDIF
	PRINTFORML %CLOTHNAME(改造部位,EQUIP:TARGET:改造部位)%は%CSTR:TARGET:(50 + 改造部位)%になりました。
ENDIF
TIME += 60
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE370
;実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(370)
	RETURN RESULT
;アリスがいないとダメ
SIF CFLAG:17:現在位置 != CFLAG:MASTER:現在位置
	RETURN 0
;仕事中
SIF CFLAG:行動 == 5
	RETURN 0
;気力0
SIF BASE:17:気力 <= 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;酒虫の様子を見る
;日常系コマンド、レベル1
;-------------------------------------------------

@COM450
#DIM 醸造量
#DIM 酒ID
VARSET LOCAL

酒ID = ALC_ID()
醸造量 = GET_INT(0, "酒データ", 酒ID, "醸造数")
LOCALS '= @"%VAR_WATER%と入れ替える"
LOCAL = 1
IF VAR_WATER == ""
	LOCALS '= "替えの水も無しにくみ出すわけにはいかない"
	LOCAL = 0
ENDIF

PRINTFORML %CALLNAME:MASTER%は酒虫の入っている瓶を覗き込んだ
PRINTFORML 瓶は%VAR_FERMENT%で満たされている…
DRAWLINE
{
CALL ASK_M("何もしない",1,@"%LOCALS%", LOCAL,"アイテムを足す",1,
"時間操作酒造（TSP2400消費）",BASE:MASTER:8 >= 2400,"『キング・クリムゾン』！（TSP4000&キング・クリムゾン消費）", BASE:MASTER:8 >= 4000 && ITEM:キング・クリムゾン)
}
SELECTCASE RESULT
	CASE 0
		RETURN -1
	CASE 1
		IF ITEM:酒ID >= 99
			PRINTFORMW これ以上%VAR_FERMENT%を持てない
			RETURN -1
		ENDIF
		PRINTFORML %CALLNAME:MASTER%は%VAR_FERMENT%を汲み出して%VAR_WATER%と入れ替えた
		IF 醸造量 > 0
			CALL GET_ITEM(@"%VAR_FERMENT%", 醸造量, 2)
		ENDIF
		VAR_FERMENT = %VAR_WATER%
		VAR_WATER =
		DAY:10 = 0
	CASE 2
		CALL COM450_1
	CASE 3
		IF GET_STR(0, "酒データ", 酒ID, "変化先") == ""
			PRINTFORMW %VAR_FERMENT%にはやる意味がない
			RETURN -1
		ENDIF
		PRINTFORML %CALLNAME:MASTER%は時間停止能力を応用して強引に瓶の中の時を進めた
		BASE:MASTER:8 -= 2400
		CALL 酒造
	CASE 4
		IF GET_STR(0, "酒データ", 酒ID, "変化先") == ""
			PRINTFORMW %VAR_FERMENT%にはやる意味がない
			RETURN -1
		ENDIF
		PRINTFORML %CALLNAME:MASTER%は時間を消し飛ばして『結果』だけを得た
		BASE:MASTER:8 -= 4000
		ITEM:キング・クリムゾン --
		DAY:10 += 9
		CALL 酒造
ENDSELECT
TIME += 5
RETURN 1



@COM450_1
IF LOCAL == 0
	{
	CALL ASK_M("イモ＊１０", ITEM:イモ >= 10 && CAN_FERMENT(),
	"はちみつ＊１０", ITEM:はちみつ >= 10 && CAN_FERMENT(),
	"キイチゴ＊２０", ITEM:キイチゴ >= 20 && CAN_FERMENT(),
	"ヤマブドウ＊２０", ITEM:ヤマブドウ >= 20 && CAN_FERMENT(),
	"かびたブドウ＊２０", ITEM:かびたブドウ >= 20 && CAN_FERMENT(),
	"次へ",1,
	"やめる",1)
	}
	SELECTCASE RESULT
		CASE 0
			CALL LOST_ITEM("イモ", 10, 1)
			VAR_FERMENT = イモ入り水
		CASE 1
			CALL LOST_ITEM("はちみつ", 10, 1)
			VAR_FERMENT = ハチミツ水
		CASE 2
			CALL LOST_ITEM("キイチゴ", 20, 1)
			VAR_FERMENT = キイチゴ水
		CASE 3
			CALL LOST_ITEM("ヤマブドウ", 20, 1)
			CALL GET_ITEM("ポマース", 1, 1)
			VAR_FERMENT = ブドウ汁
		CASE 4
			CALL LOST_ITEM("かびたブドウ", 20, 1)
			CALL GET_ITEM("ポマース", 1, 1)
			VAR_FERMENT = 貴腐ブドウ汁
		CASE 5
			LOCAL = 1
			CLEARLINE 7
			RESTART
		CASE 6
	ENDSELECT
ENDIF
IF LOCAL == 1
	{
	CALL ASK_M("どぶろく＊５", ITEM:どぶろく >= 5 && VAR_WATER == "ただの水",
	"甘味材＊１０", ITEM:甘味材 >= 10 && CAN_FERMENT(),
	"熟成前ラム", ITEM:熟成前ラム && CAN_FERMENT(),
	"熟成前フランボワーズ", ITEM:熟成前フランボワーズ && CAN_FERMENT(),
	"蒸留酒", ITEM:蒸留酒 && VAR_FERMENT == "ブドウ汁",
	"熟成前ブランデー", ITEM:熟成前ブランデー && CAN_FERMENT(),
	"次へ",1,
	"やめる",1)
	}
	SELECTCASE RESULT
		CASE 0
			PRINTFORML %CALLNAME:MASTER%は瓶にどぶろくを注ぎ込んだ
			PRINTFORML これで少し早くできるはずだ…
			DAY:10 += 3
			CALL LOST_ITEM("どぶろく", 5, 1)
		CASE 1
			CALL LOST_ITEM("甘味材", 10, 1)
			VAR_FERMENT = 砂糖水
		CASE 2
			CALL LOST_ITEM("熟成前ラム", 1, 1)
			VAR_FERMENT = 熟成前ラム
		CASE 3
			CALL LOST_ITEM("熟成前フランボワーズ", 1, 1)
			VAR_FERMENT = 熟成前フランボワーズ
		CASE 4
			CALL LOST_ITEM("蒸留酒", 1, 1)
			VAR_FERMENT = ブドウ汁(ALC添加)
		CASE 5
			CALL LOST_ITEM("熟成前ブランデー", 1, 1)
			VAR_FERMENT = 熟成前ブランデー
		CASE 6
			LOCAL = 2
			CLEARLINE 8
			RESTART
		CASE 7
	ENDSELECT
ENDIF

IF LOCAL == 2
	{
	CALL ASK_M("上質食材", ITEM:上質食材 && CAN_FERMENT(),
	"草木灰", ITEM:草木灰 && VAR_WATER == "どぶろく",
	"次へ",1,
	"やめる",1)
	}
	SELECTCASE RESULT
		CASE 0
			CALL LOST_ITEM("上質食材", 1, 1)
			VAR_FERMENT = 米こうじ
		CASE 1
			CALL LOST_ITEM("草木灰", 1, 1)
			VAR_FERMENT = 灰持酒
		CASE 2
			LOCAL = 0
			CLEARLINE 4
			RESTART
		CASE 3
	ENDSELECT
ENDIF
RETURN

;酒虫の様子を見る
@COM_ABLE450
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;移動実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(450)
	RETURN RESULT
;台所
SIF CFLAG:MASTER:現在位置 != 拠点_酒虫
	RETURN 0
SIF CFLAG:うふふ
	RETURN 0
RETURN 1

@CAN_FERMENT(ARG)
#FUNCTION
SIF DAY:10 >= 3
	RETURNF 0
SIF GROUPMATCH(VAR_FERMENT, "ただの水", "霧の湖の水", "地獄温泉の水", "奇跡の水")
	RETURNF 1
RETURNF 0


;-------------------------------------------------
;銭湯
;日常系コマンド、レベル1
;-------------------------------------------------
@COM432
#DIM 回復前体力１
#DIM 回復前気力１
#DIM 回復後体力１
#DIM 回復後気力１
#DIM 回復前体力２
#DIM 回復前気力２
#DIM 回復後体力２
#DIM 回復後気力２
#DIMS 台詞
回復前体力１ = BASE:MASTER:体力
回復前気力１ = BASE:MASTER:気力
IF DAY * 1440 + TIME - EX:MASTER:前回の入浴時刻 < 360
	TFLAG:194 = 1
	PRINTFORML さっき入ったばかりだ
	RETURN -1
ELSEIF PLACE_SENTO(CFLAG:MASTER:現在位置) == 2
	;番台ちゃんを怒らせてしまった場合
	IF TFLAG:209 == 4
		CALL SPTALK, 0, "怒り", 14, "「………………」",1
		PRINTFORML 番台の方から鋭い視線が飛んできている……
		PRINTFORML また日を改めて来たほうが良さそうだ……
		RETURN -1
	;番台ちゃん通常運行
	ELSE
		;お金足りなかったイベントを見ている
		IF TFLAG:209 == 3
			台詞 = 「……あれ、どったの？ もしかして お代持ってきたとか？」
		;やめるを選んだけどやっぱり入りたくなった
		ELSEIF TFLAG:209 == 2
			台詞 = 「……なに、もしかして気が変わった？」
		;それ以外
		ELSE
			;ぐうたらであまりやる気がない番台ちゃん
			台詞 = 「……いらっしゃぁせー」
		ENDIF
		台詞 = %台詞%/「お代は\\400でいーよ」
		CALL SPTALK, 0, "通常", 14, @"%台詞%",1
		DRAWLINE
		SIF MAIN_MAP == 2
			PRINTL [2] \5000払って貸し切る
		PRINTL [1] \400払って入浴する
		PRINTL [0] やめる
		$LOOP_hitozato
		INPUT
		IF RESULT == 2 && MAIN_MAP == 2
			IF MONEY < 5000
				{
				台詞 = 「……え、貸し切りたいって？」
						/「それならねぇ、相応の代金ってのが要るわけ」
						/「ビンボー人は大人しく入浴で我慢するんだね！」
				}
				CALL SPTALK, 0, "通常", 14, @"%台詞%",1
				RETURN -1
			ELSE
				MONEY -= 5000
				{
				台詞 = 「……え、貸し切りたいって？」
						/「フーン……まぁ何に使うかは大体想像つくけど……」
						/「支払いのいいお客には寛大だからね、それじゃ24時まで自由に使っていいよ」
						/「あとはどーぞ、ごゆっくり……」
				}
				CALL SPTALK, 0, "通常", 14, @"%台詞%",1
				CALLF 銭湯貸切(DAY, 1)
				RETURN 1
			ENDIF
		ELSEIF RESULT == 1
			IF MONEY < 400
				{
				台詞 = 「……ん、どーぞ、ごゆっくり」
						/「って、ダメじゃん足りてないじゃん！」
						/「…………」
				}
				CALL SPTALK, 0, "通常", 14, @"%台詞%",1
				;煽り耐性無い番台ちゃんブチギレ
				IF TFLAG:209 >= 2
					台詞 = 「…………あのさ、アタシもこう見えて暇じゃないから」/「……しまいにゃ、怒るよ？」
					CALL SPTALK, 0, "怒り", 14, @"%台詞%",1
					TFLAG:209 = 4
				ELSE
					{
					台詞 = 「…………あのさ、余計なお世話かもしんないけど」
							/「……もし、お金が無くて困ってるとかだったら」
							/「ここから結構歩くけど温泉があるから そこ行ってみれば？」
					}
					CALL SPTALK, 0, "通常", 14, @"%台詞%",1
					TFLAG:209 = 3
				ENDIF
				RETURN -1
			ELSE
				MONEY -= 400
				台詞= 「……ん、どーぞ、ごゆっくり」
				SIF TARGET > 0 && CFLAG:TARGET:6 > 0 && !CFLAG:TARGET:睡眠 
					台詞 = %台詞%/「……あ、言っとくけどアタシ見張ってるから変なことしないでよ」
				CALL SPTALK, 0, "通常", 14, @"%台詞%",1
				IF TARGET > 0 && CFLAG:TARGET:6 > 0 && !CFLAG:TARGET:睡眠 
					回復前体力２ = BASE:TARGET:体力
					回復前気力２ = BASE:TARGET:気力
					CALL ODEKAKE_OFURO(TARGET,1)
					回復後体力２ = BASE:TARGET:体力
					回復後気力２ = BASE:TARGET:気力
					PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%と脱衣所の前で一先ず別れ、浴場に向かう
				ENDIF

				CALL ODEKAKE_OFURO(MASTER,1)

				DRAWLINE
				PRINTW …
				PRINTW ……
				PRINTW …………
				PRINTFORMW %CALLNAME:PLAYER%は浴場に浸かった
				PRINTW …………
				PRINTW ……
				PRINTW …
				;大衆浴場は30分固定
				TIME += 30
				;大衆浴場総入湯回数はフラグごとオミット
				;大衆浴場時限入湯回数+1
				TFLAG:210 += 1
				;大衆浴場フラグを1に設定
				TFLAG:209 = 1
				RETURN 1
			ENDIF
		ELSEIF RESULT == 0
			;煽り耐性無い番台ちゃんブチギレ
			IF TFLAG:209 >= 2
				台詞 = 「…………あのさ、アタシもこう見えて暇じゃないから」/「……しまいにゃ、怒るよ？」
				CALL SPTALK, 0, "怒り", 14, @"%台詞%",1
				TFLAG:209 = 4
			ELSE
				{
				台詞 = 「やめんの？……まぁ、いいけどさ」
						/「…………あのさ、余計なお世話かもしんないけど」
						/「……もし、お金が無くて困ってるとかだったら」
						/「ここから結構歩くけど温泉があるから そこ行ってみれば？」
				}
				CALL SPTALK, 0, "通常", 14, @"%台詞%",1
				TFLAG:209 = 2
			ENDIF
			RETURN -1
		ELSE
			GOTO LOOP_hitozato
		ENDIF
	ENDIF
ELSEIF PLACE_SENTO(CFLAG:MASTER:現在位置) == 9
	LOCAL = 0
	;番台を怒らせてしまった場合
	IF TFLAG:211 == 4
		PRINTFORML 番台の方から鋭い視線が飛んできている……
		PRINTFORML また日を改めて来たほうが良さそうだ……
		RETURN -1
	;番台妖怪通常運行
	ELSE
		;初めて旧地獄温泉で入浴コマンドを選択した場合。周回後にゅーぷらん開放
		IF !STRCOUNT(CSTR:0:0, "地獄温泉/")
			CSTR:0:0 = %CSTR:0:0%地獄温泉/
			PRINTL 「いらっしゃいましぇだぎゃー！」
			PRINTL 「おんやぁー！　珍しいぎゃ、人間がこげんにゃ所ににゃーの用だぎゃ？」
			PRINTFORMW  %CALLNAME:PLAYER%は番台の妖怪に入浴したいことを告げた
			PRINTL 「にゃーぎゃて？……ふん……ふん」
			PRINTL 「……にゃーのにゃーの、お客しゃまだったぎゃ！」
			IF !FLAG:周回数
				PRINTL 「やけんど、よおーこぎゃな所まで来ただぎゃ！　あんしゃん、変わりモンだぎゃねー」
			ELSE
				PRINTW 「そんにゃら、あんしゃん…ぐっーどてゃーみんぐぎゃ！」
				PRINTL 「きょーはぎゃーが考ギャーた『にゅーぷらん』とーとーお披露目の日なんだぎゃ！」
				PRINTL 「きれーどころのメスっこをてゃーくしゃん揃えて」
				PRINTW 「お客しゃまぎゃ、てゃーっぷり金を落っことすの、お待ちしとったぎゃ！」
				PRINTL 「ほれっ、ほれっ！　みゃよってにゃーで、しゃっしゃと入るぎゃ！」
				PRINTFORMW 番台の妖怪は、げっへっへと厭らしい笑みを浮かべ、%CALLNAME:PLAYER%を急き立ててくる……
				GOTO SKIP
			ENDIF
		;お金足りなかったイベントを見ている
		ELSEIF TFLAG:211 == 3
			PRINTL 「金のにゃー奴は相手にしたくにゃーで。金をぎゃーに落とせばお客しゃまとして扱うぎゃ！」
		;やめるを選んだけどやっぱり入りたくなった
		ELSEIF TFLAG:211 == 2
			PRINTL 「せーっかく、こぎゃな所まで来て温泉入りゃねーにゃんて、一生の六割は損してるぎゃ！」
		ELSE
			PRINTL 「いらっしゃいましぇだぎゃー！」
		ENDIF
		;それ以外
		PRINTL 「んーぎゃ、貰ゃうモンもりゃっとくだぎゃー」
		PRINTL 「こんだけチョーだい！」
		PRINTFORML 番台の妖怪は、すぐ側に置かれていたメモのようなものを指差し、はよ！はよ！と%CALLNAME:PLAYER%を急き立ててくる……
		$SKIP
		DRAWLINE
		PRINTL [1] \1000払って入浴する
		;にゅーぷらん選択肢
		SIF FLAG:周回数
			PRINTFORML [2] \\10000払って%\@ STRCOUNT(CSTR:0:0,"泡入浴") ? お風呂を借りる # 『にゅーぷらん』を試してみる \@%
		PRINTL [0] やめる
		$LOOP_titei
		INPUT
		SELECTCASE RESULT
			CASE 0
				;番台ブチギレ
				IF TFLAG:211 >= 2
					PRINTL 「んがー！！　んがー！！　んがー！！」
					PRINTL 「金を落としゃん奴に構ってるヒマはにゃーだぎゃー！！」
					TFLAG:211 = 4
				ELSE
					PRINTL 「んあー、入らないんだったらしゃっしゃとどっか行くぎゃー！」
					PRINTL 「金にならん奴はお客しゃまでもなんでもにゃいぎゃー！」
					TFLAG:211 = 2
				ENDIF
				RETURN -1
			CASE 1
				IF MONEY < 1000
					PRINTW  番台の妖怪は一生懸命、金額が合っているかどうか数えながら確かめている……
					PRINTW 「…………」
					PRINTW 「おみゃーしゃん、ヒドいギャー！　金足りねーのによくもみゃー図々しく払うにゃんて言えたもんだぎゃ！」
					;番台ブチギレ
					IF TFLAG:211 >= 2
						PRINTL 「んがー！！　んがー！！　んがー！！」
						PRINTL 「金を落としゃん奴に構ってるヒマはにゃーだぎゃー！！」
						TFLAG:211 = 4
					ELSE
						PRINTL 「おみゃーしゃん、こぎゃな所まで来て、いってーにゃにしに来ただぎゃー！」
						PRINTL 「ぎゃーをからかいに来ただきゃー？！」
						TFLAG:211 = 3
					ENDIF
					RETURN -1
				ELSE
					MONEY -= 1000
					PRINTW  番台の妖怪は一生懸命、金額が合っているかどうか数えながら確かめている……
					PRINTL 「うんぎゃ、ゆーっくりしてくといいぎゃー！」
					IF TARGET > 0 && CFLAG:TARGET:6 > 0 && !CFLAG:TARGET:睡眠 && TALENT:TARGET:性別 != 2
						PRINTL （げっへっへ、メスっこも一緒に連れて来るにゃんて、いい奴だぎゃー）
						PRINTL （ここは ぎゃーのすーぱーてくにっくを使って、思う存分あのメスっこを視姦させてもらうぎゃー！）
						PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%と一緒に脱衣所に向かった
						PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%から送られる視線に恥ずかしそうにしている……
						回復前体力２ = BASE:TARGET:体力
						回復前気力２ = BASE:TARGET:気力
						CALL ODEKAKE_OFURO(TARGET,2)
						IF CHK_DATENOW(CFLAG:TARGET:デート中)
							EXP:MASTER:デート経験 ++
							EXP:TARGET:デート経験 ++
						ENDIF
							CALL DATE_GOES_ON(1)
						SOURCE:TARGET:露出 += 300 + (ABL:TARGET:親密 * 15)
						回復後体力２ = BASE:TARGET:体力
						回復後気力２ = BASE:TARGET:気力
					ENDIF
					$OFURO
					CALL ODEKAKE_OFURO(MASTER,2)
					DRAWLINE
					PRINTW …
					PRINTW ……
					PRINTW …………
					IF TARGET > 0 && CFLAG:TARGET:6 > 0 && !CFLAG:TARGET:睡眠
						PRINTFORMW %CALLNAME:PLAYER%と%CALLNAME:TARGET%は二人仲良く温泉でくつろいだ
					ELSE
						PRINTFORMW %CALLNAME:PLAYER%は\@ LOCAL ? 一人寂しく個室の # \@温泉でくつろいだ
					ENDIF
					PRINTW …………
					PRINTW ……
					PRINTW …
					;旧地獄温泉は30分固定
					TIME += 30
					;旧地獄温泉総入湯回数はフラグごとオミット
					;旧地獄温泉時限入湯回数+1
					TFLAG:212 += 1
					;旧地獄温泉フラグを1に設定
					TFLAG:211 = 1
					PRINTL ここのお湯からもお酒がつくれるかもしれない……
					PRINTL 汲んで帰りますか？
					PRINTL [0] いいえ　[1] はい
					INPUT
					IF RESULT
						TFLAG:水汲み = 3
						PRINTFORML %STR:(7000 + TFLAG:水汲み)%を汲んだ
						IF MAIN_MAP != 9
							PRINTL 持ったまま歩くのはしんどいので帰宅します
							CALL 外出先から帰宅
						ENDIF
					ENDIF
					RETURN 1
				ENDIF
			CASE 2
				SIF !FLAG:周回数
					GOTO LOOP_titei
				IF FLAG:甲斐性無
					PRINTL 「おみゃーしゃん…手足も声も震えとるぎゃ」
					PRINTL 「やめとけやめとけ。こーんな意気地なし、メスっこも呆れてみゃうぎゃー」
					GOTO LOOP_titei
				ELSEIF FLAG:デート相手
					PRINTL 「おみゃーしゃん、今はやめたほーがいーんでにゃーぎゃ？」
					PRINTW 「隣の娘っこ、えっれぇー顔してにらんでくるぎゃ…」
					GOTO LOOP_titei
				ELSEIF GETBIT(TFLAG:一日一回, 9)
					PRINTL 「……おんやぁ？」
					PRINTW 「わりぃーな、おみゃーしゃん。いみゃ部屋はいっぴゃーだぎゃ」
					GOTO LOOP_titei
				ELSEIF (TARGET > 0 && CFLAG:TARGET:態度 > 0 && !CFLAG:TARGET:睡眠) || CFLAG:MASTER:同行
					PRINTL 「わりぃーな、おみゃーしゃん」
					PRINTL 「このぷらんは、ひとりよーなんだぎゃ！」
					GOTO LOOP_titei
				ENDIF
				CALL INVITE_NEWPLAN
				SIF RESULT < 2
					RETURN RESULT
				LOCAL = 1
				GOTO OFURO
			CASEELSE
				GOTO LOOP_titei
		ENDSELECT
	ENDIF
ENDIF
回復後体力１ = BASE:MASTER:体力
回復後気力１ = BASE:MASTER:気力
DRAWLINE
PRINTFORML 体力+{ABS(回復後体力１ - 回復前体力１)}（%CALLNAME:MASTER%）
PRINTFORML 気力+{ABS(回復後気力１ - 回復前気力１)}（%CALLNAME:MASTER%）
IF TARGET
	PRINTFORML 体力+{ABS(回復後体力２ - 回復前体力２)}（%CALLNAME:TARGET%）
	PRINTFORML 気力+{ABS(回復後気力２ - 回復前気力２)}（%CALLNAME:TARGET%）
ENDIF
RETURN 1

;銭湯
@COM_ABLE432
;移動実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(432)
	RETURN RESULT
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;添い寝中
SIF CFLAG:添い寝中
	RETURN 0
;お風呂のある場所以外は不可
SIF !PLACE_SENTO(CFLAG:MASTER:現在位置)
	RETURN 0
;物陰
SIF FLAG:潜伏
	RETURN 0
SIF CFLAG:うふふ == 2
	RETURN 0
;貸し切っているなら必要はない
SIF 銭湯貸切(DAY)
	RETURN 0
RETURN 1

@PLACE_SENTO(ARG)
#FUNCTION
;人里
IF ARG == GET_MAP_REPLACEMENT(223)
	RETURNF 2
;地底
ELSEIF ARG == GET_MAP_REPLACEMENT(934)
	RETURNF 9
ELSE
	RETURNF 0
ENDIF

@ODEKAKE_OFURO(ARG,ARG:1)
;ARG=1人里　ARG=2地底 =3プール
RESET_STAIN ARG
STAIN:ARG:4 = 0
CFLAG:ARG:膣内射精 = 0
CFLAG:ARG:アナル射精 = 0
CFLAG:ARG:口に精液 = 0
CFLAG:ARG:顔に精液 = 0
CFLAG:ARG:手に精液 = 0
CFLAG:ARG:風呂 = MIN(CFLAG:ARG:風呂 - 1000,0)
SELECTCASE ARG:1
	CASE 1
		CALL RECOVER_PERMIL(ARG,100,"体力",1)
		CALL RECOVER_PERMIL(ARG,250,"気力",1)
	CASE 2
		CALL RECOVER_PERMIL(ARG,150,"体力",1)
		CALL RECOVER_PERMIL(ARG,400,"気力",1)
	CASE 3
		CALL RECOVER_PERMIL(ARG,60,"体力",1)
		CALL RECOVER_PERMIL(ARG,150,"気力",1)
ENDSELECT
SIF ARG == MASTER
	EX:MASTER:前回の入浴時刻 = DAY * 1440 + TIME
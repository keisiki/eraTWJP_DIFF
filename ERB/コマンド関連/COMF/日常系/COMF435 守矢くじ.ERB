;-------------------------------------------------
;守矢くじ
;日常系コマンド、レベル1
;-------------------------------------------------
;守矢ガｔ……くじ
;LOCAL
@COM435
#DIM ガチャ回数
#DIM カリスマ払い
#DIMS 台詞
#DIMS 表情
VARSET LOCAL
ガチャ回数 = 0
IF !BETWEENTIME(600,1200)
	PRINTFORML 閉まっている……
	PRINTFORMW 営業時間外のようだ
	RETURN -1
ENDIF
IF !GETBIT(FLAG:999, 2)
	PRINTFORMW 境内の片隅に張り紙があった
	PRINTFORML ┌─────────────┐
	PRINTFORML ｜　　　　　　　　　　　　　｜
	PRINTFORML ｜　　守矢くじ　　　　　　　｜
	PRINTFORML ｜　　　　　絶賛販売中！　　｜
	PRINTFORML ｜　　　　　　　　　　　　　｜
	PRINTFORML ｜　　　　　　　　　　　　　｜
	PRINTFORML ｜　　　　　　　　　　　　　｜
	PRINTFORML ｜　　ご利益は保証します　　｜
	PRINTFORML ｜　　　　　　　　　　　　　｜
	PRINTFORML ｜　　　　　　　　守矢神社　｜
	PRINTFORML └─────────────┘
	PRINTFORMW 
	PRINTFORML 手伝いに来ているであろう、巫女装束（袴は緑、当然ですな）の少女がにこやかな笑顔でこちらを見ている
	PRINTFORMW そして、少女の横には「お布施箱」と書かれた箱が置かれている
	PRINTFORML …
	PRINTFORML ……
	PRINTFORMW なんだ、この嫌な予感しかしないほどに嫌な予感は…

	SETBIT FLAG:999, 2
	;最初のくじのセットを作成
	CALL FISHER_YATES_SHAFFLE(300)
	ARRAYCOPY "SHAFFLE_ARRAY", "MORIYA_BOX"
ENDIF

IF FLAG:守矢くじデート相手
	CALL SPTALK, 0, @"通常", 15, @"「くじなんて引いてる場合じゃないでしょう」/「神様とのデート権ですよ、一分一秒だって無駄にしちゃいけませんよ！」", 1
	RETURN -1
ENDIF
IF FLAG:守矢デート権 && !FLAG:デート相手
	CALL ASK_YN(@"%CALLNAME:(FLAG:守矢デート権)%とのデート権を行使", "くじを引く")
	IF !RESULT
		CALL DATE_RIGHT
		TIME += 3
		RETURN 1
	ENDIF
ENDIF
IF MONEY < 3000 && MONEY:2 < 20
	PRINTFORMW 財布を確認したらお金が足りなかった
	PRINTFORMW さぁ、お布施のためにお金を集めてくるのです
	RETURN -1
ENDIF

CALL SPTALK, 0, @"笑顔", 15, @"「守矢くじは1回3000円か20カリスマ、もしくは守矢の守札5枚です」", 1
PRINTL どうしますか？
CALL ASK_M("危険が危なそうなのでやめておく",1,"\\3000で引いてみる", MONEY >= 3000, "20CMで引いてみる", MONEY:2 >= 20, "守札5枚で引く", ITEM:131>= 5, "\\27000で10連ガチャ", MONEY >= 27000, "180CMで10連ガチャ", MONEY:2 >= 180, "守札50枚で10連ガチャ", ITEM:131 >= 50)
SELECTCASE RESULT
	CASE 0
		IF 顔絵付きメッセージ
			IF !RAND:8
				台詞 = 「えー、ひやかしですか」/「……祟られますよ？」
				表情 = 怒り
			ELSE
				台詞 = 「%TEXTR("……危なくないですよ？/ご利益あるんですよー？/うーん、次はお願いしますね")%」
				表情 = 通常
			ENDIF
			CALL SPTALK, 0, @"%表情%", 15, @"%台詞%", 1
		ENDIF
		RETURN -1
	CASE 1
		MONEY -= 3000
	CASE 2
		MONEY:2 -= 20
	CASE 3
		ITEM:131 -= 5
	CASE 4
		ガチャ回数 = 9
		MONEY -= 27000
	CASE 5
		ガチャ回数 = 9
		MONEY:2 -= 180
	CASE 6
		ガチャ回数 = 9
		ITEM:131 -= 50
ENDSELECT

IF (RESULT == 1 || RESULT == 2) && ITEM:兎符「開運大紋」
	PRINTFORML 兎符「開運大紋」を使う？
	CALL ASK_YN
	IF !RESULT
		ITEM:兎符「開運大紋」 --
		TFLAG:194 = 5
	ENDIF
ENDIF

$RENZOKU

IF GETBIT(FLAG:999, 3)
	IF MORIYA_BOX:0 == -1
		PRINTFORMW 現在のくじ箱を引ききったので新しい箱を用意します
		CALL FISHER_YATES_SHAFFLE(300)
		ARRAYCOPY "SHAFFLE_ARRAY", "MORIYA_BOX"
	ELSE
		PRINTFORML このくじ箱からは既に特賞が出ています
		PRINTFORML このくじ箱を廃棄してもらい新しいくじを引きますか？
		PRINTFORML [1] 次の箱を出してもらう
		PRINTFORML [0] まだこの箱を引き続ける
		$LOOP_INPUT_2
		INPUT
		SELECTCASE RESULT
			CASE 0
				PRINTW では、この箱から引きます
			CASE 1
				PRINTW では交換しますね
				CLEARBIT FLAG:999, 3
				CALL FISHER_YATES_SHAFFLE(300)
				ARRAYCOPY "SHAFFLE_ARRAY", "MORIYA_BOX"
			CASEELSE
				GOTO LOOP_INPUT_2
		ENDSELECT
	ENDIF
ENDIF

PRINTL お金をお布施箱に収め、
PRINTL 箱のなかからくじを１枚引き、
PRINTW 受付の子に手渡した。
PRINTL ……
PRINTL …
CALL FOTUNE_MORIYA
TIME += 2
IF ガチャ回数 > 0
	PRINTFORMW 
	ガチャ回数 --
	GOTO RENZOKU
ENDIF

SIF 顔絵付きメッセージ
	CALL SPTALK, 0, @"笑顔", 15, @"「ありがとうございましたー」", 1
RETURN 1

;守谷くじ
@COM_ABLE435
;一括管理
SIF GLOBAL_COMABLE(435)
	RETURN RESULT
SIF !TFLAG:100
	RETURN 0
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;気力0では不可
SIF BASE:MASTER:気力 <= 0
	RETURN 0
SIF !MORIYA_OMIKUJI(CFLAG:MASTER:現在位置)
	RETURN 0
RETURN 1

@MORIYA_OMIKUJI(ARG)
#FUNCTION
;守矢神社の社務所
SIF ARG == GET_MAP_REPLACEMENT(813)
	RETURNF 1
RETURNF 0

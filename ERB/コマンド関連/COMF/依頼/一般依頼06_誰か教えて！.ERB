;存否関数
;F関数はTRYCALL系が使えないので通常関数で存否配列を作っちゃう
@EXIST_一般依頼6
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
;ここでARGで渡されるのはCLIENT_IDです
@一般依頼6(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "依頼名"
	CALLF MAKE_STR(V_NAME, "誰か教えて！")
CASE "依頼内容表示"
	CALLF MAKE_STR(V_NAME, @"%CALLNAME:ARG%のギモンに答える")
CASE "依頼報酬表示"
	CALLF MAKE_STR(V_NAME, @"\\500")
CASE "依頼実行コマンド名"
	IF IS_LIBRARY(CFLAG:MASTER:現在位置)
		CALLF MAKE_STR(V_NAME, "しらべもの")
	ELSE
		CALLF MAKE_STR(V_NAME, "教えてあげる")
	ENDIF
CASE "依頼内容"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(0, "その他"))
CASE "依頼サブ"
	CALLF MAKE_INT(V_NAME, (ABL:ARG:教養 + RAND(1, 10)) * 10)
CASE "依頼期限"
;発生時ではなく受託時に確定します
CASE "依頼発生率"
;これは他の一般依頼との相対確率なんで10％ではありません
	CALLF MAKE_INT(V_NAME, 10)
CASE "依頼発生条件"
	SIF ABL:ARG:教養 <= ABL:MASTER:教養
		CALLF MAKE_INT(V_NAME, 1)
CASE "依頼受託可能条件"
	CALLF MAKE_INT(V_NAME, 1)
CASE "依頼実行可能条件"
	;しらべものはいつでも
	IF FLAG:70
	ELSEIF IS_LIBRARY(CFLAG:MASTER:現在位置)
		CALLF MAKE_INT(V_NAME, 1)
	;教えるのはCLIENTをTARGETにする必要がある
	ELSEIF TARGET == ARG && SHIRAHU(ARG)
		CALLF MAKE_INT(V_NAME, 1)
	ENDIF
;成功/失敗処理はCARRYOUTで直接書く
;SOURCEやITEM入手、消費が煩雑になるので
CASE "依頼消滅条件"
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
CASE "依頼人連れ出し可能"
	CALLF MAKE_INT(V_NAME, 1)
ENDSELECT



@IRAI_一般6(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
#DIM  LIBRARY_ID
#DIMS LIBRARY_NAME
#DIM  CHK_SLOT
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
CHK_SLOT  = SEARCH_IRAI_SLOT(IRAI_ID)
SELECTCASE SCENE
CASE "依頼条件表示"
PRINTFORML 　　理解度：%IRAI_一般6_DIFFICULTY(CHK_SLOT)%
;何か専用の表示情報を設けたい時用
;受託済みの依頼表示で使用されます
;
;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも無しで
;ここ以外の表示項目は
;PRINTFORML 　　　　　：
;となっているのでスペースを揃えたほうが見栄えが良いです

CASE "依頼実行時"
;依頼実行時にはRESULTが意味を持ちます
;このCASEに限り、RESULTが意味を持ちます
;依頼実行時のRESULTにはIRAI_CONST_RESULT()を使用してください
;可能な引数は"キャンセル", "実行して成功", "実行して失敗", "実行して未解決"です
;独自処理を仕込みたい時は"実行して未解決"で先延ばしするなりしてください
	LIBRARY_ID = IS_LIBRARY(CFLAG:MASTER:現在位置)
	IF LIBRARY_ID
		IF INRANGE(LIBRARY_ID, 1, 99)
			LIBRARY_NAME '= STR:LIBRARY_ID
		ELSE
			LIBRARY_NAME '= STR:(8000 + LIBRARY_ID)
		ENDIF
		IF TARGET == CLIENT_ID
			PRINTFORML %CALLNAME:MASTER%は%CALLNAME:TARGET%の疑問に答えるために%LIBRARY_NAME%を訪れた
			PRINTFORML ここなら役に立つ資料が見つかりそうだ
			CALL ASK_M("やめておく", 1, "しらべもの", 1, "回答してみる", 1)
			SELECTCASE RESULT
			CASE 0
				RETURN IRAI_CONST_RESULT("キャンセル")
			CASE 1
				PRINTFORMW %CALLNAME:MASTER%と%CALLNAME:TARGET%は%LIBRARY_NAME%で調べ物をした
				EXP:MASTER:学習経験 ++
				EXP:TARGET:学習経験 ++
				IRAI_SUB:CHK_SLOT -= ABL:MASTER:教養 + ABL:TARGET:教養 + 5
				IRAI_SUB:CHK_SLOT = MAX(IRAI_SUB:CHK_SLOT, 1)
				TIME += 30
			CASE 2
				GOTO ANSWER
			ENDSELECT
		ELSE
			PRINTFORML %CALLNAME:MASTER%は%LIBRARY_NAME%で調べ物をした
			EXP:MASTER:学習経験 ++
			IRAI_SUB:CHK_SLOT -= ABL:MASTER:教養 + 3
			IRAI_SUB:CHK_SLOT = MAX(IRAI_SUB:CHK_SLOT, 1)
		ENDIF
		PRINTFORML 現在のあなたの理解度
		PRINTFORMW 　%IRAI_一般6_DIFFICULTY(CHK_SLOT)%
		RETURN IRAI_CONST_RESULT("実行して未解決")
	ELSE
		$ANSWER
		PRINTFORML %CALLNAME:MASTER%は%CALLNAME:TARGET%の疑問に答えた
		IF IRAI_一般6_SUCCESS(CHK_SLOT)
			PRINTFORMW %CALLNAME:TARGET%の疑問は氷解した！
			SOURCE:歓楽 = 300
			EXP:MASTER:学習経験 += ABL:TARGET:教養 * 5
			EXP:TARGET:学習経験 += ABL:TARGET:教養 * 5
			IF ITEM:103
				EXP:MASTER:学習経験 ++
				EXP:TARGET:学習経験 ++
			ENDIF
			DOWNBASE:MASTER:体力 = 70
			DOWNBASE:MASTER:気力 = 100
			DOWNBASE:体力 = 50
			DOWNBASE:気力 = 70
			TIME += 30
			RETURN IRAI_CONST_RESULT("実行して成功")
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は納得できなかったようだ……
			RETURN IRAI_CONST_RESULT("実行して失敗")
		ENDIF
	ENDIF
CASE "依頼成功時"
;成功報告時の報酬など
	CALL CHANGE_CFLAG(2, TARGET, 50)
	CALL CHANGE_CFLAG(4, TARGET, 30)
	CALL KIGEN_CHANGE(TARGET,100,1)
	CALL GET_MONEY_YEN(500, 2)
CASE "依頼失敗時"
;失敗報告時の処理
	CALL CHANGE_CFLAG(2, TARGET, -10)
	CALL CHANGE_CFLAG(4, TARGET, -10)
ENDSELECT

@IRAI_一般6_SUCCESS(CHK_SLOT)
#FUNCTION
#DIM CHK_SLOT
SIF RAND:MAX(1, IRAI_SUB:CHK_SLOT) <= (1 + ABL:MASTER:教養) * 20
	RETURNF 1


@IRAI_一般6_DIFFICULTY(CHK_SLOT)
#FUNCTIONS
#DIM CHK_SLOT
SIF IRAI_SUB:CHK_SLOT <= (1 + ABL:MASTER:教養) * 20
	RETURNF "完璧に理解させてみせる"
;難易度全体で差分をパーセント化したもの
SELECTCASE (IRAI_SUB:CHK_SLOT - ((1 + ABL:MASTER:教養) * 20)) * 100 / IRAI_SUB:CHK_SLOT
CASE 0, 1, 2, 3
	RETURNF "うまく教えられそうだ"
CASE 4, 5, 6
	RETURNF "十分に教えられる"
CASE 7 TO 10
	RETURNF "答えられるだろう"
CASE 11 TO 33
	RETURNF "これで納得してくれないかな"
CASE 34 TO 50
	RETURNF "きっとどうにかなる、たぶん"
CASE 51 TO 70
	RETURNF "難しい質問だ"
CASEELSE
	RETURNF "まるで見当もつかない"
ENDSELECT



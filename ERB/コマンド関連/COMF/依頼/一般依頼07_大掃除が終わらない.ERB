;存否関数
;F関数はTRYCALL系が使えないので通常関数で存否配列を作っちゃう
@EXIST_一般依頼7
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
;ここでARGで渡されるのはCLIENT_IDです
@一般依頼7(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "依頼名"
	CALLF MAKE_STR(V_NAME, "大掃除が終わらない")
CASE "依頼内容表示"
	CALLF MAKE_STR(V_NAME, "大掃除のお手伝い")
CASE "依頼報酬表示"
	CALLF MAKE_STR(V_NAME, @"\\1500")
CASE "依頼実行コマンド名"
	IF YOGORE:(GET_CHARAHOME(ARG)) < 1000
		CALLF MAKE_STR(V_NAME, "掃除できた")
	ELSE
		CALLF MAKE_STR(V_NAME, "掃除をする")
	ENDIF

CASE "依頼内容"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(0, "作業"))
CASE "依頼サブ"
CASE "依頼期限"
;発生時ではなく受託時に確定します
CASE "依頼発生率"
	CALLF MAKE_INT(V_NAME, 80)
CASE "依頼発生条件"
	IF MAIN_MAP != GET_MAPID(GET_CHARAHOME(ARG))
	ELSEIF !(DAY:2 == 4 && INRANGE(DAY:3, 15, 31))
	ELSEIF ABL:ARG:親密 >= 3
		CALLF MAKE_INT(V_NAME, 1)
	ENDIF
CASE "依頼受託可能条件"
;その依頼者の自宅近辺にいるとき
;（基本条件としてまず依頼者とあなたが同一地点にいるはずなのでこれでよい）
	IF MAIN_MAP != GET_MAPID(GET_CHARAHOME(ARG))
	ELSEIF CFLAG:ARG:宴会参加
	ELSEIF YOGORE:(GET_CHARAHOME(ARG)) < 1000
	ELSEIF GET_MAPID(CFLAG:MASTER:現在位置) == MAIN_MAP
		CALLF MAKE_INT(V_NAME, 1)
	ENDIF
CASE "依頼実行可能条件"
;依頼者とTARGETが同一であり、その依頼者の自宅にいるとき
;（基本条件としてまず依頼者とあなたが同一地点にいるはずなのでこれでよい）
	IF MAIN_MAP != GET_MAPID(GET_CHARAHOME(ARG))
	ELSEIF CFLAG:ARG:宴会参加
	ELSEIF CFLAG:ARG:現在位置 == GET_CHARAHOME(ARG)
		CALLF MAKE_INT(V_NAME, 1)
	ENDIF
;成功/失敗処理はCARRYOUTで直接書く
;SOURCEやITEM入手、消費が煩雑になるので
CASE "依頼消滅条件"
	SIF !(DAY:2 == 4 && INRANGE(DAY:3, 15, 31))
		CALLF MAKE_INT(V_NAME, 1)
CASE "依頼人連れ出し可能"
	CALLF MAKE_INT(V_NAME, 1)
ENDSELECT



@IRAI_一般7(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
#DIM  SWEEP_ID
#DIM  SWEEP_POWER
#DIM  PREV_YOGORE
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
SELECTCASE SCENE
CASE "依頼条件表示"
;何か専用の表示情報を設けたい時用
;受託済みの依頼表示で使用されます
;
;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも無しで
;ここ以外の表示項目は
;PRINTFORML 　　　　　：
;となっているのでスペースを揃えたほうが見栄えが良いです

CASE "依頼実行時"
;このCASEに限り、RESULTが意味を持ちます
;依頼実行時のRESULTにはIRAI_CONST_RESULT()を使用してください
;可能な引数は"キャンセル", "実行して成功", "実行して失敗", "実行して未解決"です
;独自処理を仕込みたい時は"実行して未解決"で先延ばしするなりしてください
	IF YOGORE:(GET_CHARAHOME(TARGET)) < 1000
		PRINTFORMW %CALLNAME:TARGET%の部屋は十分にきれいだ
		TIME += 3
	ELSE
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:TARGET%と協力して徹底的に部屋を掃除した
		PRINTFORML 
		SWEEP_ID = CFLAG:MASTER:現在位置
		SWEEP_POWER = (2 + ABL:MASTER:清掃技能 + 3 * TALENT:MASTER:清掃中毒) * (5 - TALENT:MASTER:61) / 5
		PREV_YOGORE = YOGORE:SWEEP_ID
		;掃除効果さらに高め
		YOGORE:SWEEP_ID = YOGORE:SWEEP_ID / (SWEEP_POWER + 2 + ABL:清掃技能)
		SIF GROUPMATCH(CFLAG:MASTER:現在位置, 9, 10)
			YOGORE:(54) = 0
			;PRINTL ついでに虫かごも綺麗にした
			;PRINTFORML 針妙丸の私物をどけて逆さに振るだけだから簡単だ
		EX:MASTER:今日の掃除回数 += PREV_YOGORE - YOGORE:SWEEP_ID
		EXP:清掃経験 ++
		EXP:MASTER:清掃経験 ++

		DOWNBASE:MASTER:体力 = 50
		DOWNBASE:MASTER:気力 = 100
		DOWNBASE:気力 = 100
		TIME += 40
	ENDIF
	SOURCE:歓楽 = 500
	SOURCE:征服 = 200
	RETURN IRAI_CONST_RESULT("実行して成功")
CASE "依頼成功時"
;成功報告時の報酬など
	CALL CHANGE_CFLAG(2, TARGET, 30)
	CALL CHANGE_CFLAG(4, TARGET, 30)
	CALL GET_MONEY_YEN(1500, 2)
CASE "依頼失敗時"
;失敗報告時の処理
	CALL CHANGE_CFLAG(2, TARGET, -20)
	CALL CHANGE_CFLAG(4, TARGET, -10)
ENDSELECT

@固有依頼42(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE ARG % 1000
;ID101：グルメリポート
CASE 101
	SELECTCASE O_DATA
	CASE "依頼名"
		CALLF MAKE_STR(V_NAME, "グルメリポート")
	CASE "依頼内容表示"
		CALLF MAKE_STR(V_NAME, "人里の料理の感想をまとめる")
	CASE "依頼報酬表示"
		CALLF MAKE_STR(V_NAME, @"\\3000")
	CASE "依頼実行コマンド名"
		CALLF MAKE_STR(V_NAME, "食レポする")
		
	CASE "依頼内容"
		;小料理屋(218)が対象
		CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(218, "伝令（場所）"))
	CASE "依頼サブ"
	CASE "依頼期限"
	;発生時ではなく受託時に確定します
		CALLF MAKE_INT(V_NAME, 7)
	CASE "依頼発生率"
	;これは他の固有依頼との相対確率なんで10％ではありません
		CALLF MAKE_INT(V_NAME, 10)
	CASE "依頼発生条件"
	SIF CFLAG:1005 >= 1 && ABL:MASTER:教養 >= 1
		CALLF MAKE_INT(V_NAME, 1)
	CASE "依頼受託可能条件"
	;受託可能は@COM_ABLE366も確認のこと
	;ここを参照するのは依頼人をTARGETにしてる時なので素通しでよし
		CALLF MAKE_INT(V_NAME, 1)
	CASE "依頼実行可能条件"
	;実行可能は@COM_ABLE367も確認のこと
		IF FLAG:70
		ELSE
			CALLF MAKE_INT(V_NAME, 1)
		ENDIF
	;成功/失敗処理はCARRYOUTで直接書く
	;SOURCEやITEM入手など、内容が複雑なので
	CASE "依頼消滅条件"
	;30％で消失
		SIF RAND:100 < 30
			CALLF MAKE_INT(V_NAME, 1)
	ENDSELECT
ENDSELECT



;固有依頼の設定用
;この関数は実行/成功/失敗時の
;報酬とペナルティと地の文のみを扱います
;各キャラのメッセージは別所にて
@IRAI_固有42(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
SELECTCASE STR_DATA_IRAI(IRAI_ID, "依頼名", CLIENT_ID)
	
CASE "グルメリポート"
	SELECTCASE SCENE
	CASE "依頼条件表示"
	;何か専用の表示情報を設けたい時用
	;受託済みの依頼表示で使用されます
	;
	;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも無しで
	;ここ以外の表示項目は
	;PRINTFORML 　　　　　：
	;となっているのでスペースを揃えたほうが見栄えが良いです
	
	CASE "依頼実行時"
	;このCASEに限り、RESULTが意味を持ちます
	;依頼実行時のRESULTにはIRAI_CONST_RESULT()を使用してください
	;可能な引数は"キャンセル", "実行して成功", "実行して失敗", "実行して未解決"です
	;独自処理を仕込みたい時は"実行して未解決"で先延ばしするなりしてください
		DOWNBASE:MASTER:気力 += 50
		PRINTFORMW %CALLNAME:MASTER%は小料理屋に入り、店主に断りを入れて料理の写真を撮ると、食べながら感想をまとめた…
		TIME += 120
		IF (ABL:話術技能 + ABL:教養 + ABL:料理技能 + RAND:5) >= RAND:12
			PRINTFORML いい写真だ。食欲をそそるような文章も書けた。これならはたても納得してくれるだろう
			PRINTW 
			RETURN IRAI_CONST_RESULT("実行して成功")
		ELSEIF (ABL:話術技能 + ABL:教養 + ABL:料理技能 + RAND:5) == RAND:11
			SELECTCASE RAND:2
			CASE 0
				PRINTFORML 文章はまずまずだが、写真がいまいち美味しそうに撮れていない…妥協はしたくない
				PRINTW 
			CASE 1
				PRINTFORML 写真はよく撮れたと思うが、文章が料理の魅力を伝えきれてない気がする…
				PRINTW 
			ENDSELECT
			RETURN IRAI_CONST_RESULT("実行して未解決")
		ELSE
			PRINTFORML 写真もボケボケなら、文章も何が言いたいのか訳が分からないものになってしまった…これは駄目だ
			PRINTFORML 仕方ないが、これで報告するしかないだろう
			PRINTW 
			RETURN IRAI_CONST_RESULT("実行して失敗")
		ENDIF
	CASE "依頼成功時"
	;成功報告時の報酬など
		CALL KIGEN_CHANGE(TARGET, 100, 1)
		SOURCE:TARGET:歓楽 = 1000
		SOURCE:TARGET:征服 = 500
		SOURCE:TARGET:達成 = 500
		;システムメッセージつき好感度変化
		;最後の引数だけ好感度が変化
		CALL CHANGE_CFLAG(2, TARGET, 40)
		;システムメッセージつき信頼度変化
		;最後の引数だけ信頼度が変化
		CALL CHANGE_CFLAG(4, TARGET, 25)
		CALL GET_MONEY_YEN(3000, 2)
	CASE "依頼失敗時"
	;失敗報告時の処理
		;システムメッセージつき好感度変化
		;最後の引数だけ好感度が変化
		CALL CHANGE_CFLAG(2, TARGET, -20)
		;システムメッセージつき信頼度変化
		;最後の引数だけ信頼度が変化
		CALL CHANGE_CFLAG(4, TARGET, -10)
	ENDSELECT
ENDSELECT

;存否関数
;F関数はTRYCALL系が使えないので通常関数で存否配列を作っちゃう
@EXIST_一般依頼12
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
;ここでARGで渡されるのはCLIENT_IDです
@一般依頼12(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "依頼名"
	CALLF MAKE_STR(V_NAME, "懲らしめて！")
CASE "依頼内容表示"
	CALLF MAKE_STR(V_NAME, @"討伐対象を弾幕勝負で負かす")
CASE "依頼報酬表示"
	CALLF MAKE_STR(V_NAME, @"賞金（時価）")
CASE "依頼実行コマンド名"
	CALLF MAKE_STR(V_NAME, "懲らしめる")
CASE "依頼内容"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(RAND_HANTING(ARG), "討伐"))
CASE "依頼サブ"
CASE "依頼期限"
;発生時ではなく受託時に確定します
	CALLF MAKE_INT(V_NAME, 10)
CASE "依頼発生率"
;これは他の一般依頼との相対確率なんで10％ではありません
	CALLF MAKE_INT(V_NAME, 2)
CASE "依頼発生条件"
;CSVの戦闘能力が1以上
	SIF CSVABL(ARG, GETNUM(ABL, "戦闘能力")) >= 1
		CALLF MAKE_INT(V_NAME, 1)
CASE "依頼受託可能条件"
;あなたの戦闘能力が1以上
	SIF ABL:MASTER:戦闘能力 >= 1
		CALLF MAKE_INT(V_NAME, 1)
CASE "依頼実行可能条件"
	CALLF MAKE_INT(V_NAME, 1)
;成功/失敗処理はCARRYOUTで直接書く
;SOURCEやITEM入手、消費が煩雑になるので
CASE "依頼消滅条件"
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
CASE "依頼人連れ出し可能"
ENDSELECT



@IRAI_一般12(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
#DIM  CHK_SLOT
#DIM  OPPONENT_REMAIN
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
CHK_SLOT  = SEARCH_IRAI_SLOT(IRAI_ID)
SELECTCASE SCENE
CASE "依頼条件表示"
;何か専用の表示情報を設けたい時用
;受託済みの依頼表示で使用されます
;
;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも無しで
;ここ以外の表示項目は
;PRINTFORML 　　　　　：
;となっているのでスペースを揃えたほうが見栄えが良いです

CASE "依頼実行時"
;このCASEに限り、RESULTが意味を持ちます
;依頼実行時のRESULTにはIRAI_CONST_RESULT()を使用してください
;可能な引数は"キャンセル", "実行して成功", "実行して失敗", "実行して未解決"です
;独自処理を仕込みたい時は"実行して未解決"で先延ばしするなりしてください
;
;
	;実行できないパターン
	;拠点では屋内だとダメ（原作だとめっちゃやってるけど）
	IF CANNOT_DANMAKU() == 1
		PRINTFORMW 屋外で挑もう…
		RETURN IRAI_CONST_RESULT("実行して未解決")
	;仕事中はダメ
	ELSEIF CANNOT_DANMAKU() == 2
		PRINTFORMW お取り込み中だ…
		RETURN IRAI_CONST_RESULT("実行して未解決")
	;あなたが疲労
	ELSEIF CANNOT_DANMAKU() == 3
		PRINTFORMW 今は疲れている…
		RETURN IRAI_CONST_RESULT("実行して未解決")
	;対象が疲労
	ELSEIF CANNOT_DANMAKU() == 4
		PRINTFORMW 今挑むのはフェアじゃない…
		RETURN IRAI_CONST_RESULT("実行して未解決")
	;既に挑んでる
	ELSEIF CANNOT_DANMAKU() == 5
		PRINTFORMW 今日はもう挑めない…
		RETURN IRAI_CONST_RESULT("実行して未解決")
	ELSE
		;ほぼ弾幕勝負のコピペ
		SETCOLOR C_GREEN
		PRINTFORML ＜討伐戦＞
		RESETCOLOR
		PRINTFORMW 勝負開始！！
		CALL DANMAKU_BATTLE(TARGET)
		OPPONENT_REMAIN = RESULT
		CALL KOJO_MESSAGE_SEND("DANMAKU", OPPONENT_REMAIN, TARGET, -1, -1, "戦闘後")
		PRINTL 
		TCVAR:TARGET:弾幕勝負不能 = 1
		SIF OPPONENT_REMAIN == 0
			CFLAG:TARGET:弾幕勝負勝利 = 1
		DOWNBASE:気力 += 500
		DOWNBASE:MASTER:気力 += 500
		DOWNBASE:体力 += 500
		DOWNBASE:MASTER:体力 += 500
		SOURCE:歓楽 += (3000 + ABL:親密 * 100) / (OPPONENT_REMAIN + 1)
		TFLAG:好感度ボーナス = 1600 / (OPPONENT_REMAIN + 1)
		TFLAG:信頼度管理 = 3 - LOCAL

		EXP:MASTER:戦闘経験 += 3

		TIME += 60

		SIF AT_HOME(MASTER)
			YOGORE:(CFLAG:MASTER:現在位置) = LIMIT(YOGORE:(CFLAG:MASTER:現在位置) + 3000, 0, 6000)
		IF OPPONENT_REMAIN == 0
			PRINTL 
			SETCOLOR C_YELLOW
			PRINTFORMW %CALLNAME:TARGET%を懲らしめた！
			RESETCOLOR
			RETURN IRAI_CONST_RESULT("実行して成功")
		ELSE
			SETCOLOR C_GREEN
			PRINTFORML %CALLNAME:TARGET%の討伐に失敗した……
			PRINTFORMW ……こうなったら後日出直すしかない
			RESETCOLOR
			RETURN IRAI_CONST_RESULT("実行して未解決")
		ENDIF
	ENDIF
CASE "依頼成功時"
;成功報告時の報酬など
	SELECTCASE ABL:(IRAI_SLOT_TO_PERSON(CHK_SLOT)):戦闘能力
	CASE IS <= 1
		SOURCE:歓楽 = 1000
		CALL CHANGE_CFLAG(2, TARGET, 10 + RAND:5)
		CALL CHANGE_CFLAG(4, TARGET, 15 + RAND:5)
		CALL GET_SPELL_CARD_BONUS_YEN(1000, 2)
	CASE 2
		SOURCE:歓楽 = 1500
		CALL CHANGE_CFLAG(2, TARGET, 15 + RAND:5)
		CALL CHANGE_CFLAG(4, TARGET, 20 + RAND:10)
		CALL GET_SPELL_CARD_BONUS_YEN(2000, 2)
	CASE 3
		SOURCE:歓楽 = 2000
		CALL CHANGE_CFLAG(2, TARGET, 20 + RAND:10)
		CALL CHANGE_CFLAG(4, TARGET, 25 + RAND:10)
		CALL GET_SPELL_CARD_BONUS_YEN(5000, 2)
	CASE 4
		SOURCE:歓楽 = 2500
		CALL CHANGE_CFLAG(2, TARGET, 25 + RAND:10)
		CALL CHANGE_CFLAG(4, TARGET, 30 + RAND:10)
		CALL GET_SPELL_CARD_BONUS_YEN(10000, 2)
	CASE 5
		SOURCE:歓楽 = 3000
		CALL CHANGE_CFLAG(2, TARGET, 30 + RAND:20)
		CALL CHANGE_CFLAG(4, TARGET, 35 + RAND:20)
		CALL GET_SPELL_CARD_BONUS_YEN(15000, 2)
	CASEELSE
		SOURCE:歓楽 = 4000
		CALL CHANGE_CFLAG(2, TARGET, 40 + RAND:20)
		CALL CHANGE_CFLAG(4, TARGET, 40 + RAND:20)
		CALL GET_SPELL_CARD_BONUS_YEN(30000, 2)
	ENDSELECT
CASE "依頼失敗時"
;失敗報告時の処理
;過酷な依頼なので少しだけ
	PRINTFORML BoNus FAilEd...
	CALL CHANGE_CFLAG(2, TARGET, -3)
	CALL CHANGE_CFLAG(4, TARGET, -2)
ENDSELECT

;依頼での弾幕勝負ができない場合は真
@CANNOT_DANMAKU
#FUNCTION
;拠点では屋内だとダメ（原作だとめっちゃやってるけど）
IF AT_HOME(MASTER) && !OUTROOF(CFLAG:MASTER:現在位置) && !戦闘訓練可(CFLAG:MASTER:現在位置)
	RETURNF 1
;仕事中はダメ
ELSEIF CFLAG:TARGET:行動 == 5
	RETURNF 2
;あなたが疲労
ELSEIF BASE:MASTER:気力 < 500 || BASE:MASTER:体力 < 500
	RETURNF 3
;対象が疲労
ELSEIF BASE:TARGET:気力 < 500 || BASE:TARGET:体力 < 500
	RETURNF 4
;既に挑んでる
ELSEIF TCVAR:TARGET:弾幕勝負不能
	RETURNF 5
ENDIF
RETURNF 0

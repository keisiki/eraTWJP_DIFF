;存否関数
;F関数はTRYCALL系が使えないので通常関数で存否配列を作っちゃう
@EXIST_一般依頼16
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
;ここでARGで渡されるのはCLIENT_IDです
@一般依頼16(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "依頼名"
	CALLF MAKE_STR(V_NAME, "悩み事を聞いて！")
CASE "依頼内容表示"
	CALLF MAKE_STR(V_NAME, @"悩み事を聞いてあげる")
CASE "依頼報酬表示"
	CALLF MAKE_STR(V_NAME, @"野菜か果物（解消できたら）")
CASE "依頼実行コマンド名"
	CALLF MAKE_STR(V_NAME, "お悩み相談")
CASE "依頼内容"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(0, "その他"))
CASE "依頼サブ"
CASE "依頼期限"
;発生時ではなく受託時に確定します
CASE "依頼発生率"
;これは他の一般依頼との相対確率なんで10％ではありません
	CALLF MAKE_INT(V_NAME, 5)
CASE "依頼発生条件"
	SIF ABL:MASTER:話術技能 >= 1
		CALLF MAKE_INT(V_NAME, 1)
CASE "依頼受託可能条件"
	CALLF MAKE_INT(V_NAME, 1)
CASE "依頼実行可能条件"
	SIF TARGET == ARG && SHIRAHU(ARG)
		CALLF MAKE_INT(V_NAME, 1)
;成功/失敗処理はCARRYOUTで直接書く
;SOURCEやITEM入手、消費が煩雑になるので
CASE "依頼消滅条件"
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
CASE "依頼人連れ出し可能"
ENDSELECT



@IRAI_一般16(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
#DIM 正解の選択肢
#DIM 逆張りナシ
#DIM お悩みナンバー
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
SELECTCASE SCENE
CASE "依頼条件表示"
;何か専用の表示情報を設けたい時用
;受託済みの依頼表示で使用されます
;
;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも無しで
;ここ以外の表示項目は
;PRINTFORML 　　　　　：
;となっているのでスペースを揃えたほうが見栄えが良いです

CASE "依頼実行時"
;このCASEに限り、RESULTが意味を持ちます
;依頼実行時のRESULTにはIRAI_CONST_RESULT()を使用してください
;可能な引数は"キャンセル", "実行して成功", "実行して失敗", "実行して未解決"です
;独自処理を仕込みたい時は"実行して未解決"で先延ばしするなりしてください
	VARSET RESULT
	;口上に委託するか判定
	CALL KOJO_MESSAGE_SEND("IRAI", TARGET, IRAI_ID, 0, 0, "TARGET", "口上委託率")
	;口上専用ルート
	IF RESULT:1 > RAND:100
		VARSET RESULT
		VARSET RESULTS
		CALL KOJO_MESSAGE_SEND("IRAI", TARGET, IRAI_ID, 0, 0, "TARGET", "口上専用")
	;汎用ルート
	ELSE
		RESULT = 0
		WHILE !RESULT
			;お悩みパターンを増やしたらここを調整（あえてちょっと多めにしてます）
			お悩みナンバー = RAND:26
			CALL IRAI_一般16_お悩みデータベース(お悩みナンバー, TARGET)
		WEND
	ENDIF
	正解の選択肢 = RESULT:1
	逆張りナシ = RESULT:2
	DRAWLINE
	[IF_DEBUG]
		PRINTFORML ｺﾀｴﾊ No.{正解の選択肢} ﾀﾞﾖ
	[ENDIF]
	PRINTFORML ～%CALLNAME:TARGET%の今日のお悩み～
	PRINTFORML 　　　『%RESULTS%』
	PRINTFORML [ 1] - %RESULTS:1%
	PRINTFORML [ 2] - %RESULTS:2%
	PRINTFORML [ 3] - %RESULTS:3%
	INPUT
	LOCAL = RESULT
	TIME += 10
	;正解の選択肢を選ぶ
	IF RESULT == 正解の選択肢
		;6分の5の確率でそのまま正解
		IF RAND:6 || 逆張りナシ
			CALL COLORMESSAGE(@"＜NICE!!＞", C_YELLOW, 0)
			PRINTFORML 【%RESULTS:LOCAL%】
			PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%の答えに満足した！
			TFLAG:193 = 1
			SOURCE:歓楽 = 500 + ABL:親密 * 50
			EXP:MASTER:会話経験 += 5
			TFLAG:好感度ボーナス = ABL:親密 * 5
			TFLAG:信頼度管理 = 2
			RETURN IRAI_CONST_RESULT("実行して成功")
		;6分の1の確率で逆にハズレ
		ELSE
			CALL COLORMESSAGE(@"＜SAD…＞", C_P_PURPLE, 0)
			PRINTFORML 【%RESULTS:LOCAL%】
			PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%のありきたりな返答にがっかりしている…
			SOURCE:歓楽 = 100 + ABL:親密 * 10
			SOURCE:反感 = 50
			EXP:MASTER:会話経験 += 1
			TFLAG:193 = -2
			RETURN IRAI_CONST_RESULT("実行して失敗")
		ENDIF
	;6分の1の確率で逆に正解
	ELSEIF !RAND:6 && !逆張りナシ
		CALL COLORMESSAGE(@"＜COOL!!＞", C_AQUA, 0)
		PRINTFORML 【%RESULTS:LOCAL%】
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%の意外な返答にときめいた！
		TFLAG:193 = 2
		SOURCE:歓楽 = 2000 + ABL:親密 * 100
		EXP:MASTER:会話経験 += 10
		TFLAG:好感度ボーナス = ABL:親密 * 10
		TFLAG:信頼度管理 = 5
		RETURN IRAI_CONST_RESULT("実行して成功")
	;ハズレの選択肢を選ぶ
	ELSE
		CALL COLORMESSAGE(@"＜BAD…＞", C_RED, 0)
		PRINTFORML 【%RESULTS:LOCAL%】
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%の答えに満足しなかった…
		SOURCE:歓楽 = 50 + ABL:親密 * 5
		SOURCE:反感 = 100
		TFLAG:193 = -1
		RETURN IRAI_CONST_RESULT("実行して失敗")
	ENDIF
CASE "依頼成功時"
;成功報告時の報酬など
	IF TFLAG:192 == 2
		CALL CHANGE_CFLAG(4, TARGET, 10 + ABL:親密 * 2)
		TALENT:機嫌 = MIN(TALENT:機嫌 + 1, 1)
		LOCAL:1 = 3
	ELSE
		CALL CHANGE_CFLAG(4, TARGET, 5 + ABL:親密)
		TALENT:機嫌 = MIN(TALENT:機嫌 + 1, 0)
		LOCAL:1 = 1
	ENDIF
	;野菜召喚の儀
	LOCALS = 
	WHILE LOCALS == ""
		LOCAL = RAND:40 + GETNUM(ITEM, "キャベツ")
		LOCALS = %ITEMNAME:LOCAL%
	WEND
	CALL GET_ITEM(LOCALS, LOCAL:1, 2)
CASE "依頼失敗時"
;失敗報告時の処理
	IF TFLAG:192 == -2
		CALL CHANGE_CFLAG(4, TARGET, -5)
	ELSE
		CALL CHANGE_CFLAG(4, TARGET, -1)
	ENDIF
ENDSELECT


;---------------------------------------
;お悩み相談のデータ
;ARG           お悩みナンバー
;RETURN        発生判定
;RESULT:1      正解の選択肢
;RESULT:2      逆張り正解／ハズレ判定の消去
;RESULTS:0     お悩み文章
;RESULTS:1～3  選択肢１～３
;---------------------------------------
@IRAI_一般16_お悩みデータベース(ARG, CHARA)
#DIM CHARA
VARSET RESULT
VARSET RESULTS
VARSET LOCAL
SELECTCASE ARG
CASE 1, 2
	;弾幕ごっこの悩み
	;もともとの戦闘能力がないなら除外
	SIF CSVABL(CHARA, GETNUM(ABL, "戦闘能力")) <= 0
		RETURN
	RESULTS:0 = 新しいスペルカードはどんなのがいい%性格傾向語尾(CHARA, "かな？")%
	IF ARG % 2 == 0
		RESULTS:1 = ばらまき弾幕
		RESULTS:2 = 足止め弾幕
		RESULTS:3 = 誘導弾幕
		SELECTCASE CFLAG:CHARA:性格傾向
		;気弱系
		CASE 1
			RESULT:1 = 3
		;強気系
		CASE 2
			RESULT:1 = 2
		;素直系
		CASE 3
			RESULT:1 = 1
		;真面目系
		CASE 4
			RESULT:1 = 3
		;その他
		CASEELSE
			RESULT:1 = 1
		ENDSELECT
	ELSE
		RESULTS:1 = 見栄え重視の美しい弾幕
		RESULTS:2 = 殺意剥き出しの鬼畜弾幕
		RESULTS:3 = 幻惑するような不思議弾幕
		SELECTCASE CFLAG:CHARA:性格傾向
		;気弱系
		CASE 1
			RESULT:1 = 1
		;強気系
		CASE 2
			RESULT:1 = 2
		;素直系
		CASE 3
			RESULT:1 = 2
		;真面目系
		CASE 4
			RESULT:1 = 1
		;その他
		CASEELSE
			RESULT:1 = 3
		ENDSELECT
	ENDIF
CASE 3, 4
	;晩酌の悩み
	;お酒が飲めないなら除外
	SIF TALENT:CHARA:酒耐性 <= 0
		RETURN
	IF ARG % 2 == 0
		RESULTS:0 = 今日の晩酌は何がいいと%性格傾向語尾(CHARA, "思う？")%
		RESULTS:1 = ビール
		RESULTS:2 = ワイン
		RESULTS:3 = 日本酒
		IF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "料理：好きな味"), "和食")
			RESULT:1 = 3
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "料理：好きな味"), "洋食")
			RESULT:1 = 2
		ELSE
			SELECTCASE CFLAG:CHARA:性格傾向
			;気弱系
			CASE 1
				RESULT:1 = 2
			;強気系
			CASE 2
				RESULT:1 = 3
			;素直系
			CASE 3
				RESULT:1 = 1
			;真面目系
			CASE 4
				RESULT:1 = 2
			;その他
			CASEELSE
				RESULT:1 = 1
			ENDSELECT
		ENDIF
	ELSE
		RESULTS:0 = 今日の晩酌は%性格傾向語尾(CHARA, "どうしよう？")%
		RESULTS:1 = 浴びるほどのビールで深酔い
		RESULTS:2 = ほんのり甘いリキュールでほろ酔い
		RESULTS:3 = 今日は禁酒！
		IF TALENT:CHARA:酒耐性 > 2
			RESULT:1 = 1
		ELSEIF TALENT:CHARA:酒耐性 < 1
			RESULT:1 = 2
		ELSE
			SELECTCASE CFLAG:CHARA:性格傾向
			;気弱系
			CASE 1
				RESULT:1 = 3
			;強気系
			CASE 2
				RESULT:1 = 1
			;素直系
			CASE 3
				RESULT:1 = 1
			;真面目系
			CASE 4
				RESULT:1 = 3
			;その他
			CASEELSE
				RESULT:1 = 2
			ENDSELECT
		ENDIF
	ENDIF
CASE 5, 6
	;本の悩み
	;本を読まないようなら除外
	SIF ABL:CHARA:教養 <= 0
		RETURN
	RESULTS:0 = 新しく本を読むならどれがいい%性格傾向語尾(CHARA, "かな？")%
	IF ARG % 2 == 0
		RESULTS:1 = 恋愛小説
		RESULTS:2 = 推理小説
		RESULTS:3 = SF小説
		IF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(壮大|不思議|マニアック|未来)")
			RESULT:1 = 3
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(威厳|知的|繊細|奇妙|レトロ)")
			RESULT:1 = 2
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(かわいい|ポップ|ロリータ)")
			RESULT:1 = 1
		ELSE
			RESULT:1 = RAND:3 + 1
		ENDIF
	ELSE
		RESULTS:1 = 流行りのライトノベル
		RESULTS:2 = 厳かな純文学
		RESULTS:3 = マニアックな奇書
		IF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(マニアック|知的|奇妙)")
			RESULT:1 = 3
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(威厳|レトロ|シック)")
			RESULT:1 = 2
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(かわいい|ポップ|ロリータ)")
			RESULT:1 = 1
		ELSE
			RESULT:1 = RAND:3 + 1
		ENDIF
	ENDIF
CASE 7, 8
	;香料の悩み
	;条件なし
	RESULTS:0 = 部屋に香料を置くならどれがいい%性格傾向語尾(CHARA, "かな？")%
	IF ARG % 2 == 0
		RESULTS:1 = フレッシュ系
		RESULTS:2 = メロウ系
		RESULTS:3 = ディープ系
		IF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(妖術|不思議|威厳)")
			RESULT:1 = 2
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(芸術的|オシャレ|繊細)")
			RESULT:1 = 3
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(自然|シンプル)")
			RESULT:1 = 1
		ELSE
			RESULT:1 = RAND:3 + 1
		ENDIF
	ELSE
		RESULTS:1 = シンプルな消臭剤
		RESULTS:2 = アロマオイル
		RESULTS:3 = %CALLNAME:CHARA%の匂いでいい
		IF TALENT:CHARA:汚臭耐性 < 0
			RESULT:1 = 1
		ELSEIF GROUPMATCH(GET_TRIBENAME(CHARA, GETNUM(TALENT, "妖怪")), "妖獣", "妖鳥", "妖蟲", "鬼")
			RESULT:1 = 3
		ELSEIF GROUPMATCH(GET_TRIBENAME(CHARA, GETNUM(TALENT, "妖怪")), "吸血鬼", "悪魔")
			RESULT:1 = 2
		ELSEIF GROUPMATCH(GET_TRIBENAME(CHARA, GETNUM(TALENT, "人間")), "天人", "月人", "魔界人")
			RESULT:1 = 2
		ELSEIF TALENT:CHARA:妖精
			RESULT:1 = 2
		ELSEIF TALENT:CHARA:調合知識
			RESULT:1 = 2
		ELSE
			RESULT:1 = 1
		ENDIF
	ENDIF
CASE 9, 10
	;音楽の悩み
	;条件なし
	IF ARG % 2 == 0
		RESULTS:0 = 気分転換にいい音楽はどれ%性格傾向語尾(CHARA, "かな？")%
		RESULTS:1 = オシャレなジャズ
		RESULTS:2 = イカしたロック
		RESULTS:3 = 渋いクラシック
		IF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(かっこいい|ポップ|オシャレ|不思議)")
			RESULT:1 = 1
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(怖い|オシャレ|パワフル|派手)")
			RESULT:1 = 2
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(美しい|芸術的|知的|威厳|壮大)")
			RESULT:1 = 3
		ELSE
			RESULT:1 = RAND:3 + 1
		ENDIF
	ELSE
		RESULTS:0 = 東方の曲ってどれがいい%性格傾向語尾(CHARA, "かな？")%
		RESULTS:1 = やっぱりボス曲！
		RESULTS:2 = 通は道中曲！
		RESULTS:3 = 旧作曲もいいよ！
		;めんどいけどちまちま
		IF GROUPMATCH(CHARA, [[魅魔]], [[里香]], [[明羅]], [[エレン]], [[カナ]], [[理香子]], [[ちゆり]], [[夢美]], [[る～こと]])
			RESULT:1 = 3
		ELSEIF GROUPMATCH(CHARA, [[くるみ]], [[エリー]], [[夢月]], [[幻月]], [[ルイズ]], [[ユキ]], [[マイ]], [[夢子]], [[神綺]])
			RESULT:1 = 3
		ELSEIF GROUPMATCH(CHARA, [[阿求]], [[幽香]], [[蓮子]], [[メリー]], [[豊姫]], [[依姫]], [[レイセン]], [[朱鷺子]], [[小鈴]], [[典]])
			RESULT:1 = 3
		ELSEIF GROUPMATCH(CHARA, [[大妖精]], [[小悪魔]], [[リリーＷ]], [[リリーＢ]], [[てゐ]], [[静葉]], [[椛]], [[キスメ]], [[屠自古]])
			RESULT:1 = 2
		ELSEIF CSVABL(CHARA, GETNUM(ABL, "戦闘能力")) >= 5
			RESULT:1 = 1
		ELSE
			;3番にはならない
			RESULT:1 = RAND:2 + 1
		ENDIF
	ENDIF
CASE 11, 12
	;服の悩み
	;条件なし
	RESULTS:0 = 衣替えするならどんな服がいい%性格傾向語尾(CHARA, "かな？")%
	IF ARG % 2 == 0
		RESULTS:1 = かわいいワンピース
		RESULTS:2 = クールなカッターシャツ
		RESULTS:3 = セクシーなキャミソール
		IF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "かわいい")
			RESULT:1 = 1
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "かっこいい")
			RESULT:1 = 2
		ELSE
			;怖い/美しい派
			RESULT:1 = 3
		ENDIF
	ELSE
		RESULTS:1 = 斬新なファッションコーデ
		RESULTS:2 = 落ち着いたカジュアルコーデ
		RESULTS:3 = 今の服装がスキ
		IF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(芸術的|ポップ|オシャレ|派手)")
			RESULT:1 = 1
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(シンプル|シック|繊細|知的)")
			RESULT:1 = 2
		ELSE
			RESULT:1 = 3
		ENDIF
	ENDIF
CASE 13
	;髪型の悩み
	;条件なし
	RESULTS:0 = 髪を切ろうと思うんだけど、どんな髪型にしよう%性格傾向語尾(CHARA, "かな？")%
	RESULTS:1 = 今の髪型がスキ
	;めんどいけどちまちま
	IF GROUPMATCH(CHARA, [[小町]], [[にとり]], [[サニー]], [[キスメ]], [[はたて]], [[小鈴]], [[ちゆり]])
		RESULTS:2 = ポニーテール
	ELSE
		RESULTS:2 = ツインテール
	ENDIF
	;参考『東方ロング化タグ』,ありがとうございます
	IF GROUPMATCH(CHARA, [[魅魔]], [[明羅]], [[エレン]], [[理香子]], [[くるみ]], [[エリー]], [[神綺]])
		RESULTS:3 = ショートヘア
	ELSEIF GROUPMATCH(CHARA, [[美鈴]], [[小悪魔]], [[パチュリー]], [[リリーＷ]], [[リリーＢ]], [[紫]], [[萃香]])
		RESULTS:3 = ショートヘア
	ELSEIF GROUPMATCH(CHARA, [[慧音]], [[鈴仙]], [[永琳]], [[輝夜]], [[妹紅]], [[早苗]], [[天子]], [[勇儀]], [[お空]])
		RESULTS:3 = ショートヘア
	ELSEIF GROUPMATCH(CHARA, [[白蓮]], [[布都]], [[こころ]], [[影狼]], [[弁々]], [[クラウンピース]], [[純狐]])
		RESULTS:3 = ショートヘア
	ELSEIF GROUPMATCH(CHARA, [[紫苑]], [[ネムノ]], [[あうん]], [[成美]], [[隠岐奈]], [[スター]], [[豊姫]], [[依姫]])
		RESULTS:3 = ショートヘア
	ELSE
		RESULTS:3 = ロングヘア
	ENDIF
	IF TALENT:CHARA:性的興味 < 0
		RESULT:1 = 1
	ELSEIF TALENT:CHARA:度胸 < 0
		RESULT:1 = 1
	ELSEIF TALENT:CHARA:プライド > 0
		RESULT:1 = 1
	ELSEIF TALENT:CHARA:羞恥心 > 0
		RESULT:1 = 1
	ELSEIF TALENT:CHARA:陽気／陰気 < 0
		RESULT:1 = 1
	ELSEIF TALENT:CHARA:性的興味 > 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:陽気／陰気 > 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:羞恥心 < 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:応答 < 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:態度 < 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:応答 > 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:自己愛 > 0
		RESULT:1 = 3
	ELSE
		;1番にはならない
		RESULT:1 = RAND:2 + 2
	ENDIF
CASE 14
	;参拝の悩み
	;所属している人らは除外
	IF GROUPMATCH(CHARA, [[霊夢]], [[る～こと]], [[カナ]], [[魅魔]], [[萃香]], [[こころ]], [[針妙丸]], [[あうん]])
		RETURN
	ELSEIF GROUPMATCH(CHARA, [[ナズーリン]], [[小傘]], [[ぬえ]], [[白蓮]], [[マミゾウ]], [[一輪]], [[ムラサ]], [[星]], [[響子]], [[女苑]])
		RETURN
	ELSEIF GROUPMATCH(CHARA, [[早苗]], [[神奈子]], [[諏訪子]])
		RETURN
	;月の人たちとわかさぎ姫
	ELSEIF GROUPMATCH(CHARA, [[依姫]], [[豊姫]], [[レイセン]], [[サグメ]], [[わかさぎ姫]])
		RETURN
	ENDIF
	;ほぼ運ゲーです
	RESULTS:0 = 願掛けに行くならどこがいい%性格傾向語尾(CHARA, "かな？")%
	RESULTS:1 = 博麗神社
	RESULTS:2 = 守矢神社
	RESULTS:3 = 命蓮寺
	;命蓮寺はヤダ！（3番にはならない）
	IF GROUPMATCH(CHARA, [[芳香]], [[青娥]], [[屠自古]], [[布都]], [[神子]])
		RESULT:1 = RAND:2 + 1
	ELSEIF GROUPMATCH(CHARA, [[お燐]], [[お空]], [[パルスィ]], [[キスメ]], [[ヤマメ]], [[ムラサ]], [[星]], [[響子]], [[女苑]])
		RESULT:1 = RAND:2 + 1
	ELSEIF GROUPMATCH(CHARA, [[早苗]], [[神奈子]], [[諏訪子]])
		RESULT:1 = RAND:2 + 1
	ELSEIF TALENT:CHARA:妖精
		RESULT:1 = RAND:2 + 1
	ELSE
		RESULT:1 = RAND:3 + 1
	ENDIF
	;この悩みは逆張りナシ
	RESULT:2 = 1
CASE 15
	;ダイエットの悩み
	;条件なし
	RESULTS:0 = お腹のお肉が気になるのだけど、どうしたらいい%性格傾向語尾(CHARA, "かな？")%
	RESULTS:1 = 日々の節食！
	RESULTS:2 = 健康的な運動！
	RESULTS:3 = お薬パワー！
	IF TALENT:CHARA:健啖
		RESULT:1 = 1
	ELSEIF CSVABL(CHARA, GETNUM(ABL, "戦闘能力")) <= 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:調合知識
		RESULT:1 = 3
	ELSEIF ABL:CHARA:料理技能 >= 3
		RESULT:1 = 1
	ELSEIF ABL:CHARA:戦闘能力 <= 3
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:調合Lv >= 3
		RESULT:1 = 3
	ELSE
		SELECTCASE CFLAG:CHARA:性格傾向
		;気弱系
		CASE 1
			RESULT:1 = 1
		;強気系
		CASE 2
			RESULT:1 = 2
		;素直系
		CASE 3
			RESULT:1 = 1
		;真面目系
		CASE 4
			RESULT:1 = 2
		;その他
		CASEELSE
			RESULT:1 = 2
		ENDSELECT
	ENDIF
	;この悩みは逆張りナシ
	RESULT:2 = 1
CASE 16
	;菜園の悩み
	;冬は除外
	SIF DAY:2 == 4
		RETURN
	RESULTS:0 = 家庭菜園で何を作ろう%性格傾向語尾(CHARA, "かな？")%
	;答え＝DAY:2だと簡単すぎるのでシャッフルする
	RESULT:1 = RAND:3 + 1
	FOR LOCAL, 1, 4
		;LOCAL:2に季節を入れる
		;答えはDAY:2
		IF LOCAL == RESULT:1
			LOCAL:2 = DAY:2
		;１つ目の間違い
		ELSEIF !LOCAL:3
			LOCAL:2 = DAY:2 + 1
			LOCAL:3 ++
		;２つ目の間違い
		ELSE
			;秋なら1つ前、それ以外なら2つ先の季節
			IF DAY:2 == 3
				LOCAL:2 = DAY:2 - 1
			ELSE
				LOCAL:2 = DAY:2 + 2
			ENDIF
		ENDIF
		;キャベツを基準に季節の作物を抽選
		LOCAL:1 = GETNUM(ITEM, "キャベツ") + (10 * LOCAL:2) - 10 + RAND:3
		RESULTS:LOCAL = %ITEMNAME:(LOCAL:1)%
	NEXT
	;この悩みは逆張りナシ
	RESULT:2 = 1
CASE 17
	;菜園の悩み～花編～
	;条件なし
	RESULTS:0 = プランターで何を育てよう%性格傾向語尾(CHARA, "かな？")%
	;答え＝DAY:2だと簡単すぎるのでシャッフルする
	RESULT:1 = RAND:3 + 1
	FOR LOCAL, 1, 4
		;答えには正解の花
		IF LOCAL == RESULT:1
			;関数化してもいいけどするほどでもないかなと思い適当に
			SELECTCASE DAY:2
			CASE 1
				SELECTCASE RAND:3
				CASE 1
					RESULTS:LOCAL = チューリップ
				CASE 2
					RESULTS:LOCAL = アネモネ
				CASEELSE
					RESULTS:LOCAL = スミレ
				ENDSELECT
			CASE 2
				SELECTCASE RAND:3
				CASE 1
					RESULTS:LOCAL = アサガオ
				CASE 2
					RESULTS:LOCAL = キキョウ
				CASEELSE
					RESULTS:LOCAL = ユリ
				ENDSELECT
			CASE 3
				SELECTCASE RAND:3
				CASE 1
					RESULTS:LOCAL = コスモス
				CASE 2
					RESULTS:LOCAL = キク
				CASEELSE
					RESULTS:LOCAL = 月見草
				ENDSELECT
			CASE 4
				SELECTCASE RAND:3
				CASE 1
					RESULTS:LOCAL = ヒナギク
				CASE 2
					RESULTS:LOCAL = スイセン
				CASEELSE
					RESULTS:LOCAL = スノードロップ
				ENDSELECT
			ENDSELECT
		ELSE
			;LOCAL:2に季節を入れる
			;１つ目の間違い
			IF !LOCAL:3
				LOCAL:2 = DAY:2 + 1
				LOCAL:3 ++
			;２つ目の間違い
			ELSE
				;秋なら1つ前、それ以外なら2つ先の季節
				IF DAY:2 == 3
					LOCAL:2 = DAY:2 - 1
				ELSE
					LOCAL:2 = DAY:2 + 2
				ENDIF
			ENDIF
			SELECTCASE LOCAL:2
			CASE 1
				SELECTCASE RAND:3
				CASE 1
					RESULTS:LOCAL = チューリップ
				CASE 2
					RESULTS:LOCAL = アネモネ
				CASEELSE
					RESULTS:LOCAL = スミレ
				ENDSELECT
			CASE 2
				SELECTCASE RAND:3
				CASE 1
					RESULTS:LOCAL = アサガオ
				CASE 2
					RESULTS:LOCAL = キキョウ
				CASEELSE
					RESULTS:LOCAL = ユリ
				ENDSELECT
			CASE 3
				SELECTCASE RAND:3
				CASE 1
					RESULTS:LOCAL = コスモス
				CASE 2
					RESULTS:LOCAL = キク
				CASEELSE
					RESULTS:LOCAL = 月見草
				ENDSELECT
			CASE 4
				SELECTCASE RAND:3
				CASE 1
					RESULTS:LOCAL = ヒナギク
				CASE 2
					RESULTS:LOCAL = スイセン
				CASEELSE
					RESULTS:LOCAL = スノードロップ
				ENDSELECT
			ENDSELECT
		ENDIF
	NEXT
	;この悩みは逆張りナシ
	RESULT:2 = 1
CASE 18
	;休日の悩み
	;条件なし,仕事ないキャラもいるけど
	RESULTS:0 = 次の休日は何をして過ごそう%性格傾向語尾(CHARA, "かな？")%
	RESULTS:1 = 外に出て散策
	RESULTS:2 = 町で買い物
	RESULTS:3 = 家でゴロゴロ
	SELECTCASE CFLAG:CHARA:性格傾向
	;気弱系
	CASE 1
		RESULT:1 = 1
	;強気系
	CASE 2
		RESULT:1 = 1
	;素直系
	CASE 3
		RESULT:1 = 3
	;真面目系
	CASE 4
		RESULT:1 = 2
	;その他
	CASEELSE
		RESULT:1 = 2
	ENDSELECT
CASE 19
	;人気の悩み
	;条件なし
	RESULTS:0 = 最近人気が落ちてる%性格傾向語尾(CHARA, "気がする")%……
	RESULTS:1 = そんなことないよ！
	RESULTS:2 = 自分を磨こう！
	RESULTS:3 = もはや脱ぐしか無い
	SELECTCASE CFLAG:CHARA:性格傾向
	;気弱系
	CASE 1
		RESULT:1 = 2
	;強気系
	CASE 2
		RESULT:1 = 2
	;素直系
	CASE 3
		RESULT:1 = 1
	;真面目系
	CASE 4
		RESULT:1 = 2
	;その他
	CASEELSE
		;3番にはならない）
		RESULT:1 = RAND:2 + 1
	ENDSELECT
	;この悩みは逆張りナシ
	RESULT:2 = 1
CASE 20
	;集中力の悩み
	;条件なし
	RESULTS:0 = 最近どうも気が抜け%性格傾向語尾(CHARA, "てるなぁ")%……
	RESULTS:1 = ちゃんと寝たほうがいい
	RESULTS:2 = 気晴らしをしよう
	RESULTS:3 = 闘魂注入ビンタ！！
	SELECTCASE CFLAG:CHARA:性格傾向
	;気弱系
	CASE 1
		RESULT:1 = 3
	;強気系
	CASE 2
		RESULT:1 = 3
	;素直系
	CASE 3
		RESULT:1 = 2
	;真面目系
	CASE 4
		RESULT:1 = 1
	;その他
	CASEELSE
		RESULT:1 = 2
	ENDSELECT
	;この悩みは逆張りナシ
	RESULT:2 = 1
CASE 21
	;御三家の悩み
	RESULTS:0 = 最初の三匹のうち%性格傾向語尾(CHARA, "どれにしよう？")%
	RESULTS:1 = くさタイプ
	RESULTS:2 = ほのおタイプ
	RESULTS:3 = みずタイプ
	IF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(土|植物|緑|虫)")
		RESULT:1 = 1
	ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(火|日|赤|朱)")
		RESULT:1 = 2
	ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(水|氷|青|魚)")
		RESULT:1 = 3
	ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "自然")
		RESULT:1 = 1
	ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "パワフル")
		RESULT:1 = 2
	ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "知的")
		RESULT:1 = 3
	ELSE
		RESULT:1 = RAND:3 + 1
	ENDIF
CASE 22
	;進化の悩み
	RESULTS:0 = 何タイプに進化%性格傾向語尾(CHARA, "させよう？")%
	RESULTS:1 = みずタイプ
	RESULTS:2 = でんきタイプ
	RESULTS:3 = ほのおタイプ
	IF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(水|氷|青|魚)")
		RESULT:1 = 1
	ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(雷|星|黄)")
		RESULT:1 = 2
	ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(火|日|赤|朱)")
		RESULT:1 = 3
	ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(美しい|知的)")
		RESULT:1 = 1
	ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(かっこいい|派手)")
		RESULT:1 = 2
	ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(かわいい|パワフル)")
		RESULT:1 = 3
	ELSE
		RESULT:1 = RAND:3 + 1
	ENDIF
CASEELSE
	;プレゼントの悩み,一番クイズっぽいので確率をちょっと上げてます
	;相性のいいキャラを探す
	CALL FISHER_YATES_SHAFFLE(CHARANUM)
	FOR LOCAL:1, 1, CHARANUM
		LOCAL = SHAFFLE_ARRAY:(LOCAL:1)
		IF RELATION:CHARA:LOCAL > 100
			LOCAL:2 = LOCAL
			BREAK
		ENDIF
	NEXT
	;いないなら除外
	SIF !LOCAL:2
		RETURN
	;友人キャラを隔離
	LOCAL:10 = LOCAL:2
	[IF_DEBUG]
		PRINTFORML %CALLNAME:(LOCAL:10)%
	[ENDIF]
	;答えを先に入れるパターン
	LOCAL = RAND:3 + 1
	;答えの選択肢に高得点を入れる,あえて701点ではなく700点から
	CALL CREATE_GIFT_FROM_CHARA(LOCAL:10, 700, 1000)
	LOCALS:LOCAL = %RESULTS%
	;ハズレの選択肢にダミーを入れる
	FOR LOCAL:1, 1, 4
		SIF LOCAL:1 == LOCAL
			CONTINUE
		CALL CREATE_GIFT_FROM_CHARA(LOCAL:10, 0, 699)
		LOCALS:(LOCAL:1) = %RESULTS%
	NEXT
	;関数を呼んだのでこの時点ではRESULT/RESULTSがぐちゃってます
	RESULTS:0 = %CALLNAME:(LOCAL:10)%にプレゼントするならどれがいい%性格傾向語尾(CHARA, "かな？")%
	RESULTS:1 = %LOCALS:1%
	RESULTS:2 = %LOCALS:2%
	RESULTS:3 = %LOCALS:3%
	RESULT:1 = LOCAL
	RESULT:2 = 0
[SKIPSTART]
CASE 
	RESULTS:0 = 
	RESULTS:1 = 
	RESULTS:2 = 
	RESULTS:3 = 
	RESULT:1 = 
[SKIPEND]
ENDSELECT
RETURN 1

;性格傾向によって汎用台詞の語尾を変える
@性格傾向語尾(ARG, ARGS)
#FUNCTIONS
;気弱系＆素直系＆その他系はそのまま返す
SIF !GROUPMATCH(CFLAG:ARG:性格傾向, 2, 4)
	RETURNF ARGS
SELECTCASE ARGS
CASE "かな？"
	SELECTCASE CFLAG:ARG:性格傾向
	;強気系
	CASE 2
		RETURNF "かしら？"
	;真面目系
	CASE 4
		RETURNF "でしょうか？"
	ENDSELECT
CASE "思う？"
	SELECTCASE CFLAG:ARG:性格傾向
	;強気系
	CASE 2
		RETURNF "思うかしら？"
	;真面目系
	CASE 4
		RETURNF "思いますか？"
	ENDSELECT
CASE "どうしよう？"
	SELECTCASE CFLAG:ARG:性格傾向
	;強気系
	CASE 2
		RETURNF "どうしようかしら？"
	;真面目系
	CASE 4
		RETURNF "どうしましょう？"
	ENDSELECT
CASE "どれにしよう？"
	SELECTCASE CFLAG:ARG:性格傾向
	;強気系
	CASE 2
		RETURNF "どれにしようかしら？"
	;真面目系
	CASE 4
		RETURNF "どれにしましょう？"
	ENDSELECT
CASE "させよう？"
	SELECTCASE CFLAG:ARG:性格傾向
	;強気系
	CASE 2
		RETURNF "させようかしら？"
	;真面目系
	CASE 4
		RETURNF "させましょう？"
	ENDSELECT
CASE "気がする"
	SELECTCASE CFLAG:ARG:性格傾向
	;強気系
	CASE 2
		RETURNF "気がするわ"
	;真面目系
	CASE 4
		RETURNF "気がします"
	ENDSELECT
CASE "てるなぁ"
	SELECTCASE CFLAG:ARG:性格傾向
	;強気系
	CASE 2
		RETURNF "てるわ"
	;真面目系
	CASE 4
		RETURNF "てます"
	ENDSELECT
ENDSELECT

;存否関数
;F関数はTRYCALL系が使えないので通常関数で存否配列を作っちゃう
@EXIST_一般依頼3
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
;ここでARGで渡されるのはCLIENT_IDです
@一般依頼3(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "依頼名"
	CALLF MAKE_STR(V_NAME, "いっしょにあそぼ")
CASE "依頼内容表示"
	CALLF MAKE_STR(V_NAME, @"%CALLNAME:ARG%と一緒に遊ぶ")
CASE "依頼報酬表示"
	CALLF MAKE_STR(V_NAME, "なかよくなる")
CASE "依頼実行コマンド名"
	CALLF MAKE_STR(V_NAME, "遊ぶ")

CASE "依頼内容"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(0, "作業"))
CASE "依頼サブ"
CASE "依頼期限"
;発生時ではなく受託時に確定します
CASE "依頼発生率"
;これは他の一般依頼との相対確率なんで15％ではありません
	CALLF MAKE_INT(V_NAME, 15)
CASE "依頼発生条件"
;幼稚キャラ限定
;レミリアとてゐでも発生してしまうが気にするな
	SIF TALENT:ARG:幼稚
		CALLF MAKE_INT(V_NAME, 1)
CASE "依頼受託可能条件"
	CALLF MAKE_INT(V_NAME, 1)
CASE "依頼実行可能条件"
	CALLF MAKE_INT(V_NAME, 1)
;成功/失敗処理はCARRYOUTで直接書く
;SOURCEやITEM入手、消費が煩雑になるので
CASE "依頼消滅条件"
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
ENDSELECT



@IRAI_一般3(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
SELECTCASE SCENE
CASE "依頼条件表示"
;何か専用の表示情報を設けたい時用
;受託済みの依頼表示で使用されます
;
;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも無しで
;ここ以外の表示項目は
;PRINTFORML 　　　　　：
;となっているのでスペースを揃えたほうが見栄えが良いです

CASE "依頼実行時"
;このCASEに限り、RESULTが意味を持ちます
;依頼実行時のRESULTにはIRAI_CONST_RESULT()を使用してください
;可能な引数は"キャンセル", "実行して成功", "実行して失敗", "実行して未解決"です
;独自処理を仕込みたい時は"実行して未解決"で先延ばしするなりしてください
;
;"依頼実行時"に限っていえば、CHARAはTARGETと同一であり、必ずしもCLIENT_IDと一致するとは限りません
;個々のケースで確認する必要があります
;この依頼は「作業」扱いであり、CLIENTがTARGETになっている時のみ実行できます
;
;
	SOURCE:歓楽 = 1500
	PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:TARGET%と遊んだ
	PRINTFORML 
	TIME += 20
	DOWNBASE:体力 = 150
	DOWNBASE:気力 = 100
	DOWNBASE:MASTER:体力 = 150
	DOWNBASE:MASTER:気力 = 100
	RETURN IRAI_CONST_RESULT("実行して成功")
CASE "依頼成功時"
;成功報告時の報酬など
	TALENT:TARGET:機嫌 = LIMIT(TALENT:機嫌 + 1, -1, 1)
	CALL CHANGE_CFLAG(2, TARGET, 30)
	CALL CHANGE_CFLAG(4, TARGET, 5)
CASE "依頼失敗時"
;失敗報告時の処理
	CALL CHANGE_CFLAG(2, TARGET, -10)
	CALL CHANGE_CFLAG(4, TARGET, -5)
ENDSELECT

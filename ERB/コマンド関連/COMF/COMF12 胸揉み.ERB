;-------------------------------------------------
;胸揉み
;愛撫系コマンド、レベル1
;-------------------------------------------------
@COM12
;着脱の処理
TEQUIP:胸揉み = !TEQUIP:胸揉み

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;奉仕快楽
TCVAR:PLAYER:108 = 1

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 20
	SOURCE:快Ｂ += ABL:PLAYER:指 * 20 + 30

ELSE
	DOWNBASE:体力 += 10
	DOWNBASE:気力 += 50

	SOURCE:情愛 = 15
	SOURCE:露出 = 20
	SOURCE:逸脱 = 30
	SOURCE:反感 = 50

	SOURCE:快Ｂ += ABL:PLAYER:指 * 20 + 30

	SIF ABL:親密 >= 7
		SOURCE:情愛 += (ABL:親密 * 10)

	SIF TALENT:PLAYER:器用な指
		TIMES SOURCE:快Ｂ , 1.50

	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;抑圧、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 100
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 5
	ENDIF
ELSE
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 5
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 5
	ENDIF
ENDIF
RETURN 1
;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM12(ARG)

;これするいみないよね？
SELECTCASE SELECTCOM
;	CASE 0 TO 9,11,40 TO 49,65,66,70,71,83 TO 85,91 TO 94,100 TO 105,120 TO 189,410
;		PRINTL ＜胸揉み解除＞
;		TEQUIP:胸揉み = !TEQUIP:胸揉み
	CASEELSE
		;胸揉み中
		PRINTL ＜胸揉み中＞
		;-------------------------------------------------
		;ソース追加処理
		;-------------------------------------------------
		IF FLAG:70 == 1
			BASE:MASTER:TSP -= 20
			SOURCE:快Ｂ += (ABL:PLAYER:指 + 1) * 10
		ELSE
			DOWNBASE:ARG:体力 += 5
			DOWNBASE:ARG:気力 += 20
			DOWNBASE:PLAYER:体力 += 15
			DOWNBASE:PLAYER:気力 += 20
			SOURCE:快Ｂ += (ABL:PLAYER:指 + 1) * 10
			SOURCE:露出 = 20
			SOURCE:逸脱 = 30
			SOURCE:反感 = 30
			SOURCE:情愛 += ABL:PLAYER:指 * 5

			SIF ABL:親密 >= 5
				SOURCE:情愛 += (ABL:親密 * 2)
		ENDIF

		EXP:PLAYER:手淫経験 ++
ENDSELECT
RETURN 1

@MESSAGE_EQUIP_COM12
SIF CFLAG:PLAYER:今忙しくて手が離せない == 1
	PRINTFORM %CALLNAME:TARGET%の
	PRINTFORM %OPPAI_DESCRIPTION(TARGET)%を揉みしだきながら

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE12
;胸揉み実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(12)
	RETURN RESULT
;乳房電極装着中はダメ
SIF TEQUIP:24
	RETURN 0
;[絶壁]だと不可
SIF TALENT:バストサイズ == -2
	RETURN 0
RETURN 1


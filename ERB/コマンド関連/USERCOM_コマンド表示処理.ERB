;FileName_USERCOM.ERB ------------------------------ Rev1.00
;コマンド表示処理
;CALL		SYSTEM
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@SHOW_USERCOM
TFLAG:100 = !TFLAG:101
VARSET LOCAL
;前ターンのMASTERが射精した相手をLOCAL:1へセット
IF TCVAR:MASTER:112 == 1
	;V
	LOCAL:1 = MASTER_POSE(SET_V,SET_P,1)
ELSEIF TCVAR:MASTER:112 == 2
	;A
	LOCAL:1 = MASTER_POSE(SET_A,SET_P,1)
ENDIF


CALL LOOK_AT
PRINTL

CALL フィルタ表示

;コマンド表示
FOR LOCAL,0,800
	SIF LOCAL == 355 && (!FLAG:70 || CFLAG:TARGET:344 == 1)
		CONTINUE
	SIF LOCAL == 200 && TFLAG:102 == 2
		PRINTL
	SIF LOCAL == 350 && TFLAG:102 != 2
		PRINTL
	SIF LOCAL == 400 && TFLAG:102 != 2
		PRINTL
	SIF GLOBAL_COMABLE2(LOCAL)
		CONTINUE
	SIF GROUPMATCH(LOCAL,400,604)
		CONTINUE
	RESULT = 0
	;現状で対象コマンドが使用可能かどうか判定
	TRYCALLFORM COM_ABLE{LOCAL}
	IF RESULT
		SIF !QUICKFILTER(LOCAL)
			CONTINUE
		IF (LOCAL == 42 && TEQUIP:11) || (LOCAL == 43 && TEQUIP:12) || (LOCAL == 44 && TEQUIP:13) || (LOCAL == 45 && TEQUIP:14) || (LOCAL == 46 && TEQUIP:15) || (LOCAL == 47 && TEQUIP:16) || (LOCAL == 48 && TEQUIP:17)
			PRINTFORMC %TRAINNAME:LOCAL%解除[{LOCAL,3}]
		ELSEIF LOCAL == 15
			IF CFLAG:MASTER:イタズラ
				PRINTFORMC 秘所愛撫[{LOCAL,3}]
			ELSE
				PRINTFORMC クリ愛撫[{LOCAL,3}]
			ENDIF
		ELSEIF LOCAL == 81 && LOCAL:1
			PRINTFORMC お掃除フェラ[{LOCAL,3}]
		ELSEIF LOCAL == 82 && (TALENT:105 == -2 || TALENT:105 == -1)
			PRINTFORMC ナイズリ[{LOCAL,3}]
		ELSEIF LOCAL == 60 && (!BEDROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20))
			PRINTFORMC 対面立位[{LOCAL,3}]
		ELSEIF LOCAL == 61 && (!INROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20))
			PRINTFORMC 背面立位[{LOCAL,3}]
		ELSEIF LOCAL == 62 && (!BEDROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20))
			PRINTFORMC 対面立位アナル[{LOCAL,3}]
		ELSEIF LOCAL == 63 && (!INROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20))
			PRINTFORMC 背面立位アナル[{LOCAL,3}]
		ELSEIF LOCAL == 73 && TCVAR:PLAYER:イきそう
			IF TEQUIP:TARGET:体位 == 1 && (BEDROOM(CFLAG:PLAYER:現在位置) || (BATHROOM() && ITEM:20))
				PRINTFORMC 種付けプレス[{LOCAL,3}]
			ELSEIF TEQUIP:TARGET:体位 == 2
				IF CFLAG:TARGET:生理周期 == 7
					PRINTFORMC 孕ませ交尾[{LOCAL,3}]
				ELSE
					PRINTFORMC 種付け交尾[{LOCAL,3}]
				ENDIF
			ELSE
				PRINTFORMC %TRAINNAME:LOCAL%[{LOCAL,3}]
			ENDIF
		ELSEIF LOCAL == 92 && (!BEDROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20))
			PRINTFORMC Ａ対面立位される[{LOCAL,3}]
		ELSEIF LOCAL == 93 && (!INROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20))
			PRINTFORMC Ａ背面立位される[{LOCAL,3}]
		ELSEIF LOCAL == 100 && TALENT:TARGET:幼稚
			PRINTFORMC お尻ぺんぺん[{LOCAL,3}]
		ELSEIF LOCAL == 130 && (!BEDROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20))
			PRINTFORMC 対面立位される[{LOCAL,3}]
		ELSEIF LOCAL == 131 && (!INROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20))
			PRINTFORMC 背面立位される[{LOCAL,3}]
		ELSEIF LOCAL == 301 && CFLAG:TARGET:行動 == 5
			PRINTFORMC お茶を差し入れ[{LOCAL,3}]
		ELSEIF LOCAL == 301 && GETBIT(TCVAR:MASTER:特別なお茶, 0)
			PRINTFORMC 紅茶を淹れる[{LOCAL,3}]
		ELSEIF LOCAL == 334 && CFLAG:TARGET:350 == 49 && (TALENT:TARGET:酒耐性 == -2 || CFLAG:TARGET:妊娠自覚状態 == 1)
			PRINTFORMC 料理を勧める[{LOCAL,3}]
		ELSEIF LOCAL == 304 && CFLAG:TARGET:350 == 46
			PRINTFORMC 遊びに参加する[{LOCAL,3}]
		ELSEIF LOCAL == 315
			IF GETBIT(TALENT:TARGET:性別,1)
				PRINTFORMC 竿愛撫[{LOCAL,3}]
			ELSEIF CFLAG:MASTER:イタズラ
				PRINTFORMC 秘所愛撫[{LOCAL,3}]
			ELSE
				PRINTFORMC クリ愛撫[{LOCAL,3}]
			ENDIF
		ELSEIF LOCAL == 350
			IF FLAG:70 == 1 && CFLAG:TARGET:344 == 0
				LOCALS = イタズラする
			ELSEIF (CFLAG:現在位置 == 連れ込み宿 || CFLAG:現在位置 == ラブホ)&& !CFLAG:うふふ
				LOCALS = 抱き寄せる
			ELSEIF CFLAG:現在位置 == 寺子屋
				LOCALS = いけない個人授業
			ELSEIF CFLAG:現在位置 == OMANEKIBEYA() && TARGET == 43
				LOCALS = 房中術の修行
			;ベッドがない
			;室外
			ELSEIF !INROOM(CFLAG:PLAYER:現在位置)
				LOCALS = 抱き寄せる
			ELSE
				LOCALS = 押し倒す
			ENDIF
			SIF GET_TARGETNUM() > 1
				LOCALS = まとめて%LOCALS%
			PRINTFORMC %LOCALS%[{LOCAL,3}]
		ELSEIF LOCAL == 351
			IF CFLAG:TARGET:同行
			ELSEIF COM_351_CONDITION(TARGET) == 1
				SETCOLOR 100, 100, 100
			ELSEIF CAN_IRAI_CHARA_GO_TOGATHER(TARGET)
				SETCOLOR C_GREEN
			ELSE
				SETCOLOR C_AQUA
			ENDIF
			PRINTFORMC %TRAINNAME:LOCAL%[{LOCAL,3}]
			RESETCOLOR
		ELSEIF LOCAL == 360 && BASE:MASTER:気力 <= 0
			PRINTFORMC なすがまま[{LOCAL,3}]
		ELSEIF LOCAL == 366 && GROUPMATCH(IRAI_CHARA_TO_PROGRESS(TARGET), "依頼達成", "依頼失敗")
			SETCOLOR C_GREEN
			PRINTFORMC 依頼の報告[{LOCAL,3}]
			RESETCOLOR
		ELSEIF LOCAL == 366 && GROUPMATCH(IRAI_CHARA_TO_PROGRESS(TARGET), "依頼未受託")
			SETCOLOR C_GREEN
			PRINTFORMC 依頼を聞く[{LOCAL,3}]
			RESETCOLOR
		ELSEIF LOCAL == 367
			SETCOLOR C_GREEN
			PRINTFORMC %NAME_IRAI_ACT()%[{LOCAL,3}]
			RESETCOLOR
		ELSEIF LOCAL == 368
			;PRINTFORMC %自由行動に付き合う_名称(TARGET)%[{LOCAL,3}]
			PRINTFORMC %FreeAct_Name(TARGET)%[{LOCAL,3}]
		ELSEIF LOCAL == 337
			PRINTFORMC %子供ふれあい名称(TFLAG:描写中の子供)%[{LOCAL,3}]
		ELSEIF LOCAL == 417 && TIME >= 660 && TIME <= 900
			PRINTFORMC 昼寝する[{LOCAL,3}]
		ELSEIF LOCAL == 417
			PRINTFORMC 仮眠する[{LOCAL,3}]
		ELSEIF LOCAL == 420
			SELECTCASE MAIN_MAP
				CASE 0
					LOCALS = る～こと
				CASE 3
					LOCALS = メイド妖精
				CASE 9
					LOCALS = ゾンビフェアリー
			ENDSELECT
			PRINTFORMC %LOCALS%と話す[{LOCAL,3}]
		ELSEIF LOCAL == 422 && CFLAG:MASTER:現在位置 == 103
			PRINTFORMC 御仏の声を聴く[{LOCAL,3}]
		ELSEIF LOCAL == 431 && (CFLAG:MASTER:現在位置 == 24 || CFLAG:MASTER:現在位置 == 441)
			PRINTFORMC 温泉でくつろぐ[{LOCAL,3}]
		ELSEIF LOCAL == 431 && (CFLAG:MASTER:現在位置 == 大衆浴場 || CFLAG:MASTER:現在位置 == 940)
			PRINTFORMC 入浴する[{LOCAL,3}]
		ELSEIF LOCAL == 431 && !SMALL_PLACE(CFLAG:MASTER:現在位置) && ITEM:簡易プール && !BATHCHECK(CFLAG:MASTER:現在位置)
			PRINTFORMC プールでくつろぐ[{LOCAL,3}]
		ELSEIF LOCAL == 405 && CFLAG:TARGET:同行 && TARGET
			PRINTFORMC デート[{LOCAL,3}]
		ELSEIF LOCAL == 410 && OUTROOF(CFLAG:MASTER:現在位置) && DAY:2 == 4
			PRINTFORMC 雪かき[{LOCAL,3}]
		ELSEIF LOCAL == 414 && TIME >= 300 && TIME <= 540 && (CFLAG:MASTER:現在位置 == 9 || CFLAG:MASTER:現在位置 == OMANEKIBEYA())
			PRINTFORMC 朝食にする[{LOCAL,3}]
		ELSEIF LOCAL == 414 && TIME >= 660 && TIME <= 900 && (CFLAG:MASTER:現在位置 == 9 || CFLAG:MASTER:現在位置 == OMANEKIBEYA())
			PRINTFORMC 昼食にする[{LOCAL,3}]
		ELSEIF LOCAL == 414 && TIME >= 1020 && TIME <= 1200 && (CFLAG:MASTER:現在位置 == 9 || CFLAG:MASTER:現在位置 == OMANEKIBEYA())
			PRINTFORMC 夕食にする[{LOCAL,3}]
		ELSEIF LOCAL == 414 && TIME >= 1200 && TIME <= 1440 && (CFLAG:MASTER:現在位置 == 9 || CFLAG:MASTER:現在位置 == OMANEKIBEYA())
			PRINTFORMC 晩食にする[{LOCAL,3}]
		ELSEIF LOCAL == 414 && TIME >= 0 && TIME <= 180 && (CFLAG:MASTER:現在位置 == 9 || CFLAG:MASTER:現在位置 == OMANEKIBEYA())
			PRINTFORMC 夜食にする[{LOCAL,3}]
		ELSEIF LOCAL == 414 && CFLAG:TARGET:行動 == 5
			PRINTFORMC 食事休憩[{LOCAL,3}]
		ELSEIF LOCAL == 443
			PRINTFORMC %NAME_UNIQUE_COM(TARGET)%[{LOCAL,3}]
		ELSEIF LOCAL == 448
			IF HUNTING_SPOT(CFLAG:MASTER:現在位置) && MUSHITORI_SPOT(CFLAG:MASTER:現在位置)
				IF GET_MUSHIDATA(MUSHI_TRAP, "場所") && GROUPMATCH(GET_MUSHIDATA(MUSHI_TRAP, "場所"), TRAP_POINT:1, TRAP_POINT:2, TRAP_POINT:3)
					PRINTFORMC 罠の様子を見る[{LOCAL,3}]
				ELSE
					PRINTFORMC %TRAINNAME:LOCAL%[{LOCAL,3}]
				ENDIF
			ELSEIF GROUPMATCH(CFLAG:MASTER:現在位置, TRAP_POINT:1, TRAP_POINT:2, TRAP_POINT:3)
				PRINTFORMC 罠の様子を見る[{LOCAL,3}]
			ELSEIF GROUPMATCH(CFLAG:MASTER:現在位置, GET_MUSHIDATA(MUSHI_TRAP, "場所"))
				PRINTFORMC 虫トラップの確認[{LOCAL,3}]
			ELSEIF MUSHITORI_SPOT(CFLAG:MASTER:現在位置)
				PRINTFORMC 虫トラップ[{LOCAL,3}]
			ELSE
				PRINTFORMC %TRAINNAME:LOCAL%[{LOCAL,3}]
			ENDIF
		ELSEIF LOCAL == 464
			SELECTCASE CFLAG:MASTER:現在位置
				CASE 210
					IF MAIN_MAP == 8
						PRINTFORMC 索道駅へ[{LOCAL,3}]
					ELSE
						PRINTFORMC 絶景の丘へ[{LOCAL,3}]
					ENDIF
				CASE 820
					IF MAIN_MAP == 2
						PRINTFORMC 人里北通りへ[{LOCAL,3}]
					ELSE
						PRINTFORMC 広場へ[{LOCAL,3}]
					ENDIF
				CASE 570
					PRINTFORMC 中有の道へ[{LOCAL,3}]
				CASE 760
					PRINTFORMC 灼熱地獄跡へ[{LOCAL,3}]
				CASE 970
					PRINTFORMC 間欠泉地下センターへ[{LOCAL,3}]
				CASEELSE
					LOCALS = %STR:(6000 + SPECIAL_AREAMOVE() / 10)%
					PRINTFORMC %LOCALS%へ[{LOCAL,3}]
			ENDSELECT
		ELSEIF LOCAL == 482
			PRINTFORMC %CALLNAME:STALKED%を追う[{LOCAL,3}]
		ELSEIF LOCAL == 626
			PRINTFORMC %GIFTSHOP_NAME(CFLAG:MASTER:デート中, 1)%[{LOCAL,3}]
		ELSEIF LOCAL == 603 && PREVCOM == 603
			PRINTFORMC 手を引く[{LOCAL,3}]
		ELSEIF LOCAL == 698
			PRINTFORMC %OMANEKI_PLACE()%に行く[{LOCAL,3}]
		ELSEIF STRLENS(TRAINNAME:LOCAL)
			PRINTFORMC %TRAINNAME:LOCAL%[{LOCAL,3}]
		ENDIF
	ENDIF
NEXT
RESULT = 0
;自動実行中なら下記表示をスキップ
SIF !TFLAG:100
	GOTO SKIP
PRINTL
CALL PRINTBUTTON_EX(TFLAG:USERCOM非表示 ? "▽[＋][Ex_COM] ==" # "▼[－][Ex_COM] ==", 3002, TFLAG:USERCOM非表示)
PRINTFORML %"=" * 100%
SIF TFLAG:USERCOM非表示
	GOTO SKIP
;SIF !GETBIT (FLAG:USERCOMフィルタ, 12)
;	PRINTC 履歴表示[800]
IF GETBIT(FLAG:瞬間移動, 1) && !CFLAG:MASTER:同行
	SETCOLOR 0x5090FF
ELSE
	SETCOLOR C_GREEN
ENDIF
RESULT = 0
CALL COM_ABLE400
SIF RESULT
	PRINTC 移動[400]
IF GETBIT(FLAG:瞬間移動, 0) && !FLAG:デート相手
	SETCOLOR 0x5090FF
ELSE
	SETCOLOR C_GREEN
ENDIF
RESULT = 0
CALL COM_ABLE604
SIF RESULT
	PRINTC 散策[604]
RESETCOLOR
SIF !GETBIT (FLAG:USERCOMフィルタ, 0)
	PRINTC 汚れ表示[801]
SIF !CFLAG:添い寝中 && GET_TARGETNUM() > 1 && !GETBIT (FLAG:USERCOMフィルタ, 1)
	PRINTC 相手を変える[802]
SIF !GETBIT (FLAG:USERCOMフィルタ, 2)
	PRINTC 能力表示[803]
SIF !GETBIT (FLAG:USERCOMフィルタ, 3)
	PRINTC 能力表示(マスター)[804]
SIF !GETBIT (FLAG:USERCOMフィルタ, 4)
	PRINTC アイテム確認[805]
SIF !GETBIT(FLAG:USERCOMフィルタ, 11)
	PRINTC コマンドフィルタ[809]
SIF FLAG:1005
	PRINTC CONFIG[810]
SIF TALENT:MASTER:居場所察知 && AT_HOME(MASTER) && !CFLAG:うふふ && !CFLAG:添い寝中
	PRINTC 居場所察知[811]
SIF TALENT:MASTER:汚部屋察知 && AT_HOME(MASTER) && !CFLAG:うふふ && !CFLAG:添い寝中
	PRINTC 汚部屋察知[812]
SIF GETBIT(TALENT:MASTER:劣情察知, 1) && !CFLAG:うふふ
	PRINTC 劣情探知[813]
SIF FLAG:70 && CFLAG:TARGET:344 == 0 && !CFLAG:TARGET:睡眠 && AT_HOME(MASTER) == 1 && TARGET > 0 && !CFLAG:うふふ
	PRINTC お持ち帰り[815]
SIF FLAG:70 && TALENT:MASTER:集合 && AT_HOME(MASTER) == 1 && !CFLAG:うふふ
	PRINTC 集合[817]
SIF GATHERING_SPOT(CFLAG:MASTER:現在位置) != 0
	PRINTC 採集リスト[818]
IF CAN_DATE(TARGET)
	SIF TIME_REQUIRED(MAIN_MAP,CFLAG:MASTER:デート中) < 10
		SETCOLOR 100, 250, 140
	PRINTFORMC %GET_MAPNAME(MAIN_MAP)%に誘う({TIME_REQUIRED(MAIN_MAP,CFLAG:MASTER:デート中)}分)[990]
	RESETCOLOR
ENDIF

SIF CAN_DATE(TARGET)
	PRINTC デートに誘う[992]
SIF CFLAG:MASTER:現在位置 == CFLAG:MASTER:初期位置
	PRINTC カレンダー[993]
SIF FLAG:パンツ輸送
	PRINTC 手持ちパンツ確認[994]
IF AT_HOME(MASTER) && GETBIT(FLAG:瞬間移動, 1)
	SETCOLOR 0x5090FF
ELSEIF !AT_HOME(MASTER) && GETBIT(FLAG:瞬間移動, 0)
	SETCOLOR 0x5090FF
ENDIF
SIF !GETBIT(FLAG:USERCOMフィルタ, 13)
	PRINTC 自動時止め移動[995]
RESETCOLOR

;自動掃除
IF AT_HOME(MASTER) && GETBIT(FLAG:自動掃除, 1)
	SETCOLOR 0x5090FF
ELSEIF !AT_HOME(MASTER) && GETBIT(FLAG:自動掃除, 0)
	SETCOLOR 0x5090FF
ENDIF
SIF !GETBIT(FLAG:USERCOMフィルタ, 14) & AT_HOME(MASTER) && !CFLAG:うふふ && !CFLAG:添い寝中
	PRINTC 自動掃除[996]
RESETCOLOR

PRINTL
IF FLAG:70 == 1 && CFLAG:TARGET:344 == 0
	SIF EQUIP:0 && TARGET && CFLAG:うふふ
		PRINTC 服を脱がせる[820]
	IF EQUIP:0 && TARGET && EQUIP:TARGET:下半身下着２ != 0
		IF !PANTS_ALREADY_COLLECTED()
			FONTBOLD
			SETCOLOR COLOR("PINK")
		ENDIF
			PRINTC パンツをくすねる[821]
			RESETCOLOR
			FONTREGULAR
	ENDIF
	IF (TEQUIP:5 == 0 || EQUIP:TARGET:上半身下着１) && CFLAG:うふふ
		SIF EQUIP:0 && TARGET && EQUIP:TARGET:上半身下着１ != 0 && (EQUIP:TARGET:着物 != 0 || EQUIP:TARGET:上半身上着２ != 0 || EQUIP:TARGET:ボディースーツ == 3)
			PRINTC 上下着を外す[822]
	ENDIF
	IF TEQUIP:5 == 0 && CFLAG:うふふ
		SIF EQUIP:0 && TARGET && (EQUIP:TARGET:上半身下着１ || EQUIP:TARGET:上半身下着２ || EQUIP:TARGET:下半身下着１ || EQUIP:TARGET:下半身下着２ || EQUIP:TARGET:レオタード || EQUIP:TARGET:ボディースーツ) && (EQUIP:TARGET:上半身上着１ || EQUIP:TARGET:上半身上着２ || EQUIP:TARGET:ワンピース || EQUIP:TARGET:着物 || EQUIP:TARGET:外衣)
			PRINTC 下着姿にする[823]
	ENDIF
	IF MEASUREMENTCHECK(CFLAG:TARGET:現在位置)
		IF CFLAG:TARGET:身体測定 != 2
			PRINTC 身体測定する[824]
		ELSE
			PRINTC 身体測定を終える[824]
		ENDIF
	ENDIF
	SIF TFLAG:パンツ収奪
		PRINTC パンツ収奪[825]
	IF TARGET
		IF TEQUIP:焦らしモード
			PRINTC 焦らし終了[826]
		ELSE
			PRINTC 焦らす[826]
		ENDIF
	ENDIF
	IF CFLAG:うふふ
		PRINTC イタズラをやめる[999]
	ELSE
		PRINTC 時間停止解除[999]
	ENDIF
ELSEIF FLAG:70 == 1 && CFLAG:TARGET:344 == 1
		PRINTC 時間停止解除[999]
	IF CFLAG:うふふ
		SIF EQUIP:0 && TARGET
			PRINTC 服を脱がせる[820]

		IF CFLAG:うふふ == 2
			IF TFLAG:抱きつきモード
				PRINTC 振り払う[850]
			ELSE
				PRINTC 抜け出す[850]
			ENDIF
			IF TEQUIP:焦らしモード
				PRINTC 焦らし終了[826]
			ELSE
				PRINTC 焦らす[826]
			ENDIF
		ELSE
			PRINTC 逢引終了[999]
		ENDIF
	ENDIF
ELSEIF CFLAG:うふふ && !CFLAG:イタズラ
	SIF EQUIP:0 && TARGET && !(TFLAG:102 == 3 && TFLAG:抱きつきモード)
		PRINTC 服を脱がせる[820]
		IF TARGET
			IF TEQUIP:焦らしモード
				PRINTC 焦らし終了[826]
			ELSE
				PRINTC 焦らす[826]
			ENDIF
		ENDIF
	IF FLAG:70 == 1 && CFLAG:TARGET:344 == 0
		PRINTC 時間停止解除[999]
	ELSEIF CFLAG:うふふ == 2
		IF TFLAG:抱きつきモード
			PRINTC 振り払う[850]
		ELSE
			PRINTC 抜け出す[850]
		ENDIF
	ELSE
		PRINTC 逢引終了[999]
	ENDIF
ENDIF

IF CFLAG:イタズラ
	SIF CFLAG:イタズラ < 2 && (EXP:TARGET:睡眠姦経験 > 100 || TCVAR:睡眠薬強度 || TCVAR:睡眠深度 > 2000)
		PRINTC 服をはだける[830]
	IF CFLAG:イタズラ < 2
		PRINTC イタズラ終了[999]
		IF EQUIP:TARGET:下半身下着２
			IF !PANTS_ALREADY_COLLECTED()
				FONTBOLD
				SETCOLOR COLOR("PINK")
			ENDIF
			PRINTC パンツをくすねる[821]
			RESETCOLOR
			FONTREGULAR
		ENDIF
	ENDIF
	SIF CFLAG:イタズラ > 1
	PRINTC 睡眠姦終了[999]
ENDIF
IF CFLAG:添い寝中
	PRINTC 添い寝終了[999]
ENDIF
IF CHK_DATENOW(CFLAG:MASTER:デート中) && (!FLAG:70 || CFLAG:TARGET:344 == 1) && !CFLAG:MASTER:うふふ && CFLAG:MASTER:現在位置 != OMANEKIBEYA() && TFLAG:妄想中 != 1
	SIF TIME_REQUIRED(MAIN_MAP,CFLAG:MASTER:デート中) < 10
		SETCOLOR 100, 250, 140
	PRINTFORMC 帰路につく({TIME_REQUIRED(MAIN_MAP,CFLAG:MASTER:デート中)}分)[999]
	RESETCOLOR
ENDIF
$SKIP
IF PREVCOM >= 0
	IF TFLAG:151
		STR:0 = %STR:(1500 + TFLAG:151)%
	ELSE
		STR:0 = %TRAINNAME:PREVCOM%
	ENDIF
ENDIF
SIF PREVCOM == 304 && CFLAG:TARGET:350 == 49 && TALENT:TARGET:酒耐性 != -2
	STR:0 = 杯を交わす
SIF PREVCOM == 304 && CFLAG:TARGET:350 == 49 && (TALENT:TARGET:酒耐性 == -2 || CFLAG:TARGET:妊娠自覚状態 == 1)
	STR:0 = 料理を勧める
SIF PREVCOM == 304 && CFLAG:TARGET:350 == 51
	STR:0 = 介抱する
IF !TFLAG:USERCOM非表示
	IF PREVCOM >= 0 && PREVCOM < 300
		PRINTL 
		PRINT ＜前回調教コマンド:
		PRINTS STR:0
		SIF TFLAG:150
			PRINT (助手)
		PRINTL ＞
	ELSE
		PRINTL 
		PRINT ＜前回コマンド:
		PRINTS STR:0
		PRINTL ＞
	ENDIF
ENDIF

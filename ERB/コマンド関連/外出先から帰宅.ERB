@外出先から帰宅
#DIM DIST_TO_HOME
#DIM ReqTSP
#DIM 道迷い時間
#DIMS 低気圧位置

低気圧位置 = %GET_LOCATION_LOW()%

ReqTSP = TIME_REQUIRED(MAIN_MAP,CFLAG:MASTER:デート中) * 3
IF !FLAG:デート相手 && !FLAG:70 && BASE:MASTER:TSP >= ReqTSP
	PRINTL TSPを消費して、時間経過なしで帰宅できます。
	PRINTL TSPを使いますか？
	CALL ASK_YN
	IF !RESULT
		IF BASE:MASTER:TSP < ReqTSP
			PRINTW TSPが足りなかった
		ELSE
			BASE:MASTER:TSP -= ReqTSP
			GOTO WARP
		ENDIF
	ENDIF
ENDIF
IF !FLAG:70
	;(天候パッチ)連続降水カウンター20以上で土砂崩れに巻き込まれる可能性
	IF CONTINUOUS_RAINFALL >= 20 && RAND:(MAX(2, (40 - CONTINUOUS_RAINFALL) / 2)) == 0
		PRINTFORML …道を歩いていると山手から異音が聞こえてきた
		PRINTFORMW ！！　土砂崩れだ！！
		
		PRINTFORML 
		PRINTFORML %CALLNAME:MASTER%は土砂に巻き込まれながらも
		PRINTFORML なんとか脱出することに成功した…
		PRINTL 
		CALL ASK_DIARY("土砂崩れに遭遇した")
		BASE:MASTER:体力 = 0
		
		GOTO WARP2
	ENDIF
	;(天候パッチ)猛烈な台風の暴風域にいるとき飛来物に巻き込まれる可能性 颶風時には確率上昇
	IF (低気圧位置 == "台風接近暴風域" || 低気圧位置 == "台風通過暴風域") && IS_TYPHOON:1 >= 5 && (RAND:20 == 0 || (WIND_VELOCITY >= 3 && RAND:3 == 0))
		PRINTFORML …道を歩いていると何かがこっちに飛ばされてきた
		PRINTFORMW ！！　屋根瓦だ！！
		
		PRINTFORML 
		PRINTFORML 暴風に飛ばされた瓦が%CALLNAME:MASTER%に直撃した…
		PRINTL 
		CALL ASK_DIARY("飛来物が直撃した")
		BASE:MASTER:体力 = 0
		
		GOTO WARP3
	ENDIF
	;(天候パッチ)雪崩発生フラグが立っているとき雪崩に巻き込まれる可能性
	IF POSSIBLITY_AVALANCHE == 1 && POWER_COLD_AIR >= 3 && RAND:((6 - POWER_COLD_AIR) * 10) == 0
		PRINTFORML …道を歩いていると山手から何かが迫ってきた
		PRINTFORMW ！！　雪崩だ！！
		
		PRINTFORML 
		PRINTFORML %CALLNAME:MASTER%は雪崩に巻き込まれながらも
		PRINTFORML なんとか脱出することに成功した…
		PRINTL 
		
		POSSIBLITY_AVALANCHE = 0
		CONTINUOUS_SNOWFALL = 0
		
		CALL ASK_DIARY("雪崩に遭遇した")
		BASE:MASTER:体力 = 0
		
		GOTO WARP4
	ELSEIF POSSIBLITY_AVALANCHE == 2 && POWER_WARM_AIR >= 2 && RAND:((6 - POWER_WARM_AIR) * 10) == 0
		PRINTFORML …道を歩いていると山手から何かが迫ってきた
		PRINTFORMW ！！　雪崩だ！！
		
		PRINTFORML 
		PRINTFORML %CALLNAME:MASTER%は雪崩に巻き込まれながらも
		PRINTFORML なんとか脱出することに成功した…
		PRINTL 
		
		SNOW_COVERAGE = 0
		CONTINUOUS_SNOWFALL = 0
		SNOW_COVERAGE = 0
		
		CALL ASK_DIARY("雪崩に遭遇した")
		BASE:MASTER:体力 = 0
		
		GOTO WARP5
	ENDIF
	;(天候パッチ)暴風雪発生時にホワイトアウト発生の可能性 颶風時には確率上昇
	IF TIME:5 == 9 && ((WIND_VELOCITY == 2 && RAND:3 == 0) || WIND_VELOCITY >= 3)
		道迷い時間 = 60 + RAND:120
		PRINTFORML 降りしきる雪が猛烈な風にあおられて
		PRINTFORMW 目の前が真っ白になった！
		PRINTFORML 
		PRINTFORML ……道に迷って{道迷い時間}分余計に歩いてしまった
		TIME:0 += 道迷い時間
		BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - (20 * 道迷い時間), 0)
		
		GOTO WARP6
	ELSEIF TIME:5 == 8 && ((WIND_VELOCITY == 2 && RAND:10 == 0) || WIND_VELOCITY >= 3)
		道迷い時間 = 30 + RAND:60
		PRINTFORML 降りしきる雪が猛烈な風にあおられて
		PRINTFORMW 目の前が真っ白になった！
		PRINTFORML 
		PRINTFORML ……道に迷って{道迷い時間}分余計に歩いてしまった
		TIME:0 += 道迷い時間
		BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - (15 * 道迷い時間), 0)
		
		GOTO WARP7
	ENDIF
	CALL お出かけイベント
	$WARP2
	$WARP3
	$WARP4
	$WARP5
	$WARP6
	$WARP7
	CALL 帰宅の時間経過
ELSEIF BASE:MASTER:TSP < ReqTSP
	PRINTW TSPが足りないので時を止めたままは帰れない
	RETURN -1
ELSE
	BASE:MASTER:TSP -= ReqTSP
ENDIF

$WARP
IF CFLAG:負け犬 && TALENT:行きずり == 1
	PRINTFORML %CALLNAME:TARGET%をお持ち帰りしました
ELSEIF BASE:MASTER:体力 <= 0
	PRINTFORML %CALLNAME:MASTER%は最後の力を振り絞り%GET_MAPNAME(MAIN_MAP)%まで戻ってきた
	PRINTFORML しかし%NAME_FROM_PLACE(ENTRANCE)%に着いたところで意識を失ってしまった…
	PRINTFORMW 
ELSE
	PRINTFORMW \@ CFLAG:MASTER:デート中 != CFLAG:TARGET:デート中 || TARGET == MASTER ? 外出 # デート \@から帰って来ました
ENDIF
CALL 守矢くじデート相手のリセット()
FOR LOCAL,1,CHARANUM
	IF CHK_DATENOW(CFLAG:LOCAL:デート中)
		CALL DATE_EVENT(LOCAL)
		CALL SET_DATE(99,LOCAL)
	ENDIF
NEXT
SIF CHK_DATENOW(CFLAG:MASTER:デート中)
	CALL SET_DATE(99,MASTER)

CFLAG:MASTER:現在位置 = GET_ENTRANCE(MAIN_MAP)

@帰宅の時間経過
;残り時間算出
IF TFLAG:デート道中
	;道中の場合
	TIME = TIME + TIME_REQUIRED(MAIN_MAP, CFLAG:MASTER:デート中) - TFLAG:デート道中
ELSE
	;デート先到着済みの場合
	TIME = TIME + TIME_REQUIRED(MAIN_MAP, CFLAG:MASTER:デート中)
ENDIF
CALL 守矢くじデート相手のリセット()

@外出先から誘えるか(ARG)
LOCAL = FLAG:デート相手
IF CFLAG:ARG:行動 == 5
	PRINTFORMW %CALLNAME:ARG%は今仕事中だ
	RETURN 0
ENDIF
IF LOCAL != ARG && LOCAL != MASTER
	PRINTFORM %CALLNAME:LOCAL%が%CALLNAME:MASTER%
	;気弱系
	IF CFLAG:LOCAL:性格傾向 == 1
		PRINTFORML の服の裾を掴んで首を横に振っている
	;強気系
	ELSEIF CFLAG:LOCAL:性格傾向 == 2
		PRINTFORML をぎらりと光る目で見つめてくる
	;素直系
	ELSEIF CFLAG:LOCAL:性格傾向 == 3
		PRINTFORML を見つめながら私だけを見て欲しいと告げてくる
	;真面目系
	ELSEIF CFLAG:LOCAL:性格傾向 == 4
		PRINTFORML にソレはダメだと口を出してくる
	;その他
	ELSEIF CFLAG:LOCAL:性格傾向 == 5
		PRINTFORML へため息を向けて、首を横に振った
	ENDIF
	PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:ARG%を誘うのを諦めた
	RETURN 0
ENDIF
IF !VISIT(ARG)
	PRINTFORMW 今は駄目だと言われた
	RETURN 0
ENDIF
IF ARG == 93 && !ITEM:簡易プール
	PRINTW 彼女は連れて行けない…
	RETURN 0
ELSEIF BASE:ARG:気力 < 500
	PRINTFORMW %CALLNAME:ARG%は疲れているようだ
	RETURN 0
ELSEIF !CHARA_HOLIDAY(ARG) && BASE:ARG:仕事量 && TCVAR:ARG:仕事開始 < TIME + 180
	PRINTFORMW %CALLNAME:ARG%はこれから仕事がある
	RETURN 0
ELSE
	CFLAG:ARG:拠点来訪 = 1
	RETURN 1
ENDIF


;FileName_DATE_CMN.ERB ------------------------------- Rev1.00
;デートに誘えるか
;-----------------------------------------------------------
@CAN_DATE(ARG)
#FUNCTION

SIF !CHK_DATENOW(CFLAG:MASTER:デート中)
	RETURNF 0
SIF !SHIRAHU(ARG)
	RETURNF 0
SIF CFLAG:MASTER:現在位置 == OMANEKIBEYA()
	RETURNF 0
IF !CFLAG:ARG:負け犬
	SIF !CFLAG:ARG:態度 && TARGET != RANDOM_CHARANUM
		RETURNF 0
	SIF CFLAG:ARG:ブチギレ
		RETURNF 0
ENDIF
;潜伏モード中は不可
SIF GETBIT(FLAG:潜伏,0)
	RETURNF 0
SIF CHK_DATENOW(CFLAG:ARG:デート中)
	RETURNF 0
SIF (!TALENT:TARGET:思慕 && !TALENT:TARGET:恋慕 && !TALENT:TARGET:愛欲 && TARGET != RANDOM_CHARANUM)
	RETURNF 0
RETURNF 1


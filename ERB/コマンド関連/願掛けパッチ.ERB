@GANKAKE_0(ARG,ARG:1)

IF CFLAG:ARG:神社在住 && ARG > 0
;	// 好感度上昇 //
	CFLAG:ARG:好感度 += 10 + RAND:20
	CFLAG:ARG:信頼度 += 10 + RAND:20
ENDIF
FOR LOCAL, 1, CHARANUM
;	// 引っ越しで自宅が変わるので自宅メンバーも願掛けフラグは立てることにする //
;	SIF CFLAG:LOCAL:神社在住 > 0
;		CONTINUE
	IF ARG == LOCAL
		CFLAG:LOCAL:翌日来訪 = 1
	ELSE
		CFLAG:LOCAL:翌日来訪 = 0
	ENDIF
NEXT

FLAG:願掛け内容 = ARG

@GANKAKE_1(ARG)
#FUNCTION
#DIM バフ可能個所数
#DIM バフする個所候補, 12
#DIM 実際バフする個所

;バフする個所候補:0~12に素質番号を割り振り、その度にバフ可能個所数を増やす

VARSET バフする個所候補
バフ可能個所数 = 0
実際バフする個所 = 0

IF TALENT:ARG:思慕 || IS_LOVER(ARG)
	IF TALENT:ARG:性的興味 < 1
		バフする個所候補:バフ可能個所数 = 23
		バフ可能個所数 ++
	ENDIF
	IF TALENT:ARG:自己愛 < 1
		バフする個所候補:バフ可能個所数 = 31
		バフ可能個所数 ++
	ENDIF
	IF TALENT:ARG:濡れやすさ < 1
		バフする個所候補:バフ可能個所数 = 41
		バフ可能個所数 ++
	ENDIF
	IF TALENT:ARG:快感応答 < 1
		バフする個所候補:バフ可能個所数 = 70
		バフ可能個所数 ++
	ENDIF
	IF TALENT:ARG:倒錯的 < 1
		バフする個所候補:バフ可能個所数 = 80
		バフ可能個所数 ++
	ENDIF
	IF TALENT:ARG:サド < 1
		バフする個所候補:バフ可能個所数 = 82
		バフ可能個所数 ++
	ENDIF
	IF TALENT:ARG:マゾ < 1
		バフする個所候補:バフ可能個所数 = 83
		バフ可能個所数 ++
	ENDIF
ENDIF

FOR LOCAL,101,105
	IF TALENT:ARG:LOCAL < 1
		バフする個所候補:バフ可能個所数 = LOCAL
		バフ可能個所数 ++
	ENDIF
NEXT
IF TALENT:ARG:106 < 1
	バフする個所候補:バフ可能個所数 = 106
	バフ可能個所数 ++
ENDIF

SIF !バフ可能個所数
	RETURNF バフ可能個所数

実際バフする個所 = バフする個所候補:(RAND:バフ可能個所数)
TALENT:ARG:実際バフする個所 ++
TCVAR:TARGET:願掛け補正 = 実際バフする個所
TFLAG:193 = 実際バフする個所

RETURNF バフ可能個所数

@SHOW_CHALALIST
SIF FLAG:並び替え
	CALL CHARASORT_EX
FOR LOCAL:1, 1, EX_CHARANUM
	IF FLAG:並び替え
		LOCAL = RESULT:(LOCAL:1)
		SIF !LOCAL
			CONTINUE
	ELSE
		LOCAL = LOCAL:1
		SIF LOCAL >= CHARANUM
			BREAK
	ENDIF
	SIF !CFLAG:LOCAL:出禁 && !TALENT:LOCAL:行きずり
		PRINTFORMLC [{LOCAL, 3}] %CALLNAME:LOCAL,15,LEFT%
NEXT
;21/08/27 反発履歴削除追加、反発解除時に反発履歴も消去可に変更
@IKENIE(ARG,ARG:1)
;ARG 対象
;ARG:1 願い(1=反発解除 2=感度一時上昇 3=小槌 4=時間停止バレ解除[オミット]
;			 5=天罰 6=子宝祈願 7=サドっ気減少 8=反発履歴削除)
#DIM 総計
#DIM 種類数
#DIM ダブり
#DIM 捧げた枚数
#DIM 捧げる枚数
#DIM 必要枚数
VARSET LOCAL
CALL CHECK_COLLECTION(ARG)
総計 = RESULT:0
種類数 = RESULT:1
ダブり = 総計 - 種類数
捧げた枚数 = 0
捧げる枚数 = 0
SELECTCASE ARG:1
	CASE 1
		必要枚数 = 10
	CASE 2
		必要枚数 = 3
	CASE 3
		必要枚数 = CFLAG:MASTER:魔力回収 / 2 + 1
	CASE 4,6,8
		必要枚数 = 1
	CASE 5,7
		必要枚数 = 3
ENDSELECT

PRINTL 頭の中に声が響く…
PRINTW ((その望み叶えたくば、贄を捧げよ…))
PRINTFORMW ((%CALLNAME:ARG%のパンツを{必要枚数}枚捧げよ……))
PRINTFORM 現在%CALLNAME:ARG%のパンツを
IF !種類数
	PRINTL 一枚も持っていない
	RETURN -1
ENDIF
PRINTFORML 全部で{総計}枚{種類数}種、ダブってるのは{ダブり}枚持っている
IF 総計 < 必要枚数
	PRINTFORML 所望している枚数に{必要枚数 - 総計}枚足りない
	RETURN -1
ENDIF

LOCALS = 捧げる
SIF ダブり < 必要枚数
	LOCALS = ダブってないのも使うことになるけど捧げる
$LOOP
CALL ASK_M("捧げない",1,@"%LOCALS%",総計 >= 必要枚数)
IF RESULT == 1
	FOR LOCAL, 0, MAXPANTS
		IF CFLAG:ARG:(パンツ管理 + LOCAL) > 1
			捧げる枚数 = MIN(CFLAG:ARG:(パンツ管理 + LOCAL) - 1,(必要枚数 - 捧げた枚数))
			CFLAG:ARG:(パンツ管理 + LOCAL) -= 捧げる枚数
			捧げた枚数 += 捧げる枚数
			SIF 捧げた枚数 >= 必要枚数
				BREAK
		ENDIF
	NEXT
	IF 捧げた枚数 < 必要枚数
		FOR LOCAL, 0, MAXPANTS
			捧げる枚数 = MIN(CFLAG:ARG:(パンツ管理 + LOCAL),(必要枚数 - 捧げた枚数))
			CFLAG:ARG:(パンツ管理 + LOCAL) -= 捧げる枚数
			捧げた枚数 += 捧げる枚数
			FLAG:700 --
			SIF 捧げた枚数 >= 必要枚数
				BREAK
		NEXT
	ENDIF
	SIF ARG:1 == 1
		CALL ASK_M("履歴も消す",1,"履歴は残す",1)
	SELECTCASE ARG:1
		CASE 1
			MARK:ARG:反発刻印 --
			SIF !RESULT
				MARK:ARG:反発取得履歴 --
			TFLAG:193 = 30
		CASE 2
			FLAG:願掛け内容 = 999
		CASE 3
			CFLAG:MASTER:魔力回収 = 1
			SIF (CFLAG:MASTER:小槌 == 4 || CFLAG:MASTER:小槌 == 5) && TALENT:MASTER:形状 == 2
				TALENT:MASTER:形状 = CFLAG:MASTER:382
		CASE 4
			CFLAG:ARG:時間停止バレ = 0
		CASE 5
			FLAG:願掛け内容 = 204
			TFLAG:193 = 200
			FLAG:開運大紋購入回数 = 1
			BASE:53:体力 = 1
			BASE:53:気力 = 0
		CASE 6
			FLAG:願掛け内容 = 206
			IF FLAG:子宝祈願 == TARGET
				CFLAG:子宝補正 ++
			ELSE
				FOR LOCAL,1,CHARANUM
					CFLAG:LOCAL:子宝補正 = 0
				NEXT
				CFLAG:子宝補正 ++
			ENDIF
			SIF TALENT:妊娠願望
				CFLAG:子宝補正 ++
			SIF TALENT:恋人
				CFLAG:子宝補正 ++
			FLAG:子宝祈願 = TARGET
		CASE 7
			ABL:ARG:サドっ気 -= 1
			JUEL:ARG:優越 = 0
			EXP:ARG:嗜虐快楽経験 = MAX(EXP:ARG:嗜虐快楽経験 - 200, 0)
		CASE 8
			MARK:ARG:反発取得履歴 = 0
			TFLAG:193 = 35
	ENDSELECT
	PRINTFORML %CALLNAME:MASTER%が秘蔵のコレクションを並べて祈ると、この世ならぬ炎が{必要枚数}枚のパンツを燃えカスひとつ残さず焼き尽くした！
	FLAG:篤信 += 必要枚数
	IF ARG:1 == 7
		CALL COLORMESSAGE(@"%CALLNAME:ARG%のサドっ気が下がった",C_YELLOW,2,1)
		CALL COLORMESSAGE(@"%CALLNAME:ARG%の嗜虐快楽経験が下がった",C_YELLOW,2,1)
		CALL COLORMESSAGE(@"%CALLNAME:ARG%の優越の珠が0になった",C_YELLOW,2,1)
	ENDIF
	IF ARG:1 == 6
		PRINTFORML ((その娘への情と祈りを欠かすべからず))
		PRINTFORMW ((さすればいずれ願いは成就しよう……))
	ELSEIF ARG:1 == 2 && !GANKAKE_1(ARG)
		PRINTW
		PRINTW …
		PRINTW ……
		PRINTW ………
		PRINTW …………
		PRINTFORML おぱんつ様にも%CALLNAME:ARG%はこれ以上エッチにできないようだ……
		RETURN -1
	ELSE
		PRINTW ((汝の願いは成就せり……))
	ENDIF
	PRINTFORML %CALLNAME:MASTER%はおぱんつ様への敬神の念をあらたにした
	RETURN 0
ELSE
	RETURN -1
ENDIF

@PINK_WIND
SETCOLOR 255,100,255
FONTBOLD
PRINTFORMW 幻想郷に桃色の風が吹いた！
FOR LOCAL,1,CHARANUM
	IF !RAND:15
		PRINTFORML 桃色の風を浴びた%CALLNAME:LOCAL%はおかしな気分になった…
		CFLAG:LOCAL:溜まってる度 += LIMIT((10 + RAND:20 + RAND:20 + RAND:20) * ABL:LOCAL:欲望,200,1000)
	ENDIF
NEXT
RESETCOLOR
FONTREGULAR
PRINTW ((汝の願いは成就せり……))
PRINTFORML %CALLNAME:MASTER%はおぱんつ様への敬神の念をあらたにした

@DOWSING_PANTS
#DIM CHK_SLOT
#DIM CHARA
#DIM HIT
PRINTFORMW %CALLNAME:MASTER%は依頼された探しものが見つかるように祈りを捧げた……
PRINTL 
HIT = 0
FOR CHK_SLOT, 1, 6
	SIF !GETBIT(CAN_DOWSING(), CHK_SLOT)
		CONTINUE
	CHARA = 0
	SELECTCASE IRAI_SLOT_TO_TASKNAME(CHK_SLOT)
	CASE "捜索（人物）"
		CHARA = IRAI_SLOT_TO_PERSON(CHK_SLOT)
		;手持ち
		CALL CHECK_COLLECTION(CHARA, 1)
		IF RESULT
			CALL DOWSING_PANTS_MESSAGE(CHARA, 1)
			HIT = 1
		ELSE
			;コレクション
			CALL CHECK_COLLECTION(CHARA, 0)
			IF RESULT
				CALL DOWSING_PANTS_MESSAGE(CHARA, 0)
				HIT = 1
			ENDIF
		ENDIF
	CASE "捜索（場所）"
		;自宅があるキャラを抽出
		FOR CHARA, 1, CHARANUM
			IF CFLAG:CHARA:自宅位置 == GET_MAP_REPLACEMENT(IRAI_SLOT_TO_PLACE(CHK_SLOT))
				;手持ち
				CALL CHECK_COLLECTION(CHARA, 1)
				IF RESULT
					CALL DOWSING_PANTS_MESSAGE(CHARA, 1)
					HIT = 1
					BREAK
				ELSE
					;コレクション
					CALL CHECK_COLLECTION(CHARA, 0)
					IF RESULT
						CALL DOWSING_PANTS_MESSAGE(CHARA, 0)
						HIT = 1
						BREAK
					ENDIF
				ENDIF
			ENDIF
		NEXT
	CASEELSE
		CONTINUE
	ENDSELECT
NEXT
SIF !HIT
	PRINTFORMW ……しかし祈りは届かなかった

@DOWSING_PANTS_MESSAGE(CHARA, HAND)
#DIM CHARA
#DIM HAND
#DIM 種類
#DIM 所有数
CALL FISHER_YATES_SHAFFLE(MAXPANTS)
SETCOLOR C_YELLOW
FOR LOCAL, 0, MAXPANTS
	種類 = SHAFFLE_ARRAY:LOCAL
	IF !HAND
		IF CFLAG:CHARA:(種類 + パンツ管理)
			PRINTFORMW なんと！ 保管していた%CALLNAME:CHARA%の%PANTSNAME(種類,CHARA)%が輝き出した！
			BREAK
		ENDIF
	ELSE
		IF PANTS_TEMP:CHARA:種類
			PRINTFORMW なんと！ 手に持っていた%CALLNAME:CHARA%の%PANTSNAME(種類,CHARA)%が輝き出した！
			BREAK
		ENDIF
	ENDIF
NEXT
RESETCOLOR


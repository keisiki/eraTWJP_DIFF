;-----------------------------------------------------------
;キャラカスタマイズ
;-----------------------------------------------------------
@CUSTOM_TERMINAL(ARG, HIKITSUGI)
#DIM HIKITSUGI
CALL CUSTOM_INIT(ARG)
CLEARLINE 1000
;名前ボタン表示
DRAWLINE
CALL CUSTOM_NAME_BUTTON(ARG)
;基礎ボタン付き表示
DRAWLINE
CALL CUSTOM_BASE_BUTTON(ARG)
;能力・性技表示
CALL PRINT_STATE_ABL(ARG)
PRINTL
CALL COLORMESSAGE(@"　[2000] 能力の編集",0x70C070,1)
;素質表示
CALL PRINT_STATE_TALENT(ARG)
SETCOLOR 0x70C070
PRINTL 　[3000] 素質の編集
RESETCOLOR
;経験表示
CALL PRINT_STATE_EXP(ARG)
PRINTL
CALL COLORMESSAGE(@"　[4000] 経験の編集",0x70C070,1)
IF HIKITSUGI
	CALL COLORMESSAGE(@"　[5000] 引継ぎ設定の保存",0x70C070,1)
ENDIF
RESETCOLOR
DRAWLINE
PRINTL 　[9999] 設定終了
PRINTL
INPUT
SELECTCASE RESULT
	CASE 9999
		PRINTL これで本当によろしいですか？
		CALL ASK_YN()
		SIF !RESULT
			RETURN 1
	;NAME
	CASE 0 TO 999
		CALL CUSTOM_NAME(ARG, RESULT)
	;BASE
	CASE 1000 TO 1999
		CALL CUSTOM_BASE(ARG, RESULT - 1000)
	;ABL
	CASE 2000
		CALL CUSTOM_MENU("ABL", ARG)
	;TALENT
	CASE 3000
		CALL CUSTOM_MENU("TALENT", ARG)
	;EXP
	CASE 4000
		CALL CUSTOM_MENU("EXP", ARG)
	CASE 5000
		IF HIKITSUGI
			CALL SAVE_CUSTOM_TALENT(ARG, "恋人")
		ENDIF
ENDSELECT
RESTART
;-----------------------------------------------------------
;排他処理
;-----------------------------------------------------------
@CUSTOM_INIT(ARG)
;ARGが女、ふたなり時の排他
IF TALENT:ARG:性別 & 1
	ABL:ARG:ホモっ気 = 0
	ABL:ARG:ゲイ中毒 = 0
	EXP:ARG:ゲイ経験 = 0
	IF !(TALENT:ARG:2 & 2)
		EXP:ARG:射精経験 = 0
		EXP:ARG:挿入経験 = 0
	ENDIF
	SIF TALENT:ARG:性別 != 1
		TALENT:ARG:具現 = 0
;ARGが男時の排他
ELSE
	ABL:ARG:Ｖ感覚 = 0
	ABL:ARG:レズっ気 = 0
	ABL:ARG:レズ中毒 = 0
	EXP:ARG:レズ経験 = 0
	EXP:ARG:噴乳経験 = 0
	EXP:ARG:Ｖ経験 = 0
	EXP:ARG:パイズリ経験 = 0
	EXP:ARG:Ｖ性交経験 = 0
	EXP:ARG:Ｖ拡張経験 = 0
	EXP:ARG:子宮口開発経験 = 0
	TALENT:ARG:Ｖ感度 = 0
	TALENT:ARG:処女 = 0
	TALENT:ARG:母乳体質 = 0
	TALENT:ARG:バストサイズ = -2
	TALENT:ARG:ヤリマン = 0
	TALENT:ARG:初潮 = 0
	TALENT:ARG:具現 = 0
ENDIF
;チ○コ
IF TALENT:ARG:2 & 2
	MAXBASE:ARG:精力 = 1000
	BASE:ARG:精力 = 1000
	MAXBASE:ARG:勃起 = 1500
	;実績ボーナス分
	SIF ARG == MASTER && GETBIT(FLAG:702,8)
		MAXBASE:MASTER:精力 += 100
ELSE
	TALENT:ARG:非童貞 = 0
	MAXBASE:ARG:精力 = 0
	BASE:ARG:精力 = 0
	MAXBASE:ARG:勃起 = 0
ENDIF
;処女ならV系経験無し
IF TALENT:ARG:処女 == 1
	ABL:ARG:Ｖ感覚 = 0
	EXP:ARG:Ｖ経験 = 0
	EXP:ARG:Ｖ性交経験 = 0
	EXP:ARG:Ｖ拡張経験 = 0
	EXP:ARG:子宮口開発経験 = 0
	CSTR:ARG:処女喪失履歴 = 
	CFLAG:ARG:処女喪失記念日 = 0
	CFLAG:ARG:処女喪失対象 = 0
;非処女はV経験１
ELSEIF TALENT:ARG:2 & 1 && EXP:ARG:Ｖ経験 == 0
	EXP:ARG:Ｖ経験 = 1
ENDIF

;ファーストキス
IF !EXP:ARG:キス経験
	TALENT:ARG:キス未経験 = 1
ELSE
	TALENT:ARG:キス未経験 = 0
ENDIF


;-----------------------------------------------------------
;名前変更ボタン
;-----------------------------------------------------------
@CUSTOM_NAME_BUTTON(ARG)
#DIMS CONST colortext = "<font color='#70C070'>[変更]</font>"
#DIMS html
;名前
html '= @"<button value='{0}'>" + HTML_SPACE(2) + @"名前：%NAME:ARG%"+ HTML_SPACE(2) + colortext + "</button>"
html += HTML_SPACE(4)
;呼び名
html += @"<button value='{1}'>" + @"呼び名：%CALLNAME:ARG%"+ HTML_SPACE(2) + colortext + "</button>"
HTML_PRINT html + "<br><br>"

@CUSTOM_NAME(ARG, ARG:1)
SELECTCASE ARG:1
	CASE 0
		PRINTL
		PRINTFORML 現在の名前は%NAME:ARG%です。
		PRINTFORML 変更する場合は新しい名前を入力してください。(空欄でキャンセル)
		PRINTL
		INPUTS
		IF RESULTS != ""
			NAME:ARG '= RESULTS
			PRINTFORMW 変更しました。現在の名前は%NAME:ARG%です。
		ENDIF
	CASE 1
		PRINTL
		PRINTFORML 現在の呼び名は%CALLNAME:ARG%です。
		PRINTFORML 変更する場合は新しい呼び名を入力してください。(空欄でキャンセル)
		PRINTL
		INPUTS
		IF RESULTS != ""
			CALLNAME:ARG '= RESULTS
			PRINTFORMW 変更しました。現在の呼び名は%CALLNAME:ARG%です。
		ENDIF
ENDSELECT

;-----------------------------------------------------------
;基礎変更ボタン
;-----------------------------------------------------------
@CUSTOM_BASE_BUTTON(ARG)
#DIM baseButtonVal = 1000
PRINT  
CALL BASE_BAR(ARG, "体力", 28, "緑")

IF BASE:ARG:体力 > 1000
	SETCOLOR 0x70C070
	PRINTBUTTON "[－]", 0 + baseButtonVal
ELSE
	SETCOLOR 0x969696
	PRINT [－]
ENDIF
IF BASE:ARG:体力 < 2500
	SETCOLOR 0x70C070
	PRINTBUTTON "[＋]", 1 + baseButtonVal
ELSE
	SETCOLOR 0x969696
	PRINT [＋]
ENDIF
RESETCOLOR
PRINTL
PRINT  
CALL BASE_BAR(ARG, "気力", 28, "青")
IF BASE:ARG:気力 > 1000
	SETCOLOR 0x70C070
	PRINTBUTTON "[－]", 2 + baseButtonVal
ELSE
	SETCOLOR 0x969696
	PRINT [－]
ENDIF
IF BASE:ARG:気力 < 2500
	SETCOLOR 0x70C070
	PRINTBUTTON "[＋]", 3 + baseButtonVal
ELSE
	SETCOLOR 0x969696
	PRINT [＋]
ENDIF
RESETCOLOR
PRINTL
PRINTL

;-----------------------------------------------------------
;基礎変更
;ARG:0 キャラ登録番号
;ARG:1 0=体力減少, 1=体力増加, 2=気力減少, 3=気力増加
;-----------------------------------------------------------
@CUSTOM_BASE(ARG, ARG:1)
SELECTCASE ARG:1
	CASE 0
		MAXBASE:ARG:体力 -= 100
	CASE 1
		MAXBASE:ARG:体力 += 100
	CASE 2
		MAXBASE:ARG:気力 -= 100
	CASE 3
		MAXBASE:ARG:気力 += 100
	CASE 4
ENDSELECT
MAXBASE:ARG:体力 = LIMIT(MAXBASE:ARG:体力, 1000, 2500)
BASE:ARG:体力 = MAXBASE:ARG:体力
MAXBASE:ARG:気力 = LIMIT(MAXBASE:ARG:気力, 1000, 2500)
BASE:ARG:気力 = MAXBASE:ARG:気力

;-----------------------------------------------------------
;メニュー呼び出し
;-----------------------------------------------------------
@CUSTOM_MENU(ARGS, ARG)
PRINTL
PRINTL
CALLFORM CUSTOM_%ARGS%_MENU(ARG)
RESETCOLOR
PRINTL
PRINTL
PRINTL   [9999] 戻る
PRINTL
INPUT
SIF RESULT == 9999
	RETURN
CALLFORM CUSTOM_%ARGS%(RESULT, ARG)
RESTART

;-----------------------------------------------------------
;能力変更
;-----------------------------------------------------------
@CUSTOM_ABL_MENU(ARG)
LOCAL:1 = 0
FOR LOCAL,0,50
	SIF CUSTOM_選べない能力(LOCAL, ARG)
		CONTINUE
	PRINTFORM %ABLNAME:LOCAL,12%:{ABL:ARG:LOCAL,5,LEFT} 
	IF ABL:ARG:LOCAL != 0
		PRINTBUTTON "[－]", LOCAL * 2
	ELSE
		SETCOLOR 0x969696
		PRINT [－]
		RESETCOLOR
	ENDIF
	IF ABL:ARG:LOCAL != 2
		PRINTBUTTON "[＋]", LOCAL * 2 + 1
	ELSE
		SETCOLOR 0x969696
		PRINT [＋]
		RESETCOLOR
	ENDIF
	LOCAL:1 ++
	SIF LOCAL:1 % 3 == 0
		PRINTL 
NEXT

;-----------------------------------------------------------
;-----------------------------------------------------------
@CUSTOM_ABL(ARG, ARG:1)
	LOCAL = ARG / 2
;例外処理
SIF CUSTOM_選べない能力(LOCAL, ARG:1)
	RETURN 0
;増減と正常化
;奇数(+)
IF (ARG) % 2
	ABL:(ARG:1):LOCAL ++
;偶数(-)
ELSE
	ABL:(ARG:1):LOCAL --
ENDIF
ABL:(ARG:1):LOCAL = LIMIT(ABL:(ARG:1):LOCAL, 0, 2)
RETURN 1

;-----------------------------------------------------------
;-----------------------------------------------------------
@CUSTOM_選べない能力(argAbl, ARG)
#DIM argAbl
#FUNCTION
;個別処理
SELECTCASE ABLNAME:argAbl
	;未使用
	CASE ""
		RETURNF 1
	;時間停止
	CASE "TSLv"
		RETURNF 1
	;同性愛関係
	CASE "レズっ気", "レズ中毒"
		SIF !(TALENT:ARG:2 & 1)
			RETURNF 1
	CASE "ホモっ気", "ゲイ中毒"
		SIF TALENT:ARG:2 & 1
			RETURNF 1
ENDSELECT
;汎用処理
SELECTCASE argAbl
	;中毒関係
	CASE 30 TO 39
		RETURNF 1
	;生活系
	CASE 40 TO 49
		RETURNF 1
	;性技
	CASE 50 TO 59
		RETURNF 1
ENDSELECT
RETURNF 0

;-----------------------------------------------------------
;素質変更
;-----------------------------------------------------------
@CUSTOM_TALENT_MENU(ARG)
VARSET LOCAL
FOR LOCAL, 0, 157
	IF CUSTOM_選べない素質(LOCAL, ARG) == 1
		CONTINUE
	ELSEIF CUSTOM_選べない素質(LOCAL, ARG) == 2
		;表示はさせる
		SETCOLOR 50, 50, 50
		LOCALS = [{LOCAL,3}]%TALENTNAME:LOCAL%
		GOTO SKIP
	ENDIF
	SETCOLOR 150, 150, 150
	SELECTCASE TALENTNAME:LOCAL
	;TALENT.CSVに登録されていない
	CASE ""
		CONTINUE
	;オンオフ型の素質(TALENTNAMEと素質表示が一致)
	CASE GET_TALENTNAME(LOCAL,1)
		SIF TALENT:ARG:LOCAL
			SETCOLOR C_AQUA
		LOCALS = [{LOCAL,3}]%TALENTNAME:LOCAL%
	CASE "非童貞"
		SIF !TALENT:ARG:LOCAL
			SETCOLOR C_AQUA
		LOCALS = [{LOCAL,3}]童貞
	CASE "キス未経験"
		SIF !TALENT:ARG:LOCAL
			SETCOLOR C_AQUA
		LOCALS = [{LOCAL,3}]%TALENTNAME:LOCAL%
	;量的素質
	CASEELSE
		LOCALS =  [{LOCAL,3}]%TALENTNAME:LOCAL%
		IF STRLENS(GET_TALENTNAME(LOCAL, TALENT:ARG:LOCAL))
			LOCALS:1 = 【%GET_TALENTNAME(LOCAL, TALENT:ARG:LOCAL)%】
			SETCOLOR C_AQUA
		ELSEIF TALENTNAME:LOCAL == "性別"
			LOCALS:1 = 【オンナ】
			SETCOLOR C_AQUA
		ELSE
			LOCALS:1 = 【－】
		ENDIF
		LOCALS += LOCALS:1
	ENDSELECT
	$SKIP
	IF LOCAL:1 % 3 == 0
		PRINTL 
		PRINT   
	ENDIF
	IF CUSTOM_選べない素質(LOCAL, ARG) == 2
		PRINTPLAINFORM %LOCALS, 30, LEFT%
	ELSE
		PRINTFORM %LOCALS, 30, LEFT%
	ENDIF
	LOCAL:1 ++
NEXT

;-----------------------------------------------------------
@CUSTOM_TALENT(inputed, chara)
#DIM inputed
#DIM chara
SIF inputed < 0|| inputed > 500
	RETURN 0
SIF TALENTNAME:inputed == ""
	RETURN 0
SIF CUSTOM_選べない素質(inputed, chara)
	RETURN 0
SELECTCASE TALENTNAME:inputed
	;先に例外処理
	CASE "性別"
		IF TALENT:chara:性別 == 3
			TALENT:chara:性別 = 1
		ELSE
			TALENT:chara:性別 ++
		ENDIF
	CASE "性別嗜好"
		IF TALENT:chara:inputed == 3
			TALENT:chara:inputed = -1
		ELSE
			TALENT:chara:inputed ++
		ENDIF
	CASE "性別嗜好"
		;体型が「小人(-5)」と「巨人(3)」の場合を独立処理
		IF TALENT:chara:inputed == -5
			TALENT:chara:inputed = -2
		ELSEIF TALENT:chara:inputed == 5
			TALENT:chara:inputed = -5
		ELSEIF TALENT:chara:inputed == 2
			TALENT:chara:inputed = 5
		ELSE
			TALENT:chara:inputed ++
		ENDIF
	CASE "非童貞", "キス未経験"
		TALENT:chara:inputed = !TALENT:chara:inputed
	CASE "母乳体質"
		TALENT:chara:母乳体質 = !TALENT:chara:母乳体質
		IF TALENT:chara:母乳体質
			MAXBASE:chara:母乳 = 2000
		ELSE
			MAXBASE:chara:母乳 = 0
		ENDIF
	;汎用処理
	CASEELSE
		;真偽型の素質は反転
		IF TALENTNAME:inputed == GET_TALENTNAME(inputed,1)
			TALENT:chara:inputed = !TALENT:chara:inputed
		ELSE
		;他は端まで行くと-1を掛ける
			IF STRLENS(GET_TALENTNAME(inputed,TALENT:chara:inputed + 1)) || TALENT:chara:inputed == -1
				TALENT:chara:inputed ++
			ELSE
				TALENT:chara:inputed *= -1
			ENDIF
		ENDIF
ENDSELECT

;-----------------------------------------------------------
@CUSTOM_選べない素質(ARG, CHARA)
#FUNCTION
#DIM CHARA
;RETURNF 2は表示はさせるもの
SELECTCASE ARG
	;劣情察知～,ボーナス素質および特殊陥落,種族,孕ませ系
	CASE IS >=157
		RETURNF 1
	;あなたは選べない１
	CASE GETNUM(TALENT, "習得速度") TO GETNUM(TALENT, "針さばき"), GETNUM(TALENT, "薬毒耐性"), GETNUM(TALENT, "即落ち"), GETNUM(TALENT, "盲信"), GETNUM(TALENT, "小悪魔")
		SIF CHARA == NO:MASTER
			RETURNF 1
	;あなたは選べない２
	CASE GETNUM(TALENT, "魅力") TO GETNUM(TALENT, "謎の魅力"), GETNUM(TALENT, "回復速度") TO GETNUM(TALENT, "不思議な根"), GETNUM(TALENT, "狂気"), GETNUM(TALENT, "人気")
		SIF CHARA == NO:MASTER
			RETURNF 1
	;あなたは選べず、女性器がないと選べない
	CASE GETNUM(TALENT, "母乳体質"), GETNUM(TALENT, "妊娠願望") 
		SIF CHARA == NO:MASTER
			RETURNF 1
		SIF !GETBIT(TALENT:CHARA:性別, 0)
			RETURNF 2
	;女性器がないと選べない
	CASE GETNUM(TALENT, "処女"), GETNUM(TALENT, "ヤリマン"), GETNUM(TALENT, "Ｖ感度")
		SIF !GETBIT(TALENT:CHARA:性別, 0)
			RETURNF 2
	;男性器がないと選べない
	CASE GETNUM(TALENT, "非童貞")
		SIF !GETBIT(TALENT:CHARA:性別, 1)
			RETURNF 2
	;男性器があると選べない
	CASE GETNUM(TALENT, "具現")
		SIF GETBIT(TALENT:CHARA:性別, 1)
			RETURNF 2
	;あなた以外は選べない
	CASE GETNUM(TALENT, "年齢")
		SIF CHARA != NO:MASTER
			RETURNF 1
	;全員選べないが表示はする
	CASE GETNUM(TALENT, "キス未経験")
		RETURNF 2
	;全員選べない１
	CASE GETNUM(TALENT, "恋慕") TO GETNUM(TALENT, "思慕"), GETNUM(TALENT, "機嫌"), GETNUM(TALENT, "容姿"), GETNUM(TALENT, "弱味"), GETNUM(TALENT, "調合知識"), GETNUM(TALENT, "調合Lv"), GETNUM(TALENT, "採集Lv"), GETNUM(TALENT, "釣りLv"), GETNUM(TALENT, "伐採Lv")
		RETURNF 1
	;全員選べない２
	CASE GETNUM(TALENT, "自慰狂い") TO GETNUM(TALENT, "ポルチオ性感"), GETNUM(TALENT, "妄信"), GETNUM(TALENT, "嫉妬"), GETNUM(TALENT, "濃厚精液"), GETNUM(TALENT, "精愛味覚"), GETNUM(TALENT, "禁断の知識"), GETNUM(TALENT, "絶倫") TO GETNUM(TALENT, "母性")
		RETURNF 1
	;女性器があって幼児か幼稚がないと選べない
	CASE GETNUM(TALENT, "初潮")
		SIF !GETBIT(TALENT:CHARA:性別, 0)
			RETURNF 2
		SIF !TALENT:CHARA:幼児／幼児退行 && !TALENT:CHARA:幼稚
			RETURNF 2
ENDSELECT
RETURNF 0

;-----------------------------------------------------------
;経験変更
;-----------------------------------------------------------
@CUSTOM_EXP_MENU(ARG)
VARSET LOCAL
FOR LOCAL,0,100
	SIF CUSTOM_選べない経験(LOCAL, ARG)
		CONTINUE
	PRINTFORM   [{LOCAL,2}] - %EXPNAME:LOCAL,16%:{EXP:ARG:LOCAL,5,LEFT} 
	LOCAL:1 ++
	SIF LOCAL:1 % 3 == 0
		PRINTL 
NEXT

;-----------------------------------------------------------
@CUSTOM_EXP(argExp, argChara)
#DIM argExp
#DIM argChara
SIF CUSTOM_選べない経験(argExp, argChara)
	RETURN 0
PRINTFORM %EXPNAME:argExp%を入力してください    (0-30)  
	PRINTBUTTON "[キャンセル]",-1
	PRINTL   [ 0]  [ 1]  [ 5]  [10]  [20]  [30]
	PRINTL
INPUT
SIF RESULT < 0
	RETURN 0
EXP:argChara:argExp = RESULT
EXP:argChara:argExp = LIMIT(EXP:argChara:argExp, 0, 30)
RETURN 1

;-----------------------------------------------------------
@CUSTOM_選べない経験(argExp, argChara)
#FUNCTION
#DIM argExp
#DIM argChara
SELECTCASE EXPNAME:argExp
	CASE "", "肛射経験", "膣射経験"
		RETURNF 1
	;女性器なしは不可
	CASE "レズ経験", "Ｖ経験", "Ｖ絶頂経験", "Ｖ性交経験", "Ｖ拡張経験", "子宮口開発経験", "噴乳経験", "パイズリ経験"
		SIF !(TALENT:argChara:性別 & 1)
			RETURNF 1
	;女性器ありは不可
	CASE "ゲイ経験"
		SIF TALENT:argChara:性別 & 1
			RETURNF 1
	;男性器なしは不可
	CASE "挿入経験", "射精経験"
		SIF !(TALENT:argChara:性別 & 0)
			RETURNF 1
	;時間停止がらみは不可
	CASE "時姦経験", "無自覚Ｃ経験", "無自覚Ｖ経験", "無自覚Ａ経験", "無自覚Ｂ経験", "無自覚Ｍ経験", "無自覚絶頂経験"
		RETURNF 1
ENDSELECT
SELECTCASE argExp
	;生活系
	CASE 70 TO 89
		RETURNF 1
	;行為系はすり合わせが面倒
	CASE 100 TO 109
		RETURNF 1
ENDSELECT
RETURNF 0
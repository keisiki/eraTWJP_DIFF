;-----------------------------------------------------------
;キャラ選択用関数
;-----------------------------------------------------------
;-----------------------------------------------------------
;CALLNAMEによる選択肢
;あなたとモブ以外のすべてをリストアップ
;-----------------------------------------------------------
@ASK_CALLNAME
;生成
FOR LOCAL, 0, CHARANUM
	IF !IS_NAMED(LOCAL)
		CONTINUE
	ENDIF
	PRINTFORMLC [{LOCAL, 3}] %CALLNAME:LOCAL%
NEXT
PRINTL
DRAWLINE
PRINTL [999] 戻る
;入力受付
DO
	INPUT
	SIF RESULT == 999 || IS_NAMED(RESULT)
		RETURN RESULT
	CLEARLINE 1
LOOP 1
;-----------------------------------------------------------
;モブとあなたと出禁以外なら1を返す
;-----------------------------------------------------------
@IS_NAMED(ARG)
#FUNCTION
	IF ARG == MASTER || TALENT:ARG:行きずり || CFLAG:ARG:出禁
		RETURNF 0
	ENDIF
RETURNF 1
;-----------------------------------------------------------
;10個までの選択肢
;-----------------------------------------------------------
@ASK_CHOICES10(argChoices:0="", argChoices:1="", argChoices:2="", argChoices:3="", argChoices:4="", argChoices:5="", argChoices:6="", argChoices:7="", argChoices:8="", argChoices:9="")
#DIMS argChoices, 10
CALL ASK_CHOICES(argChoices)
RETURN RESULT

;-----------------------------------------------------------
;参照渡しで数無制限の選択肢
;空欄は改行するが連続して改行しない、全て空欄ならTHROW
;-----------------------------------------------------------
@ASK_CHOICES(refChoices, argCanselNo=-1,argCanselText="キャンセル")
#DIMS REF refChoices, 0
#DIM DYNAMIC argCanselNo
#DIMS DYNAMIC argCanselText
#DIMS html
#DIM skip
;生成
VARSET skip
VARSET html
FOR LOCAL, 0, VARSIZE("refChoices")
	;空欄
	IF refChoices:LOCAL == ""
		;空欄改行は連続しない
		IF !skip
			html += "<br>"
		ENDIF
		skip ++
	;中身あり
	ELSE
		html += HTML_SPACE(2) +HTML_BUTTON(refChoices:LOCAL, LOCAL, 3) + "<br>"
		skip = 0
	ENDIF
NEXT
SIF !skip
	html += "<br>"
SIF argCanselNo >= 0
	html += HTML_SPACE(2) + HTML_BUTTON(argCanselText, argCanselNo, 3) + "<br>"
SIF html == "<br>"
	THROW 選択肢が全て空です。
;表示
HTML_PRINT "<p align='left'><nobr>"+ "<br>" + html + "<br>" + "</nobr></p>"
;入力受付
DO
	INPUT
	SIF argCanselNo || (RESULT < VARSIZE("refChoices") && refChoices:RESULT != "")
		RETURN RESULT
	CLEARLINE 1
LOOP 1
;-----------------------------------------------------------
;参照渡しで選択肢をボタンで横にならべる
;空欄は無視、全て空欄ならTHROW
;-----------------------------------------------------------
@ASK_BUTTONS(refChoices)
#DIMS REF refChoices, 0
#DIMS DYNAMIC html
;生成
FOR LOCAL, 0, VARSIZE("refChoices")
	IF refChoices:LOCAL != ""
		html += HTML_SPACE(2) + HTML_BUTTON(refChoices:LOCAL, LOCAL)
	ENDIF
NEXT
SIF html == ""
	THROW 選択肢が全て空です。
;表示
HTML_PRINT "<p align='left'>" + html + "<br>" + "</p>"
;入力受付
DO
	INPUT
	SIF RESULT < VARSIZE("refChoices") && refChoices:RESULT != ""
		RETURN RESULT
	CLEARLINE 1
LOOP 1
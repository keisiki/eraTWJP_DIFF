;-----------------------------------------------------------
;陥落キャラの素質変更
;-----------------------------------------------------------
@周回特典
PRINTL
PRINTL
PRINTL ＊周回特典＊
PRINTL 以前恋人にしたことのあるキャラの素質を変更することが出来ます
PRINTL 以前恋慕または愛欲を取得したことのあるキャラの一部の素質を変更することが出来ます
PRINTL
PRINTL
CALL 周回特典_キャラ選択
SELECTCASE RESULT
	CASE 999
		RETURN
	;CASE 997
	;	CALL SaveTalentSet
	CASE 998
		CALL LoadTalentSet
	CASEELSE
		IF GETBIT(CFLAG:RESULT:引き継ぎ, 1)
			CALL CUSTOM_TERMINAL(RESULT, 1)
		ELSEIF GETBIT(CFLAG:RESULT:引き継ぎ, 0)
			CALL LIMITED_CUSTOM(RESULT)
		ENDIF
ENDSELECT
RESTART

;-----------------------------------------------------------
;-----------------------------------------------------------
@周回特典_キャラ選択
;生成
LOCAL:1 = 0
FOR LOCAL, 0, CHARANUM
	IF !IS_NAMED(LOCAL)
		CONTINUE
	ENDIF
	SIF SuccSetting:LOCAL
		FONTBOLD
	IF GETBIT(CFLAG:LOCAL:引き継ぎ, 1)
		SETCOLOR C_PINK
		PRINTFORMLC [{LOCAL, 3}] %CALLNAME:LOCAL%
		LOCAL:1 ++
	ELSEIF GETBIT(CFLAG:LOCAL:引き継ぎ, 0)
		SETCOLOR C_YELLOW
		PRINTFORMLC [{LOCAL, 3}] %CALLNAME:LOCAL%
		LOCAL:1 ++
	ENDIF
NEXT
RESETCOLOR
FONTREGULAR
;その他の選択肢
PRINTL
DRAWLINE
;PRINTL [997] 引き継ぎ設定をセーブ
PRINTL [998] 引き継ぎ設定を一括ロード
DRAWLINE
PRINTL [999] 設定完了
PRINTL
;入力受付
DO
	INPUT
	SELECTCASE RESULT
	;CASE 997
	;	RETURN RESULT
	CASE 998
		RETURN RESULT
	CASE 999
		RETURN RESULT
	CASEELSE
		SIF IS_NAMED(RESULT) && (GETBIT(CFLAG:RESULT:引き継ぎ, 1) || GETBIT(CFLAG:RESULT:引き継ぎ, 0))
			RETURN RESULT
	ENDSELECT
	CLEARLINE 1
LOOP 1

;-----------------------------------------------------------
;-----------------------------------------------------------
@周回特典_適用
FOR LOCAL,1,CHARANUM
	SIF TCVAR:LOCAL:感度上昇部位
		TALENT:LOCAL:(TCVAR:LOCAL:感度上昇部位) ++
NEXT

;-----------------------------------------------------------
;表示名
;-----------------------------------------------------------
@周回特典_表示
#FUNCTIONS
FOR LOCAL, 1, CHARANUM
	IF IS_NAMED(LOCAL) && (GETBIT(CFLAG:LOCAL:引き継ぎ, 1) || GETBIT(CFLAG:LOCAL:引き継ぎ, 0))
		RETURNF "<font color='#FFCCFF'>" + "周回特典：素質変更が行えるキャラがいます" + "</font>"
	ENDIF
NEXT
RETURNF ""

;-----------------------------------------------------------
;保存
;-----------------------------------------------------------
;@SaveTalentSet
;#DIM C_ID
;FOR C_ID,1,CHARANUM
;	FOR LOCAL, 0, 160
;		TalentSet:C_ID:LOCAL = TALENT:C_ID:LOCAL
;	NEXT
;NEXT
;FLAG:引継ぎ設定保存済み = CHARANUM - 1
;PRINTFORMW 引継ぎ設定をセーブしました
;-----------------------------------------------------------
@SAVE_CUSTOM_TALENT(C_ID, ARGS)
#DIM C_ID
LOCALS = 現在の設定を保存
CALL ASK_M(@"現在の設定を保存",1, "保存した設定をリセット", SuccSetting:C_ID ,"キャンセル",1)
SELECTCASE RESULT
	CASE 0
		FOR LOCAL, 0, 160
			TalentSet:C_ID:LOCAL = TALENT:C_ID:LOCAL
		NEXT
		SuccSetting:C_ID = 1
		IF ARGS == "恋人"
			SuccBASE:C_ID:0 = MAXBASE:C_ID:0
			SuccBASE:C_ID:1 = MAXBASE:C_ID:1
			SuccSetting:C_ID = 2
			FOR LOCAL,0,100
				SuccABL:C_ID:LOCAL = ABL:C_ID:LOCAL
				SuccEXP:C_ID:LOCAL = EXP:C_ID:LOCAL
			NEXT
		ENDIF
		PRINTFORMW %CALLNAME:C_ID%の設定を保存しました。
	CASE 1
		VARSET TalentSet:C_ID:0, 0
		SuccSetting:C_ID = 0
		PRINTFORMW %CALLNAME:C_ID%の設定をリセットしました。
	CASE 2
ENDSELECT
;-----------------------------------------------------------
@LoadTalentSet
#DIM C_ID
;IF !FLAG:引継ぎ設定保存済み
;	THROW 引き継ぎ設定がありません。
;ENDIF
FOR C_ID, 1, CHARANUM
	SIF !SuccSetting:C_ID
		CONTINUE
	FOR LOCAL,0,160
		TALENT:C_ID:LOCAL = TalentSet:C_ID:LOCAL
	NEXT
	IF SuccSetting:C_ID == 2
		MAXBASE:C_ID:0 = SuccBASE:C_ID:0
		MAXBASE:C_ID:1 = SuccBASE:C_ID:1
		FOR LOCAL,0,100
			ABL:C_ID:LOCAL = SuccABL:C_ID:LOCAL
			EXP:C_ID:LOCAL = SuccEXP:C_ID:LOCAL
		NEXT
	ENDIF
	PRINTFORML %CALLNAME:C_ID%の引継ぎ設定をロードしました
NEXT

;-----------------------------------------------------------
;周回時恋慕特典によるカスタマイズ用
;-----------------------------------------------------------
@LIMITED_CUSTOM(ARG)
#DIMS choices, 10
#DIM part
;表示
PRINTL
PRINTL
PRINTFORML %NAME:ARG%
PRINTFORML 現在の体型: %GET_TALENTNAME(100,TALENT:ARG:100)%\@ TALENT:ARG:100 == 0? 普通# \@
PRINTFORML 現在のバストサイズ: %GET_TALENTNAME(105,TALENT:ARG:105)%\@ TALENT:ARG:105 == 0? 普乳# \@
FOR LOCAL, 101, 107
	IF LOCAL == 105
		CONTINUE
	ENDIF
	PRINTFORM %TALENTNAME:LOCAL%：
	SELECTCASE TALENT:ARG:LOCAL
		CASE -1
			PRINTFORM 鈍感　
		CASE 0
			PRINTFORM 普通　
		CASE 1
			PRINTFORM 敏感　
	ENDSELECT
NEXT
PRINTFORML 
DRAWLINE
;選択肢
VARSET choices
choices:0 '= "体型変更", "バストサイズ変更", "感度変更", "全部位敏感","設定保存"
CALL ASK_CHOICES(choices, 100, "終了")
SELECTCASE RESULT
	CASE 100
		RETURN
	CASE 0
		VARSET choices
		choices:0 '= "小人", "", "", "幼児体型", "小柄体型", "普通", "長身", "巨躯"
		CALL ASK_CHOICES(choices, 100, "戻る")
		SELECTCASE RESULT
			CASE 0, 3 TO 7
				TALENT:ARG:体型 = RESULT - 5
		ENDSELECT
	CASE 1
		VARSET choices
		choices:0 '= "絶壁", "貧乳", "普乳", "巨乳", "爆乳"
		CALL ASK_CHOICES(choices, 100, "戻る")
		SELECTCASE RESULT
			CASE 0 TO 4
				TALENT:ARG:バストサイズ = RESULT - 2
		ENDSELECT
	CASE 2
		VARSET choices
		FOR LOCAL, 1, 7
			IF LOCAL == 5
				CONTINUE
			ENDIF
			choices:LOCAL '= TALENTNAME:(LOCAL + 100)
		NEXT
		CALL ASK_CHOICES(choices, 100, "戻る")
		part = RESULT + 100
		SELECTCASE TALENTNAME:part
			CASE "Ｃ感度", "Ｖ感度", "Ａ感度", "Ｂ感度", "Ｍ感度"
				CALL ASK_M("鈍感",1,"普通",1,"敏感",1)
				TALENT:ARG:part = RESULT - 1
		ENDSELECT
	CASE 3
		FOR LOCAL, 101, 107
			IF LOCAL == 105
				CONTINUE
			ENDIF
			TALENT:ARG:LOCAL = 1
		NEXT
	CASE 4
		CALL SAVE_CUSTOM_TALENT(ARG, "恋慕")
ENDSELECT
RESTART
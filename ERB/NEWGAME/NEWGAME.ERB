;-----------------------------------------------------------
;はじめから
;-----------------------------------------------------------
@NEWGAME
ゲーム開始処理中 = 1
;本体バージョンを記録
SAVESTR:本体バージョン = %eraTW_Version%
;クラス関数初期化
CALL MAKE_ALL_CLASS_LIST
;デフォルトオプション
CALL DEFAULT_OPTION
;カリスマ初期化
CALL SET_CHARISMA("NEW")
PBAND = 12

;日付１日目
DAY = 1
DAY:2 = 1
DAY:3 = 1
FLAG:酒虫 = 1

;キャラ生成
CALL ADD_ALL_CHARACTERS
;MASTER生成
CALL NEWGAME_MAKE
;recolored map or default
CALL DEFAULT_MAP_COLOR
;住居設定
CALL SET_MAINHOME
;オプション
CALL NEWGAME_OPTION

;キャラ設定
FOR LOCAL,0,CHARANUM
	;開始位置
	CFLAG:LOCAL:現在位置 = CFLAG:LOCAL:初期位置
	;服装
	CALL SELECT_CLOTHES(LOCAL, "今日の衣装")
	CALL CTRL_CLOTHES_SET(LOCAL, "現在衣装の変更_普段着")
	CALL CLOTHES_SETTING_TRAIN(LOCAL)
	CALL 下着確認リセット(LOCAL)
	CVARSET BASE, GETNUM(BASE, "酒気"), 0
NEXT
;MASTER起床時間セット（7時）
TIME:3 = 420
;MASTER睡眠中フラグON
CALL SET_SLEEP(1,MASTER,0)
ゲーム開始処理中 = 0
RETURN 0

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;CALL		USER
;ARG		NONE
;RETURN		VOID
;COMMENT	
;キャラを追加したときにいちいちここをいじる必要の無いように
;キャラクタ数上限はERHファイルで定義
;-----------------------------------------------------------
@ADD_ALL_CHARACTERS
FOR LOCAL,CHARANUM,キャラクタ数上限
	SIF !EXISTCSV(LOCAL)
		CONTINUE
	ADDCHARA LOCAL
	CALL SET_MINIMUM_EXP_ALL(LOCAL)
	CALL KOJO_MULTIPLE(LOCAL)
	IF !EXP:LOCAL:キス経験
		TALENT:LOCAL:キス未経験 = 1
	ENDIF
NEXT


;-----------------------------------------------------------
;設定変更
;-----------------------------------------------------------
@NEWGAME_OPTION
#DIMS choices, 100
;初期化
;甲斐性なし、農民
SETBIT FLAG:1000, 1
SETBIT FLAG:1000, 2
;TINKO
SIF !TALENT:MASTER:形状
	TALENT:MASTER:形状 = 3
;服
SIF !CFLAG:MASTER:基本服装セット
	CALL MASTER_CLOTHES_INIT
;
SIF !ゲーム開始処理中
	ニューゲーム特典 '= "なし"
;選択肢
VARSET choices
PRINTL
PRINTL
PRINTL ゲーム開始前に各種設定を変更することができます。
DRAWLINE
;あなた設定
SIF !FLAG:なりきり
	choices:0 '= @"%CALLNAME:MASTER%をカスタマイズしなおす"
SIF HAS_PENIS(MASTER)
	choices:1 '= @"%CALLNAME:MASTER%の男性器は%TINKO_SIMPLE(MASTER)%です"
choices:2 '= @"%CALLNAME:MASTER%の服装は%MASTER_CLOTHES_NAME(CFLAG:MASTER:基本服装セット)%です"
IF FLAG:甲斐性無
	choices:3 '= @"%CALLNAME:MASTER%には告白をするような度胸はありません"
ELSE
	choices:3 '= @"%CALLNAME:MASTER%の度胸はふつうです"
ENDIF
IF FLAG:地主
	choices:4 '= @"%CALLNAME:MASTER%は%CALLNAME:(FLAG:地主)%の下で働く農民です"
ELSE
	choices:4 '= @"%CALLNAME:MASTER%は身軽な自由人です"
ENDIF
choices:5 '= @"%CALLNAME:MASTER%は%GET_MAPNAME(MAIN_MAP)%の%STR:(デフォルト初期位置 + (MAIN_MAP ? 8000 # 0) )%に住みます"
;幻想郷
choices:10 '= @"各キャラの処女・非処女を変更できます"
choices:11 '= @"各キャラの体型を変更できます"
;特典
IF ゲーム開始処理中
	choices:20 '= NEWGAMEBONUS_TEXT()
ELSEIF ゲーム引き継ぎ処理中
	choices:20 '= 周回特典_表示()
ENDIF

CALL ASK_CHOICES(choices, 100, "終了して開始する")
SELECTCASE RESULT
	CASE 0
		IF !FLAG:なりきり
			CALL CUSTOM_TERMINAL(MASTER)
			CALL SET_MAINHOME
		ENDIF
	CASE 100
		CALL VIRGIN_SET_APPLY
		CALL SET_BODY_APPLY
		CALL NEWGAME_BONUS_APPLY
		CALL SET_APPLY_NOUMIN
		;CALL 周回特典_適用
		ITEM:安物食材 = 10
		RETURN
	CASE 1
		MAIN_MAP = 0
		SIF HAS_PENIS(MASTER)
			CALL TINKO_DECIDE(MASTER)
	CASE 2
		CALL MASTER_CLOTHES
	CASE 3
		CALL KAISYONASI
	CASE 4
		CALL RESET_NOUMIN
		CALL SET_NOUMIN
	CASE 5
		CALL SET_MAINHOME
	CASE 10
		CALL VIRGIN_SET
	CASE 11
		CALL BODY_SET
	CASE 20
		IF ゲーム開始処理中
			CALL NEWGAME_BONUS_SELECT
		ELSEIF ゲーム引き継ぎ処理中
			CALL 周回特典
		ENDIF
ENDSELECT
RESTART

;-----------------------------------------------------------
;設定変更
;-----------------------------------------------------------
@NEWGAME_VERUP
;恋人修正済フラグを立てる
SETBIT FLAG:1000, 3
SETBIT FLAG:1000, 4
CALL VERSION_UP
PRINTW ゲーム開始処理終了
SAVEDATA 99, "はじめから"
PRINTL オートセーブしました
PRINTW ゲームのアップデート処理に移りますか？
CALL ASK_YN()
IF !RESULT
	CALL UPDATE
ELSE
	PRINTL アップデートをスキップしました
	PRINTW 起床前のSHOP画面からアップデートしてください
ENDIF

;=========================================================
;	連番画像アニメーション
;
;	sRes_Name	：リソースの名前文字列　この名前の後に連番数字を付ける
;	iStart		：開始番号
;	iEnd		：開始からの描画枚数
;	iAnime_Num	：全体のアニメーション繰り返し回数 省略0でオプション設定したフラグ回数になります
;	iRollback	：ロールバックさせるかフラグ
;	iWait		：1枚の表示時間
;	iSize		：一行の高さに対するサイズ％数値　縦基準　縦横比維持　省略で1000(10倍)
;	sAlign		：align属性　省略で左寄せ　left, center, rightの三種が指定できます
;=========================================================
@PRINT_IMAGE_ANIMATION(sRes_Name,iStart=0,iEnd=4,iAnime_Num=0,iRollback=1,iWait=100,iSize=1000,sAlign="left")
#DIMS sRes_Name
#DIM iStart
#DIM iEnd
#DIM iAnime_Num
#DIM iRollback
#DIM iWait
#DIM iSize
#DIMS sAlign


SIF !FLAG:画像表示 || sRes_Name == ""
	RETURN RESULT

SIF !FLAG:アニメーション
	RETURN RESULT

SIF !iAnime_Num
	iAnime_Num = FLAG:アニメーション

FOR LOCAL,0,iAnime_Num
	FOR LOCAL:1,iStart,(iStart+iEnd)
		CALL PRINT_IMAGE(@"%sRes_Name%{LOCAL:1}",iSize,sAlign)
		TWAIT iWait, 0
		CLEARLINE iSize/100
	NEXT
	
	;ロールバックさせるか
	SIF !iRollback
		CONTINUE

	FOR LOCAL:1,(iStart+iEnd)-2,iStart-1,-1
		CALL PRINT_IMAGE(@"%sRes_Name%{LOCAL:1}",iSize,sAlign)
		TWAIT iWait, 0
		CLEARLINE iSize/100
	NEXT
NEXT
RETURN RESULT

;=========================================================
;	正常位のアニメーション画像表示
;
;	sRes_Name	：最後に表示する画像のリソース名
;	iSize		：一行の高さに対するサイズ％数値　縦基準　縦横比維持　省略で1000(10倍)
;	sAlign		：align属性　省略で左寄せ　left, center, rightの三種が指定できます
;=========================================================
@PRINT_IMAGE_ANIMATION_正常位(sRes_Name,iSize=1000,sAlign="left")
#DIMS sRes_Name
#DIM iSize
#DIMS sAlign
#DIMS insType

#DIM iStart
#DIMS sPrint_Res_Name
#DIMS sAnime_Res_Name

SIF !FLAG:画像表示 || sRes_Name == "" || !FLAG:挿絵表示
	RETURN RESULT

IF STRCOUNT(sRes_Name,"_射精")
	sPrint_Res_Name = %sRes_Name%_{FLAG:射精画像種類}
ELSE
	sPrint_Res_Name = %sRes_Name%
ENDIF

IF !FLAG:アニメーション
	CALL PRINT_IMAGE(sPrint_Res_Name,iSize,sAlign)
	RETURN RESULT
ENDIF

sAnime_Res_Name = %\@ STRCOUNT(sRes_Name,"Ａ") ? Ａ # \@%正常位_挿入_

IF STRCOUNT(sRes_Name,"_射精") || STRCOUNT(sRes_Name,"_絶頂")
	iStart = 1
ELSE
	iStart= 0
ENDIF

CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,iStart,4-iStart,FLAG:アニメーション,1,,iSize,sAlign)
CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,iStart,4-iStart,1,0,,iSize,sAlign)

IF STRCOUNT(sRes_Name,"_射精")
	sAnime_Res_Name = %sRes_Name%_{FLAG:射精画像種類}_
	CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,0,2,FLAG:アニメーション,0,,iSize,sAlign)
	CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,2,2,FLAG:アニメーション,1,,iSize,sAlign)
ENDIF
CALL PRINT_IMAGE(sPrint_Res_Name,iSize,sAlign)
RETURN RESULT

;=========================================================
;	後背位のアニメーション画像表示
;	コマ数など変わったので別で
;
;	sRes_Name	：最後に表示する画像のリソース名
;	iTarget		：射精した人物(0:あなた, 1:それ以外)
;	iSize		：一行の高さに対するサイズ％数値　縦基準　縦横比維持　省略で1000(10倍)
;	sAlign		：align属性　省略で左寄せ　left, center, rightの三種が指定できます
;=========================================================
@PRINT_IMAGE_ANIMATION_後背位(sRes_Name,iTarget=0,iSize=1000,sAlign="left")
#DIMS sRes_Name
#DIM iTarget
#DIM iSize
#DIMS sAlign

#DIMS sPrint_Res_Name
#DIMS sAnime_Res_Name
#DIM iAnimeCon
#DIMS ins_Type

SIF !FLAG:画像表示 || sRes_Name == "" || !FLAG:挿絵表示
	RETURN RESULT

IF STRCOUNT(sRes_Name,"_射精")
	sPrint_Res_Name = %sRes_Name%_{FLAG:射精画像種類}
	SIF NOWEX:MASTER:射精 > 1
		sPrint_Res_Name = %sRes_Name%_{FLAG:射精画像種類}_9
ELSE
	sPrint_Res_Name = %sRes_Name%
ENDIF

IF !FLAG:アニメーション
	CALL PRINT_IMAGE(sPrint_Res_Name,iSize,sAlign)
	RETURN RESULT
ENDIF

IF STRCOUNT(sRes_Name,"_挿入")
	
	iAnimeCon = FLAG:アニメーション/2?FLAG:アニメーション/2#1
	CALL PRINT_IMAGE_ANIMATION(@"%sRes_Name%_",0,6,iAnimeCon,1,,iSize,sAlign)
		
	iAnimeCon = (FLAG:アニメーション-iAnimeCon)?FLAG:アニメーション-iAnimeCon#1
	CALL PRINT_IMAGE_ANIMATION(@"%sRes_Name%2_",0,12,iAnimeCon,0)
ELSE
	IF STRCOUNT(sRes_Name,"_絶頂")
		iAnimeCon = FLAG:アニメーション/2?FLAG:アニメーション/2#1
		ins_Type = %\@ STRCOUNT(sRes_Name,"Ａ") ? Ａ # \@%
		CALL PRINT_IMAGE_ANIMATION(@"%ins_Type%後背位_挿入2_",0,12,iAnimeCon,0)
	ENDIF
	
	CALL PRINT_IMAGE_ANIMATION(@"%ins_Type%後背位_絶頂_",0,4,FLAG:アニメーション,0)
	CALL PRINT_IMAGE_ANIMATION(@"%ins_Type%後背位_絶頂_",0,2,1,0)
ENDIF
IF STRCOUNT(sRes_Name,"_射精")
	sAnime_Res_Name = %sRes_Name%_{FLAG:射精画像種類}_
	CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,0,8,1,0,,iSize,sAlign)
	CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,6,2,FLAG:アニメーション,1,,iSize,sAlign)
	
	;大量なら追加で
	IF iTarget == 0
		IF NOWEX:MASTER:射精 > 1
			CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,8,2,FLAG:アニメーション,1,,iSize,sAlign)
		ENDIF
	ELSE
		IF NOWEX:TARGET:射精 > 1
			CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,8,2,FLAG:アニメーション,1,,iSize,sAlign)
		ENDIF
	ENDIF
	
ENDIF
CALL PRINT_IMAGE(sPrint_Res_Name,iSize,sAlign)

RETURN RESULT
;=========================================================
;	射精後のアニメーション画像表示
;
;	sRes_Name	：最後に表示する画像のリソース名
;	iTarget		：射精した人物(0:あなた, 1:それ以外)
;	iSize		：一行の高さに対するサイズ％数値　縦基準　縦横比維持　省略で1000(10倍)
;	sAlign		：align属性　省略で左寄せ　left, center, rightの三種が指定できます
;=========================================================
@PRINT_IMAGE_ANIMATION_射精後(sRes_Name="正常位_中出し",iTarget=0,iSize=1000,sAlign="left")
#DIMS sRes_Name
#DIM iTarget
#DIM iSize
#DIMS sAlign

#DIM iWait
#DIMS sAnime_Res_Name
#DIMS sType
#DIM SAVE_RESULT

SIF !FLAG:画像表示 || sRes_Name == "" || !FLAG:挿絵表示
	RETURN RESULT

IF !FLAG:アニメーション
	CALL PRINT_IMAGE(sRes_Name,iSize,sAlign)
	RETURN RESULT
ENDIF
SAVE_RESULT = RESULT
SPLIT sRes_Name,"_",sType

IF STRCOUNT(sType,"後背位") 
	IF iTarget == 0
		sAnime_Res_Name = %sType%_射精_{FLAG:射精画像種類}_\@NOWEX:MASTER:射精>1?9#7\@
	ELSE
		sAnime_Res_Name = %sType%_射精_{FLAG:射精画像種類}_\@NOWEX:TARGET:射精>1?9#7\@
	ENDIF
ELSE
	sAnime_Res_Name = %sType%_射精_{FLAG:射精画像種類}_3
ENDIF

iWait = 500
CALL PRINT_IMAGE(@"%sAnime_Res_Name%",iSize,sAlign)
sAnime_Res_Name = %sType%_挿入_
TWAIT iWait, 0
CLEARLINE iSize/100

iWait = 100
FOR LOCAL,3,0,-1
	CALL PRINT_IMAGE(@"%sAnime_Res_Name%{LOCAL}",iSize,sAlign)
	TWAIT iWait, 0
	CLEARLINE iSize/100
NEXT

;後背位のみアニメ
IF STRCOUNT(sType,"後背位") 
	sAnime_Res_Name = %sType%_射精後_
	CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,0,2,1,0,iWait,iSize,sAlign)
	CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,2,2,0,0,iWait,iSize,sAlign)
ENDIF

CALL PRINT_IMAGE(sRes_Name,iSize,sAlign)

RESULT = SAVE_RESULT
RETURN RESULT
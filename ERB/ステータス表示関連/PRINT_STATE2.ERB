
@PRINT_STATE_JUEL(選択中キャラID)
#DIM 選択中キャラID
#DIM 表示項目数

PRINTL
PRINTL □ 宝珠 □---------------------------------------------------------------------------
表示項目数 = 0
FOR LOCAL, 0, 22
	;PALAM.CSVに名称が登録されていないとCONTINUE
	SIF !STRLENS(PALAMNAME:LOCAL)
		CONTINUE
	;1000,1000000,単位で区切る
	LOCALS = {JUEL:選択中キャラID:LOCAL}
	SIF JUEL:選択中キャラID:LOCAL > 1000000
		LOCALS = {JUEL:選択中キャラID:LOCAL / 1000}k
	SIF JUEL:選択中キャラID:LOCAL > 1000000000
		LOCALS = {JUEL:選択中キャラID:LOCAL / 1000000}m
	PRINTFORM %PALAMNAME:LOCAL,6%の珠:%LOCALS,7,LEFT%
	表示項目数 += 1
	;改行or表示位置あけ
	SIF 表示項目数 % 4 == 0
		PRINTL 
NEXT
;否定の珠
LOCALS = {JUEL:選択中キャラID:100}
SIF JUEL:選択中キャラID:100 > 1000000
	LOCALS = {JUEL:選択中キャラID:100 / 1000}k
PRINTFORM %PALAMNAME:100,6%の珠:%LOCALS,7,LEFT%

@PRINT_STATE_EX(選択中キャラID)
#DIM 選択中キャラID
#DIM 表示項目数

SIF EX:選択中キャラID:Ｃ絶頂 + EX:選択中キャラID:Ｖ絶頂 + EX:選択中キャラID:Ａ絶頂 + EX:選択中キャラID:Ｂ絶頂 + EX:選択中キャラID:Ｍ絶頂 + EX:選択中キャラID:噴乳 + EX:選択中キャラID:射精 == 0
	RETURN 0
表示項目数 = 0
PRINTL
PRINTL □ 絶頂回数 □-----------------------------------------------------------------------
FOR LOCAL, 0, VARSIZE("EX")
	SIF !EX:選択中キャラID:LOCAL
		CONTINUE
	SIF !GROUPMATCH(EXNAME:LOCAL, "Ｃ絶頂", "Ｖ絶頂", "Ａ絶頂", "Ｂ絶頂", "Ｍ絶頂", "噴乳", "射精", "放尿")
		CONTINUE
	PRINTFORM %EXNAME:LOCAL,12%:{EX:選択中キャラID:LOCAL,5,LEFT} 
	表示項目数 ++
	SIF 表示項目数 % 4 == 0
		PRINTL
NEXT


@PRINT_STATE_PERSONAL(選択中キャラID)
#DIM 選択中キャラID
#DIMS KARADAMEATE
PRINTL
PRINT □ 
CALL PRINTBUTTON_EX("個人情報 ",169,TFLAG:169)
PRINTL □-----------------------------------------------------------------------
PRINTL  
IF !TFLAG:169
PRINTFORML 性格：%PERSONALITY_TYPE(選択中キャラID)%
	IF 選択中キャラID == MASTER
		IF CFLAG:MASTER:睡眠
			PRINTFORML 昨日の就寝時刻：%時刻表示(CFLAG:MASTER:就寝時間 - DAY * 1440)%
		ELSE
			PRINTFORML 昨日の就寝時刻：%時刻表示(CFLAG:MASTER:就寝時間 + 1440 - DAY * 1440)%
		ENDIF
		PRINTFORML 起床時刻：%時刻表示(TIME:3)%
		IF FLAG:地主
			PRINTFORML 
			CALL SHOW_NOUMIN
		ENDIF
		IF TRAP_POINT:1 || TRAP_POINT:2 || TRAP_POINT:3
			PRINTL 
			PRINTFORML ◆罠の設置状況
			FOR LOCAL, 1, 4
				IF TRAP_POINT:LOCAL
					PRINTFORML 　罠{LOCAL}の設置場所：%猟場名(TRAP_POINT:LOCAL)%
					PRINTFORML 　　 　　設置日：%PRINT_DATE_F(TRAP_SETUPDATA:LOCAL)%
				ENDIF
			NEXT
		ENDIF
		IF MUSHI_TRAP
			PRINTFORML ◆虫トラップの設置状況
			PRINTFORML 　 　　設置場所：%NAME_FROM_PLACE((GET_MUSHIDATA(MUSHI_TRAP, "場所")))%（%STR:((GET_MUSHIDATA(MUSHI_TRAP, "場所")) / 100 + 5000)%）
		ENDIF
	ELSE
		PRINT 活動時間帯:
		IF CFLAG:選択中キャラID:神社在住 || CFLAG:MASTER:現在位置 == OMANEKIBEYA()
			CALL 起床時間表示(選択中キャラID)
		ELSE
			CALL 滞在時間表示(選択中キャラID)
		ENDIF
		IF CFLAG:選択中キャラID:よく行く地域 >= 0
			PRINTFORML よく行く地域：%GET_MAPNAME(CFLAG:選択中キャラID:よく行く地域)%
		ENDIF
		IF CFLAG:選択中キャラID:自宅位置 >= 10 && !CFLAG:選択中キャラID:神社在住
			PRINT 自宅位置：
			LOCAL = CFLAG:選択中キャラID:自宅位置 / 100 + 5000
			PRINTFORM %STR:LOCAL% 
			LOCAL = CFLAG:選択中キャラID:自宅位置 / 10 + 6000
			PRINTFORML 【 %STR:LOCAL% 】
		ELSEIF CFLAG:選択中キャラID:神社在住
			PRINT 自宅位置：
			LOCAL = CFLAG:選択中キャラID:自宅位置 / 100 + 5000
			PRINTFORM %STR:LOCAL% 
			LOCAL = CFLAG:選択中キャラID:初期位置 + 8000
			PRINTFORML 【 %STR:LOCAL% 】
		ELSE
			PRINTL 自宅位置：■■■■■ 【 ■■■■■ 】
		ENDIF
		PRINT 仕事情報：
		PRINTFORML %CSTR:選択中キャラID:仕事情報%
		PRINT 　職場　：
		IF TALENT:選択中キャラID:風俗嬢
			PRINTFORML 旧地獄温泉
		ELSE
			PRINTFORML %CSVCSTR(選択中キャラID, 2)%
		ENDIF
	ENDIF
ENDIF
PRINTL 　
IF 選択中キャラID == RANDOM_CHARANUM
	;現状、複雑な種族分岐などは無しで
	PRINTL □ キャラ紹介 □---------------------------------------------------------------------
	PRINTL 
	PRINTFORML ～\@ TALENT:選択中キャラID:風俗嬢 ? # モブ \@%CSTR:選択中キャラID:モブ子種族%～　●能力：%CSTR:RANDOM_CHARANUM:モブ子能力%
	PRINTFORML %CSTR:RANDOM_CHARANUM:モブ子プロフィール１%
	PRINTFORML %CSTR:RANDOM_CHARANUM:モブ子プロフィール２%
ELSEIF CSVCSTR(選択中キャラID, 10) != ""
	PRINTL □ キャラ紹介 □---------------------------------------------------------------------
	PRINTL 
	;キャラ紹介文を口上の展開によって改変したい場合「@CHARA_INFO_KOJO_KX」を使用する
	RESULTS =
	;口上の存在判定 and RESULTSに文字列代入
	IF CFLAG:選択中キャラID:口上セレクタ
		TRYCALLFORM M_KOJO_K{選択中キャラID}_{CFLAG:選択中キャラID:口上セレクタ}
	ELSE
		TRYCALLFORM M_KOJO_K{選択中キャラID}
	ENDIF
	TRYCALLFORM CHARA_INFO_KOJO%RESULTS%_K{選択中キャラID}
	IF !RESULT
		FOR LOCAL,10,20
			SIF CSVCSTR(選択中キャラID, LOCAL) != ""
				PRINTFORML %CSVCSTR(選択中キャラID, LOCAL,)%
		NEXT
	ENDIF
ENDIF
SETCOLOR C_YELLOW
IF 選択中キャラID == MASTER && FLAG:甲斐性無
	PRINTFORML どうしようもない甲斐性なしだ
	PRINTFORML 
ENDIF
SETCOLOR C_PINK
IF CFLAG:選択中キャラID:引き継ぎ
	SIF GETBIT (CFLAG:選択中キャラID:引き継ぎ, 0)
		PRINTFORML %CALLNAME:MASTER%と不思議な縁がある
	SIF GETBIT (CFLAG:選択中キャラID:引き継ぎ, 1)
		PRINTFORML %CALLNAME:MASTER%ととても強い縁がある
ENDIF
IF 選択中キャラID == MASTER
	IF TALENT:選択中キャラID:恋人
		LOCAL:3 = 1
		FOR LOCAL:1 ,1 , CHARANUM
			IF TALENT:(LOCAL:1):恋人
				SIF !(LOCAL:3 % 15)
					PRINTL 
				LOCAL:3 ++
				PRINTFORM ［%CALLNAME:(LOCAL:1)%］
			ENDIF
		NEXT
		PRINTL と恋仲である
	ENDIF
ENDIF
SIF CFLAG:選択中キャラID:清い交際
	PRINTFORML 清いお付き合いを経て%CALLNAME:MASTER%と恋仲になった
SIF CFLAG:選択中キャラID:既成事実 & 合意_キス
	PRINTFORML %CALLNAME:MASTER%に唇を許している
IF CFLAG:選択中キャラID:既成事実 & 合意_うふふ
	PRINTFORML %CALLNAME:MASTER%に体を許している
ELSEIF CFLAG:選択中キャラID:ヤラせちゃった
	PRINTFORML 勢いで行為に及んでしまったことはあるが、%CALLNAME:MASTER%に体を許したつもりはない
ENDIF
SIF TALENT:選択中キャラID:処女 && TALENT:選択中キャラID:ヤリマン
	PRINTFORML 一応処女だが、求められれば簡単に体を開くだろう
IF CFLAG:選択中キャラID:体目当て
	IF TALENT:選択中キャラID:体型 == -2
		KARADAMEATE = その幼い
	ELSEIF TALENT:選択中キャラID:バストサイズ > 0
		KARADAMEATE = その豊満な
	ELSEIF TALENT:選択中キャラID:処女 > 0
		KARADAMEATE = その穢れない
	ELSE
		KARADAMEATE = その瑞々しい
	ENDIF
	PRINTFORML %CALLNAME:MASTER%に%KARADAMEATE%肢体を狙われている
ENDIF
RESETCOLOR
IF TALENT:選択中キャラID:恋慕
	IF TALENT:選択中キャラID:無知
		PRINTFORML %CALLNAME:選択中キャラID%は%CALLNAME:MASTER%のお嫁さんになると密かに決めている
	ELSEIF !CFLAG:選択中キャラID:既成事実 & 合意_うふふ
		IF TALENT:選択中キャラID:貞操 > 0
			PRINTFORML 肉体関係などふしだらな事と考えているにも関わらず、%CALLNAME:選択中キャラID%は心の奥底では%CALLNAME:MASTER%と交わりたいと思っている
		ELSE
			PRINTFORML %CALLNAME:MASTER%が未だに手をつけないことに苛立ちと不安を感じている
		ENDIF
	ENDIF
ENDIF
IF TALENT:選択中キャラID:妊娠願望
	IF !CFLAG:選択中キャラID:面識
	ELSEIF CFLAG:選択中キャラID:生解禁 == 2
		PRINTFORML %CALLNAME:MASTER%の子供が欲しいと思っている
	ELSE
		PRINTFORML はっきりと意識してはいないが、%CALLNAME:MASTER%の子供が欲しいと感じている
	ENDIF
ENDIF
CALL Valentine_History(選択中キャラID)
PRINTFORML 
PRINTL □ 弾幕スキル □---------------------------------------------------------------------
CALL DANMAKU_OPPONENT_STATUS(選択中キャラID, 1)

@滞在時間表示(選択中キャラID)
#DIM 選択中キャラID

IF CFLAG:選択中キャラID:来訪時間 < CFLAG:選択中キャラID:帰宅時間
	PRINTFORML {CFLAG:選択中キャラID:来訪時間 / 60}時～{CFLAG:選択中キャラID:帰宅時間 / 60}時
ELSE
	PRINTFORML {CFLAG:選択中キャラID:来訪時間 / 60}時～翌 {CFLAG:選択中キャラID:帰宅時間 / 60}時
ENDIF



@起床時間表示(選択中キャラID)
#DIM 選択中キャラID

IF CFLAG:選択中キャラID:起床時間 < CFLAG:選択中キャラID:就寝時間
	PRINTFORML {CFLAG:選択中キャラID:起床時間 / 60}時～{CFLAG:選択中キャラID:就寝時間 / 60}時
ELSE
	PRINTFORML {CFLAG:選択中キャラID:起床時間 / 60}時～翌 {CFLAG:選択中キャラID:就寝時間 / 60}時
ENDIF



@肉体情報(選択中キャラID)
#DIM 選択中キャラID
VARSET LOCALS
VARSET LOCAL
PRINTL
PRINTL □ 肉体情報 □-----------------------------------------------------------------------
PRINTL 
PRINTL 【口】　
PRINTFORM ファーストキス履歴：
;IF !EXP:選択中キャラID:キス経験
IF TALENT:選択中キャラID:キス未経験
	SELECTCASE CFLAG:選択中キャラID:ファーストキス喪失対象
		CASE 0
			CALL COLORMESSAGE(@"まだ誰にも唇を許していない",C_YELLOW,1)
		CASE 1
			CALL COLORMESSAGE(@"%CSTR:選択中キャラID:ファーストキス喪失履歴%",C_PINK,1)
		CASE 2
			CALL COLORMESSAGE(@"まだ誰にも唇を許していない",C_YELLOW,1)
			PRINTFORM と思っているが、実は
			CALL COLORMESSAGE(@"%CSTR:選択中キャラID:ファーストキス喪失履歴%",C_PINK,1)
	ENDSELECT
ELSEIF CFLAG:選択中キャラID:ファーストキス喪失記念日
	IF STRFIND(CSTR:選択中キャラID:ファーストキス喪失履歴, "￥ＢＲ") > 0
		LOCAL = RESULT
		SPLIT CSTR:選択中キャラID:ファーストキス喪失履歴, "￥ＢＲ", LOCALS
		CALL COLORMESSAGE(@"%LOCALS:0%",C_PINK,1)
		PRINTFORM と思っているが、実は
		CALL COLORMESSAGE(@"%LOCALS:1%",C_PINK,1)
		RESULT = LOCAL
		VARSET LOCALS
	ELSE
		CALL COLORMESSAGE(@"%CSTR:選択中キャラID:ファーストキス喪失履歴%",C_PINK,1)
	ENDIF
ELSEIF 選択中キャラID != MASTER
	CALL COLORMESSAGE(@"%CALLNAME:MASTER%に出会う前に失っている",C_AQUA,1)
	;だと思うんだけど…
ENDIF


SIF EXP:選択中キャラID:キス経験
	PRINTFORML 今まで{EXP:選択中キャラID:キス経験}回の口づけを経験した
SIF CFLAG:選択中キャラID:累計精飲
	PRINTFORML 今まで{CFLAG:選択中キャラID:累計精飲}の精液を飲み干した
IF EXP:選択中キャラID:精飲経験 > 300 && ABL:選択中キャラID:精液中毒 >= 10
	PRINTFORML 精液を飲み下すだけで完全に発情する身体になっている
ELSEIF EXP:選択中キャラID:精飲経験 > 150 && ABL:選択中キャラID:精液中毒 >= 5
	PRINTFORML 精液を味わうと身体が疼いてしまう
ELSEIF TALENT:選択中キャラID:精愛味覚
	PRINTFORML 精液の味に興奮を禁じ得ない
ELSEIF ABL:選択中キャラID:精液中毒 >= 3
	PRINTFORML 精液を抵抗なく飲むことが出来る
ENDIF
SIF GETBIT (CFLAG:選択中キャラID:引き継ぎ, 6)
	PRINTFORML どうすれば感じるのか%CALLNAME:MASTER%に知り尽くされている
IF 選択中キャラID == MASTER
	IF ABL:選択中キャラID:舌 >= 5
		PRINTFORML 卓越した舌技で少女を虜にする
	ELSEIF ABL:選択中キャラID:舌 >= 3
		PRINTFORML 巧みな舌使いで少女を悦ばせる
	ENDIF
ENDIF
PRINTL 
PRINTL 【胸】
SELECTCASE ABL:選択中キャラID:Ｂ感覚
	CASE IS >= 10
		PRINTL 感度抜群　
	CASE IS >= 5
		PRINTL 感度良好　
ENDSELECT
IF TALENT:選択中キャラID:淫乳
	PRINT 完全に調教されつくした
	IF TALENT:選択中キャラID:バストサイズ < 0
		PRINT 胸
	ELSE
		PRINT 乳房
	ENDIF
	PRINTFORML はひと撫でするだけで腰が砕けるほどになっている
ENDIF

SIF GETBIT (CFLAG:選択中キャラID:引き継ぎ, 5)
	PRINTFORML どうすれば感じるのか%CALLNAME:MASTER%に知り尽くされている
SIF CFLAG:選択中キャラID:累計ぶっかけ
	PRINTFORML 今まで{CFLAG:選択中キャラID:累計ぶっかけ}の精液にまみれた　
IF TALENT:選択中キャラID:母乳体質
	;PRINTFORML \@ ABL:選択中キャラID:噴乳中毒 >= 3 ? 甘みのあるミルクがたっぷりと詰まっている# 刺激してやれば甘みのあるミルクが滲みだすだろう\@
	PRINTFORML 刺激してやれば甘みのあるミルクが滲みだすだろう
ENDIF
SELECTCASE ABL:選択中キャラID:胸
	CASE IS >= 5
	CASE IS >= 3
ENDSELECT
PRINTL

PRINTL 【指】
SELECTCASE ABL:選択中キャラID:指
	CASE IS >= 5
		PRINTL 凶器　
	CASE IS >= 3
		PRINTL 達人　
ENDSELECT
IF 選択中キャラID == MASTER
	IF ABL:選択中キャラID:指 >= 5
		PRINTFORML しなやかな指での愛撫はたちまち少女を蕩けさせてしまう
	ELSEIF ABL:選択中キャラID:指 >= 3
		PRINTFORML 巧みな指裁きで少女を悦ばせる
	ENDIF
ENDIF
PRINTL
PRINTL 【アナル】
PRINTFORM アナル処女喪失履歴：
IF CSTR:選択中キャラID:アナル処女喪失履歴 != ""
	CALL COLORMESSAGE(@"%CSTR:選択中キャラID:アナル処女喪失履歴%",C_PINK,1)
ELSEIF !EXP:選択中キャラID:Ａ性交経験
	CALL COLORMESSAGE(@"まだ誰も受け入れていない",C_YELLOW,1)
ELSE
	PRINTFORML 
ENDIF
SELECTCASE ABL:選択中キャラID:Ａ感覚
	CASE IS >= 10
		PRINT 感度抜群　
	CASE IS >= 5
		PRINT 感度良好　
ENDSELECT
SIF GETBIT (CFLAG:選択中キャラID:引き継ぎ, 4)
	PRINTFORML どうすれば感じるのか%CALLNAME:MASTER%に知り尽くされている
IF 選択中キャラID
	SELECTCASE 挿入不可(選択中キャラID)
		CASE 3
			PRINTFORML とても%CALLNAME:MASTER%のペニスを受け入れられるようなサイズではない
			LOCAL:6 ++
		CASE 2
			IF EXP:選択中キャラID:Ａ経験 < 50 && TALENT:MASTER:禁断の知識
				PRINTFORML もう少しほぐさないと%CALLNAME:MASTER%のペニスは入らないだろう
				LOCAL:6 ++
			ELSEIF !TALENT:MASTER:禁断の知識
				PRINTFORML %CALLNAME:MASTER%のペニスを挿入するのは酷だろう
				LOCAL:6 ++
			ENDIF
		CASE 1
			IF EXP:選択中キャラID:Ａ経験 < 50 || !TALENT:MASTER:禁断の知識
				PRINTFORML もう少しほぐさないと%CALLNAME:MASTER%のペニスは入らないだろう
				LOCAL:6 ++
			ENDIF
		CASE -1
			IF ABL:選択中キャラID:アナル < 2
				PRINTFORML %CALLNAME:MASTER%のペニスでは少々サイズが合わない
				LOCAL:6 ++
			ENDIF
		CASE -2
			IF ABL:選択中キャラID:アナル < 3
				PRINTFORML 大き過ぎて入れてもあまり気持ちよくなさそうだ…
				LOCAL:6 ++
			ENDIF
	ENDSELECT
	SELECTCASE ABL:選択中キャラID:アナル
		CASE IS >= 5
			PRINTL 極上名器
			PRINTFORML 完全に%CALLNAME:MASTER%のカタチに作り替えられたアナルは肉ヒダを蠢かせて絡みついてくる
		CASE IS >= 3
			PRINTL 名器
			PRINTFORML 使い込んだ甲斐あって大変具合のいい穴になった
		CASE 2
			PRINTFORML %CALLNAME:MASTER%を受け入れることに慣れきっている
		CASE 1
			PRINTL ほぐれ始めて具合が良くなってきた
		CASE 0
			IF !CFLAG:選択中キャラID:アナル処女喪失記念日
;				PRINTL 未開通
			ELSEIF EXP:選択中キャラID:Ａ経験 < 20
					PRINTL まだ異物を受け入れることに慣れていない
			ENDIF
	ENDSELECT
ENDIF
SIF CFLAG:選択中キャラID:累計アナル精液
	PRINTFORML 今まで{CFLAG:選択中キャラID:累計アナル精液}の精液を注ぎ込まれた

SELECTCASE ABL:選択中キャラID:肛射中毒
	CASE IS >= 7
		PRINTFORML アナル内射精されると多幸感を感じる
	CASE IS >= 3
		PRINTFORML アナル内射精されると腸内が疼いてしまう
ENDSELECT

SIF ABL:選択中キャラID:Ａ感覚 >= 5 && CFLAG:選択中キャラID:溜まってる度 > 300 && 選択中キャラID
	PRINTFORML %CALLNAME:MASTER%に抱かれたくてうずうずしている

IF 充填率(選択中キャラID,2) >= 100
	PRINTFORML 直腸は重みを感じるほどの量の精液で完全に満たされている
ELSEIF 充填率(選択中キャラID,2) >= 50
	PRINTFORML 直腸で夥しい数の精子が蠢いている
ELSEIF 充填率(選択中キャラID,2) >= 20
	PRINTFORML 直腸で精子が元気に泳ぎ回っている
ENDIF

PRINTL
IF TALENT:選択中キャラID:2 & 1
	PRINTL 【膣】
	PRINTFORM 処女喪失履歴：
	IF CSTR:選択中キャラID:処女喪失履歴 != ""
		CALL COLORMESSAGE(@"%CSTR:選択中キャラID:処女喪失履歴%",C_PINK,1)
	ELSEIF !CSVTALENT(選択中キャラID, GETNUM(TALENT, "処女")) && TALENT:選択中キャラID:処女 == 1 && !TALENT:選択中キャラID:行きずり
		CALL COLORMESSAGE(@"世界線が変わり、誰も受け入れていない",C_YELLOW,1)
	ELSEIF !CSVTALENT(選択中キャラID, GETNUM(TALENT, "処女")) && TALENT:選択中キャラID:処女 == 0
		CALL COLORMESSAGE(@"%CALLNAME:MASTER%に出会う前に失っている",C_AQUA,1)
	ELSE
		IF TALENT:選択中キャラID:処女 == 1
			SIF ABL:選択中キャラID:Ｖ感覚 >= 5
				LOCALS:0 = にもかかわらず、刺激を与えられれば容易く絶頂するほど淫らに開発されている
			CALL COLORMESSAGE(@"まだ誰一人として受け入れたことがない%LOCALS:0%",C_YELLOW,1)
		ELSEIF TALENT:選択中キャラID:処女 == -1
			CALL COLORMESSAGE(@"すでに処女を奪われていることに気付いていない",C_PINK,1)
		ENDIF
	ENDIF

	SELECTCASE ABL:選択中キャラID:Ｖ感覚
		CASE IS >= 10
			PRINT 感度抜群　
		CASE IS >= 5
			PRINT 感度良好　
	ENDSELECT
	SIF GETBIT (CFLAG:選択中キャラID:引き継ぎ, 3)
	PRINTFORML どうすれば感じるのか%CALLNAME:MASTER%に知り尽くされている
	IF 選択中キャラID
		SELECTCASE 挿入不可(選択中キャラID)
			CASE 3
				PRINTFORML とても%CALLNAME:MASTER%のペニスを受け入れられるようなサイズではない
				LOCAL:5 ++
			CASE 2
				IF EXP:選択中キャラID:Ｖ経験 < 50 && TALENT:MASTER:禁断の知識
					PRINTFORML もう少しほぐさないと%CALLNAME:MASTER%のペニスは入らないだろう
					LOCAL:5 ++
				ELSEIF !TALENT:MASTER:禁断の知識
					PRINTFORML %CALLNAME:MASTER%のペニスを挿入するのは酷だろう
					LOCAL:5 ++
				ENDIF
			CASE 1
				IF EXP:選択中キャラID:Ｖ経験 < 50 || !TALENT:MASTER:禁断の知識
					PRINTFORML もう少しほぐさないと%CALLNAME:MASTER%のペニスは入らないだろう
					LOCAL:5 ++
				ENDIF
			CASE -1
				IF ABL:選択中キャラID:膣 < 2
					PRINTFORML %CALLNAME:MASTER%のペニスでは少々サイズが合わない
					LOCAL:5 ++
				ENDIF
			CASE -2
				IF ABL:選択中キャラID:膣 < 3
					PRINTFORML 大き過ぎて入れてもあまり気持ちよくなさそうだ…
					LOCAL:5 ++
				ENDIF
		ENDSELECT
		IF !LOCAL:5
			SELECTCASE ABL:選択中キャラID:膣
				CASE IS >= 5
					PRINTL 極上名器
					IF TALENT:選択中キャラID:処女 == -1
						PRINTFORML 当人があずかり知らぬまま、%CALLNAME:MASTER%を悦ばせるための穴と化している
					ELSE
						PRINTFORML 完全に%CALLNAME:MASTER%のカタチに作り替えられた膣内は肉ヒダを蠢かせて絡みついてくる
					ENDIF
				CASE IS >= 3
					PRINTL 名器
					IF TALENT:選択中キャラID:処女 == -1
						PRINTFORML 知らぬ間に%CALLNAME:MASTER%を気持ちよくさせるための穴に作り変えられてしまった
					ELSE
						PRINTFORML 使い込んだ甲斐あって大変具合のいい穴になった
					ENDIF
				CASE 2
					PRINTFORML %CALLNAME:MASTER%を受け入れることに慣れきっている
				CASE 1
					PRINTL ほぐれ始めて具合が良くなってきた
				CASE 0
					;IF EXP:選択中キャラID:Ｖ経験
					IF TALENT:選択中キャラID:処女 == 1
						PRINTL 性を知らない未使用の膣
					ELSEIF EXP:選択中キャラID:Ｖ経験 < 10
						PRINTL 性を知り大人の階段を恐る恐る登り始めたばかりの膣は強くしがみついてくる
					ELSEIF EXP:選択中キャラID:Ｖ経験 < 20
						PRINTL 未使用に近い膣はまだ硬さを残している
					ENDIF
			ENDSELECT
		ENDIF
	ENDIF
	
	SIF CFLAG:選択中キャラID:累計膣内精液
		PRINTFORML 今まで{CFLAG:選択中キャラID:累計膣内精液}の精液を注ぎ込まれた
	SELECTCASE ABL:選択中キャラID:膣射中毒
		CASE IS >= 7
			PRINTFORML 膣内射精されると多幸感を感じる
		CASE IS >= 3
			PRINTFORML 膣内射精されると子宮が疼いてしまう
	ENDSELECT
	
	SIF ABL:選択中キャラID:Ｖ感覚 >= 5 && CFLAG:選択中キャラID:溜まってる度 > 300 && 選択中キャラID
			PRINTFORML %CALLNAME:MASTER%に抱かれたくてうずうずしている
	
	PRINTFORML
	SIF CFLAG:選択中キャラID:無理矢理膣射
		PRINTFORML %CALLNAME:MASTER%に無理矢理中出しされたことがある
	SIF CFLAG:選択中キャラID:睡眠姦 == 1
		PRINTFORML %CALLNAME:MASTER%に眠っている間にイタズラされたことがある
	SIF CFLAG:選択中キャラID:睡眠姦 == 2
		PRINTFORML %CALLNAME:MASTER%に眠っている間に犯されたことがある
	IF CFLAG:選択中キャラID:泥酔姦 == 2
		PRINTFORML %CALLNAME:MASTER%に酔いつぶれているところを犯されたことがある
	ELSEIF CFLAG:選択中キャラID:泥酔姦 == 1
		PRINTFORML %CALLNAME:MASTER%に酔いつぶれているところを犯されたことがあるが気づいていない
	ENDIF
	
	IF 充填率(選択中キャラID,1) >= 100
		PRINTFORML 膣内は%CALLNAME:MASTER%の精液で完全に満たされている
	ELSEIF 充填率(選択中キャラID,1) >= 50
		PRINTFORML 膣内で夥しい数の精子が蠢いている
	ELSEIF 充填率(選択中キャラID,1) >= 20
		PRINTFORML 膣内で精子が元気に泳ぎ回っている
	ELSEIF CFLAG:選択中キャラID:生理周期 == 7 && FLAG:生理周期可視化 && 妊娠可否(選択中キャラID)
		PRINTFORML 危険日の子宮は瑞々しい卵子を用意して受精を待っている
	ENDIF
	IF 選択中キャラID == MASTER && EX:選択中キャラID:子宮内精液
		PRINTFORML %CALLNAME:MASTER%の子宮内精液内訳
		FOR LOCAL,1,CHARANUM
			SIF !TCVAR:LOCAL:種付け量
				CONTINUE
			SIF TCVAR:LOCAL:種付け量 > 100
				SETCOLOR C_YELLOW
			PRINTFORMLC %CALLNAME:LOCAL%{TCVAR:LOCAL:種付け量}
			RESETCOLOR
		NEXT
		PRINTFORML 
	ENDIF
	;PRINTFORML {EX:選択中キャラID:子宮内精液}
	IF TALENT:選択中キャラID:妊娠
		IF CFLAG:選択中キャラID:妊娠経過日数 < 20
			PRINTFORML 生理が止まっている
		ELSE
			PRINTFORML 子宮内では%CALLNAME:MASTER%との子供がすくすくと育っている
			PRINTFORML 現在妊娠{CFLAG:選択中キャラID:妊娠経過日数}日目だ
			CALL LOOK_BELLY(選択中キャラID)
			SIF INRANGE(CFLAG:選択中キャラID:妊娠経過日数,20,29)
					PRINT まだ見た目に変化は見られない
			PRINTL
		ENDIF
	ENDIF
ENDIF


SIF 選択中キャラID == MASTER
	CALL ちん遍歴(MASTER)

@ちん遍歴(選択中キャラID)
#DIM 選択中キャラID
#DIM 暫定一位の相手
#DIM 暫定一位の量
#DIM 暫定一位の種類
SIF !GETBIT(TALENT:選択中キャラID:2, 1)
	RETURN
PRINTL 【男性器】
SELECTCASE TALENT:選択中キャラID:形状
CASE 1
	LOCALS = 子供のような可愛らしい
CASE 2
	LOCALS = 貧相な
CASE 3
	LOCALS = 何の変哲もない
CASE 4
	LOCALS = 発光する
CASE 5
	LOCALS = イボだらけの
CASE 6
	LOCALS = 長大な
ENDSELECT
PRINTFORM %CALLNAME:選択中キャラID%の%LOCALS%ペニスは、
LOCAL:1 = SUMCARRAY(CFLAG:LOCAL:累計精飲,       1, CHARANUM)
LOCAL:2 = SUMCARRAY(CFLAG:LOCAL:累計ぶっかけ,   1, CHARANUM)
LOCAL:3 = SUMCARRAY(CFLAG:LOCAL:累計膣内精液,   1, CHARANUM)
LOCAL:4 = SUMCARRAY(CFLAG:LOCAL:累計アナル精液, 1, CHARANUM)
IF !TALENT:PLAYER:1
	PRINTFORML 未使用だ
ELSE
	SIF FLAG:処女喰い
		PRINTFORML 今までに{FLAG:処女喰い}人の処女を散らし、
	PRINTFORML 少女たちの口内に{LOCAL:1}、肢体に{LOCAL:2}、膣内に{LOCAL:3}、アナルに{LOCAL:4}の精液を吐き出してきた
	;各射精シチュの最大値調査
	;→もっとも大量に出したシチュの決定
	;→もっとも大量に出した相手の決定
	LOCAL:1 = MAXCARRAY(CFLAG:LOCAL:累計精飲,       1, CHARANUM)
	LOCAL:2 = MAXCARRAY(CFLAG:LOCAL:累計ぶっかけ,   1, CHARANUM)
	LOCAL:3 = MAXCARRAY(CFLAG:LOCAL:累計膣内精液,   1, CHARANUM)
	LOCAL:4 = MAXCARRAY(CFLAG:LOCAL:累計アナル精液, 1, CHARANUM)
	暫定一位の量 = 0
	FOR LOCAL:0, 1, 5
		SIF 暫定一位の量 > LOCAL:(LOCAL:0)
			CONTINUE
		暫定一位の種類 = LOCAL:0
		暫定一位の量   = LOCAL:(LOCAL:0)
	NEXT
	SELECTCASE 暫定一位の種類
	CASE 1
		暫定一位の相手 = FINDCHARA(CFLAG:LOCAL:累計精飲,       LOCAL:1, 1, CHARANUM)
		LOCALS = 口内
	CASE 2
		暫定一位の相手 = FINDCHARA(CFLAG:LOCAL:累計ぶっかけ,   LOCAL:2, 1, CHARANUM)
		LOCALS = 肢体
	CASE 3
		暫定一位の相手 = FINDCHARA(CFLAG:LOCAL:累計膣内精液,   LOCAL:3, 1, CHARANUM)
		LOCALS = 膣内
	CASE 4
		暫定一位の相手 = FINDCHARA(CFLAG:LOCAL:累計アナル精液, LOCAL:4, 1, CHARANUM)
		LOCALS = アナル
	ENDSELECT
	SIF 暫定一位の量 > 0
	PRINTFORML 特に%CALLNAME:暫定一位の相手%の%LOCALS%がお気に入りだ
ENDIF
SIF FLAG:行きずり孕ませ数
	PRINTFORML 行きずりに孕ませた数：{FLAG:行きずり孕ませ数}人
SIF TALENT:MASTER:巫女孕ませ
	PRINTL 巫女を孕ませた罰当たりなペニスだ
SIF TALENT:MASTER:妖精孕ませ
	PRINTL 妖精を孕ませた犯罪的なペニスだ
SIF TALENT:MASTER:妖怪孕ませ
	PRINTL 妖怪を孕ませた恐れ知らずのペニスだ
SIF TALENT:MASTER:天狗孕ませ
	PRINTL 天狗を孕ませた神速のペニスだ
SIF TALENT:MASTER:鬼孕ませ
	PRINTL 鬼を孕ませた鬼をもひしぐペニスだ
SIF TALENT:MASTER:付喪神孕ませ
	PRINTL 付喪神を孕ませた節操のないペニスだ
SIF TALENT:MASTER:神霊孕ませ
	PRINTL 神霊を孕ませた神をも恐れぬペニスだ
SIF TALENT:MASTER:蓬莱孕ませ
	PRINTL 蓬莱人すら孕ませた時を超えるペニスだ
SIF TALENT:MASTER:万能孕ませ
	PRINTL 機械だろうが幽霊だろうがいかなる少女でも孕ませる人智を超えたペニスだ
SIF TALENT:MASTER:濃厚精液
	PRINTFORML 何日分もの精を溜め込んだペニスはむせ返るような雄の臭いを放っている
;MASTER決め打ちでいいんじゃない？ あと個人的趣味としてふたなりに玉はない
SIF BASE:選択中キャラID:精力 >= 1500 && !GETBIT(TALENT:選択中キャラID:性別,0)
	PRINTL ずっしりと重い陰嚢には大量の精子が蓄えられている




@妊娠関連(ARG)
PRINTL
PRINTL □ 妊娠関連 □-----------------------------------------------------------------------
;PRINTL
;PRINTFORM 前回体内射精量 : {EX:ARG:14}
;PRINTL
PRINTFORM 妊娠後経過時間: {CFLAG:ARG:903}
;CFLAG:ARG:903 = 40
;PRINTL
;PRINTFORM 自覚 : {CFLAG:(ARG):910}
;PRINTL
;PRINTFORM 無自覚妊娠 : {CFLAG:ARG:無自覚妊娠}
PRINTL
PRINTFORM 現在育児中子供数 : {CFLAG:ARG:育児人数}
PRINTL
PRINTFORM 現在育児経過時間 : {CFLAG:ARG:908}
PRINTFORML 
PRINTFORM 　　　　子供の数 : {CFLAG:ARG:子供人数}
PRINTFORML 
IF CFLAG:ARG:子供人数
	FOR LOCAL,1,11
		IF CHILDNAME:ARG:LOCAL != ""
			PRINTFORM 第{LOCAL}子：%CHILDNAME:ARG:LOCAL, 15, LEFT%　\@ CHILD_SEX:ARG:LOCAL == 1 ? ♀ # ♂ \@ 
			CALL PRINT_DATE(CHILD_BIRTHDAY:ARG:LOCAL)
			PRINTFORM ({CHILD_BIRTHDAY:ARG:LOCAL}日目) 
			SELECTCASE CHILD_WEATHER:ARG:LOCAL
			CASE "Ｃ敏感"
				PRINTFORM みんなのクリトリスが敏感な
			CASE "Ｖ敏感"
				PRINTFORM みんなの女性器が敏感な
			CASE "Ａ敏感"
				PRINTFORM みんなの肛門が敏感な
			CASE "Ｂ敏感"
				PRINTFORM みんなの胸が敏感な
			CASE "Ｍ敏感"
				PRINTFORM みんなの唇が敏感な
			CASE "スカート"
				PRINTFORM スカートをめくられて頭にくると気持ちよくなる
			CASE "パンツ"
				PRINTFORM パンツが飛び交う
			CASEELSE
				PRINTFORM %CHILD_WEATHER:ARG:LOCAL%の
			ENDSELECT
			PRINTFORM 日に生まれた　
			IF DAY - CHILD_BIRTHDAY:ARG:LOCAL < 120
				IF CFLAG:ARG:出産経過日 < CHILD_語彙
					PRINTFORML 幼児期
				ELSEIF CFLAG:ARG:出産経過日 < CHILD_自立前
					PRINTFORML 幼少期
				ELSE
					PRINTFORML 思春期
				ENDIF
			ELSE
				PRINTFORML 現在{(DAY - CHILD_BIRTHDAY:ARG:LOCAL) / 124}才
			ENDIF
		ENDIF
	NEXT
ENDIF



@予定
#DIM 依頼番号

PRINTL □ 予定 □---------------------------------------------------------------------------
PRINTL 
SELECTCASE FLAG:抱負
	CASE 1
		LOCALS = オナニー奨励
	CASE 2
		LOCALS = 同性愛推進
	CASE 3
		LOCALS = 牝穴開拓
	CASE 4
		LOCALS = 尻穴開発
	CASE 5
		LOCALS = 乳挟振興
	CASE 6
		LOCALS = 子孫繁栄
	CASE 9
		LOCALS = 子孫繁栄
ENDSELECT
IF FLAG:抱負
	PRINTFORML 今年の抱負: %LOCALS%
	PRINTL
ENDIF
IF FLAG:宴会開催フラグ
	IF FLAG:開始日 - DAY <= 0
		PRINT 本日
	ELSE
		PRINTFORM {FLAG:開始日 - DAY}日後
	ENDIF
	CALL 宴会予定表示
	PRINTL 
	PRINT 宴会規模：
		SELECTCASE FLAG:宴会規模
			CASE IS >= 5000
				PRINT 特大
			CASE IS >= 2500
				PRINT 大
			CASE IS >= 1500
				PRINT 中
			CASEELSE
				PRINT 小
		ENDSELECT

	SIF FLAG:36 > 0
		PRINTL 　雨天中止
ENDIF
PRINTL
IF FLAG:貸本返却期限 >= 2
	FOR LOCAL, 1, BookTitles + 1
		SIF ITEM:(100 + LOCAL)
			PRINTFORML 借りている本：%ITEMNAME:(100 + LOCAL)%
	NEXT
	;PRINTL 
	IF  FLAG:貸本返却期限 == 2
		PRINTFORML 貸本の返却日：本日
	ELSE
		PRINTFORML 貸本の返却日：{FLAG:貸本返却期限 - 2}日後
	ENDIF
	PRINTL
ENDIF

CALL SHOW_ALL_IRAI_SLOT

SELECTCASE CFLAG:MASTER:小槌
	CASE 4
		PRINT 小槌で一物を大きくし
		IF CFLAG:MASTER:魔力回収 == 6
			PRINTL ている
		ELSE
			PRINTL た
		ENDIF
		SIF CFLAG:MASTER:魔力回収 < 6
			PRINTFORML 魔力回収完済まであと{CFLAG:MASTER:魔力回収}日
	CASE 5
		PRINT 小槌で一物を二本にし
		IF CFLAG:MASTER:魔力回収 == 6
			PRINTL ている
		ELSE
			PRINTL た
		ENDIF
		SIF CFLAG:MASTER:魔力回収 < 6
			PRINTFORML 魔力回収完済まであと{CFLAG:MASTER:魔力回収}日
	CASE 6
		PRINT 小槌で小人になっ
		IF CFLAG:MASTER:魔力回収 == 3
			PRINTL ている
		ELSE
			PRINTL た
		ENDIF
		SIF CFLAG:MASTER:魔力回収 < 3
			PRINTFORML 魔力回収完済まであと{CFLAG:MASTER:魔力回収}日
	CASE 7
		PRINTL 小槌で小人から元に戻った
		SIF CFLAG:MASTER:魔力回収 < 3
			PRINTFORML 魔力回収完済まであと{CFLAG:MASTER:魔力回収}日
ENDSELECT
SIF CFLAG:MASTER:小槌
	PRINTL 



@パンツ収集状況(選択中キャラID)
#DIM 選択中キャラID
PRINTL □ パンツ収集状況 □-----------------------------------------------------------------
IF FLAG:パンツ収集状況タイプB == 0
	CALL DISPLAY_COLLECTION(選択中キャラID)
	PRINTFORML 総数：{RESULT:0,4}枚　　種別：{RESULT:1,2}種　　ダブり：{RESULT:0 - RESULT:1,4}枚
ELSE
	CALL DISPLAY_COLLECTION_B(選択中キャラID)
	PRINTFORML 総数：{RESULT:0,4}枚　　種別：{RESULT:1,2}/{RESULT:2}種　　ダブり：{RESULT:0 - RESULT:1,4}枚
ENDIF

@浮気把握状況(C_ID)
#DIM C_ID
#DIM LINE_BREAK
LINE_BREAK = 0
PRINT □ 
CALL PRINTBUTTON_EX(@"%CALLNAME:C_ID%の把握している%CALLNAME:MASTER%の肉体関係 （黄字は公認）", 168, TFLAG:168)
PRINTL  □------------------------------------------------------------
IF !TFLAG:浮気歴表示
	PRINTFORML 
	FOR LOCAL,1,CHARANUM
		SIF LOCAL == C_ID
			CONTINUE
		IF GET_UWAKI_HISTORY(C_ID, LOCAL)
			SIF GET_UWAKI_HISTORY(C_ID, LOCAL) == 2
				SETCOLOR C_YELLOW
			SIF LINEISEMPTY()
				PRINT 　
			PRINTFORMLC %CALLNAME:LOCAL%
			LINE_BREAK ++
			SIF LINE_BREAK % (PRINTCPERLINE() - 1) == 0
				PRINTL 
		ENDIF
		RESETCOLOR
	NEXT
	SIF LINE_BREAK % (PRINTCPERLINE() - 1) != 0
		PRINTL 
ENDIF

@PRINT_STATE_PREFERENCE(選択中キャラID)
#DIM 選択中キャラID
PRINTL □ 趣味嗜好 □-----------------------------------------------------------------------
PRINTL 好き：
CALL SHOW_TASTE(GET_STR(, "キャラデータ", 選択中キャラID, "感性：好き"), "POSITIVE", ABL:選択中キャラID:親密 < 5)
PRINTFORML 
PRINTL 嫌い：
CALL SHOW_TASTE(GET_STR(, "キャラデータ", 選択中キャラID, "感性：嫌い"), "NEGATIVE", ABL:選択中キャラID:親密 < 5)
DRAWLINE
PRINTL 好きな味：
CALL SHOW_TASTE(GET_STR(, "キャラデータ", 選択中キャラID, "料理：好きな味"), "POSITIVE", ABL:選択中キャラID:親密 < 3)
PRINTL 嫌いな味：
CALL SHOW_TASTE(GET_STR(, "キャラデータ", 選択中キャラID, "料理：嫌いな味"), "NEGATIVE", ABL:選択中キャラID:親密 < 3)
PRINTL 助手効果：
CALL DISH_TASTE_FROM_CHARA(選択中キャラID, "OUTPUT")
CALL SHOW_TASTE(RESULTS, "ASSIDATA")
IF 選択中キャラID == MASTER && EX_DISH_NAME != ""
	PRINTFORML ＜%EX_DISH_NAME%＞
	CALL SHOW_TASTE(DISH_TASTE, "DISH_DATA")
ENDIF
DRAWLINE
IF CFLAG:選択中キャラID:好きな体位
	PRINT 弱点：
	SELECTCASE CFLAG:選択中キャラID:好きな体位
	CASE 1
		PRINTL 正常位
	CASE 2 
		PRINTL 後背位
	CASE 3 
		PRINTL 騎乗位
	CASE 4 
		PRINTL 対面座位 
	CASE 5 
		PRINTL 背面座位
	CASE 6
		PRINTL 正常位アナル
	CASE 7 
		PRINTL 後背位アナル
	CASE 8 
		PRINTL 騎乗位アナル
	CASE 9 
		PRINTL 対面座位アナル
	CASE 10 
		PRINTL 背面座位アナル
	ENDSELECT
	PRINTL 　
ENDIF

@PRINT_STATE_GIFTDATA(選択中キャラID, 最大表示行数)
#DIM 選択中キャラID
#DIM 最大表示行数
#DIM 表示開始
SIF !GIFT_GOT:選択中キャラID:1
	RETURN

SIF !最大表示行数
	最大表示行数 = 100

FOR LOCAL, 0, 100
	;フラグ0番が100を兼ねているのでこの形に
	IF !GIFT_GOT:選択中キャラID:((LOCAL + 1) % 100)
		表示開始 = MAX(LOCAL + 1 - 最大表示行数, 1)
		BREAK
	ENDIF
NEXT

PRINTL □ あなたが渡した贈り物 □-----------------------------------------------------------
;お気に入りを先頭に持ってくる
SIF CFLAG:選択中キャラID:貰った贈り物
	CALL PRINT_GIFTDATA(CFLAG:選択中キャラID:貰った贈り物, 選択中キャラID, "キャラの")
FOR LOCAL, 表示開始, 表示開始 + 最大表示行数 + 1
	SIF GIFT_GOT:選択中キャラID:LOCAL && GIFT_GOT:選択中キャラID:LOCAL != CFLAG:選択中キャラID:貰った贈り物
		CALL PRINT_GIFTDATA(GIFT_GOT:選択中キャラID:LOCAL, 選択中キャラID, "キャラの")
NEXT

@Valentine_History(ARG)
IF CFLAG:ARG:バレンタイン
	SELECTCASE CFLAG:ARG:バレンタイン % 100
		CASE 1
			LOCALS = ブラックサンダー
		CASE 2
			LOCALS = 普通のチョコ
		CASE 3
			LOCALS = 高級チョコ
		CASE 4
			LOCALS = 手作りチョコ
	ENDSELECT
	PRINTFORML バレンタインに%LOCALS%をくれた\@ CFLAG:ARG:バレンタイン > 100? （返礼済）# \@
ENDIF

@PERSONALITY_TYPE(ARG)
#FUNCTIONS
SELECTCASE CFLAG:ARG:性格傾向
	CASE 1
		RETURNF "気弱系"
	CASE 2
		RETURNF "強気系"
	CASE 3
		RETURNF "素直系"
	CASE 4
		RETURNF "真面目系"
	CASE 5
		RETURNF "その他"
ENDSELECT
;
;### 関数 ###################################
;以下の関数は「映姫様口上」内の「M_KOJO_K30_関数・お説教ライブラリ」を拝借したものです
;小町口上作者が作成したものではないのでご注意ください
;同ライブラリの利用に関しては、映姫様口上の作者様のライセンスに準じます

;拝借ここから
;==================================================

;==================================================
;MASTERの恋人がその場に居るかどうか返す式中関数
;これは力技でなんとか実装できたっぽいので、使わなくても問題ない気がする（小町口上作者）
;でも絶対こっちのがスマート
;=-1 小町が恋人 =0 元から恋人なし =1 その場に居ない =2 恋人と同席
@K76_FIND_LOVER()
#FUNCTION
SIF TALENT:MASTER:恋人 == 0
	RETURNF 0
SIF TALENT:恋人
	RETURNF -1
LOCAL = TALENT:MASTER:恋人
SIF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
	RETURNF 2
RETURNF 1
;==================================================


;==================================================
;同部屋のキャラを拾う式中関数。探す順位は[恋人]持ち→知り合い→TARGET内ランダム
;アリス口上の関数お借りして平文化した映姫口上の関数を拝借しました。つまり丸パクり
;丸コピする以外の技術がないので先人たちの知恵に感謝をし続けるしかない
@K76_FIND_AROUND()
#FUNCTION
#DIM 知り合い, 3 = 30, 43, 1
#DIM 総人数
#DIM 抽選
#LOCALSIZE 200
;小町の知り合いは3人,映姫>華扇>霊夢の順に(勝手に)設定
;一応理由としては
;映姫→言わずもがな
;華扇→茨歌仙で絡みが多かった
;霊夢→茨歌仙で霊夢がどんな人物か割と詳しく語っていた描写があった（と感じた）
;冥界組とか三月精コミックで交流あったやつおるやろ、という方もいるでしょうがとりあえず口上に関係しそうなキャラ優先でピックアップ
VARSET LOCAL
抽選 = 0
総人数 = GET_TARGETNUM() 
;部屋に一人なら当然0
SIF 総人数 == 1
	RETURNF 0
;[恋人]持ちがいたら優先してIDを返す
SIF K76_FIND_LOVER() == 2
	RETURNF TALENT:MASTER:恋人
;知り合い優先度順に現在位置を調べMASTERと同じならIDを返す
FOR COUNT, 0, 2
	SIF CFLAG:(知り合い:COUNT):現在位置 == CFLAG:MASTER:現在位置
		RETURNF 知り合い:COUNT
NEXT
;知り合いがいなければ同室のTARGET_ID全部を拾う
FOR COUNT,1, 総人数 + 1
	;地獄の断頭台withジェロニモ状態を防ぐため口上主はスキップ
	SIF TARGET:COUNT == TARGET:0
		CONTINUE
	;MASTERとこまっちゃんの現在位置にいなければスキップ
	SIF (CFLAG:(TARGET:COUNT):現在位置 != CFLAG:MASTER:現在位置) || (CFLAG:(TARGET:COUNT):現在位置 != CFLAG:76:現在位置)
		CONTINUE
	;配列のカウントは別で取りながら代入
	LOCAL:抽選 = TARGET:COUNT
	抽選 ++
NEXT
;抽選してIDを決定
RETURNF LOCAL:(RAND(抽選))
;==================================================


;==================================================
;酔っぱらい度を数字で返す式中関数
;=0 素面 =1 ほろ酔い =2 酔いどれ 3=ぐでぐで
@K76_DRUNK()
#FUNCTION
SIF BASE:76:酒気 <= 2
	RETURNF 0
SELECTCASE BASE:76:酒気
	;ぐでぐで
	CASE IS > (MAXBASE:76:酒気 / 10) * 8
		RETURNF 3
	;酔いどれ
	CASE IS > (MAXBASE:76:酒気 / 10) * 5
		RETURNF 2
	;ほろ酔い
	CASE IS <= (MAXBASE:76:酒気 / 10) * 5
		RETURNF 1
ENDSELECT
;==================================================
;拝借ここまで

;==================================================
;本体関数を加工して自作関数に
;==================================================
;勃起状態を数字で返す式中関数
;=0 通常 =1 むっくり =2 ビンビン =3 Erection!!
@K76_BOKKI()
#FUNCTION
#DIM CHARA
IF BASE:CHARA:勃起 == MAXBASE:CHARA:勃起
	RETURNF 3
ELSEIF BASE:CHARA:勃起 >= 1000
	RETURNF 2
ELSEIF BASE:CHARA:勃起 >= 500
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

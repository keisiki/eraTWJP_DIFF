;育児イベントの地の文はデフォルトの文から一部流用
;-------------------------------------------------
;育児イベントの地の文。
;デフォルトのものと置き換えたい場合は上下のSKIPSTART~SKIPENDを外してください。
;置き換える気がないならスルー。
;-------------------------------------------------
@M_KOJO_EVENT_K44_CHILD_RAISING_離乳
;-------------------------------------------------
TRYCALLFORM M_KOJO_COLOR_K44
PRINTFORMW 「はい、あーん%UNICODE(0x2665)%」
PRINTFORMW 「……よしよし%UNICODE(0x2665)%」
PRINTFORMDL エレンは%CHILDNAME:44:(TALENT:44:育児中)%に離乳食を与え始めた
PRINTFORMDW 満面の笑顔で離乳食をほおばる幼子の姿に%CALLNAME:MASTER%とエレンの口元が緩む
RESETCOLOR

;-------------------------------------------------
@M_KOJO_EVENT_K44_CHILD_RAISING_つかまり立ち
;-------------------------------------------------
TRYCALLFORM M_KOJO_COLOR_K44
PRINTFORMDL %CALLNAME:MASTER%とエレンは、%CHILDNAME:44:(TALENT:44:育児中)%につかまり立ちの練習をさせた
PRINTFORMDL 少し不安もあるが、しっかりとつかまり立ちが出来ている。これならもうじき歩けるようになるだろう
PRINTFORMDW そこでいつ歩けるようになっても問題ないよう、%CHILDNAME:44:(TALENT:44:育児中)%のための安全対策を講じることにした…
PRINTFORML 「テーブルとかの角にはクッション材をつけてー、棚には鍵をかけてー、
PRINTFORMW 　それから危ないものは%CHILDNAME:44:(TALENT:44:育児中)%の目に届かないところに置いてー――」
RESETCOLOR

;-------------------------------------------------
@M_KOJO_EVENT_K44_CHILD_RAISING_よちよち
;-------------------------------------------------
TRYCALLFORM M_KOJO_COLOR_K44
PRINTFORMW 「あら、歩けるようになったのね%UNICODE(0x2665)% すごーい%UNICODE(0x2665)%」
PRINTFORMDL まだ危なっかしいが、%CHILDNAME:44:(TALENT:44:育児中)%は両足で立って歩き回るようになった
PRINTFORMDW 視界と行動半径が広がったせいで色々なものに興味を示している
PRINTFORMDL だが危険な物は全て隠しているし、危険な場所も対策を行っているので自宅内はとても安全だ
PRINTFORMDL それに%CHILDNAME:44:(TALENT:44:育児中)%が勝手に外に出ないよう%CALLNAME:MASTER%とエレンとソクラテスが常に見守っている
PRINTFORMDW これだけやればある程度は安全だろう
PRINTFORMDW そんな二人と一匹の苦労など露知らず、%CHILDNAME:44:(TALENT:44:育児中)%はよちよちと自宅内を歩き回っている……
RESETCOLOR

;-------------------------------------------------

[SKIPSTART]
;-------------------------------------------------
;育児イベントの地の文。
;デフォルトのものと置き換えたい場合は上下のSKIPSTART~SKIPENDを外してください。
;置き換える気がないならスルー。これは複数回発生します。
;-------------------------------------------------
@M_KOJO_EVENT_K44_CHILD_RAISING_会話寸前
;-------------------------------------------------

;-------------------------------------------------
[SKIPEND]

;-------------------------------------------------
@M_KOJO_EVENT_K44_CHILD_RAISING_喋る
;-------------------------------------------------
TRYCALLFORM M_KOJO_COLOR_K44
IF CFLAG:44:初期位置 == CFLAG:MASTER:初期位置
	PRINTFORMDL 自宅で家族団らんを楽しんでいると、%CHILDNAME:44:(TALENT:44:育児中)%がエレンの方を向いてあうあう言い出した
	PRINTFORMDW それを受けてエレンが%CHILDNAME:44:(TALENT:44:育児中)%を抱き上げると
	PRINTFORMDL 『マンマ』
	PRINTFORMDW と、不意打ちのように初めての言葉を発した
	PRINTFORML 「喋れるようになったのね%UNICODE(0x2665)% さすがはわたしたちの子供%UNICODE(0x2665)%」
	PRINTFORMDL 『マンマ♪』
ELSE
	PRINTFORMDL エレンがすごい勢いで駆け込んできた
	PRINTFORMW 「あっ！　%CALLNAME:MASTER%、%CHILDNAME:44:(TALENT:44:育児中)%が喋れるようになったの！」
	PRINTFORMDL 急いで行ってみると、確かにエレンの顔を見てはっきりと呼んでいる
	PRINTFORMDL 『マンマ♪』
ENDIF
PRINTFORMDW %CALLNAME:MASTER%は自分も呼んでもらえるよう、顔を近づけてお父さんと連呼した

;-------------------------------------------------

[SKIPSTART]
;-------------------------------------------------
;育児イベントの地の文。
;デフォルトのものと置き換えたい場合は上下のSKIPSTART~SKIPENDを外してください。
;置き換える気がないならスルー。
;-------------------------------------------------
@M_KOJO_EVENT_K44_CHILD_RAISING_語彙
;-------------------------------------------------

;-------------------------------------------------
[SKIPEND]

[SKIPSTART]
;-------------------------------------------------
;育児イベントの地の文。
;デフォルトのものと置き換えたい場合は上下のSKIPSTART~SKIPENDを外してください。
;置き換える気がないならスルー。
;-------------------------------------------------
@M_KOJO_EVENT_K44_CHILD_RAISING_しつけ
;-------------------------------------------------

;-------------------------------------------------
[SKIPEND]

[SKIPSTART]
;-------------------------------------------------
;育児イベントの地の文。
;デフォルトのものと置き換えたい場合は上下のSKIPSTART~SKIPENDを外してください。
;置き換える気がないならスルー。
;-------------------------------------------------
@M_KOJO_EVENT_K44_CHILD_RAISING_就学
;-------------------------------------------------

;-------------------------------------------------
[SKIPEND]

;-------------------------------------------------
@M_KOJO_EVENT_K44_CHILD_RAISING_自立前
;-------------------------------------------------
TRYCALLFORM M_KOJO_COLOR_K44
PRINTFORMDL %CHILDNAME:44:(TALENT:44:育児中)%も随分と分別がついてきた
PRINTFORMDL 背丈も伸びて、今やエレンよりも大きい
PRINTFORMDL この子も遠からず家を出てしまうだろう、残された時間を大事に過ごそう……
PRINTFORMW 「%CHILDNAME:44:(TALENT:44:育児中)%もほんと大きくなったわねー……」

;-------------------------------------------------


;-------------------------------------------------
;育児イベント登下校時の地の文。
;-------------------------------------------------
@M_KOJO_EVENT_K44_CHILD_RAISING_登下校(ARGS,ARG,ARG:1)
IF !ARG
	;登校時
	IF CFLAG:44:出産経過日 < CHILD_自立前
		PRINTFORMDL 『いってきまーす』
	ELSE
		PRINTFORMDL 『では行って来ます』
	ENDIF
	PRINTFORML 「いってらっしゃい%UNICODE(0x2665)%」
ELSE
	;下校時
	IF CFLAG:44:出産経過日 < CHILD_自立前
		PRINTFORMDL 『ただいまー♪』
	ELSE
		PRINTFORMDL 『ただいま帰りました』
	ENDIF
	PRINTFORML 「お帰りなさい%UNICODE(0x2665)%」
ENDIF

;-------------------------------------------------
;育児イベント食事時。
;-------------------------------------------------
@M_KOJO_EVENT_K44_CHILD_RAISING_食事(ARGS,ARG,ARG:1)
IF ARGS == "授乳"
	SELECTCASE RAND:4
	CASE 1
		PRINTFORMW 「いっぱい飲んで大きくなってね%UNICODE(0x2665)%」
	CASE 2
		PRINTFORMW 「慌てなくても逃げたりしないから安心して%UNICODE(0x2665)%」
	CASE 3
		PRINTFORMW 「飲み終わったらけぷってしようね%UNICODE(0x2665)%」
	CASEELSE
		PRINTFORMW 「上手にけぷってできたわね%UNICODE(0x2665)% えらいえらい%UNICODE(0x2665)%」
	ENDSELECT
ELSEIF ARGS == "離乳食"
	SELECTCASE RAND:4
	CASE 1
		PRINTFORMW 「はい、あーん%UNICODE(0x2665)%」
	CASE 2
		PRINTFORMW 「上手に食べれたわね%UNICODE(0x2665)% いい子いい子%UNICODE(0x2665)%」
	CASE 3
		SIF CFLAG:44:出産経過日 >= CHILD_喋る
		PRINTFORMDL 『まんま、まんま』
		PRINTFORMDW %CHILDNAME:44:(TALENT:44:育児中)%は美味しそうに離乳食を食べている……
	CASEELSE
		SIF CFLAG:44:出産経過日 >= CHILD_喋る
		PRINTFORMDL 『きゃっきゃっ♪』
		PRINTFORMDL %CHILDNAME:44:(TALENT:44:育児中)%は少しふざけている……
		PRINTFORMW 「こーらっ、食べ物で遊んじゃだめよ」
	ENDSELECT
ELSEIF ARGS == "幼少期"
	SELECTCASE RAND:3
	CASE 1
		PRINTFORMDL 『いただきまーす♪』
	CASE 2
		PRINTFORMDL 『うわー、おいしそー♪』
	CASEELSE
		IF GROUPMATCH(SELECTCOM, 414, 415)
			PRINTFORMD 『パパ\@ TCVAR:44:360 == 1 ? とママ #  \@
		ELSEIF SELECTCOM == 615
			PRINTFORMD 『ママ
		ELSE
			PRINTFORMD 『こ
		ENDIF
		PRINTFORMDL の料理、すっごくおいしい%UNICODE(0x2665)%』
	ENDSELECT
	PRINTFORMD %CHILDNAME:44:(TALENT:44:育児中)%は
	IF GROUPMATCH(SELECTCOM, 414, 415)
		PRINTFORMD %CALLNAME:MASTER%\@ TCVAR:44:360 == 1 ? とエレン #  \@の
	ELSEIF SELECTCOM == 615
		PRINTFORMD エレンの
	ELSE
		PRINTFORMD 出された
	ENDIF
	PRINTFORMDW 料理を美味しそうにもぐもぐしている
ELSE
	SELECTCASE RAND:3
	CASE 1
		PRINTFORMDL 『頂きます』
	CASE 2
		PRINTFORMDL 『相変わらず美味しそうです\@ CHILD_SEX:44:(TALENT:44:育児中) == 1 ? ね #  \@』
	CASEELSE
		IF GROUPMATCH(SELECTCOM, 414, 415)
			PRINTFORMD 『お父さん\@ TCVAR:44:360 == 1 ? とお母さん #  \@
		ELSEIF SELECTCOM == 615
			PRINTFORMD 『お母さん
		ELSE
			PRINTFORMD 『ここ
		ENDIF
			PRINTFORMDL の料理はとても美味しい\@ CHILD_SEX:44:(TALENT:44:育児中) == 1 ? です♪ # や \@』
	ENDSELECT
	PRINTFORMD %CHILDNAME:44:(TALENT:44:育児中)%は
	IF GROUPMATCH(SELECTCOM, 414, 415)
		PRINTFORMD %CALLNAME:MASTER%\@ TCVAR:44:360 == 1 ? とエレン #  \@の
	ELSEIF SELECTCOM == 615
		PRINTFORMD エレンの
	ELSE
		PRINTFORMD 出された
	ENDIF
	PRINTFORMDW 料理を笑顔で食している
ENDIF
;子供の描写がオンならここで食事のTCVARを消す
IF GROUPMATCH(SELECTCOM, 414, 415)
	TCVAR:44:360 = 0
	TCVAR:44:361 = 0
ENDIF

;-------------------------------------------------
;育児イベント入浴
;-------------------------------------------------
@M_KOJO_EVENT_K44_CHILD_RAISING_BATH(ARGS,ARG,ARG:1)
IF ARGS == "幼児期"
	SIF CFLAG:44:出産経過日 >= CHILD_喋る
	PRINTFORMDL 『ちゃぷちゃぷ』
	PRINTFORMDW %CHILDNAME:44:(TALENT:44:育児中)%は気持ちよさそうに湯船に浸かっている……
ELSEIF ARGS == "幼少期"
	SELECTCASE RAND:2
	CASE 1
		PRINTFORMDW %CHILDNAME:44:(TALENT:44:育児中)%は気持ちよさそうに湯船に浸かっている……
	CASEELSE
		IF !RAND:2
			PRINTFORMDL エレンは%CHILDNAME:44:(TALENT:44:育児中)%の体を洗っている
			PRINTFORML 「きれいにしましょうね%UNICODE(0x2665)%」
			PRINTFORMDL 『はーい♪』
		ELSE
			PRINTFORMDL %CHILDNAME:44:(TALENT:44:育児中)%の体を洗ってあげた
			PRINTFORMDL 『きゃー♪』
		ENDIF
		PRINTFORMDW %CHILDNAME:44:(TALENT:44:育児中)%は嬉しそうだ……
	ENDSELECT
ELSE
ENDIF
;-------------------------------------------------
;育児イベント就寝
;-------------------------------------------------
@M_KOJO_EVENT_K44_CHILD_RAISING_SLEEPING(ARGS,ARG,ARG:1)
IF ARGS == "幼児期"
	;PRINTFORML 
	;子供の寝てる隙に
	SIF !TCVAR:44:387 && !RAND:2
		CFLAG:44:溜まってる度 += ABL:欲望 * 10
ELSEIF ARGS == "幼少期"
	PRINTFORMDL 『お休みなさーい……』
	SIF !TCVAR:44:387 && !RAND:2
		CFLAG:44:溜まってる度 += ABL:欲望 * 10
ELSE
	PRINTFORMDL 『お父さん、お母さん、お休みなさい』
	SIF !TCVAR:44:387 && !RAND:2
		CFLAG:44:溜まってる度 += ABL:欲望 * 10
ENDIF

;-------------------------------------------------
;育児イベント川の字でおやすみ
;-------------------------------------------------
@M_KOJO_EVENT_K44_CHILD_RAISING_OYASUMI(ARGS,ARG,ARG:1)
PRINTFORMW 「えへへ%UNICODE(0x2665)%　お休みなさい、%CALLNAME:MASTER%%UNICODE(0x2665)% %CHILDNAME:44:(TALENT:44:育児中)%%UNICODE(0x2665)%」
;幼少期
IF CFLAG:44:出産経過日 < CHILD_自立前 && CFLAG:44:出産経過日 >= CHILD_語彙
	PRINTFORMDW 『えへー♪　おやすみなさいパパ、ママ』
;自立手前
ELSEIF CFLAG:44:出産経過日 >= CHILD_自立前
	PRINTFORMDW 『\@ CHILD_SEX:44:(TALENT:44:育児中) == 1 ? はい♪　 # ええ、 \@お休みなさい、お母さん、お父さん』
	;最終日のとき限定で追加
	IF CFLAG:44:出産経過日 == CHILD_自立
		PRINTFORMDW 『……お父さん、お母さん、今までありがとうございました』
		PRINTFORMW 「どういたしまして%UNICODE(0x2665)%　……%CHILDNAME:44:(TALENT:44:育児中)%、幸せに暮らすのよ」
		PRINTFORMDW 『はい！』
	ENDIF
ENDIF

;-------------------------------------------------
;育児イベント玩具
;-------------------------------------------------
@M_KOJO_EVENT_K44_CHILD_RAISING_TOY(ARGS,ARG,ARG:1)
IF ARGS == "幼児期"
	SIF CFLAG:44:出産経過日 >= CHILD_喋る
		PRINTFORMDL 『おー♪』
	SELECTCASE RAND:4
	CASE 1, 2
		PRINTFORMD 玩具を
		SELECTCASE RAND:3
		CASE 1
			PRINTD ころころ転がす
		CASE 2
			PRINTD ぺたぺた触る
		CASEELSE
			PRINTD ぺちぺち叩く
		ENDSELECT
		PRINTFORMDW のが楽しいようだ……
	CASE 3
		PRINTFORMDW %CHILDNAME:44:(TALENT:44:育児中)%は満足げだ……
	CASEELSE
		PRINTFORMDW ……玩具をじっと見つめたかと思うといきなりかじりついた
	ENDSELECT
ELSEIF ARGS == "幼少期"
	;PRINTFORML 
ELSE
	;PRINTFORML 
ENDIF

;-------------------------------------------------
;育児イベントその他。その他が一番分厚いのは基本
;-------------------------------------------------
@M_KOJO_EVENT_K44_CHILD_RAISING_OTHER(ARGS,ARG,ARG:1)
;各種行事イベント　ある程度成長していることが前提
IF CFLAG:44:出産経過日 >= CHILD_語彙 && !TCVAR:44:365 && !RAND:2 && FESTIVAL() != ""
	;母の日
	IF FESTIVAL() == "母の日"
		IF CFLAG:44:出産経過日 < CHILD_自立前
			PRINTFORMDL 『ママ、いつもありがとう%UNICODE(0x2665)%』
		ELSE
			PRINTFORMDL 『お母さん、いつもありがとうございます』
		ENDIF
		PRINTFORMDL %CHILDNAME:44:(TALENT:44:育児中)%はエレンに感謝の念を伝えながら一輪のカーネーションを渡した
		PRINTFORMW 「あら、ありがとう%CHILDNAME:44:(TALENT:44:育児中)%%UNICODE(0x2665)%」
	;ハロウィン　幼少期限定
	ELSEIF FESTIVAL() == "ハロウィン" && CFLAG:44:出産経過日 < CHILD_自立前
		PRINTFORMDL 『トリックオアトリート！　おかしをくれないといたずらするぞー！』
		PRINTFORML 「かわいい魔女さんね%UNICODE(0x2665)% じゃあお菓子をあげるわ」
		PRINTFORMDL 『ありがとー♪』
		PRINTFORMDW エレンと魔女に仮装した%CHILDNAME:44:(TALENT:44:育児中)%がハロウィンを楽しんでいる
		PRINTFORMDL 『パパも、トリックオアトリート！　おかしをくれないといたずらするぞー！』
		PRINTFORMDL どうやら今度はこっちの番のようだ、さて……
		CALL ASK_YN("お菓子をあげる","いたずらをもらう")
		IF !RESULT
			PRINTFORMDL 仕方ないなとお菓子をあげることにした
			PRINTFORMDL 『わーい♪』
			PRINTFORMDW %CHILDNAME:44:(TALENT:44:育児中)%は満足している……
		ELSE
			PRINTFORMDL 折角だしいたずらをもらうことにした
			PRINTFORMDL 『なんだとー、こちょこちょこうげきをくらえー♪』
			PRINTFORMDW %CHILDNAME:44:(TALENT:44:育児中)%は%CALLNAME:MASTER%の全身をくすぐってきた！
			PRINTFORMDW ……あまりくすぐったくないが、%CALLNAME:MASTER%を必死にこちょこちょする%CHILDNAME:44:(TALENT:44:育児中)%の姿を見ると笑みがこぼれる
		ENDIF
	;バレンタイン　娘限定
	ELSEIF FESTIVAL() == "バレンタインデー" && CHILD_SEX:44:(TALENT:44:育児中) == 1
		IF CFLAG:44:出産経過日 < CHILD_自立前
			PRINTFORMDL 『パパにプレゼントがあるの♪　はいっ%UNICODE(0x2665)%』
			PRINTFORMDL %CHILDNAME:44:(TALENT:44:育児中)%から小さな手作りチョコをもらった！
			PRINTFORMDW 中々の味だった……
			BASE:MASTER:体力 = MIN(BASE:MASTER:体力 + 100 , MAXBASE:MASTER:体力)
			BASE:MASTER:気力 = MIN(BASE:MASTER:気力 + 100 , MAXBASE:MASTER:気力)
		ELSE
			PRINTFORMDL 『お父さんにプレゼントです♪』
			PRINTFORMDL %CHILDNAME:44:(TALENT:44:育児中)%から手作りチョコをもらった！
			PRINTFORMDW 美味しかった……
			BASE:MASTER:体力 = MIN(BASE:MASTER:体力 + 200 , MAXBASE:MASTER:体力)
			BASE:MASTER:気力 = MIN(BASE:MASTER:気力 + 200 , MAXBASE:MASTER:気力)
		ENDIF
	ENDIF
	TCVAR:44:365 = 1
;歩き回り　幼少期以下限定
ELSEIF CFLAG:44:出産経過日 >= CHILD_よちよち && CFLAG:44:出産経過日 < CHILD_自立前 && !RAND:4
	PRINTFORMDL %CHILDNAME:44:(TALENT:44:育児中)%は辺りを\@ CFLAG:44:出産経過日 < CHILD_語彙 ? よちよち # てくてく \@と歩き回っている…
	PRINTFORM 「あまり遠くに行っちゃ駄目よー！」
	IF CFLAG:44:出産経過日 < CHILD_語彙
		PRINTFORMW 
	ELSE
		PRINTFORML 
		PRINTFORMDW 『はーい』
	ENDIF
ELSEIF ARGS == "幼児期"
	SELECTCASE RAND:6
	CASE 1
		SELECTCASE RAND:3
		CASE 1
			PRINTFORML 「いないいない、ばーっ%UNICODE(0x2665)%」
		CASE 2
			PRINTFORML 「高い高ーい%UNICODE(0x2665)%」
		CASEELSE
			PRINTFORMDL エレンは%CHILDNAME:44:(TALENT:44:育児中)%の頭を優しく撫でている
		ENDSELECT
		SIF CFLAG:44:出産経過日 >= CHILD_喋る
		PRINTFORMDL 『マンマ♪』
		PRINTFORMDW %CHILDNAME:44:(TALENT:44:育児中)%は楽しそうにきゃっきゃと笑っている……
	CASE 2
		SIF CFLAG:44:出産経過日 >= CHILD_喋る
		PRINTFORMDL 『マンマ♪』
		PRINTFORMDL %CHILDNAME:44:(TALENT:44:育児中)%はエレンに甘えている
		SELECTCASE RAND:2
		CASE 1
			PRINTFORMW 「よしよし%UNICODE(0x2665)%」
		CASEELSE
			PRINTFORMW 「どうしたの%UNICODE(0x2665)%」
		ENDSELECT
	CASE 3
		SIF CFLAG:44:出産経過日 >= CHILD_喋る
		PRINTFORMDL 『パーパ♪』
		PRINTFORMDW %CHILDNAME:44:(TALENT:44:育児中)%は%CALLNAME:MASTER%に甘えてきた
	CASE 4
		PRINTFORMDL %CALLNAME:MASTER%は%CHILDNAME:44:(TALENT:44:育児中)%をあやした……
		SIF CFLAG:44:出産経過日 >= CHILD_喋る
		PRINTFORMDL 『パーパ♪』
		PRINTFORMDW %CHILDNAME:44:(TALENT:44:育児中)%は嬉しそうに笑っている……
	CASE 5
		SIF CFLAG:44:出産経過日 >= CHILD_喋る
		PRINTFORMDL 『にゃーにゃー』
		PRINTFORMDW %CHILDNAME:44:(TALENT:44:育児中)%はソクラテスをじっと見つめている……
		;生後15日以上でたまにソクラテスの尻尾を引っ張る
		IF CFLAG:44:出産経過日 >= 15 && !RAND:3
			PRINTFORMDL ……いきなりソクラテスのしっぽを引っ張り出した、ソクラテスは嫌がっている
			PRINTFORMW 「こーらっ、ソクラテスをいじめちゃだめでしょー」
		ENDIF
	CASEELSE
		PRINTFORMDL エレンはぐずっている%CHILDNAME:44:(TALENT:44:育児中)%をあやしている……
		SELECTCASE RAND:2
		CASE 1
			PRINTFORML 「お母さんが近くにいるから安心して%UNICODE(0x2665)%」
		CASEELSE
			PRINTFORML 「ほーら、怖くないわよー」
		ENDSELECT
		PRINTFORMDW %CHILDNAME:44:(TALENT:44:育児中)%は泣き止んだようだ
	ENDSELECT
ELSEIF ARGS == "幼少期"
SELECTCASE RAND:7
	CASE 1, 2
		PRINTFORMDL %CHILDNAME:44:(TALENT:44:育児中)%はエレンに甘えている
		SELECTCASE RAND:7
		CASE 1
			PRINTFORMDL 『マーマ%UNICODE(0x2665)%』
			PRINTFORMW 「なーに%UNICODE(0x2665)%」
		CASE 2
			PRINTFORMDW 『あのねあのねー』
		CASE 3
			PRINTFORMDL 『ママもパパもだーいすき%UNICODE(0x2665)%』
			PRINTFORMW 「お母さんも%CHILDNAME:44:(TALENT:44:育児中)%\@ TALENT:恋慕 || TALENT:恋人 ? と%CALLNAME:MASTER% #  \@の事が大好きよ%UNICODE(0x2665)%」
		CASE 4
			PRINTFORMDL 『ちゅー%UNICODE(0x2665)%』
			PRINTFORMW 「ちゅー%UNICODE(0x2665)%」
		CASE 5
			PRINTFORMDL %CHILDNAME:44:(TALENT:44:育児中)%はエレンに抱きついた
			PRINTFORML 「あら、甘えんぼさんね%UNICODE(0x2665)%」
			PRINTFORMDW そんな%CHILDNAME:44:(TALENT:44:育児中)%をエレンは優しく抱きしめている
		CASE 6
			PRINTFORMDL 『ママー、あれはなーに？』
			PRINTFORMW 「あれはねー――」
		CASEELSE
			IF GO_SCHOOL()
				IF TIME:0 <= 460
					PRINTFORMDL 『今日もお勉強頑張ります！』
					PRINTFORMW 「あら、頑張ってきてね%UNICODE(0x2665)%」
				ELSE
					PRINTFORMDL 『今日は寺子屋でねー――』
					SELECTCASE RAND:3
					CASE 1
						PRINTFORMW 「へー、そんな事があったの」
					CASE 2
						PRINTFORMW 「うんうん、良かったわね%UNICODE(0x2665)%」
					CASEELSE
						PRINTFORML 「すごいじゃない%UNICODE(0x2665)% えらいえらい%UNICODE(0x2665)%」
						PRINTFORMDW %CHILDNAME:44:(TALENT:44:育児中)%は嬉しそうにしている
					ENDSELECT
				ENDIF
			ELSE
				PRINTFORMDW 『えへー%UNICODE(0x2665)%』
			ENDIF
		ENDSELECT
	CASE 3, 4
		PRINTFORMDL %CHILDNAME:44:(TALENT:44:育児中)%は%CALLNAME:MASTER%に甘えてきた
		SELECTCASE RAND:6
		CASE 1
			PRINTFORMDW 『パーパ%UNICODE(0x2665)%』
		CASE 2
			PRINTFORMDW 『ねえねえ聞いてー♪』
		CASE 3
			PRINTFORMDW 『パパもママもだーいすき%UNICODE(0x2665)%』
		CASE 4
			PRINTFORMDW 『ちゅー%UNICODE(0x2665)%』
		CASE 5
			PRINTFORMDW %CHILDNAME:44:(TALENT:44:育児中)%は%CALLNAME:MASTER%に抱きついている
		CASEELSE
			PRINTFORMDW 『にへー%UNICODE(0x2665)%』
		ENDSELECT
	CASE 5
		RESETCOLOR
		PRINTFORMDL %CHILDNAME:44:(TALENT:44:育児中)%はエレンに質問している
		PRINTFORMD 『ママー、
		;質問の内容は大体「魔法使いの弟子」と「ルプ☆さらだ」から持ってきています
		PRINTDATA
			DATAFORM どうして空は青い
			DATAFORM どうしてぺんぺん草はいろんな所にはえてる
			DATAFORM 川の水ってどこからわいてくる
			DATAFORM コーヒーを飲むと眠れなくなるのはどうしてな
			DATAFORM なんですいせいにはしっぽがある
			DATAFORM パパとママはどこで知り合った
			DATAFORM なんで好き嫌いしちゃだめな
			DATAFORM 妖精があちこちにいるのはなんでな
			DATAFORM 妖精はブラックサンダーが好きってほんとうな
		ENDDATA
		PRINTFORMDL の？』
		TRYCALLFORM M_KOJO_COLOR_K44
		PRINTFORMW 「それはね――」
	CASE 6
		PRINTFORMDL %CHILDNAME:44:(TALENT:44:育児中)%はソクラテスと遊んでいる
		IF !RAND:4
			PRINTFORMDW %CHILDNAME:44:(TALENT:44:育児中)%に振り回されてソクラテスはへとへとのようだ……
		ELSE
			PRINTFORMDW ソクラテスは面倒くさがりながらも%CHILDNAME:44:(TALENT:44:育児中)%に付き合っている……
		ENDIF
	CASEELSE
		SELECTCASE RAND:2
		CASE 1
			PRINTFORMDW %CHILDNAME:44:(TALENT:44:育児中)%はキョロキョロと辺りを見回している……
		CASEELSE
			PRINTFORMDW %CHILDNAME:44:(TALENT:44:育児中)%はぽけーっとしている
		ENDSELECT
	ENDSELECT
ELSEIF ARGS == "自立前"
	SELECTCASE RAND:3
	CASE 1
		PRINTFORMDL %CHILDNAME:44:(TALENT:44:育児中)%はエレンと会話している
		SELECTCASE RAND:4
		CASE 1
			PRINTFORMDL 『お母さんに聞きたい事が\@ CHILD_SEX:44:(TALENT:44:育児中) == 1 ? あるのですが #  \@』
			PRINTFORMW 「あら、何を聞きたいの？」
		CASE 2
			PRINTFORMDW 『この前\@ CHILD_SEX:44:(TALENT:44:育児中) == 1 ? ですね #  \@――』
		CASE 3
			PRINTFORMDL 『お母さんの料理を教えて下さい』
			PRINTFORMW 「いいわよ%UNICODE(0x2665)%　それじゃあ――」
		CASEELSE
			IF GO_SCHOOL()
				IF TIME:0 <= 460
					PRINTFORMDW 『……なるほど、そんな事もある\@ CHILD_SEX:44:(TALENT:44:育児中) == 1 ? んですね # のですか \@』
				ELSE
					PRINTFORMDL 『今日は寺子屋でこんな事が……』
					SELECTCASE RAND:3
					CASE 1
						PRINTFORMW 「そんな事があったのねー」
					CASE 2
						PRINTFORMW 「ふむふむ」
					CASEELSE
						PRINTFORMDL 「流石はわたし達の子ね%UNICODE(0x2665)% よしよし%UNICODE(0x2665)%」
						PRINTFORMDW %CHILDNAME:44:(TALENT:44:育児中)%は恥ずかしがりながらも嬉しそうにしている
					ENDSELECT
				ENDIF
			ELSE
				PRINTFORMDW 『あの\@ CHILD_SEX:44:(TALENT:44:育児中) == 1 ? ですね #  \@――』
			ENDIF
		ENDSELECT
	CASE 2
		PRINTFORMDL %CHILDNAME:44:(TALENT:44:育児中)%は%CALLNAME:MASTER%に話しかけてきた
		SELECTCASE RAND:4
		CASE 1
			PRINTFORMDW 『\@ CHILD_SEX:44:(TALENT:44:育児中) == 1 ? 少し相談してもいいですか？ # お父さんに相談したいことが \@』
		CASE 2
			PRINTFORMDW 『この前寺子屋で――』
		CASE 3
			;俗に言う親父の味
			PRINTFORMDW 『お父さんの料理を教えて\@ CHILD_SEX:44:(TALENT:44:育児中) == 1 ? 欲しいのですが…… # くれませんか？ \@』
		CASEELSE
			PRINTFORMDW 『……ふむふむ、そうなる\@ CHILD_SEX:44:(TALENT:44:育児中) == 1 ? んですね # のですか \@』
		ENDSELECT
	CASEELSE
		SELECTCASE RAND:2
		CASE 1
			PRINTFORMDW %CHILDNAME:44:(TALENT:44:育児中)%はソクラテスを優しく愛でている
		CASEELSE
			PRINTFORMDW %CHILDNAME:44:(TALENT:44:育児中)%は宿題を進めている……
		ENDSELECT
	ENDSELECT
ELSE
	PRINTFORMDW 『これは出ないはず\@ CHILD_SEX:44:(TALENT:44:育児中) == 1 ? なの # なんだ \@、出たらスレで報告してください』
ENDIF

[SKIPSTART]
;-------------------------------------------------
;口上専用手紙イベント。口上色はここで付けてください。
;CFLAG等のキャラ変数を使う場合、キャラ番号の指定が必要になります。
;専用イベントを作りたい場合は上下のSKIPSTART~SKIPENDを外してください。
;手紙イベント時に1/2の確率でこちらが発生します。
;ARG 子供の番号（例：子供の名前は『%CHILDNAME:X:ARG%』）
;-------------------------------------------------
@M_KOJO_EVENT_K44_CHILD_DAILY_口上手紙(ARG)
;-------------------------------------------------

;-------------------------------------------------
[SKIPEND]

[SKIPSTART]
;-------------------------------------------------
;手紙イベントの地の文。口上色はここで付けてください。
;CFLAG等のキャラ変数を使う場合、キャラ番号の指定が必要になります。
;デフォルトのものと置き換えたい場合は上下のSKIPSTART~SKIPENDを外してください。
;置き換える気がないならスルー。
;ARG	子供の番号（例：子供の名前は『%CHILDNAME:X:ARG%』）
;-------------------------------------------------
@M_KOJO_EVENT_K44_CHILD_DAILY_共通手紙(ARG, ARGS)
;-------------------------------------------------
IF ARGS == "すけこまし"
	
ELSEIF ARGS == "金の無心"
	
ELSEIF ARGS == "仕送り"
	
ENDIF

;-------------------------------------------------
[SKIPEND]

;-------------------------------------------------
;デイリーイベント：子供が寺子屋で
;ARG	子供の番号（例：子供の名前は『%CHILDNAME:X:ARG%』）
;-------------------------------------------------
@M_KOJO_EVENT_K44_CHILD_DAILY_寺子屋(ARGS,ARG,ARG:1)
IF ARGS == "怪我させた"
	PRINTFORML 
ELSEIF ARGS == "褒められた"
	PRINTFORML 
ENDIF

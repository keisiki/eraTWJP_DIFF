
;### 関数 ###################################
;==================================================
;人目があるか判定関数＠スカートめくり判定のをまとめただけ
;人目がある =1 無い =0
;-------------------------------------------------
@K30_BE_SEEN()
#FUNCTION
SIF GET_TARGETNUM() > 1 || DATE_HITOGOMI(CFLAG:PLAYER:現在位置) || WITH_MOB()
	RETURNF 1
RETURNF 0 
;==================================================
;挨拶を返す式中関数
;4時～10時前・10時～18時前・それ以外
;-------------------------------------------------
@K30_GREETING_EIKI()
#FUNCTIONS
SELECTCASE TIME
	CASE 240 TO 599
		SIF TALENT:恋人
			RETURNF "おはよう"
		RETURNF "おはようございます"
	CASE 600 TO 1079
		RETURNF "こんにちは"
	CASEELSE
		RETURNF "こんばんは"
ENDSELECT
;==================================================
;MASTERの恋人がその場に居るかどうか返す式中関数
;=-1 映姫様が恋人 =0 元から恋人なし =1 その場に居ない =2 恋人と同席
;非恋人時の映姫様を面倒くさい女にさせる
;-------------------------------------------------
@K30_FIND_LOVER()
#FUNCTION
SIF TALENT:MASTER:恋人 == 0
	RETURNF 0
SIF TALENT:30:恋人
	RETURNF -1
SIF CFLAG:(TALENT:MASTER:恋人):現在位置 == CFLAG:MASTER:現在位置
	RETURNF 2
RETURNF 1
;==================================================
;変Ｔで映姫様の目が死ぬか判別する式中関数
;=0 平気 =1 目が死ぬ 
;着せ替えコマンドの存在忘れてた
;-------------------------------------------------
@K30_CHECK_HEN_T()
#FUNCTION
SIF DAY % 7 == 5
	RETURNF 1
SIF FLAG:ファッション == 62
	RETURNF 1
SIF CFLAG:30:衣装一時変更 == 62
	RETURNF 1
RETURNF 0
;==================================================
;酔っぱらい度を数字で返す式中関数
;=0 素面 =1 ほろ酔い =2 酔いどれ 3=ぐでぐで
;-------------------------------------------------
@K30_DRUNK()
#FUNCTION
SIF BASE:30:酒気 <= 2
	RETURNF 0
SELECTCASE BASE:30:酒気
	;ぐでぐで
	CASE IS > (MAXBASE:30:酒気 / 10) * 8
		RETURNF 3
	;酔いどれ
	CASE IS > (MAXBASE:30:酒気 / 10) * 5
		RETURNF 2
	;ほろ酔い
	CASE IS <= (MAXBASE:30:酒気 / 10) * 5
		RETURNF 1
ENDSELECT
;==================================================
;キスその他を視線加味して通すか判断する式中関数 
;フレーバーのだから辛めでもいいや
;=0 失敗 =1 成功
;-------------------------------------------------
@K30_TRY_HARASS
#FUNCTION
;状態異常・うふふ時・お招き時は通す
SIF SHIRAHU(30) != 1 || CFLAG:30:うふふ || CFLAG:30:お招き
	RETURNF 1
;キス魔がついたらキスは全通し
SIF TALENT:キス魔 && SELECTCOM == 312
	RETURNF 1
LOCAL = RAND:110
LOCAL:1 = 25 + GET_SUCCESS_RATE() / 8
SIF LOCAL:1 > 109
	LOCAL:1 = 109
;キャップをかけた後に視線が通ってたら-33%、違ったら+20%
IF K30_BE_SEEN() == 0
	LOCAL:1 += 20
ELSE
	LOCAL:1 -= 33
ENDIF
;反発刻印1につき-20%
LOCAL:1 -= MARK:30:反発刻印 * 20
SIF LOCAL >= LOCAL:1
	RETURNF 0
RETURNF 1
;==================================================
;同部屋のキャラを拾う式中関数
;アリス口上の関数お借りして平文化しました。ERHだと動作が速かったり安定するのかな
;丸コピはあれなので知り合い以外はランダムで選んでIDを返すように。誰もいなければ0を返す
;引数が真-TARGET内ランダム、偽-優先順序付き（[恋人]持ち→知り合い→TARGET内ランダム）
;映姫様の知り合いは7人,小町>久侘歌>さとり>ヘカーティア>阿求>幽々子>紫の順に(勝手に)設定
;-------------------------------------------------
@K30_FIND_AROUND(ARG = 0)
#FUNCTION
#DIM 知り合い,7 = 76, 129, 49, 113, 80, 66, 26
#DIM 総人数
#DIM 抽選
#LOCALSIZE 200
VARSET LOCAL
抽選 = 0
総人数 = GET_TARGETNUM() 
;部屋に映姫様一人なら当然0
SIF 総人数 == 1
	RETURNF 0
IF !ARG
	;[恋人]持ちがいたら優先してIDを返す
	SIF K30_FIND_LOVER() == 2
		RETURNF TALENT:MASTER:恋人
	;知り合い優先度順に現在位置を調べMASTERと同じならIDを返す
	FOR COUNT, 0, 7
		SIF CFLAG:(知り合い:COUNT):現在位置 == CFLAG:MASTER:現在位置
			RETURNF 知り合い:COUNT
	NEXT
ENDIF
;知り合いがいなければ同室のTARGET_ID全部を拾う
FOR COUNT,1, 総人数 + 1
	;地獄の断頭台withジェロニモ状態を防ぐため口上主はスキップ
	SIF TARGET:COUNT == TARGET:0
		CONTINUE
	;MASTERと映姫様の現在位置にいなければスキップ
	SIF (CFLAG:(TARGET:COUNT):現在位置 != CFLAG:MASTER:現在位置) || (CFLAG:(TARGET:COUNT):現在位置 != CFLAG:30:現在位置)
		CONTINUE
	;配列のカウントは別で取りながら代入
	LOCAL:抽選 = TARGET:COUNT
	抽選 ++
NEXT
;この条件でここまで来たのはなんでだろう
;ともかくエラー出さないためにヨシ！
SIF 抽選 < 1
	RETURNF 0
;抽選してIDを決定
RETURNF LOCAL:(RAND(抽選))
;==================================================
;映姫様が他のキャラを呼ぶ式中関数
;呼び捨て =小町 久侘歌：敬称 =ヘカーティア「様」：他は皆「さん」づけ
;ARG=0でMASTER 条件付けで呼び名を変える。需要なさそうだけどおにいさんにも対応
;引数 =1or2をとると行きずりキャラ/MASTER/対象キャラ無し(ARG=0)が指定されたら「だれか」「他の人」と呼ぶ＠セクハラが捗る
;引数 =3で種族・属性呼び。CSV読みでTALENT分けより雰囲気出る気がするけど、個別で設定しないからやっぱり変なのはある
;-------------------------------------------------
@K30_C_NAME(ARG, TYPE = 0)
#FUNCTIONS
#DIM TYPE
#DIM VARIANT
#DIMS STR_LIST, 10
VARSET LOCALS
VARSET STR_LIST
VARIANT = 0
SIF TYPE == 3
	GOTO TRIBE
IF TYPE && (ARG == RANDOM_CHARANUM || ARG == 0)
	SIF TYPE == 1
		RETURNF "だれか"
	RETURNF "他の人"
ENDIF
SELECTCASE ARG
	;MASTERの呼び名
	CASE 0
		;反発持ち、ブチギレ時はよそよそしい感じに
		SIF CFLAG:30:ブチギレ || MARK:30:反発刻印 > 0
			RETURNF CALLNAME:MASTER + "さん"
		;恋慕無ければ通常形
		SIF !TALENT:30:恋慕
			RETURNF MASTERNAME:30 + "さん"
		;名有りキャラ周りにいない+恋慕時に設定で固有の呼び方
		SIF GET_TARGETNUM() <= 1 && CSTR:30:80 != ""
			RETURNF CSTR:30:80
		;恋人は呼び捨て
		SIF TALENT:30:恋人
			RETURNF MASTERNAME:30
		;うふふ中で人目が無い時も呼び捨て
		SIF CFLAG:30:うふふ && K30_BE_SEEN() == 0
			RETURNF MASTERNAME:30
		;デート中は固有の呼び名
		SIF CHK_DATENOW(CFLAG:デート中) && CSTR:30:80 != ""
			RETURNF CSTR:30:80
		;それ以外は通常形
		RETURNF MASTERNAME:30 + "さん"
	;小町・久侘歌
	CASE 76, 129
		RETURNF CALLNAME:ARG
	;ヘカーティア
	CASE 113
		RETURNF CALLNAME:ARG + "様"
	;その他
	CASEELSE
		RETURNF CALLNAME:ARG + "さん"
ENDSELECT
$TRIBE
;チルノだけ強烈に違和感あったので直接指定
SIF ARG == 14
	RETURNF "氷精"
;CSTR:10からの抽出はPRINT_STATEの流用。紹介文が無い、'●'で区切られてないなら飛ばす
LOCALS =  %CSVCSTR(ARG, 10)%
IF STRFIND(LOCALS, "●") > -1
	SPLIT LOCALS, "●", STR_LIST
	FOR LOCAL, 0, RESULT
		IF STRFIND(STR_LIST:LOCAL, "種族：") > -1
			LOCALS:1 = %REPLACE(STR_LIST:LOCAL, "種族：", "")%
			LOCALS:1 = %REPLACE(LOCALS:1, "　", "")%
			BREAK
		ENDIF
	NEXT
ENDIF
;'（）'で種族が括られたキャラは個別対応
IF STRFIND(LOCALS:1, "（") > -1
	SPLIT LOCALS:1, "（", LOCALS
	LOCALS:1 = %REPLACE(LOCALS:1, "）", "")%
	LOCALS:1 = %\@ GROUPMATCH(ARG,35,40,104,105) ? %LOCALS:1% # %LOCALS:0% \@%
;'人間''妖怪'はTALENTの方で更に詳しく見る
ELSEIF  LOCALS:1 == "人間"
	VARIANT = 1
	LOCALS:1 = 
ELSEIF LOCALS:1 == "妖怪"
	VARIANT = 2
	LOCALS:1 = 
ENDIF
SIF LOCALS:1 != ""
	RETURNF LOCALS:1
;'人間''妖怪'は追加種族→TALENTで判断
IF VARIANT
	IF TALENT:ARG:GETNUM(TALENT, "追加種族")
		LOCALS:1 = %GET_TRIBENAME(ARG, GETNUM(TALENT, "追加種族"))%
	ELSEIF VARIANT == 1
		LOCALS:1 = %GET_TRIBENAME(ARG, GETNUM(TALENT, "人間"))%
	ELSE
		LOCALS:1 = %GET_TRIBENAME(ARG, GETNUM(TALENT, "妖怪"))%
	ENDIF
;その他はしらみつぶしで拾いに行く
ELSE
	;条件できちんと分けるべきだが手抜き。仕様変わったらちゃんと合わせる
	FOR LOCAL, 0, 7
		IF TALENT:ARG:(190 + (LOCAL))
			LOCALS:1 = %GET_TRIBENAME(ARG, (190 + LOCAL))%
			BREAK
		ENDIF
	NEXT
	;'人間''妖怪'は追加種族を優先させる
	SIF TALENT:ARG:GETNUM(TALENT, "追加種族") && (TALENT:ARG:GETNUM(TALENT, "妖怪") == 1 || TALENT:ARG:GETNUM(TALENT, "人間") == 1)
		LOCALS:1 = %GET_TRIBENAME(ARG, GETNUM(TALENT, "追加種族"))%
ENDIF
RETURNF LOCALS:1
;==================================================
;中出しOKか判別する式中関数
;性交関連口上の準備（書けるかは不明）
;--------------------------------------------------
;=0 中出しOK =1 妊婦さんだからダメ =2 育児中だからダメ =3 合意が無い
;--------------------------------------------------
@K30_CREAMPIE()
#FUNCTION
;ピルさえ飲めば出すのは安心
SIF TCVAR:30:ピル
	GOTO PREG
;合意状況を見る
SIF (CFLAG:30:生解禁 == 0 && ESTRUS_CYCLE(30) != 15) || (CFLAG:30:生解禁 == 1 && ESTRUS_CYCLE(30) == 150)
	RETURNF 3
;育児状況を見る
SIF TALENT:30:育児中 && CFLAG:30:出産経過日 < CHILD_就学
	RETURNF 2
$PREG
;母胎の成育を見る
SIF TALENT:30:妊娠 && !INRANGE(CFLAG:妊娠経過日数, 31, 70)
	RETURNF 1
;ここまで抜けたらOK
RETURNF 0
;==================================================
;キスハメ実行可能か見る式中関数
;性交関連口上をシチュ固定して埋めやすくする
;--------------------------------------------------
;=0 OK =-1 猫舌だからダメ =-2 ばっちいからダメ
;--------------------------------------------------
@K30_KISS_CANCEL()
#FUNCTION
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:舌 < 2
	RETURNF -1
SIF TALENT:猫舌 && ABL:舌 < 2
	RETURNF -1
SIF TALENT:PLAYER:汚臭耐性 > 1
	RETURNF 0
SIF STAIN:0 & 汚れ_精液 && ABL:PLAYER:精液中毒 < 3
	RETURNF -2
SIF STAIN:0 & 汚れ_アナル
	RETURNF -2
SIF STAIN:0 & 汚れ_ペニス && TALENT:PLAYER:汚臭耐性 < 0
	RETURNF -2
SIF TALENT:PLAYER:汚臭耐性 == -2 && (STAIN:0 & 汚れ_破瓜血 || STAIN:0 & 汚れ_愛液)
	RETURNF -2
RETURNF 0
;==================================================
;REDRAW制御とCLEARLINEで直前の文章を消す関数
;擬似的にイベントをスキップする目的で使用
;--------------------------------------------------
@K30_CLEAR_TEXTLINE(ARG = 1)
LOCAL = CURRENTREDRAW()
REDRAW 0
CLEARLINE ARG
REDRAW LOCAL
RETURN
;==================================================
;子供の口上色を適当に決める関数
;名前の文字コードから起こして固定化する
;--------------------------------------------------
@K30_CHILDWORD()
#FUNCTION
SIF !FLAG:口上色
	RETURNF GETDEFCOLOR()
RETURNF GET_CN_WORD_COLOR(STR:(2050 + (ENCODETOUNI(SHOW_CHILDNAME(30)) % 13)))
;==================================================
;映姫様がどれだけヤル気か見る式中関数
;@ENDUREを仕事やスキンシップ中に使う為に少し削って持ってきただけ
;本体ではこの値が RAND:300 + 300 より大きいと押し倒されに移行
;-------------------------------------------------
@K30_PUSH_DOWN()
#FUNCTION
;素質により判定変化
LOCAL = CFLAG:30:合意判定
LOCAL += (EX:30:Ｃ寸止め + EX:30:Ｖ寸止め + EX:30:Ａ寸止め + EX:30:Ｂ寸止め + EX:30:Ｍ寸止め) * 20
;抱き着くと押し倒され率上昇
SIF SELECTCOM == 311
	LOCAL += 30
LOCAL += TALENT:30:性的興味 * 20
SIF TALENT:30:処女
	LOCAL -= 50
SIF TALENT:30:恋慕
	LOCAL = LOCAL + 50
SIF TALENT:30:プライド > 0
	LOCAL -= 20
SIF CFLAG:30:生解禁 == 2
	LOCAL = LOCAL + 50
SIF CFLAG:30:無理矢理膣射
	LOCAL -= 100
SIF TALENT:30:度胸 < 0 || TALENT:30:応答 < 0 || TALENT:30:無関心
	LOCAL = LOCAL - 30
SIF TALENT:30:羞恥心 > 0
	LOCAL -= 30
SIF TALENT:30:羞恥心 < 0
	LOCAL = LOCAL + 20
SIF TALENT:30:自制心 
	LOCAL = LOCAL - 20
SIF TALENT:30:貞操 < 0 || TALENT:30:小悪魔
	LOCAL = LOCAL + 20
SIF TALENT:30:貞操 > 0 || TALENT:30:一線越えない
	LOCAL -= 20
IF !GETBIT(CFLAG:30:既成事実, 1) && TALENT:30:貞操 > 0
	LOCAL -= 30
	SIF !TALENT:30:恋慕 && !TALENT:30:セフレ
		LOCAL -= 50
ENDIF
SIF OTOKOGIRAI(30) && !(IsHeat(30) || IsInsenceValid(30))
	LOCAL = LOCAL / 2
SIF TALENT:30:一線越えない && TALENT:30:恋慕
	LOCAL = LOCAL * 12 / 5
SIF MARK:30:反発刻印 == 3
	LOCAL = 0
SELECTCASE CFLAG:30:溜まってる度
	CASE IS >= 1000
		LOCAL *= 3
	CASE IS >= 800
		LOCAL = LOCAL * 3 / 2
	CASE IS >= 600
		LOCAL = LOCAL * 5 / 4
	CASE IS >= 500
	CASE IS >= 300
		LOCAL /= 2
	CASE IS >= 200
		LOCAL /= 4
	CASE IS >= 100
		LOCAL /= 10
	CASEELSE
		LOCAL = 0
ENDSELECT
;体力に依存して押し倒し率減少
SELECTCASE BASE:30:体力
	CASE IS < 750
		LOCAL = 0
	CASE IS < 1000
		LOCAL /= 2
	CASE IS < 1500
		LOCAL = LOCAL * 3 / 4
	CASEELSE
ENDSELECT
SELECTCASE BASE:30:気力
	CASE 0
		LOCAL = 0
	CASE IS < 500
		LOCAL /= 2
	CASEELSE
ENDSELECT
;理性に依存して押し倒し率減少
IF BASE:30:理性 > 800
	LOCAL = LOCAL / 5
ELSEIF BASE:30:理性 > 600
	LOCAL = LOCAL / 2
ENDIF
SELECTCASE BASE:30:ムード
	CASE IS < 200
		LOCAL = 0
	CASE 201 TO 400
		LOCAL /= 4
	CASE 401 TO 600
		LOCAL /= 2
	CASE 601 TO 800
	CASEELSE
		LOCAL = LOCAL * 6 / 5
ENDSELECT
SIF FLAG:甲斐性無
	LOCAL = LOCAL * 2
SIF CFLAG:30:ブチギレ
	LOCAL = 0
RETURNF LOCAL
;==================================================
;日記用に場所を文字列で返す式中関数＠月マップ対応
;数値で残すと拠点引っ越しで変わる場合があるので
;-------------------------------------------------
@K30_DIARY_PLACE(ARG)
#FUNCTIONS
#DIM 対象マップ
#DIMS TEMP_NAME
SIF CFLAG:30:初期位置 == ARG
	RETURNF "自分の部屋"
対象マップ =  GET_MAPID(ARG)
SELECTCASE 対象マップ
	CASE 0
		SELECTCASE MAP_SEARCH_REPLACEMENT_0(ARG)
			CASE 妖精の大樹
				TEMP_NAME = 三妖精の家
			CASE 夢幻遺跡
				TEMP_NAME = 夢幻遺跡
			CASEELSE
				TEMP_NAME = 博麗神社
		ENDSELECT
	CASE 1
		SELECTCASE MAP_SEARCH_REPLACEMENT_1(ARG)
			CASE 神霊廟広場, 神霊廟道場
				TEMP_NAME = 神霊廟
			CASEELSE
				TEMP_NAME = 命蓮寺
		ENDSELECT
	CASE 2
		TEMP_NAME = 人里
	CASE 3
		SELECTCASE MAP_SEARCH_REPLACEMENT_3(ARG)
			CASE 霧の湖, 氷のかまくら
				TEMP_NAME = 霧の湖
			CASE 廃洋館
				TEMP_NAME = 騒霊の廃洋館
			CASEELSE
				TEMP_NAME = 紅魔館
		ENDSELECT
	CASE 4
		SELECTCASE MAP_SEARCH_REPLACEMENT_4(ARG)
			CASE 永遠亭, 永遠亭奥ノ院
				TEMP_NAME = 永遠亭
			CASE 無名の丘
				TEMP_NAME = 無名の丘
			CASE 太陽の畑
				TEMP_NAME = 太陽の畑
			CASE 夢幻館
				TEMP_NAME = 夢幻館
			CASEELSE
				TEMP_NAME = 迷いの竹林
		ENDSELECT
	CASE 5
		SELECTCASE MAP_SEARCH_REPLACEMENT_5(ARG)
			CASE 香霖堂
				TEMP_NAME = 香霖堂
			CASE 霧雨魔法店
				TEMP_NAME = 霧雨魔法店
			CASE 人形の洋館
				TEMP_NAME = 人形の洋館
			CASE 再思の道
				TEMP_NAME = 再思の道
			CASE 無縁塚
				TEMP_NAME = 無縁塚
			CASEELSE
				TEMP_NAME = 魔法の森
		ENDSELECT
;		SIF TEMP_NAME == "魔理沙の店"
;			TEMP_NAME = 霧雨魔法店
;		SIF TEMP_NAME == "エレンの店"
;			TEMP_NAME = 魔女の店
	CASE 6
		SELECTCASE MAP_SEARCH_REPLACEMENT_6(ARG)
			CASE マヨヒガ
				TEMP_NAME = 八雲家
			CASE 彼岸
				TEMP_NAME = 是非曲直庁
			CASE 白玉楼庭, 白玉楼
				TEMP_NAME = 白玉楼
			CASE 地獄
				TEMP_NAME = 地獄
			CASE 畜生界
				TEMP_NAME = 畜生界
			CASE 霊長園
				TEMP_NAME = 霊長園
			CASEELSE
				TEMP_NAME = 三途の川
		ENDSELECT
	CASE 7
		SELECTCASE MAP_SEARCH_REPLACEMENT_7(ARG)
			CASE 正邪の隠れ家前
				TEMP_NAME = 天邪鬼の住処
			CASE 仙人の屋敷
				TEMP_NAME = 仙人の屋敷
			CASEELSE
				TEMP_NAME = 妖怪の山の山麓
		ENDSELECT
;		SIF TEMP_NAME == "秋姉妹の家"
;			TEMP_NAME = 秋神様の住まい
;		SIF TEMP_NAME == "正邪の隠れ家前"
;			TEMP_NAME = 天邪鬼の住処
	CASE 8
		SELECTCASE MAP_SEARCH_REPLACEMENT_8(ARG)
			CASE 山の湖, 守矢神社境内, 守矢神社本殿
				TEMP_NAME = 守矢神社
			CASE 虹龍洞
				TEMP_NAME = 虹龍洞
			CASE 天界
				TEMP_NAME = 天界
			CASEELSE
				TEMP_NAME = 妖怪の山の山頂
		ENDSELECT
	CASE 9
		SELECTCASE MAP_SEARCH_REPLACEMENT_9(ARG)
			CASE 旧地獄街道, 旧地獄温泉, ラブホ
				TEMP_NAME = 旧都
			CASE 地霊殿
				TEMP_NAME = 地霊殿
			CASE 灼熱地獄跡
				TEMP_NAME = 旧地獄
			CASEELSE
				TEMP_NAME = 地底
		ENDSELECT
	CASE 10
		SELECTCASE MAP_SEARCH_REPLACEMENT_10(ARG)
			CASE 槐安通路
				TEMP_NAME = 槐安通路
			CASE 月の都, 綿月亭, 玉兎の街
				TEMP_NAME = 月の都
			CASE 桃林～砂浜, 静かの海
				TEMP_NAME = 月都の郊外
			CASEELSE
				TEMP_NAME = 月の世界
		ENDSELECT
	CASEELSE
		TEMP_NAME = 見知らぬ場所
ENDSELECT
RETURNF TEMP_NAME
;==================================================
;日記用データを呼び出す式中関数
;--------------------------------------------------
;ARG: =0 日付  =1 天気 =2 特記事項
;	  =3 名前  =4 場所 =5 別記事項
;--------------------------------------------------
@K30_DIARY_DATA(キー, 種別)
#FUNCTIONS
#DIM 種別
#DIMS キー
#DIMS 戻り値
#DIMS 取得文字列, 4
#DIMS 仮天気, 5 = "晴れ", "快晴", "薄曇", "曇り", "雨"
VARSET 取得文字列
LOCAL = 0
;読み込むCSTRの選別
IF 種別 > 2
	LOCAL ++
	種別 -= 3
ENDIF
;日記IDをキーとしてCSTRを検索
戻り値 = %DIC_GET(CSTR:30:(81 + LOCAL), キー)%
IF 戻り値 != ""
	;情報種別ごとに分割
	SPLIT 戻り値, "/", 取得文字列
	;取得値なければフォローへ移動
	SIF 取得文字列:種別 == ""
		GOTO FOLLOW
	;引数に応じて文字列を返す
	RETURNF 取得文字列:種別
;VerUP,不具合等で戻り値が空だった場合のフォロー
ELSE
	$FOLLOW
	SIF LOCAL
		種別 += 3
	SELECTCASE 種別
		CASE 0
			RETURNF "1"
		CASE 1
			LOCAL = TOINT(キー) % 5
			RETURNF 仮天気:LOCAL
		CASE 2, 5
			RETURNF ""
		CASE 3
			RETURNF MASTERNAME:30
		CASE 4
			RETURNF "あの場所"
	ENDSELECT
ENDIF
;==================================================
;日記に書き込む関数。第2引数を取ると未公開日記(DIARY:30:XX = 3)
;同時に日付等をCSTR:81,CSTR:82に記録。日記IDをキーとして使える@DIC_SETを拝借
;連想配列とかちゃんと意味わかってないけど、なんか使えて動いてるからこれでよし
;-------------------------------------------------
@K30_WRITE_DIARY(日記ID, ARG = 0)
#DIM 日記ID
#DIMS 日時
#DIMS 場所
#DIMS 天気
#DIMS 名前
#DIMS 特記
#DIMS 別記
#DIMS キー
#DIMS 書き込み値, 2
#DIMS 仮天気, 5 = "晴れ", "快晴", "薄曇", "曇り", "雨"
VARSET LOCALS
特記 =
別記 =
;残す記録を変換
キー = %TOSTR(日記ID)%
日時 = %TOSTR(DAY:0)%
場所 = %K30_DIARY_PLACE(CFLAG:30:現在位置)%
;異常気象は一部言い換え
SELECTCASE FLAG:異常気象
	CASE 1 TO 6
		天気 = %仮天気:(RAND:4)%　%\@ DAY:2 == 2 ? じっとりと暑い # 妙に暖かい \@%
	CASE 7, 8
		天気 = %仮天気:(RAND:4)%　%\@ FLAG:異常気象 == 7 ? 下着が飛んでいる？ # 妙に風が強い \@%
	CASEELSE
		天気 = %GET_WEATHER()%
ENDSELECT
;UPDATEからなら天気はランダム
SIF TFLAG:100 == 0
	天気 = %仮天気:(RAND:5)%
;名前はMASTERNAMEor恋慕後の愛称。陥落無しなら「さん」付け。変遷があると日記感がでる…かもしれない
名前 = %\@ CSTR:30:80 != "" ? %CSTR:30:80% # %MASTERNAME:30% \@%%\@ TALENT:30:思慕 || TALENT:30:恋慕 ? # さん \@%
;反発３で名前を呼んではいけないあの人扱い
名前 = %\@ MARK:反発刻印 == 3 ? あの人 # %名前% \@%
;ページごとの特記事項
SELECTCASE 日記ID
	;キス：色々選別
	CASE 1
		;「と思っているが、実は」の場合、前の段落だけ拾って無自覚を弾く
		IF STRFIND(CSTR:30:ファーストキス喪失履歴, "￥ＢＲ") > -1
			SPLIT CSTR:30:ファーストキス喪失履歴, "￥ＢＲ", LOCALS
		ELSE
			LOCALS:0 = %CSTR:30:ファーストキス喪失履歴%
		ENDIF
		;無理矢理だと日記書かない
		IF STRCOUNT(LOCALS:0, "無理矢理で") 
			SETBIT CFLAG:30:1910, 2
			RETURN
		ELSE
			;UPDATEからなら場所をぼかす
			SIF TFLAG:100 == 0
				場所 = 大切な場所
			SIF TALENT:30:恋慕
				特記 = 恋
			SIF STRCOUNT(LOCALS:0, "捧げた") || GETBIT(CFLAG:30:1910, 6)
				別記 = 捧
		ENDIF
	;処女喪失：色々選別
	CASE 2
		;無意識とあなた以外で破瓜は以後スルー
		IF STRCOUNT(CSTR:30:処女喪失履歴, "時間停止中|酔いつぶれて|眠っている|不満を感じつつ") 
			SETBIT CFLAG:30:1910, 3
			RETURN
		ENDIF
		;破瓜口上用意してないせいでかなりガバガバだけど3段階で選別
		日時 = %TOSTR(CFLAG:30:処女喪失記念日)%
		IF TALENT:30:恋慕 && !STRCOUNT(CSTR:30:処女喪失履歴, "力ずくで|バイブで") 
			特記 = 恋
		ELSEIF TALENT:30:思慕 || TALENT:30:恋慕
			特記 = 思
		ELSE
			特記 = 無
		ENDIF
	;お小言もらった：理由と説教の長さ
	CASE 5
		特記 = %TOSTR(TFLAG:193)%
	;お説教たいむ：小町のサボり現場
	CASE 8
		場所 = %K30_DIARY_PLACE(CFLAG:MASTER:現在位置)%
		SIF 場所 == "三途の川"
			場所 = ちょっと別の場所
	;ぶん殴られた：耐えたor倒れた
	CASE 10
		SIF BASE:MASTER:体力 <= 500
			特記 = 倒
	;贈り物(GOOD)：贈り物フルネーム、恋慕の有無
	CASE 12
		CALL GIFTNAME(TCVAR:30:今日の贈り物)
		特記 = %RESULTS%
		別記 = %\@ TALENT:恋慕 ? 恋 # \@%
	;贈り物(NORMAL&BAD)：贈り物本体名
	CASE 13, 14
		CALL GIFTDATA_MAIN(GET_GIFTDATA(TCVAR:30:今日の贈り物, "本体"))
		特記 = %RESULTS%
	;スカートめくり：はいてるぱんつと反応
	CASE 15
		特記 = %PANTSNAME(EQUIP:下半身下着２, 30)%
		SIF 特記 == "ノーパン"
			特記 = はいてないの
		SELECTCASE TFLAG:193
			CASE IS < 0
				別記 = 怒
			CASE IS > 10
				別記 = 喜
			CASEELSE
				別記 = 照
		ENDSELECT
	;説法10回終了：MASTER現住所
	CASE 16
		場所 = %K30_DIARY_PLACE(CFLAG:MASTER:初期位置)%
	;ヘカ様からお手紙：手紙の中身
	CASE 18
		SIF STRCOUNT(CSTR:30:0, "変Ｔ指令")
			特記 = 変Ｔ
	;何か釣れた：釣ったもの
	CASE 22
		SELECTCASE TFLAG:193
			CASE 1 TO 20, 22 TO 31
				特記 = 魚
				SIF GROUPMATCH(TFLAG:193, 9, 23, 24, 28, 30, 31)
					特記 = 大きな魚
			CASE 21
				特記 = あんな変なもの
			CASE 606, 609, 643
				特記 = 素材
			CASE 131, 610
				特記 = 誰かが捨てたゴミ
			CASE 706
				特記 = ただのカエル
		ENDSELECT
	;宴会のお知らせ：開催要項
	CASE 24
		SELECTCASE FLAG:宴会開催フラグ
			CASE 2
				場所 = 博麗神社
				特記 = 忘年会
			CASE 3
				場所 = 永遠亭
				特記 = お月見会
			CASE 4
				場所 = 人里
				特記 = 収穫祭
		ENDSELECT
	;薄い本見ちゃった：本の中身、興味の有無
	CASE 25
		特記 = %\@ TFLAG:193 ? 惨 # \@%
		SIF ABL:欲望 > 6 || DIARY:30:31 != 0
			別記 = 有
	;恋慕：呼び名変えたか
	CASE 30
		IF !STRCOUNT(CSTR:30:0, "呼び名")
			特記 = 1
		ELSEIF CSTR:30:80 == "" || CSTR:30:80 == MASTERNAME:30
			特記 = 2
		ELSE
			別記 = %MASTERNAME:30%
		ENDIF
	;出産：産まれた子どもの名前
	CASE 34
		特記 = %CHILDNAME:30:(TALENT:30:育児中)%
	;小町と同時：映姫様と小町の陥落状態
	CASE 36
		SELECTCASE K30_FIND_LOVER()
			CASE -1
				特記 = 映姫
			CASE 0
			CASE 1, 2
				SIF TALENT:76:恋人
					特記 = 小町
		ENDSELECT
	;尻穴狂い：バイブデート経験、恋慕の有無
	CASE 37
		特記 = %\@ CFLAG:1608 ? 有 # \@%
		別記 = %\@ TALENT:恋慕 ? 恋 # \@%
	;セフレ：取得手段、契約時の分岐
	CASE 39
		IF COM_STR == "K30_契約" && TFLAG:193
			特記 = 契約
			SELECTCASE TFLAG:193
				CASE 3
					別記 = 前世
				CASE 2
					別記 = Ａ調教
				CASE 1
					別記 = 無意識
			ENDSELECT
		ELSE
			特記 = 素質
		ENDIF
ENDSELECT
IF ARG == 0
	DIARY:30:日記ID = 2
ELSE
	DIARY:30:日記ID = 3
	CALL CHARA_DIARY_PAGESETTING(30, 日記ID)
ENDIF
書き込み値:0 = %日時%/%天気%/%特記%
書き込み値:1 = %名前%/%場所%/%別記%
CSTR:30:81 = %DIC_SET(CSTR:30:81, キー, 書き込み値:0)%
CSTR:30:82 = %DIC_SET(CSTR:30:82, キー, 書き込み値:1)%
RETURN
;==================================================
;地の文改変時につじつま合わせる関数
;-------------------------------------------------
@K30_COMPENSATE_MESSAGE
SIF CFLAG:MASTER:うふふ != 2 && SELECTCOM != 461
	CALL カウンター呼び出し処理
CALL M_KOJO_COLOR_K30
RETURN 1
;==================================================
;白い線を引く。ただそれだけの関数
;-------------------------------------------------
@K30_D_LINE
RESETCOLOR
DRAWLINE
CALL M_KOJO_COLOR_K30
RETURN RESULT
;==================================================
;うふふ入る時の準備関数
;絶頂回数等リセット,弱み握り保存,
;さらに意識がある場合、前日行動反映をリセット・うふふ回数/時間記録・映姫様自室なら描写用フラグ立て
;-------------------------------------------------
@K30_PRESET_UFUFU
VARSET TCVAR:30:0, 0, 389, 399
TCVAR:30:381 = CFLAG:弱み握り
IF SHIRAHU(30)
	CFLAG:1603 = 0
	TCVAR:30:380 ++
	TCVAR:30:383 = TIME:1
	SIF GROUPMATCH(CFLAG:MASTER:現在位置, CFLAG:30:初期位置, OMANEKIBEYA())
		TCVAR:30:378 = 1
ENDIF
RETURN RESULT
;==================================================
;うふふ終了の準備関数
;各種フラグ掃除
;意識あり合意ありなら終了時刻を記録,
;-------------------------------------------------
@K30_UFUFU_RECORD
SIF TCVAR:30:383 && !MAXARRAY(TCVAR:30:0, 385, 388) 
	CFLAG:30:1504 = TIME:1
CFLAG:30:風呂 = MIN(CFLAG:30:風呂 + (SUMARRAY(TCVAR:30:0, 390, 395) * 150), 2500)
CFLAG:30:ディレイ += 15
VARSET TCVAR:30:0, 0, 383, 388
TCVAR:30:376 = 0
RETURN RESULT
;==================================================
;TARGET外のまま複数人うふふに入った場合のフォロー関数
;ひとコマンドおいて突然口調が変わるかもだが、そのままよりはマシなはず
;-------------------------------------------------
@K30_FOLLOW_UP
CALL K30_PRESET_UFUFU
SIF CFLAG:1801 == 0
	RETURN
IF !TALENT:恋慕
	TCVAR:30:385 = 1
ELSEIF K30_FIND_LOVER() == 2 && !GROUPMATCH(K30_FIND_AROUND(), 76, 129)
	TCVAR:30:385 = 1
	TCVAR:30:387 = 1
ENDIF
;==================================================
;画面を揺らす関数。再配布ＯＫなので流用
;ここから引用
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;「ぱにめーしょん」0527版　By ぱ。
;Emuera専用汎用、アニメーション・ビジュアル拡張関数ファイル。ほぼバリアント間互換あり
;無断再配布ＯＫ、その際改変・追記は元の機能を損なわない範囲でお願いします
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;-------------------------------------------------
;振動エフェクト関数@QUAKE
;	引数0：振動数。省略すると2
;
;画面を上下に揺らします。揺れ幅はある程度ランダムです
;-------------------------------------------------
@K30_QUAKE(ARG = 2)
#LOCALSIZE 3
;表示オフだと戻る
;SIF !ANIME_CONFIG("QUAKE")
;	RETURN RESULT
REDRAW 0
FOR LOCAL, 0, ARG
	LOCAL:1 = RAND:4+1
	FOR LOCAL:2, 0, LOCAL:1
		PRINTL 
	NEXT
	TWAIT 100, 0
	CLEARLINE LOCAL:1
	TWAIT 100, 0
NEXT
REDRAW 3
RETURN RESULT
;引用ここまで
;==================================================
;主人公の呼び名を変更する関数
;スレで話題にあがってたから、なんとなく入れてみる
;構文は命OD勢の口上からほぼそのまま持ってきてます
@K30_SET_C_NAME(ARG)
#DIMS C_NAM
;--------------------------------------------------
;ARG =0:通常の初回入力 =1:恋慕呼び名入力 =2:願掛けからの通常呼び名変更 =3:願掛けからの恋慕呼び名変更
;--------------------------------------------------
RESETCOLOR
PRINTFORMDL 呼び名を入力してください(直接入力するか、ボタンで選択してください)
IF ARG == 1 || ARG == 3
	C_NAM = %MASTERNAME:30%
	SIF ARG == 3 && CSTR:30:80 != ""
		C_NAM = %CSTR:30:80%
	PRINTBUTTON "[ご主人様がいい]", "ご主人様"
	PRINTL 
	PRINTBUTTON "[旦那様がいい]", "旦那様"
	PRINTL
	PRINTBUTTON @"[やっぱり%C_NAM%でいい]", @"%C_NAM%"
	PRINTL
ELSE
	C_NAM = %CALLNAME:MASTER%
	PRINTBUTTON @"[%C_NAM%\@ TALENT:30:恋人 ? # さん \@と呼んでもらう]", @"%C_NAM%"
	PRINTL
ENDIF
$INPUT_LOOP
RESETCOLOR
INPUTS
CALL M_KOJO_COLOR_K30
;行頭行尾の空白文字削除
C_NAM = %RESULTS%
REPLACE C_NAM, "\(\^\\s\+\|\\s\+\$)", ""
;2文字未満
IF STRLENS(C_NAM) < 2
	SIF STRLENS(RESULTS)
		CLEARLINE 1
	REUSELASTLINE 「…ご両親から頂いた名前、もっと大切にしてください」
	GOTO INPUT_LOOP
;12文字(全角6文字)超えた
ELSEIF STRLENS(C_NAM) > 12
	CLEARLINE 1
	REUSELASTLINE 「ごめんなさい。普段呼ぶにはちょっと長すぎます…」
	GOTO INPUT_LOOP
;以下そのキャラのなりきりでない時
;同姓同名
ELSEIF FLAG:なりきり != 30 && STRCOUNT(C_NAM, "\(四季\)\*\\s\*映姫")
	CLEARLINE 1
	REUSELASTLINE 「…からかわないでください」
	GOTO INPUT_LOOP
;小町or久侘歌
ELSEIF (FLAG:なりきり != 129 && STRCOUNT(C_NAM, "\(庭渡\)\*\\s\*久侘歌")) || (FLAG:なりきり != 76 && STRCOUNT(C_NAM, "\(小野塚\)\*\\s\*小町"))
	CLEARLINE 1
	REUSELASTLINE 「…その呼び方は紛らわしいので、他のでお願いします」
	GOTO INPUT_LOOP
;阿求
ELSEIF (FLAG:なりきり != 80 && STRCOUNT(C_NAM, "\(稗田\)\*\\s\*阿求"))
	CLEARLINE 1
	REUSELASTLINE 「…新たな御阿礼の子？　変な冗談はよしてください」
	GOTO INPUT_LOOP
;ヘカーティア
ELSEIF (FLAG:なりきり != 113 && STRCOUNT(C_NAM, "ヘカーティア"))
	CLEARLINE 1
	REUSELASTLINE 「…戯れ言はそこまでにしましょうか。失礼にもほどがありますよ」
	GOTO INPUT_LOOP
ENDIF
;変えるって言ったのに呼び名変えてなかったら戻る
SIF (C_NAM == CSTR:30:80 && ARG == 3) || (C_NAM == MASTERNAME:30 && ARG == 2)
	RETURN -1
;直接入力対応＠響子口上からそのまま拝借。改造したいけど正規表現わかんね
IF MASTERNAME:30 == "" || ARG == 2
	IF STRCOUNT(C_NAM, "\(お\(\(兄\|にい\)\|\(姉\|ねえ\)\)\(さん\|ちゃん\)\)\$")
		CLEARLINE 1
		REUSELASTLINE 「さすがに悪ふざけを聞く気にはなれません」
		GOTO INPUT_LOOP
	ELSEIF STRCOUNT(C_NAM, "\(お\(\(兄\|にい\)\|\(姉\|ねえ\)\)\(様\|さま\)\)\$")
		CLEARLINE 1
		REUSELASTLINE 「さすがに悪ふざけを聞く気にはなれません」
		GOTO INPUT_LOOP
	ELSEIF STRCOUNT(C_NAM, "\(\(御\|ご\)\(主人\|しゅじん\)\(様\|さま\)\)$")
		CLEARLINE 1
		IF TALENT:恋慕
			REUSELASTLINE 「えっと、人前でその呼び方は……恥ずかしいです…」
		ELSE
			REUSELASTLINE 「さすがに悪ふざけを聞く気にはなれません」
		ENDIF
		GOTO INPUT_LOOP
	ELSEIF STRCOUNT(C_NAM, "\(\(旦那\|だんな\)\(様\|さま\)\)\$")
		CLEARLINE 1
		IF TALENT:恋慕
			REUSELASTLINE 「えっと、人前でその呼び方は……恥ずかしいです…」
		ELSE
			REUSELASTLINE 「さすがに悪ふざけを聞く気にはなれません」
		ENDIF
		GOTO INPUT_LOOP
	ENDIF
ENDIF
IF ARG == 1
	PRINTFORML 「%C_NAM%…と、貴方を呼びたいけど、いい？」
ELSEIF ARG == 3
	PRINTFORML 「%C_NAM%…って、呼べばいいのね？」
ELSE
	PRINTFORML 「%C_NAM%\@ TALENT:30:恋人 ? # さん \@、ですね」
ENDIF
RESETCOLOR
CALL ASK_YN
IF !RESULT
	IF MASTERNAME:30 == "" || ARG == 2
		MASTERNAME:30 = %C_NAM%
	ELSE
		CSTR:30:80 = %C_NAM%
	ENDIF
ELSE
	PRINTFORMDL 呼び名を入力してください
	GOTO INPUT_LOOP
ENDIF
CALL M_KOJO_COLOR_K30
RETURN 1
;==================================================
;映姫様からぱんつを受け取る関数
;@PANTS_GETはスキマ使用強制だったので別関数化
;-------------------------------------------------
@K30_PANTS_GETTING
TCVAR:30:377 = EQUIP:30:下半身下着２
CFLAG:30:ノーパン = 2
EQUIP:30:下半身下着２ = 0
CALL CLOTHES_SETTING_TRAIN(30)
SIF CFLAG:30:パジャマ == 1
	CFLAG:30:パジャマパンツ = 0
CALLF ONCE("ぱんつなし")
CALLF ONCE("ぱんつ預かり")
RETURN 1
;==================================================
;顔絵付きメッセージをエフェクト込みで表示する関数
;パッチと競合したのでひとまず分けてみる
;-------------------------------------------------
@K30_SPTALK, 表情="", MESSAGE
#DIM エフェクト数
#DIM 行数
#DIMS 色
#DIMS 表情
#DIMS MESSAGE
#DIMS 服装
#DIMS MESSAGE_PRINT,50
服装 =
VARSET MESSAGE_PRINT
IF 顔絵付きメッセージ == 1
	色 = 0%TOSTR(CFLAG:1501, "X")% 
	CALL GET_BASE_RESOURCE(30, "顔絵", 服装, 表情)
	SIF FLAG:マルチサイズ
		デフォルトキャラ画像横幅 = SPRITEHEIGHT(RESULTS)
	CALL 画像セット(RESULTS, 0, 0, キャラ画像表示サイズ比, 0, 1, "100", CALLNAME:30)
	CALL GET_EFFECT_RESOURCE(30)
	エフェクト数 = RESULT
	FOR LOCAL, 0, エフェクト数
		CALL 画像セット(RESULTS:LOCAL, 0, 0, キャラ画像表示サイズ比, LOCAL + 1)
	NEXT
	MESSAGE = %SPTALK整形(MESSAGE)%
	SPLIT MESSAGE, "/", MESSAGE_PRINT
	行数 = RESULT
	FOR LOCAL, 0, 行数
		SIF エフェクト数 - LOCAL < 0
			CALL ダミーセット(0, 0, キャラ画像表示サイズ比, LOCAL)
		TEMP_HTML:LOCAL += @"<font color='#%色%'>%MESSAGE_PRINT:LOCAL%</font>"
	NEXT
	CALL 画像一斉表示(0, 0, 1, -1)
	PRINTL
ELSE
	;顔絵メッセージOFF時は通常通りに表示させる
	SPLIT MESSAGE, "/", MESSAGE_PRINT
	FOR LOCAL, 0, RESULT
		SIF MESSAGE_PRINT:LOCAL != ""
			PRINTFORML %MESSAGE_PRINT:LOCAL%
	NEXT
ENDIF
;==================================================
;映姫様のフェラをアニメーションさせる関数
;髪型とかめちゃくちゃなんであんまり映姫様に見えない
;--------------------------------------------------
;ARG	0=通常 1=エンドレス 2=オート:関数内で判定, 3= 顔射:指定時のみ 4=射精:関数内で描写判定
;--------------------------------------------------
;PAT		強化ごっくんアニメパターン
;G_TIME		強化ごっくんのウエイトタイム
;S_TIME		顔射のアニメウエイトタイム
;SUCCESS	ごっくん成功判定
;CLEAN		お掃除フェラ判定
;BLOCK		演出弱化フラグ、非陥落時や無理矢理時
;FAVOR		演出強化フラグ、恋慕+高欲情+高精液中毒
;--------------------------------------------------
@K30_BLOW_JOB_ANIME(ARG)
#DIM PAT = 1, 2, 3, 4, 3, 2, 3, 4, 3, 2, 3, 4, 3, 2, 3, 4, 5 
#DIM G_TIME = 1400, 750, 450, 350, 350, 250, 250, 250, 250, 250, 250, 250, 250, 450, 550, 750, 1200
#DIM S_TIME = 300, 900, 1000, 1200, 1000
#DIM WAIT_T
#DIM LOOP_C
#DIM SUCCESS
#DIM SIZE
#DIM BLOCK
#DIM FAVOR
#DIM CLEAN
#DIM TEMP
#DIMS HTML_FORM
SIF !FLAG:画像表示 || !FLAG:挿絵表示 || !GETBIT(CFLAG:1500, 0)
	RETURN

VARSET LOCAL
;アニメ画像サイズは顔絵表示に準拠
SIZE = K30_SET_IMAGESIZE()
BLOCK = TCVAR:30:385 || TCVAR:30:無理矢理 || !(TALENT:恋慕 || TALENT:セフレ || TALENT:愛欲)
TEMP = CURRENTREDRAW()
REDRAW 0
IF !FLAG:アニメーション
	IF ARG < 3
		CALL PRINT_IMAGE("30_blow5", SIZE, "left")
		REDRAW TEMP
		RETURN
	ELSE
		GOTO SKIP2
	ENDIF
ENDIF
SELECTCASE ARG
	;通常
	CASE 0, 1, 2
		CLEAN = GROUPMATCH(TCVAR:MASTER:112, 1, 2) && !TCVAR:21 && TALENT:恋慕
		LOOP_C = 3 + (NOWEX:MASTER:射精 > 0) - BLOCK + ((TCVAR:30:357 !=0) * 5) + CLEAN
		;前ターン咥えて無ければひと舐め
		IF MASTER_POSE(SET_M, SET_P, 1) != 30 && TALENT:恋慕 && TCVAR:30:357 < 2
			CALL PRINT_IMAGE("30_start", SIZE, "left")
			TWAIT 800, 0
			CLEARLINE SIZE / 100
		ENDIF
		;通常パターン
		IF ARG == 0 || (ARG == 2 && NOWEX:MASTER:射精) || CLEAN
			WAIT_T = 100 - (NOWEX:MASTER:射精 > 0) * 25
			FOR COUNT:1 ,0, LOOP_C
				FOR COUNT, 1, 7
					CALL PRINT_IMAGE(@"30_blow{COUNT}", SIZE, "left")
					TWAIT WAIT_T, 0
					CLEARLINE SIZE / 100
					SIF GETKEY(1) && COUNT:1
						GOTO SKIP
				NEXT
			NEXT
			IF !CLEAN
				FOR COUNT, 1, 4
					CALL PRINT_IMAGE(@"30_blow{COUNT}", SIZE, "left")
					TWAIT WAIT_T, 0
					CLEARLINE SIZE / 100
					SIF GETKEY(1)
						BREAK
				NEXT
			ENDIF
			$SKIP
			IF CLEAN
				;お掃除フェラで口を離す描写ある(=カウンター発生しない)パターン
				CALL PRINT_IMAGE(@"30_shower1", SIZE, "left")
			ELSE
				;射精直前パターン
				CALL PRINT_IMAGE(@"30_blow{COUNT}", SIZE, "left")
			ENDIF
		;エンドレスパターン
		;Emuera本体にあるアニメスプライト機能使用、ログ流しても咥えっぱなしにするテスト
		ELSE
			HTML_FORM = <img src='30_おしゃぶり映姫様' height='{SIZE}' width='{SIZE}'>
			HTML_PRINT HTML_FORM
			FOR LOCAL, 0, (SIZE / 100)
				PRINTFORML
			NEXT
			;描画間隔を制御する命令
			SETANIMETIMER 150
		ENDIF
	;射精
	CASE 3, 4
		SUCCESS = (EXP_UP(15, 30) == NOWEX:MASTER:射精 * 3) || TFLAG:193 == -999 || TCVAR:30:357
		FAVOR = (TALENT:恋慕 && ABL:精液中毒 > 4 && PALAM:欲情 > PALAMLV:7 && RAND:3) || TCVAR:30:357
		;スパート
		FOR LOCAL, 0, 4 + (ARG == 4)
			FOR COUNT, 1, 7
				CALL PRINT_IMAGE(@"30_blow{COUNT}", SIZE, "left")
				TWAIT 55, 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1) && LOCAL > 0
					GOTO SKIP2
			NEXT
		NEXT
		;ぶっかけ
		IF ARG == 3
			FOR COUNT, 1, 6
				CALL PRINT_IMAGE(@"30_shower{COUNT}", SIZE, "left")
				TWAIT (S_TIME:(COUNT -1)), 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1) && COUNT > 1
					GOTO SKIP2
			NEXT
			GOTO SKIP2
		ENDIF
		;奥まで飲み込む動作
		FOR COUNT, 1, 5
			CALL PRINT_IMAGE(@"30_blow{COUNT}", SIZE, "left")
			TWAIT 55 + (COUNT == 4) * 100, 0
			CLEARLINE SIZE / 100
			SIF GETKEY(1)
				GOTO SKIP2
		NEXT
		WAIT_T = 1200 - (!SUCCESS * 300)
		FOR COUNT, 7, 8 + !BLOCK
			CALL PRINT_IMAGE(@"30_blow{COUNT}", SIZE, "left")
			TWAIT WAIT_T, 0
			CLEARLINE SIZE / 100
			SIF GETKEY(1)
				GOTO SKIP2
		NEXT
		;陥落ない場合は演出少なめ
		SIF BLOCK
			GOTO SKIP2
		;だらだらこぼす
		IF !SUCCESS && NOWEX:MASTER:射精 > 1 
			FOR COUNT, 1, 2 + (TALENT:恋慕 && ABL:精液中毒 > 4)
				CALL PRINT_IMAGE(@"30_failed{COUNT}", SIZE, "left")
				TWAIT 1200, 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1)
					GOTO SKIP2
			NEXT
		;手に吐き出し
		ELSEIF !SUCCESS
			FOR COUNT, 2, 5
				CALL PRINT_IMAGE(@"30_success{COUNT}", SIZE, "left")
				TWAIT 550 , 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1)
					GOTO SKIP2
			NEXT
			FOR COUNT, 1, 5 + FAVOR
				CALL PRINT_IMAGE(@"30_dara{COUNT}", SIZE, "left")
				TWAIT 550 + COUNT * 75 + (COUNT == 2) * 700, 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1)
					GOTO SKIP2
			NEXT
		;ごっくんパターン1、陥落高くてMASTER大量射精
		ELSEIF FAVOR && SUCCESS
			FOR COUNT, 0, 17
				CALL PRINT_IMAGE(@"30_success{PAT:COUNT}", SIZE, "left")
				TWAIT (G_TIME:COUNT), 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1)
					GOTO SKIP2
			NEXT
		;ごっくんパターン2
		ELSE
			LOCAL = 500
			FOR COUNT, 2, 5
				CALL PRINT_IMAGE(@"30_success{COUNT}", SIZE, "left")
				TWAIT LOCAL, 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1)
					GOTO SKIP2
				LOCAL += 250
			NEXT
		ENDIF
		$SKIP2
		;顔射
		IF ARG == 3
			CALL PRINT_IMAGE("30_shower6", SIZE, "left")
		;未陥落
		ELSEIF BLOCK
			LOCALS = 30_%\@ SUCCESS ? blow8 # failed1 \@%
			CALL PRINT_IMAGE(LOCALS, SIZE, "left")
		;ごっくん
		ELSEIF SUCCESS
			CALL PRINT_IMAGE(@"30_success{FAVOR + 5}", SIZE, "left")
		;見せつけ
		ELSEIF NOWEX:MASTER:射精 == 1
			CALL PRINT_IMAGE(@"30_dara{FAVOR + 5}", SIZE, "left")
		;だらだら
		ELSE
			LOCAL = TALENT:恋慕 && ABL:精液中毒 > 4 ? 3 # 2
			CALL PRINT_IMAGE(@"30_failed{LOCAL}", SIZE, "left")
		ENDIF
ENDSELECT
REDRAW TEMP
RETURN
;==================================================
;PRINT_IMAGEの大きさを顔絵設定に合わせる式中関数
;自作絵は拡大に耐えられるクオリティないので最大値に制限かける
;--------------------------------------------------
;デフォルトキャラ画像横幅	顔絵を表示するベースサイズ(単位:ピクセル
;キャラ画像表示サイズ比		ベース画像を表示する倍率(50～150の範囲
;--------------------------------------------------
@K30_SET_IMAGESIZE()
#FUNCTION
LOCAL = デフォルトキャラ画像横幅 > 180 ? 1000 # 700
RETURNF MIN(LOCAL * キャラ画像表示サイズ比 / 10000 * 100 , 1800)
;--------------------------------------------------
;映姫様のおしりをアップで表示する関数
;IMAGE.ERBとやってることはだいたい同じ、キャラデータ使うと既存顔絵や口上削除プレイに支障でそうなので口上内に処理ベタ置き
;もっと洗練されたやり方ありそうだけどよくわかんなくなってきた、関数をライブラリ化出来る人って本当にスゴい
;--------------------------------------------------
;COM		SELECTCOM,0ならイベント等からの呼び出し
;LAYER:0	差分指定: 1=縦割れアナル 2=ぽっかりアナル 3=中出しアナル
;LAYER:1	エフェクト指定:BIT0=尻叩き-着衣 BIT1=尻叩き-裸 BIT2=濡れ BIT3=精液 BIT4=バイブ 
;LAYER:2	ぱんつ指定: 1=通常 2=ずらし 3=バイブぱんつ
;LAYER:3	腕指定: 1=手で押さえ 2=バイブ掴み 3=開帳
;LAYER:4	謎の影指定: 1=指入れ 2=バイブ挿入 3=舐め 4=スカートめくり 5=TINKO 6=尻叩き
;L_NUM		レイヤ階層数
;--------------------------------------------------
@K30_PRINT_BUTT_IMAGE(COM, LAYER:0 = 0, LAYER:1 = 0, LAYER:2 =0, LAYER:3 = 0, LAYER:4 = 0)
#DIM L_NUM
#DIM S_NUM
#DIM COM
#DIM TEMP_SIZE
#DIM TEMP_WIDTH
#DIM TEMP_SCALE
#DIM 縦割
#DIM たっぷり
#DIM 尻叩き
#DIM 腫れ
#DIM 濡れ
#DIM 白濁
#DIM 装着
#DIM 表示拡張
#DIM セットエフェクト
#DIM LAYER, 5
#DIM レイヤ配列位置 = 0, 3, 3, 3, 7
#DIMS SET_RESOURCE, 10
#DIMS SECTIONAL
#DIMS 袖
#DIMS ぱんつ
{
#DIMS レイヤ素材 = "", "30_BASE_VERTICAL", "30_BASE_HOLE", "30_BASE_CREAMPIE",
					 "_HOLD", "_GRAB", "_OPEN", "KETSU-TOUCH",
					 "30_SHADOW_HAND", "30_SHADOW_INST", "30_SHADOW_LICK", "30_SHADOW_LIFT", "30_SHADOW_PENIS", "30_SHADOW_SPANK", "30_SHADOW_DEEPER"
}

SIF !FLAG:画像表示 || !GETBIT(CFLAG:1500, 1)
	RETURN

VARSET SET_RESOURCE
VARSET SECTIONAL
L_NUM = 1
S_NUM = 1
表示拡張 = 0
縦割 = GETBIT(CFLAG:30:1910, 7)
たっぷり = 充填率(30, 2) > 30
尻叩き = TIME_PROGRESS(TCVAR:30:355) < 20
;BIT0=着衣腫れ,BIT1=裸腫れ、足し算すると0～2になるから排他表示
腫れ = 尻叩き + (尻叩き * K30_NUDITY())
濡れ = (PALAM:潤滑 > PALAMLV:3) * 4
白濁 = (充填率(30, 1) > 20) * 8
装着 = (TEQUIP:アナルバイブ) * 16
セットエフェクト = 腫れ + 濡れ + 白濁 + 装着
CALL K30_SELECT_PANTS_TYPE(LAYER:2)
ぱんつ = %RESULTS%
袖 = %\@ NUDITYCHECK(30) > 3 && !K30_CHECK_HEN_T() ? _WEAR # _NUDE \@%

;レイヤ設定、上手く構文纏められずIF文まみれ
;素体を設定
SET_RESOURCE:0 = 30_BODY%\@ K30_NUDITY() ? _NUDE # _WEAR \@%
SIF !K30_NUDITY() && K30_CHECK_HEN_T()
	SET_RESOURCE:0 = 30_BODY_HEN-T

;イベント構文から直接指定時
IF !COM
	SIF !LAYER:1
		LAYER:1 = セットエフェクト
	IF LAYER:0
		SET_RESOURCE:L_NUM = %レイヤ素材:((レイヤ配列位置:0) + LAYER:0)%
		L_NUM ++
	ENDIF
	IF LAYER:1
		CALL K30_SEARCH_EFFECT(SET_RESOURCE, LAYER:1, L_NUM)
		L_NUM += RESULT
	ENDIF
	IF LAYER:2
		SET_RESOURCE:L_NUM = %ぱんつ%
		SIF ぱんつ != ""
			L_NUM ++
	ENDIF
	IF LAYER:3
		SET_RESOURCE:L_NUM = 30_HAND%袖%%レイヤ素材:((レイヤ配列位置:3) + LAYER:3)%
		L_NUM ++
	ENDIF
	IF LAYER:4
		SET_RESOURCE:L_NUM = %レイヤ素材:((レイヤ配列位置:4) + LAYER:4)%
		L_NUM ++
	ENDIF
	GOTO PRINT_IMAGE
ENDIF

;素体差分レイヤの設定
IF 縦割
	SET_RESOURCE:1 = %\@ CFLAG:うふふ ? 30_BASE_VERTICAL # ダミー \@%
	SIF TCVAR:30:393 > 4
		SET_RESOURCE:1 = 30_BASE_HOLE
	SIF たっぷり
		SET_RESOURCE:1 = 30_BASE_CREAMPIE
	L_NUM ++
ENDIF

;エフェクトレイヤの設定
CALL K30_SEARCH_EFFECT(SET_RESOURCE, セットエフェクト, L_NUM)
L_NUM += RESULT

;ぱんつレイヤの設定
IF ぱんつ != ""
	SET_RESOURCE:L_NUM = %ぱんつ%
	L_NUM ++
ENDIF

;コマンド毎にその他レイヤを決定
SELECTCASE COM
	;尻を撫でる
	CASE 310
		SIF CFLAG:うふふ
			RETURN
		;素体を入れ替え
		SET_RESOURCE:0 = %REPLACE(SET_RESOURCE:0, "BODY_", "BODY_KETSU-TOUCH_")%
		SET_RESOURCE:1 = 30_SHADOW_KETSU-TOUCH
		L_NUM =2
		IF TFLAG:193 < 4 && SHIRAHU(30)
			SET_RESOURCE:2 = 30_HAND%袖%_GUARD
			L_NUM ++
		ENDIF
	;アナル愛撫（日常系）
	CASE 314
		;謎の影
		SET_RESOURCE:L_NUM = 30_SHADOW_%\@ TCVAR:30:364 && TEQUIP:アナルバイブ ? INST # HAND \@%
		L_NUM ++
	;スカートめくり
	CASE 330
		;素体差分個別設定
		IF TCVAR:30:364
			SET_RESOURCE:1 = 30_BASE%\@ たっぷり ? _CREAMPIE # _HOLE \@%
		ELSEIF たっぷり && 縦割
			SET_RESOURCE:1 = 30_BASE_CREAMPIE
		ELSEIF TCVAR:30:393 && 縦割
			SET_RESOURCE:1 = 30_BASE_VERTICAL
		ENDIF
		;謎の影
		SET_RESOURCE:L_NUM = 30_SHADOW_LIFT
		L_NUM ++
	;アナル舐め
	CASE 4
		;映姫様の腕
		IF SHIRAHU(30) && RAND:2 
			SET_RESOURCE:L_NUM = 30_HAND%袖%_HOLD
			L_NUM ++
		ENDIF
		;謎の影
		SET_RESOURCE:L_NUM = 30_SHADOW_LICK
		L_NUM ++
	;アナル愛撫
	CASE 5
		;映姫様の腕
		IF SHIRAHU(30) && RAND:2 
			SET_RESOURCE:L_NUM = 30_HAND%袖%_HOLD
			L_NUM ++
		ENDIF
		;謎の影
		SET_RESOURCE:L_NUM = 30_SHADOW_%\@ TEQUIP:アナルバイブ ? INST # HAND \@%
		L_NUM ++
	;自慰
	CASE 9
		SIF !TEQUIP:アナルバイブ
			RETURN
		;映姫様の腕
		SET_RESOURCE:L_NUM = 30_HAND%袖%_GRAB
		L_NUM ++
	;アナル開帳
	CASE 13
		;素体差分個別設定
		SIF SET_RESOURCE:1 == "30_BASE_VERTICAL"
			SET_RESOURCE:1 = 30_BASE_HOLE
		;映姫様の腕
		SET_RESOURCE:L_NUM = 30_HAND%袖%%\@ 縦割 && RAND:3 ? _OPEN # _HOLD \@%
		L_NUM ++
	;アナルバイブ
	CASE 45
		;素体差分個別設定
		SIF !TEQUIP:アナルバイブ && 縦割
			SET_RESOURCE:1 = 30_BASE%\@ たっぷり ? _CREAMPIE # _HOLE \@%
		;映姫様の腕
		IF SHIRAHU(30)
			SET_RESOURCE:L_NUM = 30_HAND%袖%_HOLD
			L_NUM ++
		ENDIF
		IF TEQUIP:アナルバイブ
			;謎の影
			SET_RESOURCE:L_NUM = 30_SHADOW_INST
			L_NUM ++
			;アナルバイブ断面図、本体側のフラグに従う
			表示拡張 = FLAG:射精画像種類 == 2 ? 0 # 2
		ENDIF
	;Ａ後背位/Ａ背面座位/スパンキング
	CASE 63, 70, 100
		;映姫様の腕
		IF SHIRAHU(30) && COM == 63 && RAND:2 
			SET_RESOURCE:L_NUM = 30_HAND%袖%_HOLD
			L_NUM ++
		ENDIF
		;謎の影,スパンキング
		IF COM == 100
			SET_RESOURCE:L_NUM = 30_SHADOW_SPANK
			SIF TEQUIP:51 == MASTER && GROUPMATCH(TEQUIP:体位, 2, 5)
				SET_RESOURCE:L_NUM = 30_SHADOW_SPANK-INST
		;謎の影,TINKO
		ELSE
			SET_RESOURCE:L_NUM = 30_SHADOW_PENIS
		ENDIF
		L_NUM ++
	;結腸責め/膣裏責め
	CASE 74, 75
		SIF !縦割 || GROUPMATCH(TEQUIP:体位, 1, 3, 4)
			RETURN
		;断面図表示設定、本体側のフラグに従う
		表示拡張 = FLAG:射精画像種類 == 2 ? 0 # 1
		;素体差分個別設定
		SET_RESOURCE:1 = 30_BASE%\@ たっぷり ? _CREAMPIE # _HOLE \@%
		;謎の影
		SET_RESOURCE:L_NUM = 30_SHADOW_DEEPER
		L_NUM ++
	CASEELSE
		RETURN
ENDSELECT

$PRINT_IMAGE
TEMP_SIZE = FLAG:マルチサイズ
TEMP_WIDTH = デフォルトキャラ画像横幅
TEMP_SCALE = キャラ画像表示サイズ比
FLAG:マルチサイズ = 1
デフォルトキャラ画像横幅 = 270
キャラ画像表示サイズ比 = 100
;レイヤのセットと表示
;DEBUGPRINTFORML LINECOUNT:{LINECOUNT}
FOR COUNT, 0, L_NUM
	CALL 画像セット(SET_RESOURCE:COUNT, 0, 0, キャラ画像表示サイズ比, COUNT)
;	DEBUGPRINTFORML レイヤ:{COUNT} %SET_RESOURCE:COUNT%
NEXT
IF FLAG:時間停止
	CALL 画像セット("時間停止", 0, 0, キャラ画像表示サイズ比, L_NUM)
	L_NUM ++
ENDIF
;断面図、290スレ>>545さんのアイデア使ってます
IF 表示拡張
	CALL 画像セット(@"30_SECTIONAL_RECTUM{表示拡張}", 0, 0, キャラ画像表示サイズ比, 0)
	IF たっぷり
		SECTIONAL = 30_SECTIONAL%\@ 充填率(30, 2) > 60 ? _POKKORI # _TAPPURI \@%
		CALL 画像セット(SECTIONAL, 0, 0, キャラ画像表示サイズ比, 1)
		S_NUM ++
	ENDIF
	IF FLAG:時間停止
		CALL 画像セット("時間停止", 0, 0, キャラ画像表示サイズ比, S_NUM)
		S_NUM ++
	ENDIF
	
	;FOR COUNT, S_NUM, L_NUM 
	;	CALL ダミーセット(0, 0, キャラ画像表示サイズ比, COUNT)
	;NEXT
ENDIF
CALL 画像一斉表示("LEFT", 0, 1, -1)
FLAG:マルチサイズ = TEMP_SIZE
デフォルトキャラ画像横幅 = TEMP_WIDTH
キャラ画像表示サイズ比 = TEMP_SCALE
RETURN 1
;==================================================
;おしり画像のエフェクトを決定する関数
;REF関数の部分はモブ子画像生成から拝借しました
;--------------------------------------------------
@K30_SEARCH_EFFECT(リソース, セットエフェクト, レイヤ数)
#DIM セットエフェクト
#DIM レイヤ数
#DIM L_NUM
#DIMS REF リソース, 0
#DIMS レイヤ素材 = "30_EFFECT_SPANK-W", "30_EFFECT_SPANK-N", "30_EFFECT_WET", "30_EFFECT_SPERM", "30_EFFECT_VIBE"

L_NUM = 0
FOR COUNT, 0, 5
	SIF !GETBIT(セットエフェクト, COUNT)
		CONTINUE
	リソース:(レイヤ数 + L_NUM) = %レイヤ素材:COUNT%
	L_NUM ++
NEXT
RETURN L_NUM
;==================================================
;おしり画像基準で着衣状況を判別する式中関数
;@CHECK_NUDEも@NUDITYCHECKも下半身基準だとなんか合わない
;--------------------------------------------------
;0=着衣 1=おしり丸出し
;--------------------------------------------------
@K30_NUDITY()
#FUNCTION
;上半身着衣状況(TEQUIP:1)を見る
SIF GROUPMATCH(TEQUIP:1, 1, 3)
	RETURNF 0
;下半身着衣状況(TEQUIP:0)を見る
SIF GETBIT(TEQUIP:0, 0) || GETBIT(TEQUIP:0, 3)
	RETURNF 0
;ここまで抜けたら丸出し
RETURNF 1
;==================================================
;ぱんつ画像を決定する関数
;全部描ける画力も無いので似た系統のを表示、気が向いたので追加
;--------------------------------------------------
@K30_SELECT_PANTS_TYPE(指定)
#DIM 指定
#DIM G_ID
#DIM ASIDE
#DIMS 色
#DIMS 装着
#DIMS ずらし
#DIMS ヌード
#DIMS ベースぱんつ
#DIMS TEMP_PANTS
#DIMS COLOR = "", "白", "緑", "黒"
LOCAL = 0
RESULTS =
色 = %COLOR:(DAY % 4)%
ASIDE = (CFLAG:うふふ == 1 || TEQUIP:50 >= 0 || TEQUIP:51 >= 0 || TEQUIP:バイブ || TEQUIP:アナルバイブ)
ずらし = _%\@ ASIDE ? SHIFT # NORMAL \@% 
ヌード = %\@ K30_NUDITY() ? _NUDE # _WEAR \@%
装着 = %\@ TEQUIP:アナルバイブ ? _VIBE # \@%
;指定あれば画像をそちらに合わせる
IF 指定 == 3
	RESULTS = 30_PANTS_HARNESS
	RETURN
ENDIF
SIF 指定
	ヌード = %\@ 指定 == 1 ? _NUDE # _WEAR \@%

SELECTCASE EQUIP:30:下半身下着２
	CASE 0
		;ノーパンorガーターベルトorバイブベルト
		IF TCVAR:30:364 && !CFLAG:うふふ && TEQUIP:アナルバイブ && !CHECK_NUDE(30)
			RESULTS = 30_PANTS_HARNESS
			RETURN
		ENDIF
		SIF TCVAR:30:354 > 0
			RESULTS = 30_PANTS%ヌード%_TYPE10_NO-PANTS
		
	CASE 1, 2, 26
		;1/男物のトランクス,2/緩やかな構造のドロワーズ,26/排便用に股間部が大きく開いたドロワーズ（股開き）
		IF ASIDE
			RESULTS = 30_PANTS_COMMON_TYPE1_SHIFT
		ELSE
			RESULTS = 30_PANTS%ヌード%_TYPE1_NORMAL
		ENDIF
		
	CASE 5
		;5/おしりのラインが強調されるしましまパンツ＠もちろん白黒
		RESULTS = 30_PANTS%ヌード%_TYPE4%ずらし%
		
	CASE 7, 10
		;7/飾り気のない無地のパンツ,10/シンプルなデザインのローレグパンツ
		IF ASIDE
			RESULTS = 30_PANTS_COMMON_TYPE8_SHIFT
		ELSE
			RESULTS = 30_PANTS%ヌード%_TYPE8_NORMAL
		ENDIF
		LOCAL = 1
	
	CASE 11, 20
		;11/おしりの割れ目が見えそうなローライズパンツ,20/きわめて丈が短いスキャンティ
		IF ASIDE
			RESULTS = 30_PANTS_COMMON_TYPE11_SHIFT
		ELSE
			RESULTS = 30_PANTS%ヌード%_TYPE11_NORMAL
		ENDIF
		LOCAL = 1
		
	CASE 12
		;12/きわどく切れ上がったハイレグパンツ
		RESULTS = 30_PANTS%ヌード%_TYPE3%ずらし%
		
	CASE 13, 23
		;13/清楚ながらうっすら透けるレースのパンティ（白）,23/大事な部分がスケスケのシースルーショーツ
		RESULTS = 30_PANTS%ヌード%_TYPE6%ずらし%
		
	CASE 14, 16
		;14/大人の色気たっぷりのレースのパンティ（黒）,16/細い紐で腰に結わえた紐パン
		RESULTS = 30_PANTS%ヌード%_TYPE5%ずらし%
		
	CASE 15
		;15/お尻が丸出しの大胆なＴバック
		RESULTS = 30_PANTS%ヌード%_TYPE12%ずらし%
		
	CASE 17
		;縦にジッパーの走るレザーパンツ
		RESULTS = 30_PANTS%ヌード%%装着%_TYPE7%ずらし%
		
	CASE 18
		;大事にはき古した木綿のぱんつ(固有下着)
		RESULTS = 30_PANTS%ヌード%_TYPE0%ずらし%
		
	CASE 19
		;19/少女らしいキュートなショーツ
		IF ASIDE
			RESULTS = 30_PANTS_COMMON_TYPE2_SHIFT
		ELSE
			RESULTS = 30_PANTS%ヌード%_TYPE2_NORMAL
		ENDIF
		
	CASE 21
		;きゅっとお尻に締め込んだふんどし
		RESULTS = 30_PANTS%ヌード%_TYPE13%ずらし%
	CASE 22
		;極小面積のヒモ同然なＧストリング＠なんとなくガーターベルト仕様
		RESULTS = 30_PANTS%ヌード%_TYPE10%ずらし%
		
	CASE 24
		;ぱっくりと恥丘を剥き出しにしたオープンショーツ
		RESULTS = 30_PANTS%ヌード%%装着%_TYPE9
	
	CASEELSE
		;初期or獲得可能素質では穿かない/着せ替えのみのぱんつはスルー
		;3/くまさんパンツ,4/水玉パンツ,6/フリルパンツ,25/ばんそうこう = 無知ロリ用、27以降 = 着替え可能判定なし
		RESULTS = 30_PANTS%ヌード%_TYPE0%ずらし%
ENDSELECT

SIF !LOCAL
	RETURN

;色違いぱんつの生成
ベースぱんつ = %RESULTS%
RESULTS = %RESULTS%%色%
SIF SPRITECREATED(RESULTS)
	RETURN
G_ID = 0
CALL 画像合成(G_ID, ベースぱんつ, 0, 0, 0, 0, 色)
SPRITECREATE RESULTS, G_ID
RETURN
;==================================================
;映姫様のアヘ顔を表示する関数
;行送り中に目だけ表示されると怖いのでスプライト合成を使用
;白蓮別立ち絵に使った構文をぶっこ抜き
;--------------------------------------------------
@K30_PRINT_AHEGAO
#DIM G_ID
#DIM NUM
#DIM ORG
#DIMS SET_RESOURCE,6
#DIMS RESOURC_NAME
#DIMS 差分
;睡眠中は表示しない
SIF !FLAG:画像表示 || CFLAG:睡眠
	RETURN
ORG = (SUMARRAY(NOWEX:30:0, 8, 10) > 0)
	
;ベースと素体を設定
VARSET SET_RESOURCE
SET_RESOURCE:0= 30_AHE_BASE
差分 = %\@ CHECK_NUDE(30) ? _NUDE # _WEAR \@%
SIF !CHECK_NUDE(30) && CFLAG:衣装一時変更 == 62
	差分 = _HEN-T
SET_RESOURCE:1 = 30_AHE_BODY%差分%
RESOURC_NAME = 30_あへ顔%差分%

;口を設定-ランダム２種、恋慕かつ四重絶頂以上で３種類
NUM = RAND:(2 + (ORG && TALENT:恋慕))
SET_RESOURCE:2 = 30_AHE_MOUTH{NUM}
RESOURC_NAME += @"口{NUM}"

;目を設定-恋慕で目にハート付き、恋慕かつ四重/五重絶頂で差分増
NUM = TALENT:恋慕 ? RAND:(1 + ORG) + (NOWEX:9 > 0) # 3
SET_RESOURCE:3 = 30_AHE_EYE{NUM}
RESOURC_NAME += @"目{NUM}"

;眉を設定-ランダム２択
NUM = RAND:2
SET_RESOURCE:4 = 30_AHE_BROW{NUM}
RESOURC_NAME += @"眉{NUM}"

;エフェクト-恋慕でランダム４種
IF TALENT:恋慕
	NUM = RAND:4
	SET_RESOURCE:5 = 30_AHE_EFFECT{NUM}
	RESOURC_NAME += @"効果{NUM}"
ENDIF
;合成したスプライトをリソース登録して表示
IF !SPRITECREATED(RESOURC_NAME)
	G_ID = 0
	FOR COUNT, 0, 5 + (TALENT:恋慕 > 0)
		CALL 画像合成(G_ID, SET_RESOURCE:COUNT)
	NEXT
	SPRITECREATE RESOURC_NAME, G_ID
ENDIF

CALL PRINT_IMAGE(RESOURC_NAME, 1000, "left")
PRINTFORML 
RETURN
;==================================================
;映姫様のメシ顔を表示する関数
;--------------------------------------------------
@K30_PRINT_MESHIGAO(ARGS)
SIF !FLAG:画像表示 || !顔絵付きメッセージ
	RETURN
SETANIMETIMER 200
CALL 画像セット("30_BG1", 0, 0, 100, 0)
CALL GET_BASE_RESOURCE(30, "顔絵", "", ARGS)
CALL 画像セット(RESULTS:0, 0, 0, 100, 1)
CALL 画像一斉表示("LEFT", 0, 1, -1)
RETURN

;==================================================
;汎用説教判定＆実行
@K30_LECTURE_CHECK(説教長, 理由)
;--------------------------------------------------
;説教長= 説教の行数＝怒り具合
;理由= 説教理由（=0 パンツ =1 セクハラコマンド失敗時 =2ＶＡ強制わいせつ）
;--------------------------------------------------
#DIM 説教長
#DIM 理由
#DIM ぱんつ１
#DIM ぱんつ２
#DIM セクハラ１
#DIM セクハラ２
#DIM 強制わいせつ
#DIM TEMP
;ガード低下時・その他状態異常・うふふ時は判定なし無罪
SIF (CFLAG:1801 == 0 || SHIRAHU(30) != 1 || CFLAG:30:うふふ) && 理由 != 0
	RETURN 1
;変Ｔで目が死んでいるときは説教無し
SIF K30_CHECK_HEN_T() && TALENT:機嫌 > -1
	RETURN 1
;ラブホとお招き部屋にいても説教しない
SIF (CFLAG:30:現在位置 == 連れ込み宿 || CFLAG:30:現在位置 == ラブホ || CFLAG:30:現在位置 == OMANEKIBEYA()) && 理由 != 0
	RETURN 1
SELECTCASE 理由
	;パンツ密輸摘発
	CASE 0
		;説教orぶん殴りで弱味はチャラ
		CFLAG:弱み握り -= 5
		CFLAG:1700 -= 5
		CLEARBIT CFLAG:1699, 1
		;２回まではお説教
		IF ぱんつ１ != DAY
			ぱんつ１ = DAY
			PRINTFORML 
			PRINTFORMW 「……では、%K30_C_NAME(MASTER)%」
			GOTO LECTURE
		ELSEIF ぱんつ２ != DAY
			ぱんつ２ = DAY
			PRINTFORML 
			PRINTFORMW 「%K30_C_NAME(MASTER)%もいい加減懲りないですね…」
			GOTO LECTURE
		;３回目でぶん殴られる
		ELSE
			PRINTFORML 
			PRINTFORMW 「………%CALLNAME:MASTER%さん」
			PRINTFORMW 「これだけ言ってもまだ通じないですか」
			TCVAR:30:371 = 1
			CALL K30_DISCIPLINE_HEAD
		ENDIF
	;セクハラ失敗時
	CASE 1
		;怒り具合30以下ノーカン
		IF 説教長 > 30
			;１日２回お説教
			IF セクハラ１ != DAY
				セクハラ１ = DAY
				PRINTFORML 
				PRINTFORMW 「…少し目に余りますね」
				GOTO LECTURE
			ELSEIF セクハラ２ != DAY
				セクハラ２ = DAY
				PRINTFORML 
				PRINTFORMW 「…全く懲りてませんね」
				GOTO LECTURE
			;３回目でぶん殴られる
			ELSE
				PRINTFORML 
				PRINTFORMW 「…いい加減になさい」
				PRINTFORMW 「言うだけで通じないのなら」
				TCVAR:30:371 = 1
				CALL K30_DISCIPLINE_HEAD
			ENDIF
		ENDIF
	;通常時ＶＡコマンド確率で
	CASE 2
		;最初は説教
		IF 強制わいせつ != DAY
			強制わいせつ = DAY
			PRINTFORML 
			PRINTFORMW 「…貴方、ひどすぎます」
			GOTO LECTURE
		;我慢は一回だけ
		ELSE
			PRINTFORML 
			PRINTFORMW 「…もう、許せません」
			PRINTFORMW 「口で言うだけ無駄というなら」
			TCVAR:30:371 = 2
			CALL K30_DISCIPLINE_HEAD
		ENDIF
ENDSELECT
RETURN 1
;説教
$LECTURE
PRINTFORML 「まずはそこに座りなさい」
FORCEWAIT
PRINTFORMDW 映姫が地面を指差した。%CALLNAME:MASTER%は大人しく正座をした…
PRINTFORMW 
SELECTCASE 理由
	CASE 0
		PRINTFORMW 「一体どれだけのパンツをくすねたんですか？　ええ、拾ったなんて言い訳は全く信用してません」
		PRINTFORMW 「そう、貴方は少し手癖が悪すぎる！」
	CASE 1
		PRINTFORMW 「そもそも女性にむやみに触って、自制心というものを持ってないんですか？」
		PRINTFORMW 「そう、貴方は少し欲望に忠実すぎる！」
	CASE 2
		PRINTFORMW 「\@ TALENT:30:恋人 ? 例え恋人でも # どんなに仲が良くても \@やっていい事には限度があります！　どれだけ人を辱めるんですか！」
		PRINTFORMW 「そう、貴方は少し気遣いが無さすぎる！」
	CASEELSE
		PRINTFORMDW 想定外です。お手数おかけしますが、直前使用のコマンドと共にバグ報告願います
		PRINTFORMW 「K30_LECTURE_CHECK、貴方の引数はおかしすぎる！ …あら？」
ENDSELECT
PRINTFORML 
TEMP = 説教長
WHILE TEMP
	;行頭の修飾。2行に1回確率でつける
	IF  (((説教長 + TEMP) % 2 == 0) && RAND:5 == 0) || (TEMP == 説教長)
		PRINTFORM 「%TEXTR("そもそも/考えてもみて下さい/はっきり言うと/よく聞きなさい/具体的に言うと/いいですか/")%………………
	ELSE
		PRINT 「……
	ENDIF
	;1行の長さは適当に
	REPEAT 6 + RAND:4
		;雑に百分率
		SELECTCASE RAND:100
			CASE 0 TO 9
				SELECTCASE 理由
					CASE 0
						PRINTFORM …%TEXTR("パンツ/少しは/貴方の/善行を/ぱんつ/どこから/おぱんつ/何に使って/聞いてますか…？　/何の為に")%…
					CASE 1
						PRINTFORM …%TEXTR("死後に/遠慮を/べたべたと/いやなのに/貴方の/善行を/自省を/他人の気持ちに…/恥ずかしい…/周りの目を…/遠慮もなしに…")%…
					CASE 2
						PRINTFORM …%TEXTR("死後に/意志の/ダメと/いやなのに/貴方の/善行を/自省を/他人の気持ちに…/恥ずかしい…/周りの目を…/遠慮もなしに…")%…
					CASEELSE
						PRINTFORM …%TEXTR("反省を/少しは/罪が/善行を/地獄に/だいたいあなたは/過ちを/聞いてますか…？　/死後の")%…
				ENDSELECT
			CASE 10 TO 13
				PRINTFORM ………!
			CASEELSE
				PRINTFORM ………
		ENDSELECT
	REND
	PRINTW 」
	TEMP --
WEND
説教長 *=2
TIME += 説教長
TEMP = MAXBASE:MASTER:気力 / 3
SIF TEMP > BASE:MASTER:気力
	TEMP = MAX(BASE:MASTER:気力, 1)
CALL K30_D_LINE
PRINTFORML 「………………………………」
PRINTFORMW 「…ということです。そう、貴方が今すぐやるべきことは」
SELECTCASE 理由
	CASE 0
		PRINTFORMW 「ため込んだパンツをきちんと処分すること。これが今の貴方が積める善行よ」
	CASE 1
		PRINTFORMW 「欲を満たす代償についてもっと考え反省すること。これが今の貴方が積める善行よ」
	CASE 2
		PRINTFORMW 「人の気持ちと思いやりについてしっかり学ぶこと。これが今の貴方が積める善行よ」
	CASEELSE
ENDSELECT
FORCEWAIT
PRINTFORML
PRINTFORMDW ようやく終わった……
PRINTFORMDL 映姫に{説教長}分間も説教されて、とても疲れた……
PRINTFORMDW 正座で足もシビれた…
CALL RECOVER(MASTER, -50, "体力", "足のシビれ")
CALL RECOVER(MASTER, -(TEMP), "気力", "うんざりするほど長いお説教")
;映姫様が趣味を満喫
SOURCE:鬱屈 -= 1000
SIF SOURCE:鬱屈 < 0
	SOURCE:鬱屈 = 0
SOURCE:歓楽 += ABL:30:親密 * 70
SOURCE:情愛 += 300
TEMP = RAND:5 - 理由
SIF TEMP <= 0
	TEMP = 1
;日記フラグ
IF DIARY:30:5 == 0
	;かなり行儀の悪い受け渡し方だけど、ターン終了直前の茶番だからこれでいいや
	TFLAG:193 = 理由 * 1000 + 説教長
	CALL K30_WRITE_DIARY(5)
ENDIF
PRINTFORML 
PRINTFORMDL お説教を大人しく聞いたので映姫の信頼度がほんの少し上がった
SIF 理由 == 0
	PRINTFORMDL パンツの件もこれで許してくれたようだ
CALL CHANGE_CFLAG(4, 30, TEMP)
RETURN 1
;==================================================
;映姫様の折檻＠遠慮のない一撃
@K30_DISCIPLINE_HEAD
#DIM DAMAGE
;--------------------------------------------------
LOCAL = MIN(1 + 2 * TCVAR:30:371, 7)
DAMAGE = 400 + 400 * TCVAR:30:371
SIF TCVAR:30:371 > 4
	DAMAGE += BASE:MASTER:体力 + 1000 * TCVAR:30:371
IF TCVAR:30:371 < 3
	PRINTFORMW 「貴方は少し痛い目にあって」
	SETCOLOR 206, 8, 21
	PRINTFORML 
	PRINTFORML 「自分の生活を見つめ直すがいい！」
ELSEIF TCVAR:30:371 < 5
	PRINTFORML 「ええ、甘えも情けもかけられません」
	PRINTFORMW 「己の犯した罪をしっかりと」
	SETCOLOR 206, 8, 21
	PRINTFORML 
	PRINTFORML 「悔い改めなさい！！」
ELSEIF TCVAR:30:371 == 5
	PRINTFORML 「……ええ、決まりました」
	PRINTFORMW 「貴方は今日残りの時間」
	SETCOLOR 206, 8, 21
	PRINTFORML 
	PRINTFORML 「自室で反省してなさい！！」
ELSE
	PRINTFORMDW 振り下ろす瞬間にはっきりと告げた
	SETCOLOR 206, 8, 21
	PRINTFORML 
	PRINTFORML 「地獄行きです！！」
ENDIF
CALL K30_QUAKE(LOCAL)
PRINTFORMW 
PRINTFORMDL 悔悟棒で思い切り張り倒された…
CALL M_KOJO_COLOR_K30
BASE:MASTER:体力 -= DAMAGE
PRINTFORML 
SIF DIARY:30:10 == 0
	CALL K30_WRITE_DIARY(10)
IF BASE:MASTER:体力 <= 500
	IF !TALENT:恋慕
		IF TCVAR:30:371 == 1
			PRINTFORML 「あ、あら？　ちょっとやりすぎたかしら？」
			PRINTFORMDL 映姫のとまどう声が…頭に、響く…
		ELSE
			PRINTFORML 「せいぜい反省することね」
			IF SELECTCOM == 356
				PRINTFORMDL 映姫はもうこちらに目もくれず%GET_JOBNAME(30)%の席へ戻った…
			ELSE
				PRINTFORMDL 映姫は無慈悲にもその場から去っていった…
			ENDIF
		ENDIF
	ENDIF
ELSE
	IF TCVAR:30:371 < 3
		PRINTFORML 「どうです、身に沁みましたか」
		PRINTFORMW 「これに懲りたら行動を改めることですよ」
	ELSE
		PRINTFORML 「少々手ぬるいですが今回はここまでにします」
		PRINTFORML 「しっかり反省して今すぐに行動を改めなさい」
		PRINTFORMDW ……映姫の責任感の強さを侮っていたようだ
		PRINTFORMDW とにかく仕事の邪魔はやめておこう
	ENDIF
ENDIF
RETURN 1
;==================================================
;映姫様の説法と折檻
;お茶を飲みながらお話をのんびりと聞くイメージ
;--------------------------------------------------
;CFLAG:1701 良いお話の累計
;--------------------------------------------------
@K30_LECTURE_BENEFIT
#DIM SIN_VAL
#DIM EXC
SIN_VAL = 0
EXC = 0
CFLAG:1701 ++
PRINTFORMW 
;お話し6種類しかないので累計して6で割った余りで振り分け
LOCAL = CFLAG:1701 % 6
SELECTCASE LOCAL
	CASE 0
		PRINTFORMW 「そうですね…貴方は『欲望』についてどう考えますか？」
		PRINTFORML 
		PRINTFORML 「こんな昔ばなしがあります」
		PRINTFORML 「むかし、あるところで、一人の禅僧が師の元で修業を続けていました」
		PRINTFORML 「僧が自室で坐禅を組んでると、見知らぬ女性がいきなり禅僧にもたれかかり」
		PRINTFORML 「『あなたはこうされるとどんな気持ちか』などと尋ねました」
		PRINTFORML 「この女性は師の仕込みで一種の試験として彼を誘惑させたんです」
		PRINTFORML 「僧は、顔色一つ動かさず、『枯木寒巌に倚る、三冬暖気無し』と言い放ちました」
		PRINTFORMW 「『まるで枯木が冷え切った岩に寄りかかったようなものさ、冬の真最中吹きさらしの気持ちだ』と」
		PRINTFORML 
		PRINTFORMW 「貴方はこの僧侶をどうおもいますか？　立派だと思いますか？」
		PRINTFORML 
		PRINTFORML 「いえいえ、師はこの答えに大変怒りまして『思いの外俗物の僧だった。わたしは見込み違いをしていた』」
		PRINTFORMW 「と言って、その僧を追い出してしまいました。彼の何がいけなかったのわかりますか？」
		PRINTFORML 
		PRINTFORML 「実際には、一人の女性に恋された男性として取るべき態度はいかに、というのがこの問題解決の研究です」
		PRINTFORML 「相手は見ず知らずの女性です。たとえ向うはこっちをよく知ってそれから恋したにもせよ」
		PRINTFORML 「こっちははじめて会う女性です。こういう場合には、一人前の男性として、一旦は断るにしろあるいは永久に断るにしろ」
		PRINTFORML 「相手の女性に恥をかかさず、さればといって自分の品位も堕さず、しかるべき人情味のある処置と言葉がありそうなものです」
		PRINTFORMW 「枯木寒巌のごとしと言ってすまし返った僧のような態度、相手の女性は一生恨み切るか、反発的に自殺もしかねないあしらい方です」
		PRINTFORML 
		PRINTFORML 「同じ断り方でも、その女性の気持ちを汲みながらの断り方もあります。それを誘惑に負けまいと一生懸命、肩肘張って」
		PRINTFORMW 「非人情に噛りついていなければならないとは、この僧に心に弱いところがあると師は見抜いたのです」
		PRINTFORML 
		PRINTFORML 「誘惑を避けて逃れるのは最上の人生でないと断定します」
		PRINTFORML 「むしろ泥中の蓮の花のように、雑多な野心や誘惑や愛欲の真っただなかに生活しながらも」
		PRINTFORML 「その汚れに染まずしかもその欲望、誘惑を」
		PRINTFORML 「うまく消化善用して立派な人格完成、無上の幸福という花を咲かせるのべきなのです」
		PRINTFORML 「先刻の僧も、この事を体得しなければ俗人に劣ると言わねばなりません」
		PRINTFORMW 「浮世を隠遁したり誘惑を恐れて必死になって逃げようとするなどは間違っています」
		PRINTFORML 
		PRINTFORMW 「欲を持つのが罪ではありません。欲に溺れたり、欲を捨てたと虚勢を張るのが罪なのです」
	CASE 1
		PRINTFORMW 「そうですね、貴方は『涙の価値』についてどう考えますか？」
		PRINTFORML 
		PRINTFORML 「物事をいい加減にしていれば涙はありません。苦しくないからです」
		PRINTFORML 「物事を真面目に考えて、まともに向うと涙があります。苦しいからです」
		PRINTFORMW 「なぜ、いい加減にしていれば苦しくなく、真面目になると苦しいのでしょうか？」
		PRINTFORML 
		PRINTFORML 「いい加減にしていると、矛盾も矛盾に見えず、より良きものが眼につかないからです」
		PRINTFORML 「今の状態でもどうやらお茶が濁せるからです。それで苦しくありません」
		PRINTFORML 「反対に、真面目になって、まともに向うと、矛盾が目につき、より良きものが望まれ」
		PRINTFORMW 「現状にひどい不満を感じて来るからです。それで苦しみます」
		PRINTFORML 
		PRINTFORML 「一番より良きものを望む心、仮にこれを『菩提心』と呼びましょうか」
		PRINTFORMW 「『菩提心』とは、自分を良くし、人も良くしようという実直な願い心です」
		PRINTFORML 
		PRINTFORML 「例えば良心は時代によって変り、周囲の情勢によって変ることもあります」
		PRINTFORML 「自分の肉体の貞操を売っても、夫へ心の貞操を捧げるのを良しと認めた」
		PRINTFORMW 「封建時代の女性の良心は、もう今日の女性の良心ではありません」
		PRINTFORML 
		PRINTFORML 「しかし『菩提心』は、時代により情勢によって変るものではありません」
		PRINTFORML 「人間がある限り、その中に在ってその発展の方向を示し、これを浄化推進して行く薬であります」
		PRINTFORML 「ばい菌が潜入しても、心の内にある薬つまり『菩提心』がそれが居るとは知らず浄化されています」
		PRINTFORML 「私たちは『菩提心』ありとは知らずに、心の清純を保たされています」
		PRINTFORMW 「この場合のばい菌とは『菩提心』に相反する誘惑・悪心といったものですね」
		PRINTFORML 
		PRINTFORML 「ですが万能薬などこの世にはありません。薬が効く時には何か望まぬものも生まれます」
		PRINTFORML 「悪心を『菩提心』が駆逐した時にでる残り滓、残骸が最初にいった涙であり苦しみです」
		PRINTFORML 「人間を苦しみや涙が誘うとき、それを徒然にせず、その原因を深く辿って行くときは」
		PRINTFORMW 「必ずこの『菩提心』の発露に出会います。そしてその心の指図によって新しく正しき人生の方向を執りなさい」
		PRINTFORML 
		PRINTFORML 「『生の苦しみ』という事があります。旧き生から新しき生を生み出すときには、必ず苦悩があります。涙があります」
		PRINTFORML 「樹が芽を吹くとき、樹の皮に現れるものはまず疵です。苦悩です。次に樹脂――」
		PRINTFORMW 「つまり涙です。そこからようやく新しい生なる五月の新緑が芽生えるのです」
	CASE 2
		PRINTFORMW 「そうですね…貴方は『好き嫌い』についてどう考えますか？」
		PRINTFORML 
		PRINTFORML 「人間に心があり、心に感情がある以上、だれにも好き嫌いの気持ちが働くのは当たり前です」
		PRINTFORML 「それを叱ることはしません。桜や梅や竹、その百木千草の変化があって自然の風光が面白いように」
		PRINTFORML 「人間に好き嫌いの気持ちの陰影があってこそ、むしろ人々の変化やリズムがあって面白く
		PRINTFORMW 「世の中が単調に流れません。ですから好き嫌いは大いにあってよろしいのです」
		PRINTFORML 
		PRINTFORML 「ですがこういったあとで殊にも言い添えなければならないのは、くれぐれもその好き嫌いの気持ちに」
		PRINTFORML 「捉われてはいけないということです。捉われて、それを意固地に通して行こうとするとき、その人は我儘者になるわけです」
		PRINTFORML 「例えば『私はあの人は嫌いだから友達にしない』」
		PRINTFORML 「『だからほかの誰でもみんなあの人を友達にしてはならない』というような、こんな嫌い方は絶対に我儘です」
		PRINTFORML 「『私は松は嫌いだ。世の中から松なんか一本もなくしてしまえ』」
		PRINTFORML 「『私は竹が好きだから竹ばかりにしてしまえ』というのと同じです」
		PRINTFORMW 「これでは幻想郷も竹林ばかり増えて何とも面白くない風景になってしまいます」
		PRINTFORML 
		PRINTFORML 「それと同じように人間でも」
		PRINTFORML 「『私はあの人は嫌いだけれど誰々さんにはよく見えるのかも知れないからお友達になっているのだろうからそれでよい』」
		PRINTFORML 「とこう気を落ちつけて、自分が嫌いなものを自分だけで嫌っていればよいのです。自分の嫌いな気持ちでその人を追いかけて行って」
		PRINTFORML 「その人の好かれる場所まで入って邪魔をするなどは我儘な仕業です」
		PRINTFORML 「それは世の中の調和を乱す者でありまして許されざる勝手です」
		PRINTFORMW 「この弊に落ち入ってしまった人は自分自身で身のあがきがつかぬ窮屈な境遇に立ち至らなければなりません」
		PRINTFORML 
		PRINTFORML 「私から見れば天地間のあらゆる生物草木に至るまで、どれ一つとして尊い生命の種子を備えておらぬものはありません」
		PRINTFORML 「それ故、使い方によっては何一つとして捨てるもの、無駄なものはないのです」
		PRINTFORML 「ですから何ものに対しても、そのものの価値を絶対に無視することは許されません。それは自然への冒涜です」
		PRINTFORMW 「天地仏神への不敬に亘り、やがて私に罰せられなければなりません」
		PRINTFORMW 
		PRINTFORMW 「私が罰するといえばどうなることかわかりますね？　貴方も気をつけてください」
	CASE 3
		PRINTFORMW 「そうですね…貴方は『宝塔』について何かご存知ですか？」
		PRINTFORML 
		PRINTFORML 「…よく無くしたり落としたりする物ではありません。それについては一旦忘れてください」
		PRINTFORMW 「彼女も毘沙門天の代理人として、もう少しだけしっかりしてもらえたらいいんですが…」
		PRINTFORML 
		PRINTFORML 「…話がそれてしまいましたが続けましょう」
		PRINTFORML 「ここで私が言う『宝塔』とはとても価値のある器物、道具のことです」
		PRINTFORML 「そして『宝塔』は貴方も私も持っているものです。落としたりすることなど出来ません」
		PRINTFORMW 「私たちの唯一の財産、最初にして最後の財産のこと…つまり『身体』のことを意味しています」
		PRINTFORML 
		PRINTFORML 「少し大げさに言い過ぎですか？　いえいえ決してそんなことはありません」
		PRINTFORMW 「そうですね、『身体』の素晴らしい働きについて挙げてみましょうか」
		PRINTFORML 
		PRINTFORML 「頭の中で考えて分別し、胸の内では感情を湧き上がらせる」
		PRINTFORML 「腹の中にそれを収めて貯え、実際に手足が動いて行動を起こす」
		PRINTFORMW 「そして全体として協同作業を取り、様々な事をこなしていきます」
		PRINTFORML 
		PRINTFORML 「さらに凄いことに、例えそれぞれが調和を取れなくとも『身体』は機能します」
		PRINTFORML 「頭でいけなければ胸で、胸でいけなければ腹で、腹でいけなければ手足で」
		PRINTFORMW 「思考と感情と行動が一致しなくともなんとか『身体』は動きます。貴方にも思い当たる事はありませんか？」
		PRINTFORML 
		PRINTFORML 「およそ世の中に、これだけの機能の備わっている道具は他にあるでしょうか」
		PRINTFORMW 「きちんと使えば出来ないことはなかなかありませんね」
		PRINTFORML 
		PRINTFORML 「とある経文において、宝塔の中に多宝如来とお釈迦様とが並んで座せられる場面が書いてあります」
		PRINTFORML 「この場面では、多宝如来は真理を現し、お釈迦様は智慧を現しています」
		PRINTFORML 「そして『宝塔』は私たちの『身体』を象徴したものと私は考えてます」
		PRINTFORML 「私たちのこの『身体』は、正しく使えばあらゆる真理、あらゆる智慧が取り出せる……」
		PRINTFORMW 「それほど有り難い物を、生まれたときから私たちは授かっているのです」
		PRINTFORML 
		PRINTFORML 「ですから貴方も、塔の中の宝を、『身体』の価値を、少しずつでいいので取り出していきましょう」
		PRINTFORML 「特に貴方は私にも測り知れぬ力を持っていますよね。それだけ多くの宝を持っているということです」
		PRINTFORMW 「それなのに貴方は…女性の肢体にのみ宝を探し求めていませんか？　実にもったいないことです」
		PRINTFORML 
		PRINTFORMW 「自分の『身体』を正しく使ってこそ、本当の価値ある宝は取り出されるのですよ」
	CASE 4
		PRINTFORMW 「そうですね…貴方は『苦労』についてどう考えますか？」
		PRINTFORML 
		PRINTFORML 「よく『若い時の苦労は買ってでもしろ』とか『苦労を知らないお坊っちゃんはダメだ』などと言われますが」
		PRINTFORMW 「実際苦労をしない人よりは、苦労をした人の方が人間味が深いことがあるのは確かです」
		PRINTFORML 
		PRINTFORML 「しかし、絶対ではありません。苦労を重ねるうちにすっかり苦労に負けてしまう人だっています」
		PRINTFORML 「狡くなり、卑屈になってしまい、苦労してないと思った人を見ると攻撃的になる…」
		PRINTFORMW 「貴方もこんな人に心当たりはありませんか。なぜこんなふうになってしまうのでしょう」
		PRINTFORML 
		PRINTFORML 「閻魔から見ても、人に苦労なんてあるよりは無い方が本当は良いに決まってます」
		PRINTFORML 「だけど現世に苦労がある限り、逃れるには死より他に道がありません」
		PRINTFORML 「ですから、人は苦労に立ち向って、これを凌ぐ力を養わねばいけません」
		PRINTFORML 「凌ぐ力が養えたら、苦労があっても無いのと同様です。気にならなくなるんです」
		PRINTFORMW 「つまり苦労をするのは苦労が目的でなく、苦労を無いも同様にしようとする手段であると覚えてください」
		PRINTFORML 
		PRINTFORML 「先ほど言った苦労に負けた人。この人たちはおおよそ苦労する事の目的を忘れてしまった人です」
		PRINTFORML 「苦しい事にあたった時、その原因や性質を見極めて取り除いていく。苦労する事とは、この凌ぐ力を養う試練なのです」
		PRINTFORMW 「これを知らずに苦しい事を先送りしたり、ただ息を潜めて耐えるだけではいずれ苦労に負けてしまいます」
		PRINTFORML 
		PRINTFORML 「以前私の裁きを受けた魂の中で、こう言い放った者がいました」
		PRINTFORML 「人の世は真に苦界であり、それに自分はいち早く気づくことが出来た。だから生きている間、ずっと修行に明け暮れた」
		PRINTFORMW 「わざとボロを着て、身体を火で炙り、貧困のまま死ぬことが出来た。なので自分は転生して天界に行く資格がある、と」
		PRINTFORML 
		PRINTFORML 「浄玻璃鏡に映すと、確かに彼は最低限の衣食を人々の施しに頼り、後は心身を苛む厳しい修行に明け暮れていました」
		PRINTFORML 「一見立派な行いにも見えますが……ちょっと厳しい言い方をすれば、こうも考えられませんか」
		PRINTFORML 「この魂は生前、施しという周囲の善行に支えられて生きていた。なのに考える事は自分の来世の幸福ばかりだった、と」
		PRINTFORMW 「修行という苦労を手段ではなく目的としてしまった為、結局満足に善行を積むこともできなかったのですね」
		PRINTFORML 
		PRINTFORML 「もちろん、苦労を避けて楽なところへ逃げ出すのはいけません。それはただの堕落です」
		PRINTFORML 「だからといって苦労にかじりついて、これに蝕まれるのも正しい人生の送り方ではありません」
		PRINTFORML 「苦労に蝕まれず、苦労を一つの研究材料として、そこに人生の一部一部を観て取って行く努力をする」
		PRINTFORMW 「そうやって人生の姿を、より多く、より広く、知識し経験してこそ、苦労に捉われず苦労のしがいがあった人だと言えますね」
	CASE 5
		PRINTFORMW 「そうですね…貴方は『因果』と聞いてどう思いますか？」
		PRINTFORML 
		PRINTFORML 「この言葉が使われるのは、たいてい人生の不遇や暗黒面に見舞われたときです」
		PRINTFORML 「『親の因果が子に報い』と言う事はあっても、『親の因果で子の出世』などとは聴いたことがありません」
		PRINTFORML 「あと、因果応報という言葉もありますが、これも同じように考えられてますね」
		PRINTFORMW 「過去、あるいは前世の悪行のために悪い報いを受けるという意味で使われがちです」
		PRINTFORML 
		PRINTFORML 「こう並べると悪い言葉のように思えますが、本来はこのように限定された意味ではありません」
		PRINTFORML 「『因果』とは原因と結果のことを指します。自然の理であり、人間に対して別に親切でも冷酷でもないんです」
		PRINTFORMW 「どんなことにも必ず原因があります。原因なしに起きる結果は万に一つ、億に一つもありません」
		PRINTFORML 
		PRINTFORML 「では、どんな原因によってどんな結果が生み出されるのか想像できますか」
		PRINTFORML 「難しく考えることはありません。原因と結果の関係は簡単なことです」
		PRINTFORML 「善い行いからは、幸せという結果が起こります。これが善因善果です」
		PRINTFORMW 「悪い行いからは、不幸せという結果が起こります。これが悪因悪果です」
		PRINTFORML 
		PRINTFORML 「このことはよく種まきに例えられます。畑に大根の種をまけば大根の芽、スイカの種をまけばスイカの芽が出てきますよね」
		PRINTFORML 「それと同じです。善い行為からは善い結果だけ、悪い行為からは悪い結果だけが必ず生まれるのです」
		PRINTFORML 「私が常日頃から善行を積むように教えを説くのもこれが理由です。自分の行いの報いは、自分が得るからです」
		PRINTFORMW 「これぞまさしく自業自得ですね。良いことも悪いことも全ては自分が起点となっているのですよ」
		PRINTFORML 
		PRINTFORML 「『因果』を種まきに例えたついでにもうひとつ…そうですね、またスイカでも育ててみましょうか」
		PRINTFORMW 「畑にスイカの種をまけばスイカが出てくる。これは自然の理であって当たり前のことです」
		PRINTFORML 
		PRINTFORML 「でも考えてみてください。理を知っているからといって誰でもスイカを作れるかというと違いますよね」
		PRINTFORML 「食べ終わったスイカの種を適当にまいたら、またスイカが生えてきてまた食べて…なんて、そうは上手くいきません」
		PRINTFORMW 「確実に芽を息吹かせるには、正しい知識できちんと手入れすることが必要です」
		PRINTFORML 
		PRINTFORML 「同じく善因善果、善い行いと幸せという結果には正しい知識と手順が要求されます」
		PRINTFORML 「知識が無ければ、良かれと思ってやったことが実は悪い行いであった、なんてことになるでしょう」
		PRINTFORML 「因と果の繋がりを理解してないと、こんなに頑張ってるのに結果が出ないと不満に思ってしまうでしょう」
		PRINTFORML 「スイカの種をまいたのに大根が生えてきた、なんてことは絶対にないのですが、そう言い張る人は必ずいます」
		PRINTFORMW 「そういう人は知識がないばかりに、そのような間違いをしてしまうのですね」
		PRINTFORML 
		PRINTFORML 「ですから人は学び続けなければなりません」
		PRINTFORML 「自分のこと、自分に関わること、自分の大切な人のことから理解を深めていってください」
		PRINTFORMW 「そうすればたとえ未知の『因果』に直面しても、自ずから正しい行動が取れるようになりますよ」
ENDSELECT
CALL K30_D_LINE
SIF DIARY:30:16 == 0 && CFLAG:30:1701 > 9
	CALL K30_WRITE_DIARY(16)
RESULT = 0
PRINTFORMW
PRINTFORMW 「どうでしたか。この話が善行を積む事への一助となれば私も嬉しいです」
IF CFLAG:弱み握り && !GETBIT(CFLAG:1803, 1)
	FORCEWAIT
	PRINTFORMW 「……………………」
	PRINTFORMW 「ところで%K30_C_NAME(MASTER)%」
	PRINTFORMW 「私に何か謝ること……ありませんか？」
	$INPUT_LOOP
	RESETCOLOR
	CALL ASK_M("…あの事はごめんなさい",1 , "え？　何の話", 1, "…わかった、この話はやめよう。ハイ！！　やめやめ", 1, "え？　何、この選択肢", 1)
	CALL M_KOJO_COLOR_K30
	SELECTCASE RESULT
		CASE 0
			PRINTFORMDW 心当たりはあるのでとりあえず謝っておいた
			PRINTFORML 「…誠意が見当たりませんね。そもそも何に謝っているのか自分でわかってますか？」
			PRINTFORML 「まあ、そのような方を罰するために、私はこの笏を持ち歩いているのですが」
			PRINTFORMDW 映姫は手に持った悔悟棒を%CALLNAME:MASTER%に突きつける…
			PRINTFORML 
			PRINTFORML 「きちんと鏡に映らない貴方の罪、私の記憶の範囲で裁くことになりますが…」
			PRINTFORML 「もちろんそのことに異存ありませんね？」
			RESETCOLOR
			CALL ASK_M("はい", 1, "ございません", 1, "もちろんです", 1)
			CALL M_KOJO_COLOR_K30
			PRINTFORML 「結構なことです。では…」
			PRINTFORMDW 映姫は取り出した筆で悔悟棒に書き込んでいく…
			IF GETBIT(CFLAG:1699, 1)
				PRINTFORML 
				PRINTFORML 「ぱんつを盗み持っていた罪」
				PRINTFORMW 「いったい何に使うのやら」
				SIN_VAL ++
			ENDIF
			IF GETBIT(CFLAG:1699, 2)
				PRINTFORML 
				PRINTFORML 「公共の場で姦淫に耽っていた罪」
				PRINTFORMW 「自制というものを覚えなさい」
				SIN_VAL ++
			ENDIF
			;私室ヤリ部屋化、睡姦バレは口上で拾えないのでこの辺ガバガバ
			;仕事場ヤリ部屋化のぶんでさらにガバガバガーバ。仕事案本体取り込まれたら書き直す
			IF (CFLAG:弱み握り - CFLAG:1700 - CFLAG:1698) > 15
				PRINTFORML 
				IF CFLAG:MASTER:初期位置 == CFLAG:30:初期位置
					PRINTFORML 「二人の部屋で他の女性と姦淫に耽っていた罪」
					PRINTFORML 「そう、私たちの大切な部屋で…」
					PRINTFORMDW 筆を持つ手がわなわなと震えている…
					SIN_VAL += 100
				ELSE
					PRINTFORML 「私の部屋で姦淫に耽っていた罪」
					SIF TALENT:恋人
						PRINTFORML 「しかも、私という恋人がありながら…」
					PRINTFORMW 「本当に許しがたいことです」
					SIN_VAL ++
					SIF TALENT:恋人
						SIN_VAL += 100
				ENDIF
			ELSEIF (CFLAG:弱み握り - CFLAG:1700 - CFLAG:1698)
					PRINTFORML 
					PRINTFORML 「寝ている女性にイタズラを働いた罪」
					PRINTFORMW 「とても卑怯な恥ずべき行為です」
			ELSEIF GETBIT(CFLAG:1699, 3)
				PRINTFORML 
				SIF TALENT:恋人
					PRINTFORML 「…私という恋人がありながら」
				PRINTFORML 「私の部屋で姦淫に耽っていた罪」
				PRINTFORMW 「本当に許しがたいことです」
				SIN_VAL ++
				SIF TALENT:恋人
					SIN_VAL += 100
			ENDIF
			PRINTFORML 
			PRINTFORML 「…これをもって貴方を有罪とします」
			PRINTFORML 「この笏の重さは、貴方の罪の重さ」
			PRINTFORMW 「その痛みを味わい己の罪深さをとくと知りなさい」
			PRINTFORML 
			PRINTFORMDL 映姫が書き終えた悔悟棒を振りかざした…
			IF SIN_VAL < 100
				PRINTFORMDL 両手で持つ様子からして結構重そうだ。
				PRINTFORMDW ちょっと覚悟が必要かもしれない
				LOCAL = 4
			ELSE
				PRINTFORMDL 持ち上げる様子からして尋常な重さではなさそうだ
				PRINTFORMDW この裁きには明らかに私情が混じっている！
				LOCAL = 6
			ENDIF
			PRINTFORML 
			PRINTFORMW 「この制裁をもって己の罪を…」
			SETCOLOR 206, 8, 21
			PRINTFORML 
			PRINTFORML 「悔い改めなさい！！！」
			CALL K30_QUAKE(LOCAL)
			PRINTFORMW 
			CALL M_KOJO_COLOR_K30
			PRINTFORMDW 悔悟棒で力いっぱい張り倒された…
			IF SIN_VAL > 100
				LOCAL = BASE:MASTER:体力 + 1
				TCVAR:30:371 = 2
			ELSE
				LOCAL = BASE:MASTER:体力 / 4
				TCVAR:30:371 = 1
			ENDIF
			CALL RECOVER(MASTER, -(LOCAL), "体力", "悔悟棒の強烈な一撃")
			LOCAL = GETBIT(CFLAG:1699, 0) ? 1 # 0
			CFLAG:弱み握り = 0
			SETCOLOR C_YELLOW
			PRINTFORML 弱味は全て打ち消された
			CALL M_KOJO_COLOR_K30
			VARSET CFLAG:30:0, 0, 1698, 1701
			SIF LOCAL
				SETBIT CFLAG:1699, 0
			IF BASE:MASTER:体力 <= 500
				IF !TALENT:恋慕
					PRINTFORMDL 容赦のない一撃に目の前が真っ暗になって…
					IF SIN_VAL < 100
						PRINTFORML 「あ、あら？　ちょっと、やりすぎたかしら？」
						PRINTFORMDL 映姫のとまどう声が…頭に、がんがんと…響く…
					ELSE
						PRINTFORML 「せいぜい反省することね」
						PRINTFORMDL 映姫は無慈悲にもその場から去っていく…
					ENDIF
				ENDIF
			ELSE
				PRINTFORMDL 容赦のない一撃をもらったが、なんとか踏みとどまった…
				PRINTFORMW 「その痛みを反省の糧として、これからは善行に励むのですよ」
			ENDIF
			IF DIARY:30:10 == 0
				CALL K30_WRITE_DIARY(10)
				SIF BASE:MASTER:体力 <= 500
					SETBIT CFLAG:1910, 3
			ENDIF
			RETURN 1
		CASE 1
			PRINTFORMDW …悔悟棒がぴくっと揺れる。映姫は少しイラッときたようだ
			PRINTFORML 「覚えてない、というなら…しょうがありません」
			PRINTFORML 「空っぽのものに叩き込んでも、効果は薄そうですしね」
			PRINTFORMW 「…きちんと思い出したら、その時またお話ししましょう」
			GOTO IGNORE
		CASE 2
			PRINTFORML 「……………」
			PRINTFORMDW 悔悟棒を持つ手がぷるぷると震えている。少し映姫を煽りすぎたかもしれない…
			PRINTFORML 「…そうおっしゃるのなら、別にそれで構いません」
			PRINTFORML 「罪をつぐなう機会を逃しただけです。いずれそれはやってきます」
			PRINTFORMW 「その時になって後悔しないよう、きちんと善行を積み上げておきなさい」
			SETBIT CFLAG:1803, 1
			GOTO IGNORE
		CASE 3
			PRINTFORML 
			PRINTFORMDL 『弱味』を悔悟棒の折檻一発で消すかどうかの選択肢です
			PRINTFORMDL 上から順に許可・保留・以降の選択非表示になります
			PRINTFORMDL ダメージは最大で体力上限→当日行動不可コースです
			PRINTFORML 
			PRINTFORMDL 保留・選択非表示はマイナスっぽい会話になりますが
			PRINTFORMDL 特にペナルティはありません
			PRINTFORMW 
			PRINTFORML 「…どうしました？　ちゃんと思い出せましたか？」
	ENDSELECT
	GOTO INPUT_LOOP
ELSE
	$IGNORE
	PRINTFORMDW 説法に付き合って、映姫の好感度と信頼度が\@ RESULT ? ほんの # \@すこし上がった
	LOCAL = RESULT
	CALL CHANGE_CFLAG(2, TARGET, RAND:5 + 7 - RESULT)
	CALL CHANGE_CFLAG(4, TARGET, RAND:3 + 3 - RESULT)
	SELECTCASE RAND:3
		CASE 0
			LOCALS = 有り難い
		CASE 1
			LOCALS = ためになる
		CASE 2
			LOCALS = 含蓄が深い
	ENDSELECT
	CALL RECOVER(MASTER, -100, "気力", @"%LOCALS%けど長いお話し")
	SOURCE:反感 = 0
	SOURCE:鬱屈 = 0
	SOURCE:歓楽 += ABL:30:親密 * 50
	SOURCE:情愛 += 1500
	RETURN LOCAL
ENDIF
RETURN 1

;==================================================
;キャラ紹介文関数。早苗口上を参考にしてます
;--------------------------------------------------
@CHARA_INFO_KOJO_K30
;紹介文前半は花映塚キャラ紹介テキストとか参考に
PRINTFORML %CSVCSTR(30, 10)%
PRINTFORML 
PRINTFORML 地獄に住む死者を裁く神様。日々の大半を死者の裁判と地獄の管理業務に費やしている。
PRINTFORML 性格は真面目そのもの。だが、仕事の影響もあって少し説教くさい。
PRINTFORML 例えば目の前に罪を負った人間（でも妖怪でもとにかく誰でも）がいたら、閻魔様は絶対に見過ごさない。
CALL COLORMESSAGE("趣味の", 0XD5DEDF, 0, 4)
PRINTFORM 貴重な時間を使って
CALL COLORMESSAGE("口うるさい", 0XD5DEDF, 0, 4)
PRINTFORML 有難いお話と教えをたっぷり説いてくれることだろう。
SIF !CFLAG:30:面識
	RETURN 1
PRINTFORML 
;恋慕取得前
IF !TALENT:30:恋慕
	IF TALENT:30:セフレ || TALENT:30:ヤリマン || TALENT:30:愛欲
		PRINTFORML 閻魔にあるまじき自身の淫らな行動に深く罪の意識を抱いている…
	ELSEIF TALENT:30:思慕
		PRINTFORML 目下の悩みは何者にも影響されず迷うことがないはずの自分が、とある人物にすっかり惑わされていることだ。
	ELSEIF STRCOUNT(CSTR:30:0, "変Ｔお披露目")
		PRINTFORM 目下の悩みは、最近導入された閻魔の制服があまりにも
		CALL COLORMESSAGE("変", 0XD5DEDF, 0, 4)
		PRINTFORML 特徴的でうまく着こなせないことらしい…
	ELSE
		PRINTFORML 最近知り合った浄玻璃の鏡で見通せない人物を気にかけている。
	ENDIF
	PRINTFORML
	;無自覚孕ませ
	IF CFLAG:1805
		CALL K30_UNWANTED_PREGNANCY
	ENDIF
	RETURN 1
ENDIF
;恋慕取得後
PRINTFORML 他人に影響され変化していく自分を受け入れ、%\@ TALENT:30:恋人 ? 恋人 # 想い人 \@%と過ごす時間を大切にしたいと考えるようになった。

;押し倒され実行後、独自イベントっぽいのを匂わせる。文章の加減が難しいねこれ
IF CFLAG:30:1801 && STRCOUNT(CSTR:30:0, "押し倒し")
	PRINTFORML 併せて自分から一線を超えたことで、今まで抑えつけていた欲望も全て%\@ TALENT:30:恋人 ? 恋人 # 想い人 \@%に受け入れてほしいと考えている…
	IF GETBIT(CFLAG:30:1910, 7)
		PRINTFORML 　・%CALLNAME:MASTER%専用に作り変えられたお尻をもっと愛してもらいたいらしい…
	ELSE
		PRINTFORML 　・%CALLNAME:MASTER%にお尻をたっぷり愛してもらいたいらしい…
	ENDIF
	SIF ABL:30:露出癖 > 4 && CFLAG:30:1607 > 10
		PRINTFORML 　・%CALLNAME:MASTER%にお尻を攻められて潮を吹くことがとても気持ちが良くて、もう癖になっているらしい…
	SIF !STRCOUNT(CSTR:30:0, "朝から") && ABL:30:精液中毒 > 2
		PRINTFORML 　・\@ CFLAG:MASTER:初期位置 == CFLAG:30:初期位置 ? 気分が昂ぶれば%CALLNAME:MASTER%に『特別』な朝の # %CALLNAME:MASTER%と同棲できたら、朝は『特別』な \@起こし方をしてあげたいらしい…
	SIF CFLAG:30:信頼度 >= 2000 && !STRCOUNT(CSTR:30:0, "キメセク") && CFLAG:30:生解禁 == 2
		PRINTFORML 　・身も心も溶かすくらいに%CALLNAME:MASTER%と交わりたくて、怪しげなお茶でも騙されたフリして飲んでもいいらしい…
	SIF TALENT:MASTER:恋人 != 76 && TALENT:76:恋慕 && GET_UWAKI_HISTORY(30, 76) == 2 && ABL:30:精液中毒 > 3 && ABL:76:精液中毒 > 3
		PRINTFORML 　・%CALLNAME:MASTER%には自分と同じぐらいに小町を可愛がってもらい、二人まとめて愛されたり奉仕を受けてもらいたいらしい…
	SIF TALENT:30:母乳体質 && !CFLAG:30:1802 && !STRCOUNT(CSTR:30:0, "授乳プレイ")
		PRINTFORML 　・%CALLNAME:MASTER%に乳を含ませながら、ダダ甘に甘やかしたいと密かに思っているらしい…
ENDIF
PRINTFORML 
;無自覚孕ませ
IF CFLAG:1805
	CALL K30_UNWANTED_PREGNANCY
	RETURN 1
ENDIF
;妊娠願望。恋慕後でも時姦妊娠とか鬼畜あなたを考慮したら見づらい分岐に
;願望再取得時
IF GETBIT(CFLAG:30:1502, 4)
	PRINTFORML %CALLNAME:MASTER%との子供を再び授かることを望んでいる。
	PRINTFORML 
	RETURN 1
ELSEIF TALENT:30:妊娠願望
	PRINTFORML %CALLNAME:MASTER%との子供を授かる日を夢見ている。
	PRINTFORML 
	RETURN 1
;妊娠願望再取得可
ELSEIF GETBIT(CFLAG:30:1502, 3) && !TALENT:30:育児中 && !TALENT:30:妊娠
	PRINTFORML また[願掛け]をすれば子供を授かるのではないかと期待をしている。
	PRINTFORML 
	RETURN 1
ENDIF
SIF !INRANGE(CFLAG:30:1502, 1, 7) || TALENT:30:育児中 || TALENT:30:妊娠 || EXP:30:出産経験
	RETURN 1
;bit演算を十進数で捉えてCASE分け
SELECTCASE CFLAG:30:1502
	CASE 1
		PRINTFORML %CALLNAME:MASTER%に身体は許しているが母親になる自信は無い。
		PRINTFORML 　・もっと%CALLNAME:MASTER%を[信頼]出来たらと考えている…
		PRINTFORML 　・閻魔が子供を授かるなど[願掛け]をしても難しいのではと考えている…
	CASE 2
		PRINTFORML %CALLNAME:MASTER%に子供が欲しいと打ち明けられて動揺している。
		PRINTFORML 　・まだ最後まで身体も許してないのに気が早すぎると考えている…
		PRINTFORML 　・もっと%CALLNAME:MASTER%を[信頼]出来たらと考えている…
	CASE 3
		PRINTFORML %CALLNAME:MASTER%の子供が本当に欲しいのか迷っている。
		PRINTFORML 　・もっと%CALLNAME:MASTER%を[信頼]出来たらと考えている…
	CASE 4
		PRINTFORML %CALLNAME:MASTER%との子供についてぼんやりと意識し始めている。
		PRINTFORML 　・まだ最後まで身体を許してないのに気が早すぎると考えている…
		PRINTFORML 　・閻魔が子供を授かるなど[願掛け]をしても難しいのではと考えている…
	CASE 5
		PRINTFORML %CALLNAME:MASTER%との子供についてはっきりと意識している。
		PRINTFORML 　・閻魔が子供を授かるなど[願掛け]をしても難しいのではと考えている…
	CASE 6
		PRINTFORML %CALLNAME:MASTER%と子作りをする覚悟が出来ないでいる。
		PRINTFORML 　・まだ最後まで身体を許せないくらいに迷っている…
	CASE 7
		PRINTFORML 迷いも晴れてついに%CALLNAME:MASTER%と子作りをする決心ができた。
ENDSELECT
RETURN 1

@K30_UNWANTED_PREGNANCY
IF INRANGE(CFLAG:妊娠経過日数, 1, 19)
ELSEIF INRANGE(CFLAG:妊娠経過日数, 20, 59)
	PRINTFORML 自身の身体の変調にあえて目を逸らしている
ELSEIF CFLAG:妊娠経過日数
	PRINTFORML 誰とも知らぬ子を孕まされた異常な事態に恐れ慄いている
ELSEIF CFLAG:出産経過日
	PRINTFORML 罪の意識に苛まれ%CSTR:30:83%を無事に育てることだけを支えにしている
ELSE
	CALL COLORMESSAGE, "自らを苛烈な罰にさらし二度と孕めぬ身体となった", C_RED, 1, 1
ENDIF
RETURN

;==================================================
;映姫様のお尻の穴と抵抗を緩くするEXTRASOURCE関数群
;豊姫口上参考にしました
;--------------------------------------------------
;COMF4 アナル舐め
@M_KOJO_EXTRASOURCE_COM_K30_4
CALL K30_LESS_RESISTANCE_ANAL

;COMF5 アナル愛撫
@M_KOJO_EXTRASOURCE_COM_K30_5
CALL K30_LESS_RESISTANCE_ANAL

;COMF45 アナルバイブ
@M_KOJO_EXTRASOURCE_COM_K30_45
CALL K30_LESS_RESISTANCE_ANAL

;COMF62 正常位アナル
@M_KOJO_EXTRASOURCE_COM_K30_62
CALL K30_LESS_RESISTANCE_ANAL

;COMF63 後背位アナル
@M_KOJO_EXTRASOURCE_COM_K30_63
CALL K30_LESS_RESISTANCE_ANAL

;COMF66 騎乗位アナル
@M_KOJO_EXTRASOURCE_COM_K30_66
CALL K30_LESS_RESISTANCE_ANAL

;COM74(SCOMF14) Ｓ状結腸責め
@M_KOJO_EXTRASOURCE_SCOM_K30_14
CALL K30_LESS_RESISTANCE_ANAL

;COM75(SCOMF15) 膣裏責め
@M_KOJO_EXTRASOURCE_SCOM_K30_15
CALL K30_LESS_RESISTANCE_ANAL

;SCOMF16 アナル交互挿入
@M_KOJO_EXTRASOURCE_SCOM_K30_16
CALL K30_LESS_RESISTANCE_ANAL

;COMF314 アナル愛撫
@M_KOJO_EXTRASOURCE_COM_K30_314
CALL K30_LESS_RESISTANCE_ANAL

;COMF318 腰を動かす（Ａ）
@M_KOJO_EXTRASOURCE_COM_K30_318
CALL K30_LESS_RESISTANCE_ANAL

;COMF323 相手主導後背位Ａ
@M_KOJO_EXTRASOURCE_COM_K30_323
CALL K30_LESS_RESISTANCE_ANAL

;軽減処理関数
@K30_LESS_RESISTANCE_ANAL
#DIM DIVIDED
SIF !(TALENT:思慕 || TALENT:恋慕 || TALENT:セフレ || TALENT:愛欲)
	RETURN
SOURCE:逸脱 /= 4
SOURCE:不潔 /= 6
SIF TALENT:機嫌 == -1 || MARK:反発刻印
	RETURN
DIVIDED = 3 + (TALENT:恋慕 * 2) - (K30_BE_SEEN() * 2)
SOURCE:鬱屈 = SOURCE:鬱屈 / DIVIDED
SOURCE:反感 = SOURCE:反感 / DIVIDED
RETURN
;==================================================
;映姫様が仕事中にフェラ抜きしてくれるEXTRASOURCE関数群
;本体側改造しない都合で1ターンであなたを発射させる仕様
;仕事中ならどうしても我慢できないのを手早く済ます…というシチュとしてごまかす
;--------------------------------------------------
;COMF300 会話
@M_KOJO_EXTRASOURCE_COM_K30_300
IF K30_CHECK_BLOW_JOB()
	CALL K30_SET_BLOW_JOB
	TFLAG:193 = -999
;ELSEIF K30_AFTERSEX_FELLA()
;	CALL K30_SET_BLOW_JOB
;	TFLAG:193 = -900
ENDIF
RETURN 1

;COMF304 仕事を手伝う
@M_KOJO_EXTRASOURCE_COM_K30_304
IF K30_CHECK_BLOW_JOB() && BASE:30:仕事量 > 0
	CALL K30_SET_BLOW_JOB
	DOWNBASE:MASTER:体力 -= 50
	DOWNBASE:MASTER:気力 -= 180
	;経過時間の減少と仕事量を戻す
	TIME -= 50
	BASE:仕事量 = TFLAG:194
	TFLAG:193 = -999
ENDIF

;仕事中フェラ抜き判別用式中関数
@K30_CHECK_BLOW_JOB()
#FUNCTION
SIF K30_BE_SEEN() || !仕事中チェック(30) || INRANGE(TIME, 540, 719) || CFLAG:職種 != 43
	RETURNF 0
SIF !SHIRAHU(30) || !TALENT:恋慕 || MARK:反発刻印 || TALENT:機嫌 == -1
	RETURNF 0
SIF ABL:技巧 < 3 || ABL:精液中毒 < 5 || EXP:精飲経験 < 100
	RETURNF 0 
SIF !GETBIT(CFLAG:30:既成事実, 1) || TALENT:妊娠願望 || GETBIT(CFLAG:1502, 4)
	RETURNF 0
SIF !(TALENT:PLAYER:2 & 2) || 挿入不可(30) == 3 || K30_CHECK_HEN_T()
	RETURNF 0
SIF !(TCVAR:発情 || CFLAG:溜まってる度 > 850 || TCVAR:媚薬)
	RETURNF 0
SIF !ONCE("フェラ抜き")
	RETURNF 0
;ここまで抜けたら確定
RETURNF 1

;フェラ抜きソース処理関数
@K30_SET_BLOW_JOB
CALL COM81
;映姫様とあなたを同時絶頂させる
TCVAR:MASTER:イきそう = 1
BASE:MASTER:射精 = MAXBASE:MASTER:射精
PALAM:30:4 = MAX(PALAMLV:4, PALAM:30:4)
SOURCE:反感 = 0

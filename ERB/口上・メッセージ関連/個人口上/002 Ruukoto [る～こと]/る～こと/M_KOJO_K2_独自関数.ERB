
;### ハートのみ別の色でPRINTするための関数 ##################
;ew6様のコードを参考にさせていただきました
;文字列中に含まれる"HPH"をホットピンク色のハートとしてPRINTし、それ以外の文字列は通常の色で普通にPRINTする
;ARGS "W"=PRINTSWで表示。"L"=PRINTSLで表示。"W"でも"L"でもなければPRINTSで表示
@K2_HPH_PRINT,対象文字列,行書式
#DIMS 対象文字列
#DIMS 行書式
#DIM 出力文字数
#DIM HPHインデクス値

DO
	HPHインデクス値 = STRFIND(対象文字列, "HPH")
	
	;対象文字列がHPHで始まらないが後ろにHPHがある場合
	IF HPHインデクス値 >= 1
		;HPHの直前まで文字表示
		LOCALS = %SUBSTRING(対象文字列, 0, HPHインデクス値)%
		PRINTS @"%LOCALS%"
		出力文字数 = STRLENS(LOCALS)
	;対象文字列がHPHで始まる場合
	ELSEIF HPHインデクス値 == 0
		LOCAL = GETCOLOR()
		SETCOLOR 0xFF69B4
		PRINTFORM %UNICODE(0x2764)%
		SETCOLOR LOCAL
		出力文字数 = 3
	;対象文字列中にもはやHPHが含まれない場合
	ELSE
		;ここで行書式による分岐
		SELECTCASE 行書式
		CASE "W"
			PRINTSW @"%対象文字列%"
		CASE "L"
			PRINTSL @"%対象文字列%"
		CASEELSE
			PRINTS @"%対象文字列%"
		ENDSELECT
		BREAK
	ENDIF

	;表示済みの文字分を削除して対象文字列を更新する
	対象文字列 = %SUBSTRING(対象文字列, 出力文字数, -1)%
	
LOOP 1


;### 行為を容認してくれるか判定用式中関数 ################
;意識のある状態で行為を怒らず受け入れていれば１を返す。そうでなければ０
;ARG=キャラ番号
@K2_CONSCIOUS_AND_TOLERANT(ARG)
#FUNCTION
#DIM 反発系パラメータ上昇値

SIF ARG == 0
	ARG = TARGET
	
SIF !SHIRAHU(ARG) || CFLAG:ARG:ブチギレ || TCVAR:ARG:無理矢理
	RETURNF 0
	
反発系パラメータ上昇値 = CUP:ARG:反感 + CUP:ARG:不快

SIF MARK:ARG:反発刻印 == 0 && 反発系パラメータ上昇値 >= 500
	RETURNF 0
SIF MARK:ARG:反発刻印 == 1 && 反発系パラメータ上昇値 >= 1001
	RETURNF 0
SIF MARK:ARG:反発刻印 == 2 && 反発系パラメータ上昇値 >= 2001
	RETURNF 0
SIF MARK:ARG:反発刻印 >= 3
	RETURNF 0

RETURNF 1


;### 大きな体格差の有無判定用式中関数 ################
;対象キャラと主人との体型差によって返り値が決まる
;比較して主人が大きすぎると１、主人が非常に大きすぎると２、主人が小さすぎると－１、主人が非常に小さすぎると－２
;ARG=キャラ番号
@K2_EXCESSIVE_STATURE_GAP(ARG)
#FUNCTION

SIF ARG == 0
	ARG = TARGET

IF TALENT:MASTER:体型 <= -5
	LOCAL = -8
ELSE
	LOCAL = TALENT:MASTER:体型
ENDIF

IF TALENT:ARG:体型 <= -5
	LOCAL:1 = -8
ELSE
	LOCAL:1 = TALENT:ARG:体型
ENDIF

SELECTCASE LOCAL - LOCAL:1
CASE IS > 5
	RETURNF 2
CASE 5
	RETURNF 1
CASE -5
	RETURNF -1
CASE IS < -5
	RETURNF -2
CASEELSE
	RETURNF 0
ENDSELECT


;### うふふ時基本状態判定用式中関数 ################
;K2_CONSCIOUS_AND_TOLERANTに無知とK2_EXCESSIVE_STATURE_GAPの条件を加えたもの
;ARG=キャラ番号
@K2_BASIC_CONDITION_IN_MATING(ARG)
#FUNCTION

SIF ARG == 0
	ARG = TARGET

SIF !K2_CONSCIOUS_AND_TOLERANT(ARG)
	RETURNF 0

SIF TALENT:ARG:無知
	RETURNF 0
	
SIF K2_EXCESSIVE_STATURE_GAP(ARG)
	RETURNF 0
	
RETURNF 1


;### 快感に積極的か判定用式中間数 ################
;返り値１は積極的、０は消極的
;ARG=キャラ番号
@K2_SEXUAL_FRANKNESS(ARG)
#FUNCTION

SIF ARG == 0
	ARG = TARGET

IF MARK:ARG:快楽刻印 >= 3 || TALENT:ARG:快感応答 >= 0 || TALENT:ARG:ヤリマン
	RETURNF 1
ELSE
	RETURNF 0
ENDIF


;### 表現バリエーション用式中関数 ################
@K2_EXPRESSION_VARIATION(ARGS)
#FUNCTIONS

VARSET LOCALS

IF ARGS == "SPERM"
	LOCALS = %TEXTR("白濁液/ザーメン/精液/子種汁")%

ELSEIF ARGS == "VULVA"
	LOCALS = %TEXTR("秘処/秘裂/陰門/女陰")%

ELSEIF ARGS == "ANUS"
	LOCALS = %TEXTR("尻の穴/肛門/アヌス")%

ELSEIF ARGS == "CROTCH"
	LOCALS = %TEXTR("股間/局部/股")%

ELSEIF ARGS == "PENIS"
	LOCALS = %TEXTR("ペニス/陰茎/肉棒/イチモツ")%

ELSE
	LOCALS = ※K2_EXPRESSION_VARIATION()の文字列引数が不正※

ENDIF

RETURNF LOCALS


;### 乳房描写用式中関数 ################
;ARG=キャラ番号
@K2_BREASTS_DESCRIPTION(ARG)
#FUNCTIONS

SIF ARG == 0
	ARG = TARGET

SELECTCASE TALENT:ARG:バストサイズ
CASE IS >= ( TALENT:ARG:体型 == -2 ?3 #2 )
	LOCALS = %TEXTR("はちきれんばかりの/目を見張る大きさの/どっしりと重量感のある/ボリューミーな/とびきり大きな/バインバインの/下品なほど大きい")%
CASE TALENT:ARG:体型 == -2 ?2 #1
	LOCALS = %TEXTR("豊満な/大きめの/豊かな/ふくよかな")%
CASE TALENT:ARG:体型 == -2 ?1 #0
	LOCALS = %TEXTR("やわらかな/女らしい膨らみのある/みずみずしい/＿")%
CASE TALENT:ARG:体型 == -2 ?0 #-1
	LOCALS = %TEXTR("ひかえめな/小ぶりな/小さめの")%
CASEELSE
	LOCALS = %TEXTR("薄い/なだらかな/肉のついていない")%
ENDSELECT
SELECTCASE TALENT:ARG:バストサイズ
CASE IS >= ( TALENT:ARG:体型 == -2 ?3 #2 )
	LOCALS += @"%TEXTR("乳/乳房/おっぱい/爆乳")%"
CASE TALENT:ARG:体型 == -2 ?2 #1
	LOCALS += @"%TEXTR("乳/乳房/おっぱい")%"
CASE TALENT:ARG:体型 == -2 ?1 #0
	LOCALS += @"%TEXTR("乳/乳房/おっぱい")%"
CASE TALENT:ARG:体型 == -2 ?0 #-1
	LOCALS += @"%TEXTR("胸/乳/おっぱい")%"
CASEELSE
	LOCALS += @"%TEXTR("胸/胸部")%"
ENDSELECT

RETURNF LOCALS


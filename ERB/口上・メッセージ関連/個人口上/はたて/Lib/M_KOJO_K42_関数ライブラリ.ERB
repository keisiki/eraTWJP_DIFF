;
;### 関数 ###################################
;以下の関数は「映姫様口上」内の「M_KOJO_K30_関数・お説教ライブラリ」を拝借したものです
;はたて口上作者が作成したものではないのでご注意ください
;同ライブラリの利用に関しては、映姫様口上の作者様のライセンスに準じます

;拝借ここから
;==================================================

;==================================================

;==================================================
;MASTERの恋人がその場に居るかどうか返す式中関数
;これは力技でなんとか実装できたっぽいので、使わなくても問題ない気がする（小町口上作者）
;でも絶対こっちのがスマート
;=-1 はたてが恋人 =0 元から恋人なし =1 その場に居ない =2 恋人と同席
@K42_FIND_LOVER()
#FUNCTION
SIF TALENT:MASTER:恋人 == 0
	RETURNF 0
SIF TALENT:恋人
	RETURNF -1
LOCAL = TALENT:MASTER:恋人
SIF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
	RETURNF 2
RETURNF 1
;==================================================


;==================================================
;同部屋のキャラを拾う式中関数。探す順位は[恋人]持ち→知り合い→TARGET内ランダム
;アリス口上の関数お借りして平文化した映姫口上の関数を拝借しました。つまり丸パクり
;丸コピする以外の技術がないので先人たちの知恵に感謝をし続けるしかない
@K42_FIND_AROUND()
#FUNCTION
#DIM 知り合い, 6 = 29, 65, 51, 49, 38, 1
#DIM 総人数
#DIM 抽選
#LOCALSIZE 200
;はたての知り合いは6人,文>椛>にとり>>>古明地姉妹>霊夢の順に(勝手に)設定
;一応理由としては
;文→原作でしっかり絡みがある
;椛→原作で名前を言っている、文と比べて邪険にされていない、身内と言及している
;にとり→名前は出てないけど河童の技術力は称賛してるし、ガラケー修理したのがにとりだといいなあという勝手な願望
;古明地姉妹→サトリ（種）に対してまったく嫌悪感抱いてないどころか心を読んでとかはたてちゃん大天使
;霊夢→文が興味を持つのも分かると言っていた（ような覚えがある）
VARSET LOCAL
抽選 = 0
総人数 = GET_TARGETNUM() 
;部屋に一人なら当然0
SIF 総人数 == 1
	RETURNF 0
;[恋人]持ちがいたら優先してIDを返す
SIF K42_FIND_LOVER() == 2
	RETURNF TALENT:MASTER:恋人
;知り合い優先度順に現在位置を調べMASTERと同じならIDを返す
FOR COUNT, 0, 6
	SIF CFLAG:(知り合い:COUNT):現在位置 == CFLAG:MASTER:現在位置
		RETURNF 知り合い:COUNT
NEXT
;知り合いがいなければ同室のTARGET_ID全部を拾う
FOR COUNT,1, 総人数 + 1
	;地獄の断頭台withジェロニモ状態を防ぐため口上主はスキップ
	SIF TARGET:COUNT == TARGET:0
		CONTINUE
	;MASTERとはたてちゃんの現在位置にいなければスキップ
	SIF (CFLAG:(TARGET:COUNT):現在位置 != CFLAG:MASTER:現在位置) || (CFLAG:(TARGET:COUNT):現在位置 != CFLAG:42:現在位置)
		CONTINUE
	;配列のカウントは別で取りながら代入
	LOCAL:抽選 = TARGET:COUNT
	抽選 ++
NEXT

SIF 抽選 < 1
	RETURNF 0
;抽選してIDを決定
RETURNF LOCAL:(RAND(抽選))
;==================================================


;==================================================
;酔っぱらい度を数字で返す式中関数
;=0 素面 =1 ほろ酔い =2 酔いどれ 3=ぐでぐで
@K42_DRUNK()
#FUNCTION
SIF BASE:42:酒気 <= 2
	RETURNF 0
SELECTCASE BASE:42:酒気
	;ぐでぐで
	CASE IS > (MAXBASE:42:酒気 / 10) * 8
		RETURNF 3
	;酔いどれ
	CASE IS > (MAXBASE:42:酒気 / 10) * 5
		RETURNF 2
	;ほろ酔い
	CASE IS <= (MAXBASE:42:酒気 / 10) * 5
		RETURNF 1
ENDSELECT
;==================================================
;名前を教えるイベント（関数）
;K39ひねくれナズー口上様より拝借
;==================================================

@NAME_SET_K42
PRINTL 名前を入力してください(直接入力するか、ボタンで選択してください)
IF NAME:MASTER != "あなた"
	LOCALS = %NAME:MASTER%
ELSE
	LOCALS = %CALLNAME:MASTER%
ENDIF
PRINTBUTTON @"[名前(%LOCALS%)を名乗る]", @"%LOCALS%"
PRINTL 　
;名前を入力するループ
$INPUT_LOOP_ONAMAE
INPUTS
; 行頭行尾の空白文字削除
LOCALS = %RESULTS%
REPLACE LOCALS, "\(\^\\s\+\|\\s\+\$)", ""
;10文字以上は受け付けない
IF STRLENS(LOCALS) > 10
	CLEARLINE 1
	REUSELASTLINE 「ねぇ、それ本当にあんたの名前？　嘘ついてない？ 長すぎー」
	GOTO INPUT_LOOP_ONAMAE
;空白は受け付けない
ELSEIF RESULTS == ""
	PRINTL 名前を入力してください
	GOTO INPUT_LOOP_ONAMAE
ELSE
	; 同姓同名
	IF FLAG:なりきり != 42 && STRCOUNT(LOCALS, "\(姫海棠\)\*\\s\*はたて")
		CLEARLINE 1
		REUSELASTLINE 「え、まさか同じ名前？　そんなわけないよね」
		GOTO INPUT_LOOP_ONAMAE
	; うんそうだね多々良勝五郎とか教える人もいるかもしれないもんね
	ELSEIF (FLAG:なりきり != 29 && STRCOUNT(LOCALS, "\(射命丸\)\*\\s\*文")) || (FLAG:なりきり != 51 && STRCOUNT(LOCALS, "\(河城\)\*\\s\*にとり")) || (FLAG:なりきり != 65 && STRCOUNT(LOCALS, "\(犬走\)\*\\s\*椛"))
		CLEARLINE 1
		REUSELASTLINE 「え、あんた、人間かと思ってたけど実は妖怪？　そんなわけないよね」
		GOTO INPUT_LOOP_ONAMAE
	; 直接入力ならいけると思ったか？弾くぜ！
	ELSEIF FIRSTTIME("U42-1") && ABL:親密 < 4 && STRCOUNT(LOCALS, "\(お\(\(兄\|にい\)\|\(姉\|ねえ\)\)\(さん\|ちゃん\)\)\$")
		CLEARLINE 1
		REUSELASTLINE 「そんなわけないじゃん」
		GOTO INPUT_LOOP_ONAMAE
	ELSEIF FIRSTTIME("U42-1") && ABL:親密 < 7 && STRCOUNT(LOCALS, "\(お\(\(兄\|にい\)\|\(姉\|ねえ\)\)\(様\|さま\)\)\$")
		CLEARLINE 1
		REUSELASTLINE 「はぁ？」
		GOTO INPUT_LOOP_ONAMAE
	ELSEIF FIRSTTIME("U42-1") && (ABL:従順 < 7 || ABL:奉仕精神 < 7) && STRCOUNT(LOCALS, "\(\(御\|ご\)\(主人\|しゅじん\)\(様\|さま\)\)$")
		CLEARLINE 1
		REUSELASTLINE 「はぁ？　何その発想、キモーい」
		GOTO INPUT_LOOP_ONAMAE
	ELSEIF  FIRSTTIME("U42-1") && (!TALENT:恋人 || !TALENT:恋慕) && STRCOUNT(LOCALS, "\(\(旦那\|だんな\)\(様\|さま\)\)\$")
		CLEARLINE 1
		REUSELASTLINE 「はぁ？　何その発想、キモーい」
		GOTO INPUT_LOOP_ONAMAE
	ELSE
		PRINTFORML 「%RESULTS%…ね」
	ENDIF
	PRINTL [0] はい
	PRINTL [1] いいえ
	;入力した名前を確定するループ
	$INPUT_LOOP_KAKUTEI_1
	INPUT
	IF RESULT == 0
		;あなたの名前を%MASTERNAME:42%に格納
		MASTERNAME:42 = %RESULTS%
		;K42NAME = %RESULTS%/不具合出たので封印
		PRINTFORML 
	ELSEIF RESULT == 1
		PRINTL 名前を入力してください
		GOTO INPUT_LOOP_ONAMAE
	ELSE
		PRINTL 名前を入力してください
		GOTO INPUT_LOOP_ONAMAE
	ENDIF
ENDIF
;拝借ここまで

;==================================================
;ここから一応自作関数
;==================================================
;本体関数を加工して自作関数に
;==================================================
;勃起状態を数字で返す式中関数
;=0 通常 =1 むっくり =2 ビンビン =3 Erection!!
@K42_BOKKI()
#FUNCTION
#DIM CHARA
IF BASE:CHARA:勃起 == MAXBASE:CHARA:勃起
	RETURNF 3
ELSEIF BASE:CHARA:勃起 >= 1000
	RETURNF 2
ELSEIF BASE:CHARA:勃起 >= 500
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

;==================================================
;会えなかった日を算出する関数
;==================================================
@K42_AENAI
#DIM SAIGO
SAIGO = CFLAG:1013
IF (TALENT:恋慕 || TALENT:恋人) && SAIGO > 6
	PRINTFORML 「やっと、会えた」
	PRINTFORML はたては、%CALLNAME:MASTER%の姿を見つけると、彼女にしては控えめに、首元にそっと抱きついてきた
	PRINTW 
	PRINTFORML 「{SAIGO}日ぶりだもん。ずっと、会えなくて寂しかったよ」
	PRINTFORML いつも陽気で元気なはたての声は、聞いたことがないほどとても弱弱しかった
	PRINTW 
ENDIF
RETURN 0
;==================================================
;交際を始めてからの日数を表示する関数
;==================================================
@K42_KOUSAI
#DIM KOIBIT
KOIBIT = DAY:0 - CFLAG:1004
IF KOIBIT % 124 == 0 && KOIBIT
	PRINTFORML 「今日で私たちが付き合い始めてから{KOIBIT / 124}年になるのね」
	SIF CFLAG:MASTER:初期位置 == CFLAG:42:初期位置
		PRINTW 「同じ屋根の下で暮らしたり」
	SIF EXP:出産経験 > 0
		PRINTW 「家族が増えたり」
	PRINTFORML 「色々あったなぁ」
	SIF TALENT:処女 == 1
		PRINTW あ、でもまだ私の初めて、もらってくれないのよね。どーいうことなの？」
	PRINTFORML 「これからもよろしくね、%MASTERNAME:42%。」
	PRINTW 
ENDIF
RETURN 0
;==================================================
;個人情報の「キャラ紹介」の文章を書き換える関数
;==================================================
;@CHARA_INFO_KOJO_K42()
;個人情報の「キャラ紹介」の文章を書き換える
;これがあれば「CSTR」の20番以降(20～39)に文字列を書き込むだけで書き換わるはず
;早苗口上、諏訪子口上より引用、改変
;-----------------------------------------------------------------------------------------------
@CHARA_INFO_KOJO_K42()
;===============================================================================================
;記述例
;このコメントアウトを外すと「CSTR」の20番以降に文字列を書き込むだけで書き換わるようになる
;FOR LOCAL,20,40
;	SIF CSTR:42:(LOCAL) != ""
;		PRINTFORML %CSTR:31:(LOCAL)%
;NEXT
;RETURN 1
;…とまあ、こんな感じでCSTR20番以降を使えばいいはず
;===============================================================================================
;本口上は、書き換わる文章が多いのでCSTRへの保存をせずに直接書き込む
;現時点で下の2行が書き込まれている
;PRINTL □ キャラ紹介 □---------------------------------------------------------------------
;PRINTL 

;##################################################################################
;①「～今どきの念写記者～　●種族：鴉天狗　●能力：念写をする程度の能力」をプリント
;##################################################################################
CALL M_KOJO_COLOR_K42
PRINTFORML %CSVCSTR(42, 10)%
RESETCOLOR
PRINTL 

;##################################################################################
;②キャラ紹介文を書き込み
;##################################################################################
;結婚イベント
IF CFLAG:K42C_結婚 >= 1
	PRINTFORML %CALLNAME:MASTER%と種族の垣根を越えて結ばれた
	PRINTFORML 左手の薬指には結婚指輪がはめられている
	PRINTFORML %CALLNAME:MASTER%への愛は疑うべくもない
	PRINTL 
ELSE
	IF TALENT:42:恋慕 && TALENT:42:恋人
		PRINTFORML 今どきの烏天狗は好きになったら全力だ
		PRINTFORML %CALLNAME:MASTER%と夫婦になることを内心望んでいるようだが…？
		PRINTL 
		IF EXP:42:愛情経験 >= 200
			PRINTFORML %CALLNAME:MASTER%との愛情は十分に満たされている
		ELSE
			PRINTFORML まだまだ%CALLNAME:MASTER%と愛を深めたいと思っているようだ
		ENDIF
		
		IF EXP:42:デート経験 >= 100
			PRINTFORML デートを何度も重ね、思い出をたくさんつくった
		ELSE
			PRINTFORML まだまだ%CALLNAME:MASTER%とデートで思い出づくりをしたいようだ
		ENDIF
		
		IF CFLAG:42:好感度 >= 5000
			PRINTFORML %CALLNAME:MASTER%が好きなことを公言して憚らない
		ELSE
			PRINTFORML もっと%CALLNAME:MASTER%のことを好きになりたいようだ
		ENDIF
	ENDIF
ENDIF

;妊娠願望イベント
{
	IF DAY:2 == 4 && GROUPMATCH(DAY:3, 4, 14, 24) && ESTRUS_CYCLE(TARGET) >= 70 && !TALENT:妊娠願望 && (TALENT:恋慕 || TALENT:恋人)
	 && (TALENT:MASTER:性別 == 2 || TALENT:MASTER:性別 == 3) && CFLAG:好感度 >= 10000 && EXP:愛情経験 >= 500 && !TALENT:妊娠 && !TALENT:育児中
 }
		PRINTFORML %CALLNAME:MASTER%との子供がほしいという気持ちが、これまでにないほど強くなっているようだ
	ELSE
	ENDIF

;時姦バレイベント
IF CFLAG:42:1413 == 1
	PRINTL 
	PRINTFORML 凍り付いた時の中で犯され続けた肢体は、完全に%CALLNAME:MASTER%に屈してしまった
	IF CFLAG:K42C_結婚 >= 1
		PRINTFORML それでも、愛する夫からの行為ならばと、すべてを受け入れているようだ
	ELSEIF TALENT:恋慕 || TALENT:恋人
		PRINTFORML それでも、愛する人からの行為と分かり、戸惑いながらも受け入れているようだ
	ELSEIF TALENT:愛欲 || TALENT:セフレ
		PRINTFORML 新たな刺激になると、むしろ歓迎しているようだ
	ELSE
		PRINTFORML 勝手に身体を嬲られる嫌悪感と快感で、複雑な心境のようだ
	ENDIF
	PRINTL 
ELSEIF CFLAG:42:1414 == 1
	PRINTL 
	PRINTFORML 気付かぬ間に絶頂させられる不可解な現象…
	PRINTFORML その時、必ず%CALLNAME:MASTER%が目の前にいた
	PRINTFORML %CALLNAME:MASTER%が何か関与しているのか？
	PRINTFORML はたては他に同じ被害者がいないか、取材を始めることにした
	PRINTL 
	
	IF EXP:42:無自覚絶頂経験 >= 500
		PRINTFORML 自分をいいように嬲っていることで、犯人は完全に油断していると信じているようだ
		PRINTFORML 一方で、取り返しのつかないところまで身体が調教されきっていることに気付いていない
	ELSEIF EXP:42:無自覚絶頂経験 >= 250
		PRINTFORML 犯人はうまく食らいついたと思っている。もう少し油断させねばと思っているようだ
		PRINTFORML 一方で、身体は屈服しそうになっていることに目をそらしている
	ELSE
		PRINTFORML 油断しているフリをすれば、自分を餌に犯人と接触できると思っているようだ
		PRINTFORML 決して快楽のためではないと自分に言い聞かせている
	ENDIF
	
	IF CFLAG:42:1015 >= 50
		PRINTFORML 度重なる下着被害は%CALLNAME:MASTER%の仕業だと信じて疑っていないようだ
	ELSEIF CFLAG:42:1015 >= 25
		PRINTFORML 度重なる下着被害は%CALLNAME:MASTER%の仕業ではないかと疑念を深めている
	ELSE
		PRINTFORML 下着被害は%CALLNAME:MASTER%によるものか、自信がないようだ
	ENDIF
	
	IF CFLAG:42:1011 >= 100
		PRINTFORML 被害にあった少女たちから、数々の証言を得た
		PRINTFORML 取材はもう十分と感じているようだ
	ELSEIF CFLAG:42:1011 >= 50
		PRINTFORML 少女たちの被害は思ったより深刻なようだ
		PRINTFORML 取材も正念場だと考えている
	ELSEIF CFLAG:42:1011 >= 1
		PRINTFORML 被害者は他にもいた
		PRINTFORML まだ確信を得るまでには至っていない。もっと取材を続けなければと考えている
	ELSE
		PRINTFORML 取材はまったく進んでいないようだ…
	ENDIF
ENDIF

;裏花果子念報イベント
	IF TALENT:セフレ || TALENT:愛欲
		IF CFLAG:42:1008 >= 100
			PRINTFORML 取材を通して、新聞の方向性が定まった。幻想郷初の風俗紙を刊行することにした
		ELSEIF CFLAG:42:1008 >= 50
			PRINTFORML 取材を通して、自分の書きたい新聞が何か、おぼろげなイメージがつかめてきたようだ
		ELSE
			PRINTFORML 新しい新聞の形を模索しているようだが、イメージがわかないようだ
		ENDIF
	ENDIF
PRINTL 
RETURN 1

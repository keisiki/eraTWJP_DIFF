;-------------------------------------------------
;関数名:TINKO
;概　要:二番煎じ上等
;引　数:ARG…キャラ登録番号
;戻り値:
;備　考:文中関数
;使用例:%TINKO(PLAYER)%
;-------------------------------------------------
@TINKO(ARG)
#FUNCTIONS
VARSET LOCALS
SIF !ARG
	ARG = MASTER
;地の文と辻褄合わせが必要なのでとりあえず汚れ描写は保留
[SKIPSTART]
IF GETBIT(STAIN:ARG:2,64)
	LOCALS = 破瓜血
	LOCAL ++
ENDIF
IF GETBIT(STAIN:ARG:2,8)
	SIF LOCAL
		LOCALS += "と"
	LOCALS += "腸液"
	LOCAL ++
ENDIF
IF GETBIT(STAIN:ARG:2,4)
	SIF LOCAL
		LOCALS += "と"
	LOCALS += "精液"
	LOCAL ++
ENDIF
IF GETBIT(STAIN:ARG:2,1)
	SIF LOCAL
		LOCALS += "と"
	LOCALS += "愛液"
	LOCAL ++
ENDIF
SIF LOCAL
	LOCALS += "にまみれた"
[SKIPEND]
;確率で形容詞がつく
IF !RAND:4 && ARG == MASTER && TCVAR:ARG:100 != TARGET && TCVAR:ARG:101 != TARGET
	IF BASE:ARG:勃起 > 1000 
		SELECTCASE RAND:4
			CASE 0
				LOCALS += "猛々しく反り返る"
			CASE 1
				LOCALS += "先走りをダラダラと滴らせる"
			CASE 2
				LOCALS += "血管を浮かせた"
			CASE 3
				LOCALS += "濃厚な精臭を放つ"
		ENDSELECT
	ELSEIF BASE:ARG:勃起 > 500
		SELECTCASE RAND:3
			CASE 0
				LOCALS += "むくむくと頭をもたげる"
			CASE 1
				LOCALS += "固さを増しつつある"
			CASE 2
				LOCALS += "半勃ちの"
		ENDSELECT
	ELSE
		LOCALS += "まだ柔らかい"	
	ENDIF
ELSEIF !RAND:3
	SELECTCASE TALENT:ARG:形状
		CASE 1
			;未熟
			SELECTCASE RAND:3
				CASE 0
					LOCALS += "可愛らしい"
				CASE 1
					LOCALS += "子供の様な"
				CASE 2
					LOCALS += "剥けきっていない"
			ENDSELECT
		CASE 2
			;粗末
			LOCALS += "貧相な"
		CASE 3
			;普通
			LOCALS += "逞しい"
		CASE 4
			;光る
			SELECTCASE RAND:3
				CASE 0
					LOCALS += "ぼんやりと光を放つ"
				CASE 1
					LOCALS += "チカチカと点滅する"
				CASE 2
					LOCALS += "まばゆく輝く"
			ENDSELECT
		CASE 5
			SELECTCASE RAND:3
				CASE 0
					LOCALS += "グロテスクな"
				CASE 1
					LOCALS += "凶悪な"
				CASE 2
					LOCALS += "イボだらけの"
			ENDSELECT
		CASE 6
			;ごんぶと
			LOCALS += "長大な"
		CASE 10
			;馬並
			LOCALS += "馬のような"
	ENDSELECT
ELSEIF ARG == MASTER && TARGET
	IF !RAND:3
		IF TALENT:TARGET:妊娠
			LOCALS += "彼女を孕ませた"
		ELSEIF STRFIND(CSTR:TARGET:処女喪失履歴, "ペニス") > 0
			LOCALS += "彼女を女にした"
			
		ENDIF
	ELSEIF !RAND:2 && 妊娠可否(TARGET) && CFLAG:TARGET:生理周期 == 7 && TEQUIP:TARGET:50 == MASTER
		LOCALS += "孕ませる気満々の"
	ENDIF
ENDIF
SELECTCASE RAND:12
	CASE 0
		LOCALS += "雄のシンボル"
	CASE 1
		LOCALS += "男根"
	CASE 2
		LOCALS += "怒張"
	CASE 3
		LOCALS += "陰茎"
	CASE 4
		LOCALS += "肉棒"
	CASE 5
		LOCALS += "剛直"
	CASEELSE
		LOCALS += "ペニス"
ENDSELECT
SIF !TALENT:ARG:2 & 2
	LOCALS = "ペニスバンド"
RETURNF LOCALS


@TINKO_DECIDE(ARG)
SIF !HAS_PENIS(ARG)
	RETURN
$LOOP
PRINTFORML %CALLNAME:ARG%の男性器形状を設定してください。
PRINTL [1] 未成熟
PRINTL [2] 粗チン
PRINTL [3] 普通
PRINTL [4] 発光する
PRINTL [5] イボ付き
PRINTL [6] ごんぶと
;PRINTL [7] 小ぶり
;PRINTL [8] ドリル
;PRINTL [9] パイプカット済み
INPUT
IF RESULT == 5
	PRINTW そんなの入れられても痛いだけで気持ちよくないですよ？
	PRINTW 単なる自己満足じゃないんですか？相手を思いやる気持ちを忘れていませんか？
	PRINTL [0] じゃあやめとく
	PRINTL [1] 余計なお世話だ
	INPUT
	IF RESULT == 1
		PRINTW この鬼畜！
		PRINTFORMW %CALLNAME:ARG%の男性器にイボが生えました
		TALENT:ARG:形状 = 5
	ELSE
		GOTO LOOP
	ENDIF
ELSEIF RESULT == 4
	PRINTL 意味わかんないんですけど
	PRINTL [0] ごめん間違えた
	PRINTL [1] ホタルイカと同じ原理で光るんです
	INPUT
	IF RESULT == 1
		PRINTW そうですか。
		PRINTFORMW %CALLNAME:ARG%の男性器が発光するようになりました
		TALENT:ARG:形状 = 4
	ELSE
		GOTO LOOP
	ENDIF
ELSEIF RESULT == 6
	PRINTL デカけりゃいいと安易に考えてはいませんか？大きいと血液が行き渡りにくくなる分柔らかくなります。
	PRINTL それに大事なのはサイズの大小ではなく相手の穴との相性です。サイズが合わないと互いに苦しい思いをするだけですよ。
	PRINTL [0] ロリXXXに突っ込みたいからやめとく
	PRINTL [1] 私は自分が苦しい思いをしてでも苦痛に歪む少女の顔が見たいんです
	INPUT
	IF RESULT == 1
		PRINTW なんてこった。
		PRINTFORMW %CALLNAME:ARG%の男性器が大きくなりました
		TALENT:ARG:形状 = 6
	ELSE
		GOTO LOOP
	ENDIF
ELSEIF RESULT == 3
	PRINTFORMW %CALLNAME:ARG%の男性器は至って普通の形です
	TALENT:ARG:形状 = RESULT
ELSEIF RESULT == 2
	PRINTFORMW %CALLNAME:ARG%の男性器が見るも哀れな姿になりました
	TALENT:ARG:形状 = RESULT
ELSEIF RESULT == 1
	PRINTFORMW %CALLNAME:ARG%の男性器は可愛らしいサイズになりました
	TALENT:ARG:形状 = RESULT
ENDIF
@TINKO_VSOURCE(ARG)

SELECTCASE TALENT:(TEQUIP:ARG:50):形状
	CASE 5
	;イボ付き
		CUP:ARG:快Ｖ /= 4
	CASE 2
	;粗末
		CUP:ARG:快Ｖ /= 2
	CASE 10
	;馬並
		CUP:ARG:快Ｖ *= 2
ENDSELECT
SIF 挿入不可(ARG) == -1 && ABL:ARG:膣 < 2
	CUP:ARG:快Ｖ = CUP:ARG:快Ｖ * 4 / 5
SIF 挿入不可(ARG) == -2 && ABL:ARG:膣 < 3
	CUP:ARG:快Ｖ /= 2
@TINKO_ASOURCE(ARG)

SELECTCASE TALENT:(TEQUIP:ARG:51):形状
	CASE 5
	;イボ付き
		CUP:ARG:快Ａ /= 4
	CASE 2
	;粗末
		CUP:ARG:快Ａ /= 2
	CASE 10
	;馬並
		CUP:ARG:快Ａ *= 2
SIF 挿入不可(ARG) == -1 && ABL:ARG:アナル < 2
	CUP:ARG:快Ａ = CUP:ARG:快Ｖ * 4 / 5
SIF 挿入不可(ARG) == -2 && ABL:ARG:アナル < 3
	CUP:ARG:快Ａ /= 2
ENDSELECT

@TINKO_SIMPLE(ARG)
#FUNCTIONS
VARSET LOCALS
SELECTCASE TALENT:ARG:形状
	CASE 1
		LOCALS = 幼い
	CASE 2
		LOCALS = 貧相な
	CASE 3
		LOCALS = 逞しい
	CASE 4
		LOCALS = 光る
	CASE 5
		LOCALS = 凶悪な
	CASE 6
		LOCALS = 野太い
	CASE 10
		LOCALS = 馬並みの
ENDSELECT
LOCALS += "ペニス"
RETURNF LOCALS
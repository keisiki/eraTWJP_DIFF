;射精確定後の処理
@EJAC_CHECK_AFTER(キャラ登録番号)
#DIM キャラ登録番号
#DIM 射精回数
#DIM 過剰射精

VARSET 射精回数
VARSET 過剰射精
;キャラ登録番号 射精する人
IF !TCVAR:キャラ登録番号:イきそう
	IF BASE:キャラ登録番号:射精 > MAXBASE:キャラ登録番号:射精
		IF BASE:キャラ登録番号:勃起 < 1000
			BASE:キャラ登録番号:射精 = MAXBASE:キャラ登録番号:射精 - 1
		ELSE
			TCVAR:キャラ登録番号:イきそう = 1
		ENDIF
	ENDIF
	RETURN 0
ELSEIF TEQUIP:キャラ登録番号:焦らしモード && キャラ登録番号 != MASTER
	TCVAR:キャラ登録番号:焦らし回数 += 2
	RETURN 0
ENDIF

;射精(絶頂)回数
射精回数 = MIN(BASE:キャラ登録番号:射精 / MAXBASE:キャラ登録番号:射精, (100 + BASE:キャラ登録番号:精力) / 100)


IF NOWEX:キャラ登録番号:射精量 > BASE:キャラ登録番号:精力
	NOWEX:キャラ登録番号:射精量 = (NOWEX:キャラ登録番号:射精量 - BASE:キャラ登録番号:精力) / 2 + BASE:キャラ登録番号:精力
	過剰射精 = NOWEX:キャラ登録番号:射精量 - BASE:キャラ登録番号:精力
ENDIF

NOWEX:キャラ登録番号:射精量 =BASE:キャラ登録番号:射精 * 10 / (ABL:キャラ登録番号:Ｃ感覚 / 2 + 10) / 100

NOWEX:キャラ登録番号:射精量 = NOWEX:キャラ登録番号:射精量 * 10 / (ABL:キャラ登録番号:Ｃ感覚 / 2 + 10)

IF 過剰射精
	IF TFLAG:ポイズンボディ
		DOWNBASE:キャラ登録番号:体力 += 過剰射精 * 5
	ELSE
		DOWNBASE:キャラ登録番号:体力 += 過剰射精 * 3
	ENDIF
ENDIF

BASE:キャラ登録番号:精力 = MAX(0, BASE:キャラ登録番号:精力 - NOWEX:キャラ登録番号:射精量)
TCVAR:キャラ登録番号:イきそう = 0
STAIN:キャラ登録番号:Ｐ |= 4

;BASEの処理
BASE:キャラ登録番号:射精 = 0

CSTR:キャラ登録番号:射精後処理用 = %CSTR:キャラ登録番号:射精判定用%
TCVAR:キャラ登録番号:射精した箇所フラグ = TCVAR:キャラ登録番号:射精箇所

;異常経験、射精経験、精液経験
;女性器がある場合？
SIF !EXP:キャラ登録番号:射精経験 && TALENT:キャラ登録番号:性別 & 1
	EXP:キャラ登録番号:異常経験 += 1

EXP:キャラ登録番号:絶頂経験 ++ 
EXP:キャラ登録番号:射精経験 += 射精回数

NOWEX:キャラ登録番号:射精 += 射精回数
EX:キャラ登録番号:射精 += 射精回数
TCVAR:キャラ登録番号:焦らし回数 = 0
TEQUIP:キャラ登録番号:焦らしモード = 0
SIF AT_HOME(キャラ登録番号)
	YOGORE:(CFLAG:キャラ登録番号:現在位置) += 射精回数 * 50

IF TFLAG:ポイズンボディ
	DOWNBASE:キャラ登録番号:体力 += LIMIT(NOWEX:キャラ登録番号:射精量 / 4, 30, 300)
	DOWNBASE:キャラ登録番号:気力 += LIMIT(NOWEX:キャラ登録番号:射精量 / 2, 40, 400)
ELSE
	DOWNBASE:キャラ登録番号:体力 += LIMIT(NOWEX:キャラ登録番号:射精量 / 8, 10, 100)
	DOWNBASE:キャラ登録番号:気力 += LIMIT(NOWEX:キャラ登録番号:射精量 / 4, 20, 200)
ENDIF

;MASTERでなければBASEやSOURCE等の処理を入れる
SIF キャラ登録番号 == MASTER
	RETURN 0

IF EXP:キャラ登録番号:射精経験 < EXPLV:1
	SOURCE:キャラ登録番号:露出 += 5000 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:屈従 += 2500 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:不潔 += 200 * (射精回数 + 1) / 2
ELSEIF EXP:キャラ登録番号:射精経験 < EXPLV:2
	SOURCE:キャラ登録番号:露出 += 2500 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:屈従 += 2000 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:不潔 += 150 * (射精回数 + 1) / 2
ELSEIF EXP:キャラ登録番号:射精経験 < EXPLV:3
	SOURCE:キャラ登録番号:露出 += 1000 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:屈従 += 800 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:不潔 += 100 * (射精回数 + 1) / 2
ELSEIF EXP:キャラ登録番号:射精経験 < EXPLV:4
	SOURCE:キャラ登録番号:露出 += 800 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:屈従 += 600 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:不潔 += 50 * (射精回数 + 1) / 2
ELSEIF EXP:キャラ登録番号:射精経験 < EXPLV:5
	SOURCE:キャラ登録番号:露出 += 400 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:屈従 += 400 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:不潔 += 10 * (射精回数 + 1) / 2
ELSE
	SOURCE:キャラ登録番号:露出 += 200 * (射精回数 + 1) / 2
	SOURCE:キャラ登録番号:屈従 += 250 * (射精回数 + 1) / 2
ENDIF

;-------------------------------------------------
;調教対象の射精チェック
;-------------------------------------------------
@TARGET_EJAC_CHECK(キャラ登録番号)
#DIM キャラ登録番号

;男性器がなければ帰る
SIF !HAS_PENIS(キャラ登録番号)
	RETURN 0

LOCAL = (CUP:キャラ登録番号:快Ｃ + CUP:キャラ登録番号:快Ｖ + CUP:キャラ登録番号:快Ａ + CUP:キャラ登録番号:快Ｂ) / 4

;自制心
SIF TALENT:キャラ登録番号:自制心
	LOCAL /= 2

;快感に素直
SIF TALENT:キャラ登録番号:快感応答 > 0
	TIMES LOCAL , 1.20

;快感の否定（淫乱、娼婦で無効）
SIF TALENT:キャラ登録番号:快感応答 < 0 && !TALENT:キャラ登録番号:淫乱
	TIMES LOCAL , 0.80

;媚薬
SIF (TCVAR:キャラ登録番号:媚薬 || IsInsenceValid(キャラ登録番号)) && キャラ登録番号 != MASTER
	LOCAL *= 2

;仙香玉兎
SIF IsInsenceValid(キャラ登録番号)
	LOCAL *= 2

;利尿剤
SIF TCVAR:キャラ登録番号:利尿剤
	LOCAL /= 2
;睡眠中
SIF CFLAG:キャラ登録番号:睡眠
	LOCAL /= 2
BASE:キャラ登録番号:射精 += LOCAL
;SIF BASE:キャラ登録番号:射精 > MAXBASE:キャラ登録番号:射精
;	BASE:キャラ登録番号:射精 = MAXBASE:キャラ登録番号:射精 - 1

;射精処理のキャラ別ターミナル
@SAMEN_SHOOT(射精したキャラ番号)
#DIM 射精したキャラ番号
#DIM 射精されたキャラ番号

FOR 射精されたキャラ番号, 0, CHARANUM
	SIF SAMEN_DIRECTION2(射精したキャラ番号, 射精されたキャラ番号)
		CALL SAMEN_SHOOT2(射精したキャラ番号, 射精されたキャラ番号)
NEXT

;部位別処理
@SAMEN_SHOOT2(SENDER, RECEIVER)
#DIM SENDER
#DIM RECEIVER
#DIM 無理矢理反感
#DIM 無理矢理恐怖
#DIM 信頼マイナス
#DIM 好感度マイナス
#DIM 欲求不満解消係数
#DIM 中毒ボーナス
#DIM モブ子用

中毒ボーナス = 0
IF SENDER == MASTER && CFLAG:RECEIVER:うふふ == 2
	SOURCE:RECEIVER:達成 += NOWEX:SENDER:射精 * 150
	SOURCE:RECEIVER:征服 += NOWEX:SENDER:射精 * 150
	SIF !(NOWEX:RECEIVER:Ｃ絶頂 + NOWEX:RECEIVER:Ｖ絶頂 + NOWEX:RECEIVER:Ａ絶頂 + NOWEX:RECEIVER:Ｂ絶頂 + NOWEX:RECEIVER:Ｍ絶頂)
		SOURCE:RECEIVER:征服 += NOWEX:SENDER:射精 * 100
ENDIF
IF SENDER == MASTER && TALENT:RECEIVER:行きずり == 1
	モブ子用 = MIN(NOWEX:SENDER:射精,3)
	SELECTCASE TCVAR:SENDER:射精した箇所フラグ
		CASE 1
			SIF ABL:RECEIVER:膣 < モブ子用
				CALL ChangeABL(RECEIVER, "膣", 1)
		CASE 2
			SIF ABL:RECEIVER:アナル < モブ子用
				CALL ChangeABL(RECEIVER, "アナル", 1)
		CASE 3
			SIF ABL:RECEIVER:指 < モブ子用
				CALL ChangeABL(RECEIVER, "指", 1)
		CASE 4
			SIF ABL:RECEIVER:舌 < モブ子用
				CALL ChangeABL(RECEIVER, "舌", 1)
		CASE 4
			SIF ABL:RECEIVER:胸 < モブ子用
				CALL ChangeABL(RECEIVER, "胸", 1)
	ENDSELECT
ENDIF

IF TEQUIP:SENDER:コンドーム
	;ゴムを外す
	TEQUIP:SENDER:コンドーム = 0
	IF RECEIVER
		SELECTCASE TCVAR:SENDER:射精した箇所フラグ
			CASE 1
				CFLAG:RECEIVER:溜まってる度 += ABL:RECEIVER:膣射中毒 * NOWEX:SENDER:射精 * 15
			CASE 2
				CFLAG:RECEIVER:溜まってる度 += ABL:RECEIVER:肛射中毒 * NOWEX:SENDER:射精 * 15
		ENDSELECT
	ENDIF
	;接触のリセット
	CALL 挿入解除(RECEIVER)
	TFLAG:ゴム内 = NOWEX:SENDER:射精量
	IF CFLAG:RECEIVER:自動ゴム && ITEM:コンドーム && SENDER == MASTER
		ITEM:コンドーム --
		TEQUIP:SENDER:コンドーム = 1
	ENDIF
ELSE
;TCVAR:12 射精箇所フラグ（1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手）
SELECTCASE TCVAR:SENDER:射精した箇所フラグ
	;膣内
	CASE 1
		EXP:RECEIVER:精液経験 += NOWEX:SENDER:射精 * 2
		EXP:RECEIVER:膣射経験 += NOWEX:SENDER:射精 * 2
		STAIN:RECEIVER:Ｖ |= 4
		STAIN:RECEIVER:膣内 |= 4
		TCVAR:RECEIVER:Vに中出しされたフラグ = 2
		TCVAR:RECEIVER:Vに中出ししたキャラの番号 = SENDER
		IF FLAG:時間停止 == 1
			CFLAG:RECEIVER:膣内射精 = 2
		ELSE
			CFLAG:RECEIVER:膣内射精 = 1
		ENDIF
		NOWEX:RECEIVER:膣内精液 = NOWEX:SENDER:射精量
		EX:RECEIVER:膣内精液 += NOWEX:RECEIVER:膣内精液
		欲求不満解消係数 = 1000 / MIN(100 + ABL:RECEIVER:膣射中毒 * 10,200)
		CFLAG:RECEIVER:溜まってる度 -= SQRT(NOWEX:RECEIVER:膣内精液) * 欲求不満解消係数
		CFLAG:RECEIVER:累計膣内精液 += NOWEX:RECEIVER:膣内精液
		CALL POISONBODY(RECEIVER, SENDER, 1)
		;SIF !TCVAR:RECEIVER:ピル
		;PRINTFORML %CALLNAME:SENDER%が%CALLNAME:RECEIVER%に{NOWEX:RECEIVER:膣内精液}出した
		CALL SAMEN_SHOOT3(NOWEX:RECEIVER:膣内精液 , SENDER, RECEIVER)
		IF SHIRAHU(RECEIVER)
			TCVAR:RECEIVER:種付けられ自覚 = 1
		ENDIF
		IF SHIRAHU(SENDER) && SENDER != MASTER
			TCVAR:SENDER:種付け自覚 = 1
		ENDIF
		IF TCVAR:RECEIVER:無理矢理
			IF TALENT:RECEIVER:行きずり == 1
				CSTR:RECEIVER:モブ子プロフィール２ = %REPLACE(@"%CSTR:RECEIVER:モブ子プロフィール２%","され今はもう大人しい","され、膣内射精された")%
				SIF !STRCOUNT(CSTR:RECEIVER:モブ子プロフィール２, "膣内射精された")
					CSTR:RECEIVER:モブ子プロフィール２ = %REPLACE(@"%CSTR:RECEIVER:モブ子プロフィール２%","処女を奪われた","処女を奪われた挙句、膣内射精された")%
				IF EX:RECEIVER:膣内精液 >= 300 && !STRCOUNT(CSTR:RECEIVER:モブ子プロフィール２, "たっぷりと")
					CSTR:RECEIVER:モブ子プロフィール２ = %REPLACE(@"%CSTR:RECEIVER:モブ子プロフィール２%","膣内射精された","たっぷりと精液を注ぎ込まれた")%
				ENDIF
				IF モブ子初期Ｖ性交経験 + 10 <= EXP:RECEIVER:Ｖ性交経験
					IF !STRCOUNT(CSTR:RECEIVER:モブ子プロフィール２, "ハメ倒されて")
						CSTR:RECEIVER:モブ子プロフィール２ = %REPLACE(@"%CSTR:RECEIVER:モブ子プロフィール２%","膣内射精された", "ハメ倒されて膣内射精された")%
					ENDIF
					IF モブ子初期Ｖ性交経験 + 15 <= EXP:RECEIVER:Ｖ性交経験 && !STRCOUNT(CSTR:RECEIVER:モブ子プロフィール２, "散々")
						CSTR:RECEIVER:モブ子プロフィール２ = %REPLACE(@"%CSTR:RECEIVER:モブ子プロフィール２%","ハメ倒されて", "散々ハメ倒されて")%
					ENDIF
				ENDIF
				IF MARK:RECEIVER:快楽刻印 && !STRCOUNT(CSTR:RECEIVER:モブ子プロフィール２, "イかされ")
					CSTR:RECEIVER:モブ子プロフィール２ = %REPLACE(@"%CSTR:RECEIVER:モブ子プロフィール２%","ハメ倒されて", "ハメ倒されてイかされ")%
				ENDIF
				IF MARK:RECEIVER:快楽刻印 && !STRCOUNT(CSTR:RECEIVER:モブ子プロフィール２, "イキ狂わされ")
					CSTR:RECEIVER:モブ子プロフィール２ = %REPLACE(@"%CSTR:RECEIVER:モブ子プロフィール２%","イかされ", "イキ狂わされ")%
				ENDIF
			ENDIF
			SELECTCASE ABL:RECEIVER:膣射中毒
				CASE IS < 3
					無理矢理反感 = 100000
					好感度マイナス = NOWEX:SENDER:射精 * RAND:50 + NOWEX:SENDER:射精 * RAND:50
					信頼マイナス = NOWEX:SENDER:射精 * RAND:20 + NOWEX:SENDER:射精 * RAND:20
					無理矢理恐怖 = NOWEX:RECEIVER:膣内精液 * 5
				CASE 3 TO 5
					無理矢理反感 = 50000
					好感度マイナス = NOWEX:SENDER:射精 * RAND:30 + NOWEX:SENDER:射精 * RAND:30
					信頼マイナス = NOWEX:SENDER:射精 * RAND:15 + NOWEX:SENDER:射精 * RAND:15
					無理矢理恐怖 = NOWEX:RECEIVER:膣内精液 * 3
				CASEELSE
					無理矢理反感 = 10000
					好感度マイナス = NOWEX:SENDER:射精 * RAND:10 + NOWEX:SENDER:射精 * RAND:10
					信頼マイナス = NOWEX:SENDER:射精 * RAND:10 + NOWEX:SENDER:射精 * RAND:10
					無理矢理恐怖 = NOWEX:RECEIVER:膣内精液
			ENDSELECT
			IF CFLAG:RECEIVER:生理周期 == 7
				無理矢理反感 *= 2
				無理矢理恐怖 *= 2
				好感度マイナス *= 2
				信頼マイナス *= 2
			ENDIF
			IF CFLAG:RECEIVER:無理矢理膣射 > 1
				無理矢理反感 *= 2
				無理矢理恐怖 *= 2
				好感度マイナス *= 2
				信頼マイナス *= 2
			ENDIF
			SOURCE:RECEIVER:反感 += 無理矢理反感
			IF CFLAG:RECEIVER:無理矢理膣射 > 1
				CFLAG:RECEIVER:無理矢理膣射 = 3
			ELSE
				CFLAG:RECEIVER:無理矢理膣射 = 1
			ENDIF
			TFLAG:好感度管理 -= 好感度マイナス
			CALL CHANGE_CFLAG(4,RECEIVER,信頼マイナス * (-1))
		ENDIF

	;アナル
	CASE 2
		EXP:RECEIVER:精液経験 += NOWEX:SENDER:射精 * 2
		EXP:RECEIVER:肛射経験 += NOWEX:SENDER:射精 * 2
		STAIN:RECEIVER:Ａ |= 4
		STAIN:RECEIVER:腸内 |= 4
		;中出し表示用
		TCVAR:RECEIVER:Aに中出しされたフラグ = 2
		TCVAR:RECEIVER:Aに中出ししたキャラの番号 = SENDER
		IF FLAG:時間停止 == 1
			CFLAG:RECEIVER:アナル射精 = 2
		ELSE
			CFLAG:RECEIVER:アナル射精 = 1
		ENDIF
		NOWEX:RECEIVER:アナル内精液 = NOWEX:SENDER:射精量
		EX:RECEIVER:アナル内精液 += NOWEX:SENDER:射精量
		欲求不満解消係数 = 1000 / MIN(100 + ABL:RECEIVER:肛射中毒 * 10,200)
		CFLAG:RECEIVER:溜まってる度 -= SQRT(NOWEX:RECEIVER:アナル内精液) * 欲求不満解消係数
		CFLAG:RECEIVER:累計アナル精液 += NOWEX:SENDER:射精量
		CALL POISONBODY(RECEIVER, SENDER, 2)
	;手淫
	CASE 3
		EXP:RECEIVER:精液経験 += 1 + NOWEX:SENDER:射精
		STAIN:RECEIVER:手 |= 4
		IF FLAG:時間停止 == 1
			CFLAG:RECEIVER:手に精液 = 2
		ELSE
			CFLAG:RECEIVER:手に精液 = 1
		ENDIF
		IF ABL:RECEIVER:精液中毒 > 2 && ABL:RECEIVER:技巧 > 2
			EXP:RECEIVER:精飲経験 ++
			CFLAG:RECEIVER:累計精飲 += NOWEX:SENDER:射精量 / 2
			CFLAG:RECEIVER:溜まってる度 -= NOWEX:SENDER:射精量 * ABL:RECEIVER:精液中毒 / 10 * (TALENT:RECEIVER:精愛味覚 + 1)
			CALL POISONBODY(RECEIVER, SENDER, 31)
		ELSE
			CALL POISONBODY(RECEIVER, SENDER, 3)
		ENDIF
		;接触のリセット
		TEQUIP:SENDER:指接触部位 = 0
	;口淫
	CASE 4
		EXP:RECEIVER:精液経験 += NOWEX:SENDER:射精 * 3
		STAIN:RECEIVER:口 |= 4
		IF FLAG:時間停止 == 1
			CFLAG:RECEIVER:口に精液 = 2
		ELSE
			CFLAG:RECEIVER:口に精液 = 1
		ENDIF
		;イラマチオ
		欲求不満解消係数 = 1000 / MIN(100 + ABL:RECEIVER:精液中毒 * 10,200)
		IF TALENT:RECEIVER:精愛味覚
			SOURCE:RECEIVER:快Ｍ += NOWEX:SENDER:射精 * 10
			欲求不満解消係数 += 1
		ENDIF
		IF SELECTCOM == 140
			EXP:RECEIVER:精飲経験 += NOWEX:SENDER:射精 * 3
			CFLAG:RECEIVER:累計精飲 += NOWEX:SENDER:射精量
		ELSEIF  RAND:(MIN(60, EXP:RECEIVER:精飲経験 + 1)) > 10 + RAND:30
			EXP:RECEIVER:精飲経験 += NOWEX:SENDER:射精 * 3
			CFLAG:RECEIVER:累計精飲 += NOWEX:SENDER:射精量
		ELSE
			EXP:RECEIVER:精飲経験 += NOWEX:SENDER:射精
			CFLAG:RECEIVER:累計精飲 += NOWEX:SENDER:射精量 / 3
			欲求不満解消係数 /= 2
		ENDIF
		CALL POISONBODY(RECEIVER, SENDER, 4)
		CFLAG:RECEIVER:溜まってる度 -= SQRT(NOWEX:SENDER:射精量) * 欲求不満解消係数 / 4

		;接触のリセット
		TEQUIP:SENDER:口接触部位 = 0
	;パイズリ
	CASE 5
		EXP:RECEIVER:精液経験 += NOWEX:SENDER:射精 * 3
		CFLAG:RECEIVER:累計ぶっかけ += NOWEX:SENDER:射精量
		CALL POISONBODY(RECEIVER, SENDER, 5)
		STAIN:RECEIVER:5 |= 4
		IF FLAG:時間停止 == 1
			CFLAG:RECEIVER:顔に精液 = 2
		ELSE
			CFLAG:RECEIVER:顔に精液 = 1
		ENDIF
		;TFLAG:特殊COM = 12 に該当するものがCSVに書いてない
		;(1=シックスナイン 2=岩清水 3=Gスポット刺激 4=乱れ牡丹 5=手淫フェラ 6= 7= 8= シックスナインパイズリ 13=交互挿入 )
		;TFLAGS.txtと書いてあることが違う（TFLAG:12 逆レイプ継続フラグ（VA共通））
		;	→嫌な予感
		SIF TFLAG:特殊COM == 12
			STAIN:RECEIVER:口 |= 4
		;接触のリセット
		TEQUIP:SENDER:Ｂ接触部位 = 0
	;素股
	CASE 6
		EXP:RECEIVER:精液経験 += NOWEX:SENDER:射精
		CFLAG:RECEIVER:累計ぶっかけ += NOWEX:SENDER:射精量
		CALL POISONBODY(RECEIVER, SENDER, 6)
		STAIN:RECEIVER:Ｖ |= 4
		;接触のリセット
		TEQUIP:SENDER:Ｃ接触部位 = 0
	;足コキ
	CASE 7
		EXP:RECEIVER:精液経験 += NOWEX:SENDER:射精
		CFLAG:RECEIVER:累計ぶっかけ += NOWEX:SENDER:射精量
		CALL POISONBODY(RECEIVER, SENDER, 7)
	;体表
	CASE 8
		EXP:RECEIVER:精液経験 += NOWEX:SENDER:射精 * 2
		CFLAG:RECEIVER:累計ぶっかけ += NOWEX:SENDER:射精量
		CALL POISONBODY(RECEIVER, SENDER, 8)
ENDSELECT
SIF SHIRAHU(RECEIVER)
	CALL ADDICTION_SOURCE(RECEIVER, SENDER)
ENDIF
CFLAG:RECEIVER:溜まってる度 = LIMIT(CFLAG:RECEIVER:溜まってる度, 0, 1000)
CFLAG:SENDER:溜まってる度 = MAX(CFLAG:SENDER:溜まってる度 - NOWEX:SENDER:射精 * 50, 0)

;射精された精子を子宮内へ送る処理
@SAMEN_SHOOT3(射精した精液量, 射精したキャラ, 射精されたキャラ)
#DIM 射精した精液量
#DIM 射精されたキャラ
#DIM 減算補正値
#DIM 加算値

;使われてない……
#DIM 射精したキャラ

;73,挿入子宮口責め
;後ろから上書きされてるけどこれ意味有る？
IF SELECTCOM == 73
	減算補正値 = 100
ELSE
	SELECTCASE TEQUIP:射精されたキャラ:子宮
	CASE 0
		減算補正値 = 150
	CASE 1
		減算補正値 = 120
	CASE 2
		減算補正値 = 100
ENDSELECT
ENDIF

;だいしゅきホールド
SIF TCVAR:射精されたキャラ:カウンター行動の派生 == 2 && TCVAR:射精されたキャラ:カウンター行動 == 500
	減算補正値 -= 30

加算値 = 射精した精液量 - 減算補正値

IF !TALENT:射精されたキャラ:行きずり
	SELECTCASE EXP:射精されたキャラ:子宮口開発経験
		CASE IS >= 300
		CASE IS > 100
			加算値 = 加算値 * 2 / 3
		CASEELSE
			加算値 /= 2 
	ENDSELECT
ENDIF

;計算結果が負なら加算しない
IF 加算値 < 0
	RETURN 0
ENDIF
SIF 射精されたキャラ == MASTER
	TCVAR:射精したキャラ:種付け量 += 加算値
EX:射精されたキャラ:子宮内精液 += 加算値

;通常のコマンド外であなたがＶに出した時の処理を一括でやる関数
;基本的にオナバレックス用
@ADD_SAMEN_V(ARG, 射精した精液量,無自覚)
#DIM 射精した精液量
#DIM 無自覚

射精した精液量 = MIN(BASE:MASTER:精力, 射精した精液量)
CALL SAMEN_SHOOT3(射精した精液量, MASTER, ARG)
BASE:MASTER:精力 -= 射精した精液量
EX:ARG:膣内精液 += 射精した精液量
CFLAG:ARG:累計膣内精液 += 射精した精液量

SIF !無自覚
	TCVAR:ARG:種付けられ自覚 = 1

STAIN:ARG:Ｖ |= 4
STAIN:ARG:膣内 |= 4

STAIN:MASTER:Ｐ |= 1
STAIN:MASTER:Ｐ |= 4

;通常のコマンド外であなたがＡに出した時の処理を一括でやる関数
;基本的にオナバレックス用
@ADD_SAMEN_A(ARG, 射精した精液量)
#DIM 射精した精液量

射精した精液量 = MIN(BASE:MASTER:精力, 射精した精液量)

BASE:MASTER:精力 -= 射精した精液量
EX:ARG:アナル内精液 += 射精した精液量
CFLAG:ARG:累計アナル精液 += 射精した精液量

STAIN:ARG:Ａ |= 4
STAIN:ARG:腸内 |= 4

STAIN:MASTER:Ｐ |= 8
STAIN:MASTER:Ｐ |= 4

@ADDICTION_SOURCE(RECEIVER, SENDER)
#DIM RECEIVER
#DIM SENDER
#DIM AMOUNT
#DIMS REVISED_FLAG
#DIM REVISION
#DIM 中毒レベル


SELECTCASE TCVAR:SENDER:射精した箇所フラグ
	CASE 1
		中毒レベル = ABL:RECEIVER:膣射中毒
		REVISED_FLAG = 膣内射精補正
	CASE 2
		中毒レベル = ABL:RECEIVER:肛射中毒
		REVISED_FLAG = 肛内射精補正
	CASE 4
		中毒レベル = ABL:RECEIVER:精液中毒
		REVISED_FLAG = 口内射精補正
	CASEELSE
		RETURN
ENDSELECT

TCVAR:RECEIVER:REVISED_FLAG = NOWEX:SENDER:射精量 * (10 + 中毒レベル)

IF TALENT:SENDER:濃厚精液
	REVISION = GET_REVISION(TCVAR:RECEIVER:REVISED_FLAG, 500, 1000)
ELSE
	REVISION = GET_REVISION(TCVAR:RECEIVER:REVISED_FLAG, 500, 2000)
ENDIF

;同時絶頂の場合増加
SIF SENDER == MASTER && SYNCED_ORGASM(RECEIVER)
	REVISION = REVISION * 5 / 4

SOURCE:RECEIVER:情愛 += REVISION * 2
SOURCE:RECEIVER:欲情 += REVISION * 2
SOURCE:RECEIVER:恭順 += REVISION
SOURCE:RECEIVER:屈従 += REVISION

TCVAR:RECEIVER:REVISED_FLAG = REVISION

@ADDICTION_SHOW(ARG, 補正名, 対応中毒)
#DIMS 対応中毒
#DIMS 補正名
#DIM REVISION

LOCAL = FINDELEMENT(ABLNAME, 対応中毒)

REVISION = TCVAR:ARG:補正名
SIF REVISION
	PRINTFORML %CALLNAME:ARG%（%ABLNAME:LOCAL%{ABL:ARG:対応中毒}）：情愛＋{REVISION * 2} 欲情＋{REVISION * 2} 屈従＋{REVISION} 恭順＋{REVISION}
IF TALENT:ARG:行きずり == 1 && ABL:ARG:対応中毒 < REVISION / 150 && ABL:ARG:対応中毒 < 3
	ABL:ARG:対応中毒 ++
	CALL COLORMESSAGE(@"%CALLNAME:ARG%の%ABLNAME:LOCAL%が{ABL:ARG:対応中毒}になった",C_YELLOW,2)
ENDIF

@AUTO_SKIN_EQUIP
SIF !CFLAG:TARGET:自動ゴム
	RETURN
SIF !ITEM:コンドーム
	RETURN
SIF TEQUIP:MASTER:コンドーム
	RETURN

ITEM:コンドーム --
TEQUIP:MASTER:コンドーム = 1
PRINTFORML %CALLNAME:MASTER%はコンドームをつけた 残り{ITEM:コンドーム}枚

;===========================================
;ポイズンボディの効果を規定 MASTER限定
;POINTは射精箇所
;===========================================
@POISONBODY(RECEIVER, SENDER, POINT)
#DIM RECEIVER
#DIM SENDER
#DIM POINT
#DIM ゲージ増加

ゲージ増加 = 0
IF TFLAG:ポイズンボディ && SENDER == MASTER
	SELECTCASE POINT
		;膣内。粘膜吸収1.5倍。一番効果が出る
		CASE 1
			ゲージ増加 += NOWEX:SENDER:射精量 * 3 / 2
		;アナル。粘膜吸収1.0倍。
		CASE 2
			ゲージ増加 += NOWEX:SENDER:射精量
		;手淫。経皮0.2倍。一応効果はある程度
		CASE 3
			ゲージ増加 += NOWEX:SENDER:射精量 / 5
		;手淫→舐め取り。粘膜0.5倍
		CASE 31
			ゲージ増加 += NOWEX:SENDER:射精量 / 2
		;口。粘膜吸収1.0倍。飲みこぼしなどで減免はしない
		CASE 4
			ゲージ増加 += NOWEX:SENDER:射精量
		;胸。経皮0.5倍。手よりは多い
		CASE 5
			ゲージ増加 += NOWEX:SENDER:射精量 / 2
		;素股。粘膜？0.75倍
		CASE 6
			ゲージ増加 += NOWEX:SENDER:射精量 * 3 / 4
		;足。経皮0.2倍。一応効果はある程度
		CASE 7
			ゲージ増加 += NOWEX:SENDER:射精量 / 5
		;体表。経皮0.1倍。
		CASE 8
			ゲージ増加 += NOWEX:SENDER:射精量 / 10
	ENDSELECT
	;濃厚精液の場合は２倍に補正
	SIF TALENT:SENDER:濃厚精液
		ゲージ増加 *= 2
ENDIF
TCVAR:RECEIVER:ポイズンゲージ += ゲージ増加

;────────────────────────────────────
;愛撫、性交等の継続、解除、処理
;────────────────────────────────────
@SHOW_TOUCH
;(1=ペニス 2=クリトリス 3=指 4=口 5=胸 6=膣 7=アナル 8=足)
FOR LOCAL, 1, GET_TARGETNUM() + 1
	SIF TARGET:LOCAL <= 0
		CONTINUE
	SELECTCASE TEQUIP:(TARGET:LOCAL):Ｐ接触部位
		CASE 3
			PRINT 手淫する 
		CASE 4
			PRINT フェラする 
		CASE 6
			PRINT 逆レイプ 
		CASE 7
			PRINT 逆アナル 
		CASE 8
			PRINT 足コキする 
	ENDSELECT
	SELECTCASE TEQUIP:(TARGET:LOCAL):Ｃ接触部位
		CASE 1
			PRINT 素股 
		CASE 3
			PRINT クリ愛撫 
		CASE 4
			PRINT クンニ 
	ENDSELECT
	SELECTCASE TEQUIP:(TARGET:LOCAL):指接触部位
		CASE 1
			PRINT 手淫 
		CASE 5
			PRINT 胸揉みあい 
	ENDSELECT
	SELECTCASE TEQUIP:(TARGET:LOCAL):口接触部位
		CASE 1
			PRINT フェラ 
		CASE 2
			PRINT クンニさせる 
		CASE 4
			PRINT キス 
		CASE 7
			PRINT アナル奉仕 
	ENDSELECT
	SELECTCASE TEQUIP:(TARGET:LOCAL):Ｂ接触部位
		CASE 1
			PRINT パイズリ 
		CASE 3
			PRINT 胸愛撫 
	ENDSELECT
	SELECTCASE TEQUIP:(TARGET:LOCAL):Ｖ接触部位
		CASE 1
			PRINT 性交 
		CASE 3
			PRINT 指入れ 
	ENDSELECT
	SELECTCASE TEQUIP:(TARGET:LOCAL):Ａ接触部位
		CASE 1
			PRINT アナルセックス 
		CASE 3
			PRINT アナル愛撫 
		CASE 4
			PRINT アナル舐め 
	ENDSELECT
	SELECTCASE TEQUIP:(TARGET:LOCAL):足接触部位
		CASE 1
			PRINT 足コキ 
	ENDSELECT
	PRINTFORML (%CALLNAME:(TARGET:LOCAL)%)
NEXT
PRINTL

@TOUCH_SUCCESSION(ARG)
VARSET LOCAL
SELECTCASE SELECTCOM
	;愛撫　指と口をリセット
	CASE 0
		CALL TOUCH_RESET_M(SET_YUBI)
		SIF !TEQUIP:ARG:継続キス
			CALL TOUCH_RESET_M(SET_M)
		;本来は二箇所同時だけど、システム的には一箇所しか無理なので、苦肉の策
		IF !RAND:2
			CALL TOUCH_SET(SET_FROM_C,SET_YUBI,ARG)
		ELSE
			CALL TOUCH_SET(SET_FROM_B,SET_YUBI,ARG)
		ENDIF
	;クンニ　性交解除し対象のＣに口をセット
	CASE 1
		CALL 挿入解除(ARG)
		CALL TOUCH_RESET_M(SET_YUBI)
		IF GETBIT(TALENT:ARG:性別,1)
			CALL TOUCH_SET(SET_FROM_P,SET_M,ARG)
		ELSE
			CALL TOUCH_SET(SET_FROM_C,SET_M,ARG)
		ENDIF
	;フェラする　性交解除し対象のＣに口をセット
	CASE 2
		;性交解除
		CALL 挿入解除(ARG)
		CALL TOUCH_SET(SET_FROM_P,SET_M,ARG)
	;指挿れ
	CASE 3
		IF TEQUIP:ARG:バイブ
		;指リセット
			CALL TOUCH_RESET_M(SET_YUBI)
		ELSE
			;指を対象のＶにセット
			CALL TOUCH_SET(SET_FROM_V,SET_YUBI,ARG)
		ENDIF
	;アナル舐め
	CASE 4
		;性交解除し口を対象のＡにセット
		CALL 挿入解除(ARG)
		CALL TOUCH_RESET_M(SET_YUBI)
		CALL TOUCH_SET(SET_FROM_A,SET_M,ARG)
	;アナル愛撫
	CASE 5
		IF TEQUIP:ARG:アナルバイブ || TEQUIP:ARG:アナルビーズ || TEQUIP:ARG:浣腸
			;指リセット
			CALL TOUCH_RESET_M(SET_YUBI)
		ELSE
			CALL TOUCH_SET(SET_FROM_A,SET_YUBI,ARG)
		ENDIF
	;胸愛撫
	CASE 6
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,ARG)
	;乳首責め
	CASE 7
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,ARG)
	;秘貝開帳
	CASE 8
		IF TEQUIP:ARG:Ａ接触部位 == 1 && TEQUIP:ARG:体位 == 2
			TEQUIP:ARG:体位 = 1
		ELSEIF TEQUIP:ARG:Ａ接触部位 == 1 && TEQUIP:ARG:体位 == 5
			TEQUIP:ARG:体位 = 4
		ELSEIF TEQUIP:ARG:Ａ接触部位 != 1
			CALL TOUCH_RESET_T(ARG)
		ENDIF
	;自慰
	CASE 9
	;乳首吸い
	CASE 10
		CALL TOUCH_SET(SET_FROM_B,SET_M,ARG)
;--------------------------------------
	CASE 11
		CALL TOUCH_SET(SET_FROM_B,SET_M,ARG)
;--------------------------------------
	;胸揉み
	CASE 12
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,ARG)
	;アナル開帳
	CASE 13
		IF TEQUIP:ARG:Ｖ接触部位 == 1 && TEQUIP:ARG:体位 == 1
			TEQUIP:ARG:体位 = 2
		ELSEIF TEQUIP:ARG:Ｖ接触部位 == 1 && TEQUIP:ARG:体位 == 4
			TEQUIP:ARG:体位 = 5
		ELSEIF TEQUIP:ARG:Ｖ接触部位 != 1
			CALL TOUCH_RESET_T(ARG)
		ENDIF
	;クリ愛撫
	CASE 15
		;指を対象のＣにセット
		CALL TOUCH_SET(SET_FROM_C,SET_YUBI,ARG)
	;キスする
	CASE 19,20
		CALL TOUCH_SET(SET_FROM_M,SET_M,ARG)
	;何もしない
	CASE 21
	;誘い受け
	CASE 22
	;ローター
	CASE 40
		CALL TOUCH_RESET_M(SET_YUBI)
		TEQUIP:ARG:Ｃ接触部位 = 0
		TEQUIP:ARG:Ｐ接触部位 = 0
	;Ｅマッサージャ
	CASE 41
		CALL TOUCH_SET(0,0,0,SET_P)
		TEQUIP:ARG:Ｃ接触部位 = 0
		TEQUIP:ARG:Ｐ接触部位 = 0
	;クリキャップ
	CASE 42
		CALL TOUCH_RESET_M(SET_YUBI)
		TEQUIP:ARG:Ｃ接触部位 = 0
		TEQUIP:ARG:Ｐ接触部位 = 0
	;オナホール
	CASE 43
		CALL TOUCH_RESET_M(SET_YUBI)
		TEQUIP:ARG:Ｃ接触部位 = 0
		TEQUIP:ARG:Ｐ接触部位 = 0
	;バイブ
	CASE 44
		CALL TOUCH_RESET_M(SET_YUBI)
		TEQUIP:ARG:Ｖ接触部位 = 0
	;アナルバイブ
	CASE 45
		CALL TOUCH_RESET_M(SET_YUBI)
		TEQUIP:ARG:Ａ接触部位 = 0
	;アナルビーズ
	CASE 46
		CALL TOUCH_RESET_M(SET_YUBI)
		TEQUIP:ARG:Ａ接触部位 = 0
	;ニプルキャップ
	CASE 47
		CALL TOUCH_RESET_M(SET_YUBI)
		TEQUIP:ARG:Ｂ接触部位 = 0
	;搾乳機
	CASE 48
		CALL TOUCH_RESET_M(SET_YUBI)
		TEQUIP:ARG:Ｂ接触部位 = 0
	;乳首ローター
	CASE 49
		CALL TOUCH_RESET_M(SET_YUBI)
	;正常位
	CASE 60
		CALL TOUCH_SET(SET_FROM_V,SET_P,ARG,1)
		TEQUIP:ARG:体位 = 1
	;後背位
	CASE 61
		CALL TOUCH_SET(SET_FROM_V,SET_P,ARG,1)
		TEQUIP:ARG:体位 = 2
	;正常位アナル
	CASE 62
		CALL TOUCH_SET(SET_FROM_A,SET_P,ARG,1)
		TEQUIP:ARG:体位 = 1
	;後背位アナル
	CASE 63, 323
		CALL TOUCH_SET(SET_FROM_A,SET_P,ARG,1)
		TEQUIP:ARG:体位 = 2
	;逆レイプ
	CASE 64
		CALL TOUCH_SET(SET_FROM_P,SET_V,ARG,1)
		TEQUIP:MASTER:体位 = 3
	;騎乗位
	CASE 65
		CALL TOUCH_SET(SET_FROM_V,SET_P,ARG,1)
		TEQUIP:ARG:体位 = 3
	;騎乗位アナル
	CASE 66
		CALL TOUCH_SET(SET_FROM_A,SET_P,ARG,1)
		TEQUIP:ARG:体位 = 3
	;対面座位
	CASE 67
		CALL TOUCH_SET(SET_FROM_V,SET_P,ARG,1)
		TEQUIP:ARG:体位 = 4
	;背面座位
	CASE 68
		CALL TOUCH_SET(SET_FROM_V,SET_P,ARG,1)
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,ARG)
		TEQUIP:ARG:体位 = 5
	;対面座位アナル
	CASE 69
		CALL TOUCH_SET(SET_FROM_A,SET_P,ARG,1)
		TEQUIP:ARG:体位 = 4
	;背面座位アナル
	CASE 70
		CALL TOUCH_SET(SET_FROM_A,SET_P,ARG,1)
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,ARG)
		TEQUIP:ARG:体位 = 5
	;二穴挿し
	CASE 71
		IF TEQUIP:ARG:Ｖ接触部位 == 1
			TEQUIP:ARG:Ａ接触部位 = 0
		ELSEIF TEQUIP:ARG:Ａ接触部位 == 1
			TEQUIP:ARG:Ｖ接触部位 = 0
		ENDIF
	;挿入Ｇスポット責め
	CASE 72
		CALL TOUCH_SET(SET_FROM_V,SET_P,ARG)
	;挿入子宮
	CASE 73
		CALL TOUCH_SET(SET_FROM_V,SET_P,ARG)
	;Ｓ状結腸責め
	CASE 74
		CALL TOUCH_SET(SET_FROM_A,SET_P,ARG)
	;ＡＧスポ
	CASE 75
		CALL TOUCH_SET(SET_FROM_A,SET_P,ARG)
	;小人オナホ
	CASE 79
		CALL TOUCH_SET(SET_FROM_V,SET_P,ARG,1)
		TEQUIP:ARG:体位 = 6
	;手淫
	CASE 80
		CALL TOUCH_SET(SET_FROM_YUBI,SET_P,ARG)
	;フェラチオ
	CASE 81
		IF TEQUIP:ARG:Ｐ接触部位 == 6 || TEQUIP:ARG:Ｐ接触部位 == 7
			CALL 挿入解除(ARG)
		ENDIF
		CALL TOUCH_SET(SET_FROM_M,SET_P,ARG)
	;パイズリ
	CASE 82
		IF TEQUIP:ARG:Ｐ接触部位 == 6 || TEQUIP:ARG:Ｐ接触部位 == 7
			CALL 挿入解除(ARG)
		ENDIF
		CALL TOUCH_SET(SET_FROM_B,SET_P,ARG)
	;素股
	CASE 83
		IF TEQUIP:ARG:Ｐ接触部位 == 6 || TEQUIP:ARG:Ｐ接触部位 == 7
			CALL 挿入解除(ARG)
		ENDIF
		CALL TOUCH_SET(SET_FROM_C,SET_P,ARG)
	;泡踊り
	CASE 84
		IF TEQUIP:ARG:Ｐ接触部位 == 6 || TEQUIP:ARG:Ｐ接触部位 == 7
			CALL 挿入解除(ARG)
		ENDIF
		CALL TOUCH_SET(0,0,0,1)
	;足コキ
	CASE 85
		IF TEQUIP:ARG:Ｐ接触部位 == 6 || TEQUIP:ARG:Ｐ接触部位 == 7
			CALL 挿入解除(ARG)
		ENDIF
		CALL TOUCH_SET(SET_FROM_LEG,SET_P,ARG)
	;バキュームフェラ
	CASE 88
		IF TEQUIP:ARG:Ｐ接触部位 == 6 || TEQUIP:ARG:Ｐ接触部位 == 7
			CALL 挿入解除(ARG)
		ENDIF
		CALL TOUCH_SET(SET_FROM_M,SET_P,ARG)
	;アナル愛撫される
	CASE 90
		CALL TOUCH_SET(SET_FROM_YUBI,SET_A,ARG)
	;アナル舐めされる
	CASE 91
		IF TEQUIP:ARG:Ｐ接触部位 == 6 || TEQUIP:ARG:Ｐ接触部位 == 7
			CALL 挿入解除(ARG)
		ENDIF
		CALL TOUCH_SET(SET_FROM_M,SET_A,ARG)
	;Ａ正常位される
	CASE 92
		CALL TOUCH_SET(SET_FROM_P,SET_A,ARG)
		TEQUIP:MASTER:体位 = 1
		TEQUIP:ARG:Ｃ接触部位 = 0
		TEQUIP:ARG:Ｖ接触部位 = 0
	;Ａ後背位される
	CASE 93
		CALL TOUCH_SET(SET_FROM_P,SET_A,ARG)
		TEQUIP:MASTER:体位 = 2
		TEQUIP:ARG:Ｃ接触部位 = 0
		TEQUIP:ARG:Ｖ接触部位 = 0
	;Ａ騎乗位する
	CASE 94
		CALL TOUCH_SET(SET_FROM_P,SET_A,ARG)
		TEQUIP:MASTER:体位 = 3
		TEQUIP:ARG:Ｃ接触部位 = 0
		TEQUIP:ARG:Ｖ接触部位 = 0
	;スパンキング
	CASE 100
		CALL TOUCH_RESET_M(SET_YUBI)
	;鞭
	CASE 101
		CALL TOUCH_RESET_M(SET_YUBI)
	;蝋燭
	CASE 102
		CALL TOUCH_RESET_M(SET_YUBI)
	;針
	CASE 103
		CALL TOUCH_RESET_M(SET_YUBI)
	;アイマスク
	CASE 104
		CALL TOUCH_RESET_M(SET_YUBI)
	;乳スパンキング
	CASE 108
		CALL TOUCH_RESET_M(SET_YUBI)
	;クンニ強制
	CASE 120
		CALL TOUCH_SET(SET_FROM_M,SET_C,ARG)
	;貝合わせ
	CASE 121
		CALL TOUCH_SET(SET_FROM_C,SET_C,ARG)
	;手淫する
	CASE 124
		CALL TOUCH_SET(SET_FROM_P,SET_YUBI,ARG)
	;愛撫させる
	CASE 125
		TEQUIP:ARG:指接触部位 = 0
		TEQUIP:ARG:口接触部位 = 0
	;乳の揉み合い
	CASE 126
		CALL TOUCH_RESET_M(SET_YUBI)
		TEQUIP:ARG:指接触部位 = 0
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,ARG)
		CALL TOUCH_SET(SET_FROM_YUBI,5,ARG)
	;正常位される
	CASE 130
		CALL TOUCH_SET(SET_FROM_P,SET_V,ARG,1)
		TEQUIP:MASTER:体位 = 1
	;後背位される
	CASE 131
		CALL TOUCH_SET(SET_FROM_P,SET_V,ARG,1)
		TEQUIP:MASTER:体位 = 2
	;対面座位される
	CASE 132
		CALL TOUCH_SET(SET_FROM_P,SET_V,ARG,1)
		TEQUIP:MASTER:体位 = 4
	;背面座位される
	CASE 133
		CALL TOUCH_SET(SET_FROM_P,SET_V,ARG,1)
		TEQUIP:MASTER:体位 = 5
	;イラマチオ
	CASE 140
		CALL TOUCH_SET(SET_FROM_M,SET_P,ARG)
	;フィストファック
	CASE 141
		CALL TOUCH_SET(SET_FROM_V,SET_YUBI,ARG)
	;アナルフィスト
	CASE 142
		CALL TOUCH_SET(SET_FROM_A,SET_YUBI,ARG)
	;両アナフィスト
	CASE 143
		TEQUIP:MASTER:Ｖ接触部位 = 3
		TEQUIP:MASTER:Ａ接触部位 = 3
	;アナル奉仕
	CASE 145
		CALL TOUCH_SET(SET_FROM_M,SET_A,ARG)
	;浣腸
	CASE 146
		CALL TOUCH_RESET_M(SET_YUBI)
	;拡張バルーン
	CASE 147
		CALL TOUCH_RESET_M(SET_YUBI)
	;アナル電極
	CASE 148
		CALL TOUCH_RESET_M(SET_YUBI)
		CALL TOUCH_SET(SET_FROM_A,1,ARG)
	;乳を握り潰す
	CASE 149
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,ARG)
	;胸の谷間を犯す
	CASE 150
		CALL TOUCH_SET(SET_FROM_B,SET_P,ARG)

	;日常セクハラ
	;キスする
	CASE 312
		CALL TOUCH_SET(SET_FROM_M,SET_M,ARG)
	;胸愛撫
	CASE 313
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,ARG)
	;アナル愛撫
	CASE 314
		CALL TOUCH_SET(SET_FROM_A,SET_YUBI,ARG)
	;クリ愛撫
	CASE 315
		IF GETBIT(TALENT:ARG:性別,1)
			CALL TOUCH_SET(SET_FROM_P,SET_YUBI,ARG)
		ELSE
			CALL TOUCH_SET(SET_FROM_C,SET_YUBI,ARG)
		ENDIF
	;指挿れ
	CASE 316
		CALL TOUCH_SET(SET_FROM_V,SET_YUBI,ARG)

	;相手主導正常位
	CASE 320
		IF TEQUIP:ARG:Ｐ接触部位 == 6 || TEQUIP:ARG:Ｐ接触部位 == 7
			CALL 挿入解除(ARG)
		ENDIF
		CALL TOUCH_SET(SET_FROM_V,SET_P,ARG,1)
		TEQUIP:ARG:体位 = 1
	;相手主導後背位
	CASE 322
		IF TEQUIP:ARG:Ｐ接触部位 == 6 || TEQUIP:ARG:Ｐ接触部位 == 7
			CALL 挿入解除(ARG)
		ENDIF
		CALL TOUCH_SET(SET_FROM_V,SET_P,ARG,1)
		TEQUIP:ARG:体位 = 2
	;相手主導後背位Ａ
	CASE 323
		IF TEQUIP:ARG:Ｐ接触部位 == 6 || TEQUIP:ARG:Ｐ接触部位 == 7
			CALL 挿入解除(ARG)
		ENDIF
		CALL TOUCH_SET(SET_FROM_A,SET_P,ARG,1)
		TEQUIP:ARG:体位 = 2
	
	;愛撫(慰)
	CASE 700
		CALL TOUCH_RESET_M(SET_YUBI)
	;セルフフェラ(慰)
	CASE 702
		TEQUIP:MASTER:口接触部位 = 1
		TEQUIP:MASTER:Ｐ接触部位 = 4
	;指挿れ(慰)
	CASE 703
		IF TEQUIP:ARG:バイブ
		;指リセット
			CALL TOUCH_RESET_M(SET_YUBI)
		ELSE
			;指を対象のＶにセット
			CALL TOUCH_SET(SET_FROM_V,SET_YUBI,MASTER)
		ENDIF
	;アナル愛撫(慰)
	CASE 705
		IF TEQUIP:ARG:アナルバイブ || TEQUIP:ARG:アナルビーズ || TEQUIP:ARG:浣腸
			;指リセット
			CALL TOUCH_RESET_M(SET_YUBI)
		ELSE
			CALL TOUCH_SET(SET_FROM_A,SET_YUBI,MASTER)
		ENDIF
	;胸愛撫(慰)
	CASE 706
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,MASTER)
	;乳首責め(慰)
	CASE 707
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,MASTER)
	;秘貝開帳(慰)
	CASE 708
		CALL TOUCH_RESET_T(MASTER)
	;手淫(慰)
	CASE 709
		CALL TOUCH_SET(SET_FROM_P,SET_YUBI,MASTER)
	;胸揉み(慰)
	CASE 712
		CALL TOUCH_SET(SET_FROM_B,SET_YUBI,MASTER)
	;アナル開帳(慰)
	CASE 713
		CALL TOUCH_RESET_T(MASTER)
	;ローター(慰)
	CASE 740
		CALL TOUCH_RESET_M(SET_YUBI)
		TEQUIP:MASTER:Ｃ接触部位 = 0
		TEQUIP:MASTER:Ｐ接触部位 = 0
	;Ｅマッサージャ(慰)
	CASE 741
		CALL TOUCH_SET(0,0,0,1)
		TEQUIP:MASTER:Ｃ接触部位 = 0
		TEQUIP:MASTER:Ｐ接触部位 = 0
	;クリキャップ(慰)
	CASE 742
		CALL TOUCH_RESET_M(SET_YUBI)
		TEQUIP:MASTER:Ｃ接触部位 = 0
		TEQUIP:MASTER:Ｐ接触部位 = 0
	;オナホール(慰)
	CASE 743
		CALL TOUCH_RESET_M(SET_YUBI)
		TEQUIP:MASTER:Ｃ接触部位 = 0
		TEQUIP:MASTER:Ｐ接触部位 = 0
	;バイブ(慰)
	CASE 744
		CALL TOUCH_RESET_M(SET_YUBI)
		TEQUIP:MASTER:Ｖ接触部位 = 0
	;アナルバイブ(慰)
	CASE 745
		CALL TOUCH_RESET_M(SET_YUBI)
		TEQUIP:MASTER:Ａ接触部位 = 0
	;アナルビーズ(慰)
	CASE 746
		CALL TOUCH_RESET_M(SET_YUBI)
		TEQUIP:MASTER:Ａ接触部位 = 0
	;ニプルキャップ(慰)
	CASE 747
		CALL TOUCH_RESET_M(SET_YUBI)
		TEQUIP:MASTER:Ｂ接触部位 = 0
	;搾乳機(慰)
	CASE 748
		CALL TOUCH_RESET_M(SET_YUBI)
		TEQUIP:MASTER:Ｂ接触部位 = 0
	;乳首ローター(慰)
	CASE 749

	CASE IS < 200
		CALL TOUCH_SET(0,0,0,1)
	CASE IS > 500
		CALL TOUCH_SET(0,0,0,1)
	;掃除
	CASE 410
		CALL TOUCH_SET(0,0,0,1)
ENDSELECT


;MASTERとTARGETの接触部位を設定する
@TOUCH_SET(ARG,ARG:1,ARG:2,ARG:3)
;ARG TARGETの接触部位(101=ペニス 102=クリトリス 103=指 104=口 105=胸 106=膣 107=アナル 108=足)
;ARG:1 MASTERの接触部位(1=ペニス 2=クリトリス 3=指 4=口 5=胸 6=膣 7=アナル 8=足)
;ARG:2 対象のキャラ番号
;ARG:3 真ならリセットする
VARSET LOCAL

IF ARG:3
	FOR LOCAL,1,CHARANUM
		FOR LOCAL:1,1,9
		;COM60~70の場合、口はリセットしない
			SIF LOCAL == ARG:2 && LOCAL:1 == 4 && TFLAG:3 >= 60 && TFLAG:3 <= 70
				CONTINUE
			TEQUIP:LOCAL:(100 + LOCAL:1) = 0
		NEXT
		;ついでに体位もリセット
		TEQUIP:LOCAL:体位 = 0
	NEXT
ENDIF
SIF ARG <= 100
	RETURN 0
FOR LOCAL,1,CHARANUM
	FOR LOCAL:1,1,9
		SIF TEQUIP:LOCAL:(100 + LOCAL:1) == ARG:1
			TEQUIP:LOCAL:(100 + LOCAL:1) = 0
	NEXT
NEXT
TEQUIP:(ARG:2):ARG = ARG:1
;継続系コマンドを解除

IF MASTER_POSE(SET_M,SET_M) != ARG:2 && TEQUIP:(ARG:2):継続キス && SELECTCOM != 0
	FOR LOCAL,1,CHARANUM
		TEQUIP:LOCAL:継続キス = 0
	NEXT
	PRINTL ＜キス解除＞
ENDIF

IF MASTER_POSE(SET_B,SET_YUBI) != ARG:2 && TEQUIP:(ARG:2):胸揉み
	FOR LOCAL,1,CHARANUM
		TEQUIP:LOCAL:胸揉み = 0
	NEXT
	PRINTL ＜胸揉み解除＞
ENDIF

IF MASTER_POSE(SET_B,SET_M) != ARG:2 && TEQUIP:(ARG:2):乳首吸い && SELECTCOM != 6 && SELECTCOM != 12
	FOR LOCAL,1,CHARANUM
		TEQUIP:LOCAL:乳首吸い = 0
	NEXT
	PRINTL ＜乳首吸い解除＞
ENDIF


;MASTERの接触部位をリセットする
@TOUCH_RESET_M(ARG)
;ARG MASTERの接触部位(1=ペニス 2=クリトリス 3=指 4=口 5=胸 6=膣 7=アナル 8=足)
FOR LOCAL, 1, GET_TARGETNUM() + 1
	SIF TARGET:LOCAL <= 0
		CONTINUE
	FOR LOCAL:1,1,9
		SIF TEQUIP:(TARGET:LOCAL):(100 + LOCAL:1) == ARG
				TEQUIP:(TARGET:LOCAL):(100 + LOCAL:1) = 0
	NEXT
NEXT

;TARGETの接触部位をリセットする
@TOUCH_RESET_T(ARG)
FOR LOCAL,1,9
	TEQUIP:ARG:(100 + LOCAL) = 0
NEXT

@MASTER_POSE(ARG,ARG:1,ARG:2)
;ARG MASTERの部位を占有中のTARGET:Xの部位(1=ペニス 2=クリトリス 3=指 4=口 5=胸 6=膣 7=アナル 8=足)
;ARG 0なら誰が占有しているか返す
;ARG:1 MASTERの部位(1=ペニス 2=クリトリス 3=指 4=口 5=胸 6=膣 7=アナル 8=足)
;ARG:2 真なら前ターンの履歴を見る
;戻り値 MASTERの部位を占有中のTARGET:Xがいるならそのキャラ番号を返す
;MASTER_POSE(SET_M,SET_P,1)なら前ターン(第3引数)に誰かがMASTERのペニス(第2引数)を口(第1引数)で占有していた場合、そのキャラ番号を返す
;ARGでMASTERのARG:1に触れているキャラを返すってこと
#FUNCTION
VARSET LOCAL
IF !ARG
	FOR LOCAL,1,CHARANUM
		SIF CFLAG:LOCAL:現在位置 != CFLAG:MASTER:現在位置
			CONTINUE
		FOR LOCAL:1,1,9
			SIF TEQUIP:LOCAL:(100 + LOCAL:1) == ARG:1
				RETURNF LOCAL
		NEXT
	NEXT
ENDIF
ARG:2 = ARG:2 ? 1 # 0
FOR LOCAL, 1, GET_TARGETNUM() + 1
	SIF TARGET:LOCAL <= 0
		CONTINUE
	SIF TEQUIP:(TARGET:LOCAL):(100 + ARG + 10 * ARG:2) == ARG:1
		RETURNF TARGET:LOCAL
NEXT

@挿入解除(ARG)
SIF TEQUIP:ARG:Ｖ接触部位 == 1
	TEQUIP:ARG:Ｖ接触部位 = 0
SIF TEQUIP:ARG:Ａ接触部位 == 1
	TEQUIP:ARG:Ａ接触部位 = 0
TEQUIP:ARG:Ｃ接触部位 = 0
TEQUIP:ARG:Ｐ接触部位 = 0
TEQUIP:ARG:口接触部位 = 0
TEQUIP:ARG:Ｖセックス = -1
TEQUIP:ARG:Ａセックス = -1
TEQUIP:ARG:体位 = 0
@SHIBORITORI(射精キャラ番号, 被射精キャラ番号)
#DIM 射精キャラ番号
#DIM 被射精キャラ番号
#DIMS 射精箇所1
#DIMS 射精箇所2

;なんかDIMとか使って微妙に分かりにくいかもしれないけど、これは最初に射精確定後処理に入れようとしてやっぱり止めた時の名残なので
;気にしなくていいです　地の文が最初に表示されちゃう

IF 射精キャラ番号 == MASTER
;あなたが突っ込んでる場合
	IF Ｓ度判定(被射精キャラ番号,射精キャラ番号) < RAND:(MAX((100 - ABL:被射精キャラ番号:欲望 * 5), 20))
		IF TALENT:被射精キャラ番号:献身的 && BASE:射精キャラ番号:気力 == 0
			PRINTFORMW %CALLNAME:被射精キャラ番号%は%CALLNAME:射精キャラ番号%の体調を気遣い、%CALLNAME:射精キャラ番号%のペニスを解放した……
		ELSE
			PRINTFORMW %CALLNAME:被射精キャラ番号%は%CALLNAME:射精キャラ番号%が射精し終わると、満足そうに微笑んで%CALLNAME:射精キャラ番号%のペニスを引き抜いた……
		ENDIF
		CALL 挿入解除(被射精キャラ番号)
		TFLAG:106 = 0
	ELSE
		IF TALENT:被射精キャラ番号:サド
			IF TALENT:射精キャラ番号:マゾ
				IF BASE:被射精キャラ番号:気力 > 0
					IF BASE:射精キャラ番号:気力 == 0
						PRINTFORML %CALLNAME:被射精キャラ番号%は%CALLNAME:射精キャラ番号%を解放する気は全くないようだ
						PRINTFORMW %CALLNAME:被射精キャラ番号%の膣肉が%CALLNAME:射精キャラ番号%のペニスに絡みつき、更なる射精を求めて蠕動し始めている……
					ELSE
						PRINTFORMW %CALLNAME:被射精キャラ番号%は%CALLNAME:射精キャラ番号%の腕を逃げられないようガッチリと掴み、叩きつけるように腰を振り始めた……
					ENDIF
				ELSEIF BASE:射精キャラ番号:気力 == 0
					PRINTFORMW %CALLNAME:被射精キャラ番号%はぐったりした%CALLNAME:射精キャラ番号%を押さえつけ、叩きつけるように腰を振り始めた……
				ELSE
					PRINTFORMW %CALLNAME:被射精キャラ番号%は%CALLNAME:射精キャラ番号%が射精し終わるのを確認すると、間髪要れずに腰を振り始めた……
				ENDIF
			ELSEIF BASE:被射精キャラ番号:気力 > 0
				PRINTFORMW %CALLNAME:被射精キャラ番号%は%CALLNAME:射精キャラ番号%から更に精液を搾り取ろうと再び腰を振り始めた……
			ELSEIF BASE:射精キャラ番号:気力 == 0
				PRINTFORMW %CALLNAME:被射精キャラ番号%はぐったりした%CALLNAME:射精キャラ番号%を押さえつけ、再び腰を振り始めた……
			ELSE
				PRINTFORMW %CALLNAME:被射精キャラ番号%は%CALLNAME:射精キャラ番号%が射精し終わるのを確認すると、間髪要れずに再び腰を振り始めた……
			ENDIF
		ELSE
			IF TALENT:被射精キャラ番号:献身的 && BASE:射精キャラ番号:気力 == 0
				PRINTFORMW %CALLNAME:被射精キャラ番号%はまだ満足していないのか、%CALLNAME:射精キャラ番号%に謝罪してから再び腰を振り始めた……
			ELSE
				PRINTFORMW %CALLNAME:被射精キャラ番号%は%CALLNAME:射精キャラ番号%が射精し終わると、%CALLNAME:射精キャラ番号%の頬にキスをしてから再び腰を振り始めた……
			ENDIF
		ENDIF
	ENDIF
ELSE
;あなたに突っ込んでる場合
	IF TCVAR:TARGET:射精した箇所フラグ == 1
		射精箇所1 = 膣内
		射精箇所2 = 膣
	ELSE
		射精箇所1 = アナル
		射精箇所2 = アナル
	ENDIF

	IF Ｓ度判定(射精キャラ番号, 被射精キャラ番号) - 引き抜き精力補正(射精キャラ番号) < RAND:(MAX((100 - ABL:被射精キャラ番号:欲望 * 5), 20))
		IF TALENT:射精キャラ番号:献身的 && BASE:被射精キャラ番号:気力 == 0
			PRINTFORMW %CALLNAME:射精キャラ番号%は%CALLNAME:被射精キャラ番号%の体調を気遣い、%CALLNAME:被射精キャラ番号%の%射精箇所2%からペニスを引き抜いた……
		ELSE
			PRINTFORMW %CALLNAME:射精キャラ番号%は%CALLNAME:被射精キャラ番号%の%射精箇所1%に精液を注ぎこみ終えると、満足そうに微笑んでペニスを引き抜いた……
		ENDIF
		CALL 挿入解除(被射精キャラ番号)
		CALL TOUCH_SET(0, 0, 射精キャラ番号, 1)
	ELSE
		IF TALENT:射精キャラ番号:サド
			IF TALENT:被射精キャラ番号:マゾ
				IF BASE:射精キャラ番号:気力 > 0
					IF BASE:被射精キャラ番号:気力 == 0
						PRINTFORML %CALLNAME:射精キャラ番号%は%CALLNAME:被射精キャラ番号%を解放する気は全くないようだ
						PRINTFORMW %CALLNAME:射精キャラ番号%のペニスは瞬く間に硬度と大きさと取り戻し、%CALLNAME:被射精キャラ番号%の%射精箇所2%を抉り始めた……
					ELSE
						PRINTFORMW %CALLNAME:射精キャラ番号%は%CALLNAME:被射精キャラ番号%の腕を逃げられないようガッチリと掴み、叩きつけるように腰を突き入れ始めた……
					ENDIF
				ELSEIF BASE:射精キャラ番号:気力 == 0
					PRINTFORMW %CALLNAME:射精キャラ番号%はぐったりした%CALLNAME:被射精キャラ番号%を押さえつけ、叩きつけるように腰を突き入れ始めた……
				ELSE
					PRINTFORMW %CALLNAME:射精キャラ番号%は射精し終わると、凭れるように%CALLNAME:被射精キャラ番号%に抱き付き、再び腰を振り始めた……
				ENDIF
			ELSEIF BASE:射精キャラ番号:気力 > 0
				PRINTFORMW %CALLNAME:射精キャラ番号%は%CALLNAME:被射精キャラ番号%の%射精箇所1%に更に精液を注ぎこもうと再び腰を振り始めた……
			ELSEIF BASE:被射精キャラ番号:気力 == 0
				PRINTFORMW %CALLNAME:射精キャラ番号%はぐったりした%CALLNAME:被射精キャラ番号%を押さえつけ、再び腰を振り始めた……
			ELSE
				PRINTFORMW %CALLNAME:射精キャラ番号%は射精し終わると、凭れるように%CALLNAME:被射精キャラ番号%に抱き付き、再び腰を振り始めた……
			ENDIF
		ELSE
			IF TALENT:射精キャラ番号:献身的 && BASE:被射精キャラ番号:気力 == 0
				PRINTFORMW %CALLNAME:射精キャラ番号%はまだ満足していないのか、%CALLNAME:被射精キャラ番号%に謝罪してから再び腰を振り始めた……
			ELSE
				PRINTFORMW %CALLNAME:射精キャラ番号%は%CALLNAME:被射精キャラ番号%の%射精箇所1%で射精し終わると、%CALLNAME:被射精キャラ番号%の頬にキスをしてから再び腰を振り始めた……
			ENDIF
		ENDIF
	ENDIF
ENDIF

@Ｓ度判定(攻め,受け)
#FUNCTION

#DIM 攻め
#DIM 受け

VARSET LOCAL

LOCAL += ABL:攻め:サドっ気 * 5
LOCAL -= ABL:攻め:マゾっ気 * 5
LOCAL += ABL:受け:マゾっ気 * 5
LOCAL -= ABL:受け:サドっ気 * 5

IF TALENT:攻め:サド
	IF TALENT:受け:マゾ
		IF BASE:攻め:気力 > 0
			IF BASE:受け:気力 <= 0
				LOCAL += 999
			ELSE
				LOCAL += 90
			ENDIF
		ELSEIF BASE:受け:気力 <= 0
			LOCAL += 75
		ELSE
			LOCAL += 60
		ENDIF
	ELSEIF BASE:攻め:気力 > 0
		IF BASE:受け:気力 <= 0
			LOCAL += 50
		ELSE
			LOCAL += 30
		ENDIF
	ELSEIF BASE:受け:気力 <= 0
		LOCAL += 20
	ELSE
		LOCAL += 10
	ENDIF
ELSE
	IF TALENT:受け:マゾ
		IF BASE:攻め:気力 > 0
			IF BASE:受け:気力 <= 0
				LOCAL += 75
			ELSE
				LOCAL += 60
			ENDIF
		ELSEIF BASE:受け:気力 <= 0
			LOCAL += 50
		ELSE
			LOCAL += 35
		ENDIF
	ELSE
		SIF BASE:受け:気力 <= 0
			LOCAL += 10
		SIF BASE:攻め:気力 > 0
			LOCAL += 10
	ENDIF
ENDIF

;反抗的
SIF TALENT:攻め:態度 == 1
	LOCAL += 5

;自制心
SIF TALENT:攻め:自制心
	LOCAL -= 10

;快感に素直
IF TALENT:攻め:快感応答 == -1
	LOCAL += 10
;快感に素直
ELSEIF TALENT:攻め:快感応答 == 1
	LOCAL -= 5
ENDIF

SIF TALENT:攻め:献身的 && BASE:受け:気力 <= 0
	LOCAL /= 5
SIF TALENT:受け:サド
	LOCAL /= 2

RETURNF MAX(LOCAL, 0)

@引き抜き精力補正(ARG)
#FUNCTION
RETURNF (MAXBASE:ARG:精力 - BASE:ARG:精力) * 50 / MAXBASE:ARG:精力 + 5

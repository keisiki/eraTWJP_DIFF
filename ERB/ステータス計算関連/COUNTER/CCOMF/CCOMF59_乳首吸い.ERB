;*********************************************************************************************
;カウンターコマンド：乳首吸い
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOMF59(ARG)

	;体勢の変更
	;=============================
	CALL TOUCH_SET(SET_FROM_M,5,ARG)
	TFLAG:MASTERのＢ使用中 ++
	
	;ソース計算および内部処理系統
	;=============================
	SIF TEQUIP:PLAYER:1 & 2
		CALL DATUI_TOP(ARG,0)
	SOURCE:ARG:性行動 += 300
	SOURCE:ARG:誘惑 += 150
	SOURCE:ARG:挑発 += 200
	SOURCE:ARG:征服 += 100
	SOURCE:PLAYER:快Ｂ += 400
	EXP:ARG:口淫経験 ++

	;マスターカウンター制御
	;(ビット立ちで不可 0=尻を撫でる 1=抱き付く 2=キスする 3=胸愛撫 4=アナル愛撫 5=クリ愛撫 6=指挿入れ 7=スカートめくり)
	;=============================
	SIF GROUPMATCH(TEQUIP:MASTER:体位, 1, 3, 4)
		CALL カウンター挿入され継続中処理(ARG)
	SETBIT TCVAR:ARG:26 ,2
	SETBIT TCVAR:ARG:26 ,7

RETURN 1

;*********************************************************************************************
;カウンター実行可否出力関数
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOM_ABLE59(ARG)

	SIF TFLAG:65
		RETURN 0
	SIF ABL:ARG:舌 < 1
		RETURN 0
	SIF TEQUIP:MASTER:50 >= 0 || TEQUIP:MASTER:51 >= 0
		RETURN 0

RETURN 1

;*********************************************************************************************
;カウンター実行判定値算出関数
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOM_ORDER59(コマンド番号, ARG, 基本値)
#DIM コマンド番号
#DIM 基本値
#DIMS カウンタータイプ

	;カウンタータイプ設定
	;=============================
	カウンタータイプ = 脱衣愛撫

	;実行判定値算出式
	;=============================
	LOCAL = 基本値 + 2 * TALENT:ARG:舌使い + ABL:ARG:舌 + 1 * TALENT:ARG:幼稚 - TALENT:MASTER:2

	;カウンタータイプ別計算処理 実質実行判定処理を含む
	;=============================
	LOCAL = MAX(LOCAL + COUNTER_SIKOU:ARG:コマンド番号, 1)
	LOCAL *= COUNTER_TYPE(カウンタータイプ, ARG)

RETURN LOCAL

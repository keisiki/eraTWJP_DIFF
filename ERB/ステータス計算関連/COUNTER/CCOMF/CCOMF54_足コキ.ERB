;*********************************************************************************************
;カウンターコマンド：足コキ
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOMF54(ARG)

	;体勢の変更
	;=============================
	CALL TOUCH_SET(SET_FROM_LEG,1,ARG)
	TFLAG:MASTERのＣ使用中 ++
	
	;ソース計算および内部処理系統
	;=============================
	SOURCE:ARG:性行動 += 250
	SOURCE:ARG:挑発 += 300
	SOURCE:ARG:辱め += 400
	SOURCE:ARG:奉仕 += 300
	SOURCE:ARG:加虐 += 800
	SOURCE:ARG:征服 += 600
	;射精箇所（1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
	CALL 射精対象登録(PLAYER, ARG, 7)
	;強度
	TCVAR:PLAYER:4 += 500
	TCVAR:ARG:嗜虐フラグ = 1
	;奉仕フラグ
	TCVAR:ARG:108 = 1

	;マスターカウンター制御
	;(ビット立ちで不可 0=尻を撫でる 1=抱き付く 2=キスする 3=胸愛撫 4=アナル愛撫 5=クリ愛撫 6=指挿入れ 7=スカートめくり)
	;=============================
	;CALL カウンター挿入され継続中処理(ARG)
	;複数人がありうるのでこれは………あり得ないのでは？
	SETBIT TCVAR:ARG:26 ,0
	SETBIT TCVAR:ARG:26 ,1
	SETBIT TCVAR:ARG:26 ,2
	SETBIT TCVAR:ARG:26 ,3
	SETBIT TCVAR:ARG:26 ,4
	SETBIT TCVAR:ARG:26 ,5
	SETBIT TCVAR:ARG:26 ,6

RETURN 1

;*********************************************************************************************
;カウンター実行可否出力関数
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOM_ABLE54(ARG)

	SIF TCVAR:MASTER:120 != 1
		RETURN 0
	SIF !(TALENT:PLAYER:2 & 2)
		RETURN 0
	SIF TFLAG:62
		RETURN 0
	SIF ABL:ARG:サドっ気 < 1
		RETURN 0
	SIF TEQUIP:ARG:50 >= 0 || TEQUIP:ARG:51 >= 0
		RETURN 0
	SIF TEQUIP:MASTER:50 >= 0 || TEQUIP:MASTER:51 >= 0
		RETURN 0

RETURN 1

;*********************************************************************************************
;カウンター実行判定値算出関数
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOM_ORDER54(コマンド番号, ARG, 基本値)
#DIM コマンド番号
#DIM 基本値
#DIMS カウンタータイプ

	;カウンタータイプ設定
	;=============================
	カウンタータイプ = 脱衣愛撫

	;実行判定値算出式
	;=============================
	LOCAL = 基本値 + 2 * TALENT:ARG:サド + MIN(ABL:ARG:サドっ気 , 5) + BASE:MASTER:勃起 / 500

	;カウンタータイプ別計算処理 実質実行判定処理を含む
	;=============================
	LOCAL = MAX(LOCAL + COUNTER_SIKOU:ARG:コマンド番号, 1)
	LOCAL *= COUNTER_TYPE(カウンタータイプ, ARG)

RETURN LOCAL

;*********************************************************************************************
;カウンターコマンド：だいしゅきホールド
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOMF86(ARG)

	;体勢の変更
	;=============================
	CALL TOUCH_SET(SET_FROM_V,1,ARG)
	TFLAG:MASTERのＰ挿入中 = 1
	TFLAG:MASTERのＣ使用中 ++
	
	;ソース計算および内部処理系統
	;=============================
	TCVAR:PLAYER:4 += 1800
	SOURCE:ARG:快Ｖ += 200
	SOURCE:ARG:情愛 += 450
	SOURCE:ARG:誘惑 += 1000
	SOURCE:ARG:征服 += 800
	SOURCE:ARG:強要 += 200
	SOURCE:ARG:性行動 += 250
	SOURCE:ARG:達成 += 250
	TCVAR:116 = PLAYER
	CALL PAIN_CHECK_V(ARG)
	;射精箇所（1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
	CALL 射精対象登録(PLAYER, ARG, 1)
	EXP:ARG:Ｖ性交経験 ++
	EXP:PLAYER:挿入経験 ++
	EXP:ARG:愛情経験 += TALENT:ARG:恋慕
	TEQUIP:ARG:50 = MASTER

	;マスターカウンター制御
	;(ビット立ちで不可 0=尻を撫でる 1=抱き付く 2=キスする 3=胸愛撫 4=アナル愛撫 5=クリ愛撫 6=指挿入れ 7=スカートめくり)
	;=============================
	;CALL カウンター挿入され継続中処理(ARG)
	SETBIT TCVAR:ARG:26 ,6
	SETBIT TCVAR:ARG:26 ,7

RETURN 1

;*********************************************************************************************
;カウンター実行可否出力関数
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOM_ABLE86(ARG)

	SIF TCVAR:PLAYER:12 != 1 && CFLAG:ARG:うふふ != 2
		RETURN 0
	SIF !TCVAR:PLAYER:イきそう && CFLAG:ARG:うふふ == 2
		RETURN 0
	SIF TEQUIP:ARG:50 < 0
		RETURN 0
	SIF CFLAG:ARG:生解禁 != 2
		RETURN 0
	SIF TEQUIP:ARG:体位 == 2 || TEQUIP:ARG:体位 == 5
		RETURN 0

RETURN 1

;*********************************************************************************************
;カウンター実行判定値算出関数
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOM_ORDER86(コマンド番号, ARG, 基本値)
#DIM コマンド番号
#DIM 基本値
#DIMS カウンタータイプ

	;カウンタータイプ設定
	;=============================
	カウンタータイプ = 性交

	;実行判定値算出式
	;=============================
	LOCAL = 基本値 + ABL:ARG:親密 * 3 + EX:ARG:Ｖ寸止め * 10

	;カウンタータイプ別計算処理 実質実行判定処理を含む
	;=============================
	LOCAL = MAX(LOCAL + COUNTER_SIKOU:ARG:コマンド番号, 1)
	LOCAL *= COUNTER_TYPE(カウンタータイプ, ARG)

RETURN LOCAL

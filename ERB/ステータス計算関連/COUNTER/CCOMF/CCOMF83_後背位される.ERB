;*********************************************************************************************
;カウンターコマンド：後背位される
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOMF83(ARG)

	;体勢の変更
	;=============================
	SIF MASTER_POSE(0,SET_P) == ARG
		CALL TOUCH_RESET_T(ARG)
	CALL TOUCH_SET(SET_FROM_P,6,ARG)
	TFLAG:MASTERのＶ使用中 ++
	
	;ソース計算および内部処理系統
	;=============================
	TCVAR:ARG:童貞喪失 = 1
	IF BEDROOM(CFLAG:MASTER:現在位置)
		SIF TEQUIP:ARG:0
			CALL DATUI_BOTTOM_T(ARG,0)
	ELSE
		SIF TEQUIP:ARG:0 & 14
			CALL DATUI_BOTTOM_T(ARG,1)
	ENDIF
	SOURCE:ARG:性行動 += 300
	SOURCE:ARG:征服 += 1500
	SOURCE:ARG:誘惑 += 300
	SOURCE:ARG:加虐 += 600
	SOURCE:ARG:強要 += 600
	SOURCE:PLAYER:快Ｖ += 1000
	TCVAR:ARG:4 += 1000
	CALL PAIN_CHECK_V_MASTER(ARG)
	;射精箇所（1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
	CALL 射精対象登録(PLAYER, ARG, 13)
	TCVAR:PLAYER:4 += 150
	;射精箇所（1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
	CALL 射精対象登録(ARG, PLAYER, 1)
	TEQUIP:MASTER:体位 = 2
	STAIN:ARG:2 |= STAIN:PLAYER:6
	STAIN:PLAYER:6 |= STAIN:ARG:2
	EXP:PLAYER:Ｖ性交経験 ++
	EXP:ARG:挿入経験 ++
	CALL LOST_VIRGIN(PLAYER)
	TEQUIP:PLAYER:50 = ARG
	TCVAR:ARG:嗜虐フラグ = 1

	;マスターカウンター制御
	;(ビット立ちで不可 0=尻を撫でる 1=抱き付く 2=キスする 3=胸愛撫 4=アナル愛撫 5=クリ愛撫 6=指挿入れ 7=スカートめくり)
	;=============================
	;CALL カウンター挿入され継続中処理(ARG)
	SETBIT TCVAR:ARG:26 ,0
	SETBIT TCVAR:ARG:26 ,1
	SETBIT TCVAR:ARG:26 ,2
	SETBIT TCVAR:ARG:26 ,3
	SETBIT TCVAR:ARG:26 ,4
	SETBIT TCVAR:ARG:26 ,5
	SETBIT TCVAR:ARG:26 ,6
	SETBIT TCVAR:ARG:26 ,7

RETURN 1

;*********************************************************************************************
;カウンター実行可否出力関数
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOM_ABLE83(ARG)

	SIF !(TALENT:ARG:2 & 2)
		RETURN 0
	SIF !(TALENT:PLAYER:2 & 1)
		RETURN 0
	SIF TALENT:ARG:妊娠 && (CFLAG:ARG:903 <= 30 || CFLAG:ARG:903 >= 71)
		RETURN 0
	SIF CFLAG:ARG:育児人数 >= 1 && CFLAG:ARG:育児人数 <= 30
		RETURN 0
	SIF BASE:ARG:勃起 < 1000
		RETURN 0
	SIF TFLAG:63
		RETURN 0
	SIF TEQUIP:ARG:50 >= 0 || TEQUIP:ARG:51 >= 0
		RETURN 0
	SIF TEQUIP:MASTER:50 >= 0 || TEQUIP:MASTER:51 >= 0
		RETURN 0
	;貞操帯、触手下着をはいてるとダメ
	SIF EQUIP:ARG:下半身下着２ == 27 || EQUIP:ARG:下半身下着２ == 28
		RETURN 0
	SIF !TALENT:ARG:サド && PALAM:MASTER:潤滑 < PALAMLV:3
		RETURN 0

RETURN 1

;*********************************************************************************************
;カウンター実行判定値算出関数
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOM_ORDER83(コマンド番号, ARG, 基本値)
#DIM コマンド番号
#DIM 基本値
#DIMS カウンタータイプ

	;カウンタータイプ設定
	;=============================
	カウンタータイプ = 性交

	;実行判定値算出式
	;=============================
	LOCAL = (基本値*7/10) + ABL:ARG:Ｃ感覚 + MAX(EXP:ARG:射精経験 / 15, 15) + ABL:ARG:サドっ気 / 2

	;カウンタータイプ別計算処理 実質実行判定処理を含む
	;=============================
	LOCAL = MAX(LOCAL + COUNTER_SIKOU:ARG:コマンド番号, 1)
	LOCAL *= COUNTER_TYPE(カウンタータイプ, ARG)

RETURN LOCAL

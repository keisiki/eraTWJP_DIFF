;*********************************************************************************************
;カウンターコマンド：手淫
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOMF50(ARG)

	;体勢の変更
	;=============================
	CALL TOUCH_SET(SET_FROM_YUBI,1,ARG)
	TFLAG:MASTERのＣ使用中 ++

	;ソース計算および内部処理系統
	;=============================
	IF TEQUIP:PLAYER:0
		IF TEQUIP:PLAYER:0 & 1
			CALL DATUI_BOTTOM(ARG,1)
		ELSEIF TEQUIP:PLAYER:0 & 8
			CALL DATUI_BOTTOM(ARG,2)
		ENDIF
	ENDIF
	SOURCE:ARG:性行動 += 250
	SOURCE:ARG:誘惑 += 500
	SOURCE:ARG:挑発 += 100
	SOURCE:ARG:加虐 += 100
	SOURCE:ARG:征服 += 200
	SIF !(TALENT:PLAYER:2 & 2)
		SOURCE:PLAYER:快Ｃ += 300
	;射精箇所（1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
	CALL 射精対象登録(PLAYER, ARG, 3)
	;強度
	TCVAR:PLAYER:4 += 500
	EXP:ARG:手淫経験 ++
	;奉仕フラグ
	TCVAR:ARG:108 = 1
	TCVAR:ARG:嗜虐フラグ = 1
	CALL EVENT_COUNTER_POSE_69(ARG,2)

	;マスターカウンター制御
	;(ビット立ちで不可 0=尻を撫でる 1=抱き付く 2=キスする 3=胸愛撫 4=アナル愛撫 5=クリ愛撫 6=指挿入れ 7=スカートめくり)
	;=============================
	;CALL カウンター挿入され継続中処理(ARG)

RETURN 1

;*********************************************************************************************
;カウンター実行可否出力関数
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOM_ABLE50(ARG)

	SIF TFLAG:62
		RETURN 0
	SIF ABL:ARG:指 < 1
		RETURN 0
	SIF TEQUIP:ARG:50 >= 0 || TEQUIP:ARG:51 >= 0
		RETURN 0
	SIF TEQUIP:MASTER:50 >= 0 && !HAS_PENIS(MASTER)
		RETURN 0
RETURN 1

;*********************************************************************************************
;カウンター実行判定値算出関数
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOM_ORDER50(コマンド番号, ARG, 基本値)
#DIM コマンド番号
#DIM 基本値
#DIMS カウンタータイプ

	;カウンタータイプ設定
	;=============================
	カウンタータイプ = 脱衣愛撫

	;実行判定値算出式
	;=============================
	LOCAL = 基本値 + 2 * TALENT:ARG:器用な指 + ABL:ARG:指 + BASE:MASTER:勃起 / 500

	;カウンタータイプ別計算処理 実質実行判定処理を含む
	;=============================
	LOCAL = MAX(LOCAL + COUNTER_SIKOU:ARG:コマンド番号, 1)
	LOCAL *= COUNTER_TYPE(カウンタータイプ, ARG)

RETURN LOCAL

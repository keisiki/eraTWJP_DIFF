;*********************************************************************************************
;カウンターコマンド：アナル愛撫
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOMF55(ARG)

	;体勢の変更
	;=============================
	CALL TOUCH_SET(SET_FROM_YUBI,7,ARG)
	TFLAG:MASTERのＡ使用中 ++
	
	;ソース計算および内部処理系統
	;=============================
	IF TEQUIP:PLAYER:0 & 12
		CALL DATUI_BOTTOM(ARG,2)
	ELSEIF TEQUIP:PLAYER:0 & 2
		SIF RAND:2
			CALL DATUI_BOTTOM(ARG,1)
	ENDIF
	SOURCE:ARG:性行動 += 150
	SOURCE:ARG:加虐 = 500
	SOURCE:ARG:誘惑 += 100
	SOURCE:ARG:挑発 += 100
	SOURCE:ARG:奉仕 += 100
	SOURCE:ARG:征服 += 600
	SOURCE:PLAYER:快Ａ += 300
	EXP:ARG:手淫経験 ++
	;奉仕フラグ
	TCVAR:ARG:108 = 1
	TCVAR:ARG:嗜虐フラグ = 1
	;性交中は69にならない
	IF MASTER_POSE(SET_V,SET_P) == ARG || MASTER_POSE(SET_A,SET_P) == ARG || MASTER_POSE(SET_P,SET_V) == ARG || MASTER_POSE(SET_P,SET_A) == ARG
		LOCAL = 0
	ELSE
		LOCAL = 2
	ENDIF
	CALL EVENT_COUNTER_POSE_69(ARG,LOCAL)

	;マスターカウンター制御
	;(ビット立ちで不可 0=尻を撫でる 1=抱き付く 2=キスする 3=胸愛撫 4=アナル愛撫 5=クリ愛撫 6=指挿入れ 7=スカートめくり)
	;=============================
	SIF EQUIP:PLAYER:51 < 0
		CALL カウンター挿入され継続中処理(ARG)
	IF TEQUIP:MASTER:体位 == 2
		SETBIT TCVAR:ARG:26 ,0
		SETBIT TCVAR:ARG:26 ,1
		SETBIT TCVAR:ARG:26 ,2
		SETBIT TCVAR:ARG:26 ,3
		SETBIT TCVAR:ARG:26 ,4
		SETBIT TCVAR:ARG:26 ,5
		SETBIT TCVAR:ARG:26 ,6
	ELSEIF TEQUIP:MASTER:体位 == 5
		SETBIT TCVAR:ARG:26 ,0
		SETBIT TCVAR:ARG:26 ,1
		SETBIT TCVAR:ARG:26 ,3
		SETBIT TCVAR:ARG:26 ,4
		SETBIT TCVAR:ARG:26 ,5
		SETBIT TCVAR:ARG:26 ,6
	ELSEIF TEQUIP:MASTER:体位 == 3 || TEQUIP:MASTER:体位 == 1
		SETBIT TCVAR:ARG:26 ,4
		SETBIT TCVAR:ARG:26 ,5
		SETBIT TCVAR:ARG:26 ,6
	ELSEIF TEQUIP:MASTER:体位 == 4
		SETBIT TCVAR:ARG:26 ,5
		SETBIT TCVAR:ARG:26 ,6
	ENDIF
	SETBIT TCVAR:ARG:26 ,7

RETURN 1

;*********************************************************************************************
;カウンター実行可否出力関数
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOM_ABLE55(ARG)

	SIF TFLAG:64
		RETURN 0
	SIF ABL:ARG:指 < 1
		RETURN 0
	SIF TEQUIP:MASTER:51 >= 0
		RETURN 0
	SIF PALAM:MASTER:潤滑 < PALAMLV:2
		RETURN 0

RETURN 1

;*********************************************************************************************
;カウンター実行判定値算出関数
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOM_ORDER55(コマンド番号, ARG, 基本値)
#DIM コマンド番号
#DIM 基本値
#DIMS カウンタータイプ

	;カウンタータイプ設定
	;=============================
	カウンタータイプ = 脱衣愛撫

	;実行判定値算出式
	;=============================
	LOCAL = (基本値 + 2 * TALENT:ARG:器用な指 + ABL:ARG:指) * SIGN(EXP:ARG:Ａ経験)

	;カウンタータイプ別計算処理 実質実行判定処理を含む
	;=============================
	LOCAL = MAX(LOCAL + COUNTER_SIKOU:ARG:コマンド番号, 1)
	LOCAL *= COUNTER_TYPE(カウンタータイプ, ARG)

RETURN LOCAL

;*********************************************************************************************
;カウンターコマンド：強制アソコ愛撫
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOMF76(ARG)

	;体勢の変更
	;=============================
	CALL TOUCH_SET(SET_FROM_C,3,ARG)
	TFLAG:MASTERの手使用中 ++
	
	;ソース計算および内部処理系統
	;=============================
	SOURCE:ARG:快Ｃ += 100
	SIF !TALENT:ARG:処女
		SOURCE:ARG:快Ｖ += 100
	SOURCE:ARG:性行動 += 150
	SOURCE:ARG:誘惑 += 100
	SOURCE:ARG:挑発 += 150
	SOURCE:ARG:強要 += 400
	SOURCE:ARG:征服 += 200
	;射精箇所（1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
	CALL 射精対象登録(ARG, PLAYER, 3)

	;マスターカウンター制御
	;(ビット立ちで不可 0=尻を撫でる 1=抱き付く 2=キスする 3=胸愛撫 4=アナル愛撫 5=クリ愛撫 6=指挿入れ 7=スカートめくり)
	;=============================
	;CALL カウンター挿入され継続中処理(ARG)
	SETBIT TCVAR:ARG:26 ,0
	SETBIT TCVAR:ARG:26 ,1
	SETBIT TCVAR:ARG:26 ,3
	SETBIT TCVAR:ARG:26 ,4
	SETBIT TCVAR:ARG:26 ,7

RETURN 1

;*********************************************************************************************
;カウンター実行可否出力関数
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOM_ABLE76(ARG)

	SIF !(TALENT:ARG:2 & 1)
		RETURN 0
	;潤滑
	SIF PALAM:ARG:潤滑 < 1000
		RETURN 0
	SIF TEQUIP:MASTER:50 == ARG || TEQUIP:MASTER:51 == ARG
		RETURN 0
	SIF TEQUIP:ARG:体位
		RETURN 0
	;貞操帯、触手下着、バイブパンツ、二穴バイブパンツをはいてるとダメ
	SIF EQUIP:下半身下着２ == 27 || EQUIP:下半身下着２ == 28 || EQUIP:下半身下着２ == 29 || EQUIP:下半身下着２ == 30
		RETURN 0

RETURN 1

;*********************************************************************************************
;カウンター実行判定値算出関数
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOM_ORDER76(コマンド番号, ARG, 基本値)
#DIM コマンド番号
#DIM 基本値
#DIMS カウンタータイプ

	;カウンタータイプ設定
	;=============================
	カウンタータイプ = 脱衣強要

	;実行判定値算出式
	;=============================
	LOCAL = 基本値 + (ABL:ARG:Ｃ感覚 +  ABL:ARG:Ｖ感覚) / 2

	;カウンタータイプ別計算処理 実質実行判定処理を含む
	;=============================
	LOCAL = MAX(LOCAL + COUNTER_SIKOU:ARG:コマンド番号, 1)
	LOCAL *= COUNTER_TYPE(カウンタータイプ, ARG)

RETURN LOCAL

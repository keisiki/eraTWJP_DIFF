;*********************************************************************************************
;カウンターコマンド：腰振りＡ
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOMF89(ARG)

	;体勢の変更
	;=============================
	CALL TOUCH_SET(SET_FROM_A,1,ARG)
	TFLAG:MASTERのＰ挿入中 = 1
	TFLAG:MASTERのＣ使用中 ++
	
	;ソース計算および内部処理系統
	;=============================
	DOWNBASE:MASTER:体力 += 40
	DOWNBASE:MASTER:気力 += 80
	TCVAR:PLAYER:4 += 1000
	SOURCE:ARG:快Ａ += 300
	SOURCE:ARG:情愛 += 200
	SOURCE:ARG:性行動 += 500
	SOURCE:ARG:達成 += 200
	SOURCE:ARG:征服 += 500
	SOURCE:ARG:加虐 += 100
	TCVAR:116 = PLAYER
	TCVAR:ARG:嗜虐フラグ = 1
	CALL PAIN_CHECK_A(ARG)
	;射精箇所（1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
	CALL 射精対象登録(PLAYER, ARG, 2)
	EXP:ARG:Ａ性交経験 ++
	EXP:PLAYER:挿入経験 ++
	EXP:ARG:愛情経験 += TALENT:ARG:恋慕
	TEQUIP:ARG:51 = MASTER
	;奉仕フラグ
	TCVAR:ARG:108 = 1

	;マスターカウンター制御
	;(ビット立ちで不可 0=尻を撫でる 1=抱き付く 2=キスする 3=胸愛撫 4=アナル愛撫 5=クリ愛撫 6=指挿入れ 7=スカートめくり)
	;=============================
	;CALL カウンター挿入され継続中処理(ARG)
	SETBIT TCVAR:ARG:26 ,4
	SETBIT TCVAR:ARG:26 ,7

RETURN 1

;*********************************************************************************************
;カウンター実行可否出力関数
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOM_ABLE89(ARG)

	SIF !(TALENT:ARG:2 & 1)
		RETURN 0
	SIF !(TALENT:PLAYER:2 & 2)
		RETURN 0
	SIF BASE:MASTER:勃起 < 1000
		RETURN 0
	SIF TEQUIP:ARG:51 < 0
		RETURN 0
	SIF !(TEQUIP:50 >= 0 || TEQUIP:51 >= 0)
		RETURN 0
	SIF TCVAR:ARG:101 != PLAYER
		RETURN 0
	SIF ABL:ARG:腰 < 2
		RETURN 0

RETURN 1

;*********************************************************************************************
;カウンター実行判定値算出関数
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOM_ORDER89(コマンド番号, ARG, 基本値)
#DIM コマンド番号
#DIM 基本値
#DIMS カウンタータイプ

	;カウンタータイプ設定
	;=============================
	カウンタータイプ = 性交

	;実行判定値算出式
	;=============================
	LOCAL = 基本値 + (ABL:ARG:奉仕精神 + ABL:ARG:Ａ感覚) * 2
	
	;カウンタータイプ別計算処理 実質実行判定処理を含む
	;=============================
	LOCAL = MAX(LOCAL + COUNTER_SIKOU:ARG:コマンド番号, 1)
	LOCAL *= COUNTER_TYPE(カウンタータイプ, ARG)

RETURN LOCAL

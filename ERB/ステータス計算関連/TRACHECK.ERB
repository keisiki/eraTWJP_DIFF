;FileName_TRACHECK.ERB ----------------------------- Rev1.00
;時間経過
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	SOURCE_CHECKが走るたびに5分計上
;-----------------------------------------------------------
@TRACHECK_TIME
;めんどくさいので5分刻み
IF FLAG:70 == 1
ELSE
SIF SELECTCOM < 200
	TIME += 5
ENDIF
;FileName_TRACHECK.ERB ----------------------------- Rev1.00
;好感度チェック
;CALL		USER
;ARG		0:キャラNO, 1:ソースNO
;RETURN		好感度ボーナス係数
;COMMENT	
;-----------------------------------------------------------
@MASTER_FAVOR_CHECK(ARG,ARG:1)
#FUNCTION
SIF ASSIPLAY
	RETURNF 100
SELECTCASE ARG:1
	CASE 0 TO 3
		RETURNF GET_REVISION(CFLAG:ARG:2, 200 , 20000) + 100
	CASE 10
		RETURNF GET_REVISION(CFLAG:ARG:2, 200 , 15000) + 100
	CASE 30 TO 33
		RETURNF MAX(50,100 - CFLAG:ARG:2 / 200)
	CASEELSE
		RETURNF 100
ENDSELECT

;FileName_TRACHECK.ERB ----------------------------- Rev1.00
;技巧のチェック
;CALL		USER
;ARG		0:キャラNO, 1:ソースNO
;RETURN		技巧ボーナス係数
;COMMENT	
;-----------------------------------------------------------
@TECHNIQUE_CHECK(ARG,ARG:1)
#FUNCTION
SIF TCVAR:ARG:116 < 0
	RETURNF 100
SELECTCASE ARG:1
	CASE 0 TO 3
		RETURNF GET_REVISION(ABL:(TCVAR:ARG:116):技巧 + 2, 500, 10)
	CASEELSE
		RETURNF 100
ENDSELECT

;FileName_TRACHECK.ERB ----------------------------- Rev1.00
;ムードチェック
;CALL		USER
;ARG		0:キャラNO, 1:ソースNO
;RETURN		ムードボーナス値
;COMMENT	
;-----------------------------------------------------------
@MOOD_CHECK(ARG,ARG:1)
#FUNCTION
SELECTCASE ARG:1
	CASE 10,12,15,16,18,20,21,22
		RETURNF GET_REVISION(BASE:ARG:ムード, 50 , 500) + 100
	CASE 30 TO 33
		RETURNF 200 - BASE:ARG:ムード / 100
	CASEELSE
		RETURNF 100
ENDSELECT

;FileName_TRACHECK.ERB ----------------------------- Rev1.00
;理性チェック
;CALL		USER
;ARG		0:キャラNO, 1:ソースNO
;RETURN		理性ボーナス値
;COMMENT	
;-----------------------------------------------------------
@REASON_CHECK(ARG,ARG:1)
#FUNCTION
SELECTCASE ARG:1
	CASE 0,1,2,3,10,12,15,16,18
		RETURNF GET_REVISION(1000 - BASE:ARG:理性, 50 , 500) + 100
	CASE 30 TO 33
		RETURNF 50 + BASE:ARG:理性 / 100
	CASEELSE
		RETURNF 100
ENDSELECT
;FileName_TRACHECK.ERB ----------------------------- Rev1.00
;同性の場合のチェック
;CALL		USER
;ARG		0:キャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@SOURCE_SEX_CHECK(ARG)
#DIM レズホモっ気
#DIM レズホモ中毒
#DIM あなたレズホモっ気
#DIM あなたレズホモ中毒
SIF !HETEROSEX(ARG,PLAYER) || TALENT:ARG:81 == -1
	RETURN 0
;同性愛
レズホモっ気 = ABL:ARG:(16 + HETEROSEX(ARG,PLAYER))
;ABL:レズホモっ気をみる
SELECTCASE レズホモっ気
	CASE 0 TO 5
		SOURCE:ARG:達成 = SOURCE:ARG:達成 * (レズホモっ気 + 10) / 20
		SOURCE:ARG:誘惑 = SOURCE:ARG:誘惑 * (レズホモっ気 + 10) / 25
		SOURCE:ARG:辱め = SOURCE:ARG:辱め * (レズホモっ気 + 10) / 20
		SOURCE:ARG:挑発 = SOURCE:ARG:挑発 * (レズホモっ気 + 10) / 25
		SOURCE:ARG:屈従 = SOURCE:ARG:屈従 * (レズホモっ気 + 10) / 20
		SOURCE:ARG:情愛 = SOURCE:ARG:情愛 * (レズホモっ気 + 10) / 20
		SOURCE:ARG:逸脱 = SOURCE:ARG:逸脱 * (30 - 2 * レズホモっ気) / 10
	CASE 6 TO 10
		SOURCE:ARG:達成 = SOURCE:ARG:達成 * (レズホモっ気 * 2 + 20) / 40
		SOURCE:ARG:誘惑 = SOURCE:ARG:誘惑 * (レズホモっ気 * 2 + 20) / 50
		SOURCE:ARG:辱め = SOURCE:ARG:辱め * (レズホモっ気 * 2 + 20) / 40
		SOURCE:ARG:挑発 = SOURCE:ARG:挑発 * (レズホモっ気 * 2 + 20) / 50
		SOURCE:ARG:屈従 = SOURCE:ARG:屈従 * (レズホモっ気 * 2 + 20) / 40
		SOURCE:ARG:情愛 = SOURCE:ARG:情愛 * (レズホモっ気 * 2 + 20) / 40
		SOURCE:ARG:逸脱 = SOURCE:ARG:逸脱 * (55 - 3 * レズホモっ気) / 20
	CASEELSE
		TIMES SOURCE:ARG:達成 , 1.10
		TIMES SOURCE:ARG:誘惑 , 1.10
		TIMES SOURCE:ARG:辱め , 1.20
		TIMES SOURCE:ARG:挑発 , 1.10
		TIMES SOURCE:ARG:屈従 , 1.20
		TIMES SOURCE:ARG:逸脱 , 1.00
ENDSELECT

;ABL:中毒をみる
レズホモ中毒 = ABL:ARG:(31 + HETEROSEX(ARG,PLAYER))
SELECTCASE レズホモ中毒
	CASE 0 TO 3
		SOURCE:ARG:快Ｃ = SOURCE:ARG:快Ｃ * (レズホモ中毒 + 5) / 10
		SOURCE:ARG:快Ｖ = SOURCE:ARG:快Ｖ * (レズホモ中毒 + 5) / 10
		SOURCE:ARG:快Ａ = SOURCE:ARG:快Ａ * (レズホモ中毒 + 5) / 10
		SOURCE:ARG:快Ｂ = SOURCE:ARG:快Ｂ * (レズホモ中毒 + 5) / 10
		SOURCE:ARG:快Ｍ = SOURCE:ARG:快Ｍ * (レズホモ中毒 + 5) / 10
		SOURCE:ARG:与快Ｃ = SOURCE:ARG:与快Ｃ * (レズホモ中毒 + 5) / 10
		SOURCE:ARG:与快Ｖ = SOURCE:ARG:与快Ｖ * (レズホモ中毒 + 5) / 10
		SOURCE:ARG:与快Ａ = SOURCE:ARG:与快Ａ * (レズホモ中毒 + 5) / 10
		SOURCE:ARG:与快Ｂ = SOURCE:ARG:与快Ｂ * (レズホモ中毒 + 5) / 10
		SOURCE:ARG:与快Ｍ = SOURCE:ARG:与快Ｍ * (レズホモ中毒 + 5) / 10
		SOURCE:ARG:不潔 = SOURCE:ARG:不潔 * (20 - 2 * レズホモ中毒) / 10
	CASE 4 TO 7
		SOURCE:ARG:快Ｃ = SOURCE:ARG:快Ｃ * (レズホモ中毒 + 12) / 20
		SOURCE:ARG:快Ｖ = SOURCE:ARG:快Ｖ * (レズホモ中毒 + 12) / 20
		SOURCE:ARG:快Ａ = SOURCE:ARG:快Ａ * (レズホモ中毒 + 12) / 20
		SOURCE:ARG:快Ｂ = SOURCE:ARG:快Ｂ * (レズホモ中毒 + 12) / 20
		SOURCE:ARG:快Ｍ = SOURCE:ARG:快Ｍ * (レズホモ中毒 + 12) / 20
		SOURCE:ARG:与快Ｃ = SOURCE:ARG:与快Ｃ * (レズホモ中毒 + 12) / 20
		SOURCE:ARG:与快Ｖ = SOURCE:ARG:与快Ｖ * (レズホモ中毒 + 12) / 20
		SOURCE:ARG:与快Ａ = SOURCE:ARG:与快Ａ * (レズホモ中毒 + 12) / 20
		SOURCE:ARG:与快Ｂ = SOURCE:ARG:与快Ｂ * (レズホモ中毒 + 12) / 20
		SOURCE:ARG:与快Ｍ = SOURCE:ARG:与快Ｍ * (レズホモ中毒 + 12) / 20
		SOURCE:ARG:不潔 = SOURCE:ARG:不潔 * (30 - レズホモ中毒) / 20
	CASEELSE
ENDSELECT
	;調教者のABL:レズホモっ気
	あなたレズホモっ気 = ABL:PLAYER:(16 + HETEROSEX(ARG,PLAYER))
SELECTCASE あなたレズホモっ気
	CASE 0 TO 5
		SOURCE:ARG:情愛 = SOURCE:ARG:情愛 * (あなたレズホモっ気 + 15) / 20
		SOURCE:ARG:達成 = SOURCE:ARG:達成 * (あなたレズホモっ気 + 15) / 20
	CASE 6 TO 10
		SOURCE:ARG:情愛 = SOURCE:ARG:情愛 * (あなたレズホモっ気 + 35) / 40
		SOURCE:ARG:達成 = SOURCE:ARG:達成 * (あなたレズホモっ気 + 35) / 40
	CASEELSE
		TIMES SOURCE:ARG:情愛 , 1.20
		TIMES SOURCE:ARG:達成 , 1.20
ENDSELECT
;調教者のABL:中毒
あなたレズホモ中毒 = ABL:PLAYER:(31 + HETEROSEX(ARG,PLAYER))
SELECTCASE あなたレズホモ中毒
	CASE 0 TO 7
		SOURCE:ARG:情愛 = SOURCE:ARG:情愛 * (あなたレズホモっ気 + 20) / 20
		SOURCE:ARG:達成 = SOURCE:ARG:達成 * (あなたレズホモっ気 + 20) / 20
		SOURCE:ARG:性行動 = SOURCE:ARG:性行動 * (あなたレズホモっ気 + 20) / 20
	CASEELSE
		TIMES SOURCE:ARG:情愛 , 1.40
		TIMES SOURCE:ARG:性行動 , 1.40
		TIMES SOURCE:ARG:達成 , 1.40
ENDSELECT
;調教者の自制心
IF TALENT:PLAYER:自制心
	TIMES SOURCE:ARG:性行動 , 0.50
	TIMES SOURCE:ARG:達成 , 0.50
ENDIF



;FileName_TRACHECK.ERB ----------------------------- Rev1.00
;PLAYER能力の処理
;CALL		USER
;ARG		0:キャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@PLAYER_SKILL_CHECK(ARG)
;調教者のTALENT:解放をみる
IF TALENT:PLAYER:自己愛 > 0
	TIMES SOURCE:ARG:快Ｃ , 1.20
	TIMES SOURCE:ARG:快Ｖ , 1.20
	TIMES SOURCE:ARG:快Ａ , 1.20
	TIMES SOURCE:ARG:快Ｂ , 1.20
	TIMES SOURCE:ARG:情愛 , 1.20
ENDIF
;調教者のTALENT:両刀をみる
IF TALENT:PLAYER:性別嗜好 == -1 && HETEROSEX(PLAYER,ARG)
	TIMES SOURCE:ARG:情愛 , 1.20
	TIMES SOURCE:ARG:性行動 , 1.20
	TIMES SOURCE:ARG:達成 , 1.20
ENDIF
;調教者のTALENT:サドをみる
SIF TALENT:PLAYER:サド
	TIMES SOURCE:ARG:恐怖 , 1.50
;調教者のTALENT:小悪魔をみる
SIF TALENT:PLAYER:小悪魔
	TIMES SOURCE:ARG:露出 , 1.50
;調教者のTALENT:魅惑をみる
IF TALENT:PLAYER:魅惑
	TIMES SOURCE:ARG:不潔 , 0.50
	TIMES SOURCE:ARG:逸脱 , 0.50

	TIMES SOURCE:ARG:快Ｃ , 1.20
	TIMES SOURCE:ARG:快Ｖ , 1.20
	TIMES SOURCE:ARG:快Ａ , 1.20
	TIMES SOURCE:ARG:快Ｂ , 1.20
	TIMES SOURCE:ARG:快Ｍ , 1.20
	TIMES SOURCE:ARG:性行動 , 1.20
ENDIF
;調教者のTALENT:謎の魅力をみる
IF TALENT:PLAYER:謎の魅力
	TIMES SOURCE:ARG:不潔 , 0.50
	TIMES SOURCE:ARG:逸脱 , 0.50
	TIMES SOURCE:ARG:快Ｃ , 1.20
	TIMES SOURCE:ARG:快Ｖ , 1.20
	TIMES SOURCE:ARG:快Ａ , 1.20
	TIMES SOURCE:ARG:快Ｂ , 1.20
	TIMES SOURCE:ARG:快Ｍ , 1.20
	TIMES SOURCE:ARG:性行動 , 1.20
	TIMES SOURCE:ARG:達成 , 1.50
ENDIF

;FileName_TRACHECK.ERB ----------------------------- Rev1.00
;従順の上昇による[抵抗]の消滅をチェック
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@JUJUN_UP_CHECK
IF FLAG:70 == 1 || CFLAG:イタズラ
ELSE
FOR LOCAL,0,CHARANUM
	IF ABL:LOCAL:従順 >= 7 && TALENT:LOCAL:抵抗
		PRINTFORM %NAME:LOCAL%は[抵抗]を失った
		TALENT:LOCAL:抵抗 = 0
		PRINTL 否定の珠が半分になった
		JUEL:LOCAL:100 /= 2
	ENDIF
NEXT
ENDIF
;FileName_TRACHECK.ERB ----------------------------- Rev1.00
;欲望の上昇による[抑圧]の消滅をチェック
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@YOKUBO_UP_CHECK
IF FLAG:70 == 1 || CFLAG:イタズラ
ELSE
FOR LOCAL,0,CHARANUM
	IF ABL:LOCAL:欲望 >= 7 && TALENT:LOCAL:自己愛 < 0
		PRINTFORM %NAME:LOCAL%は[抑圧]を失った
		TALENT:LOCAL:自己愛 = 0
		PRINTL 否定の珠が半分になった
		JUEL:LOCAL:100 /= 2
	ENDIF
NEXT
ENDIF
;FileName_TRACHECK.ERB ----------------------------- Rev1.00
;苦痛快楽経験、奉仕快楽経験のチェック
;CALL		USER
;ARG		0:キャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@EXP_GOT_CHECK(ARG)
#DIM 嗜虐増加量
IF FLAG:70 == 1 || CFLAG:イタズラ
ELSE
;奉仕系である、奉仕快楽のチェック
LOCAL = CUP:ARG:快Ｃ + CUP:ARG:快Ｖ + CUP:ARG:快Ａ + CUP:ARG:快Ｂ + CUP:ARG:快Ｍ + CUP:ARG:恭順 / 5
LOCAL:1 = 0


SELECTCASE CUP:ARG:欲情
	CASE IS < 100
		LOCAL = 0
	CASE 101 TO 1000
	CASE 1001 TO 5000
		LOCAL *= 2
	CASE 5001 TO 10000
		LOCAL *= 3
	CASE 10001 TO 30000
		LOCAL *= 4
	CASEELSE
		LOCAL *= 5
ENDSELECT


LOCAL += TCVAR:ARG:射精ゲージ増加量
SIF TCVAR:ARG:嗜虐フラグ
	LOCAL /= 4
;奉仕フラグが立っている
IF TCVAR:ARG:108
	;奉仕快楽のチェック
	IF LOCAL >= 100000
		LOCAL:1 = 16
		TIMES CUP:ARG:恐怖 , 0.65
		TIMES CUP:ARG:反感 , 0.30
	ELSEIF LOCAL >= 50000
		LOCAL:1 = 12
		TIMES CUP:ARG:恐怖 , 0.70
		TIMES CUP:ARG:反感 , 0.40
	ELSEIF LOCAL >= 30000
		LOCAL:1 = 8
		TIMES CUP:ARG:恐怖 , 0.75
		TIMES CUP:ARG:反感 , 0.50
	ELSEIF LOCAL >= 10000
		LOCAL:1 = 4
		TIMES CUP:ARG:恐怖 , 0.80
		TIMES CUP:ARG:反感 , 0.60
	ELSEIF LOCAL >= 5000
		LOCAL:1 = 2
		TIMES CUP:ARG:恐怖 , 0.85
		TIMES CUP:ARG:反感 , 0.70
	ELSEIF LOCAL >= 3000
		LOCAL:1 = 1
		TIMES CUP:ARG:恐怖 , 0.90
		TIMES CUP:ARG:反感 , 0.80
	ENDIF
	EXP:ARG:奉仕快楽経験 += LOCAL:1
ENDIF

;相手の嗜虐快楽のチェック
IF TCVAR:ARG:嗜虐フラグ
	LOCAL = CUP:ARG:快Ｃ + CUP:ARG:快Ｖ + CUP:ARG:快Ａ + CUP:ARG:快Ｂ + CUP:ARG:快Ｍ
	SELECTCASE CUP:ARG:優越
		CASE IS < 100
			LOCAL = 0
		CASE 101 TO 1000
		CASE 1001 TO 5000
			LOCAL *= 2
		CASE 5001 TO 10000
			LOCAL *= 3
		CASE 10001 TO 30000
			LOCAL *= 4
		CASEELSE
			LOCAL *= 5
	ENDSELECT
	;加虐ソースがないと半減
	SIF !SOURCE:ARG:加虐
		LOCAL /= 2
	;奉仕快楽経験が入る場合減少
	SIF TCVAR:ARG:108
		LOCAL /= 4
	SELECTCASE LOCAL
		CASE IS > 100000
			嗜虐増加量 = 16
		CASE IS > 50000
			嗜虐増加量 = 12
		CASE IS > 30000
			嗜虐増加量 = 8
		CASE IS > 10000
			嗜虐増加量 = 4
		CASE IS > 5000
			嗜虐増加量 = 2
		CASE IS > 3000
			嗜虐増加量 = 1
	ENDSELECT
	SIF ABL:ARG:サドっ気 < ABL:MASTER:サドっ気
		嗜虐増加量 /= 2
	EXP:ARG:嗜虐快楽経験 += 嗜虐増加量
ENDIF
;苦痛快楽のチェック
LOCAL = 0
IF CUP:ARG:欲情 >= 3000 && CUP:ARG:苦痛 >= 2000
	LOCAL = 16
	TIMES CUP:ARG:恐怖 , 0.65
ELSEIF CUP:ARG:欲情 >= 2500 && CUP:ARG:苦痛 >= 1500
	LOCAL = 12
	TIMES CUP:ARG:恐怖 , 0.70
ELSEIF CUP:ARG:欲情 >= 1500 && CUP:ARG:苦痛 >= 1000
	LOCAL = 8
	TIMES CUP:ARG:恐怖 , 0.75
ELSEIF CUP:ARG:欲情 >= 1000 && CUP:ARG:苦痛 >= 500
	LOCAL = 4
	TIMES CUP:ARG:恐怖 , 0.80
ELSEIF CUP:ARG:欲情 >= 600 && CUP:ARG:苦痛 >= 300
	LOCAL = 2
	TIMES CUP:ARG:恐怖 , 0.85
ELSEIF CUP:ARG:欲情 >= 300 && CUP:ARG:苦痛 >= 100
	LOCAL = 1
	TIMES CUP:ARG:恐怖 , 0.90
ENDIF
IF LOCAL > 0 && ARG != PLAYER
	EXP:ARG:苦痛快楽経験 += LOCAL

	;嗜虐快楽経験
	LOCAL = ABL:PLAYER:サドっ気 + ABL:ARG:マゾっ気 - 1
	SIF LOCAL > 0
		EXP:PLAYER:嗜虐快楽経験 += LOCAL
ENDIF
ENDIF
;FileName_TRACHECK.ERB ----------------------------- Rev1.00
;南無三カード発行チェック
;CALL		USER
;ARG		0:キャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CARD_GOT_CHECK(ARG)
;怒り状態でウフフ状態でなく今日中に注意札を食らってない
IF CFLAG:ARG:ブチギレ && !CFLAG:ARG:うふふ && !CFLAG:ARG:328
	IF ARG != NO:1
		PRINTFORML %CALLNAME:ARG%は霊夢に%CALLNAME:MASTER%の行いを報告した…
	ELSE
		PRINTFORML %CALLNAME:ARG%は%CALLNAME:MASTER%に御札を突き付けた
	ENDIF
	IF SELECTCOM == 333
		PRINTFORML 自分は%CALLNAME:ARG%の愚痴を聞いていただけだと弁明すると、霊夢は見逃してくれた
		GOTO SKIP
	ENDIF
	PRINTFORML %CALLNAME:MASTER%は注意札を取得しました
	;カード枚数追加
	CFLAG:MASTER:329 ++
	;1日1回しかカードは発行できない
	CFLAG:ARG:328 = 1
	PRINTL  
	PRINTFORML 現在の注意札  {CFLAG:MASTER:329}枚
	IF CFLAG:MASTER:329 > 3
		PRINTL  
		PRINTW …
		PRINTW ……
		PRINTFORML 「あんたがいて楽しかったわ。でも、やり過ぎたようね」
		PRINTFORMW ……
		PRINTL  
		PRINTL  
		PRINTFORML %CALLNAME:MASTER%は、記憶も能力も失って幻想郷から追い出されます（ゲームオーバー？）
		PRINTL [0] お許しください！(Continue)  [1] 心を入れ替えてきます！(Exit)
		$GAME_OVER
		INPUT
		IF RESULT == 0
			PRINTL 「……本当に、反省してる？」
			PRINTFORML なんども頷く%CALLNAME:MASTER%の姿に、霊夢は溜め息を吐く
			PRINTW 「今度も許されるとは思わない事ね」
			PRINTL  
			PRINTFORML 注意札が1枚無効になりました。
			CFLAG:MASTER:329 = CFLAG:MASTER:329 - 1
			PRINTFORMW 現在の注意札  {CFLAG:MASTER:329}枚
		ELSEIF RESULT == 1
			PRINTFORML 気がつくと、%CALLNAME:MASTER%は町の一角に佇んでいた
			PRINTFORML 色々と楽しいことが有った気がするが気のせいだろう
			PRINTFORML %CALLNAME:MASTER%は日常に戻っていった
			PRINTW その後痴漢で捕まった、なんてことはあるかもしれないかもしれない（ゲームオーバー）
			QUIT
		ELSE
			CLEARLINE 1
			GOTO GAME_OVER
		ENDIF
	ENDIF
	$SKIP
ENDIF


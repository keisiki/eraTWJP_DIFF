;FileName_ANOTHER_TALK.ERB ------------------------- Rev1.01
;TARGET以外メンバー会話
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@ANOTHERTARGET_TALK
#DIM TALKER,2
#DIM HATAKE
VARSET LOCAL
VARSET HATAKE
;DEBUGPRINTL 【ANOTHERTARGET_TALK】CALL ANOTHERTARGET_TALK

;CHARA_ARRAYフォルト対策
SIF CHARANUM > キャラクタ数上限
	RETURN 0

;話者の決定処理
CALL ANOTHER_TALK_SET_TALKER(TALKER)
HATAKE = INT_NOUMIN(FLAG:地主, "場所ID")

DEBUGPRINTFORML 【ANOTHERTARGET_TALK】会話者１ = {TALKER:0} 会話者２ = {TALKER:1}
IF (TCVAR:TARGET:仕事開始 - TIME:0 <= 30 && TCVAR:TARGET:仕事開始 - TIME:0 > 0) && !FLAG:70
	CALL MESSAGE_TALK(12)
ELSEIF CFLAG:TARGET:睡眠
	CALL MESSAGE_TALK(3)
ELSEIF GETBIT(FLAG:潜伏,0)
	CALL MESSAGE_TALK(16)
ELSEIF TFLAG:描写中の子供
	CALL MESSAGE_TALK(14)
ELSEIF ((CFLAG:TARGET:就寝時間 - TIME:0 <= 30 && CFLAG:TARGET:就寝時間 - TIME:0 > 0) || (TCVAR:睡眠薬強度 >= 2 && !RAND:3)) && !FLAG:70
	CALL MESSAGE_TALK(13)
ELSEIF TALKER:0 && TALKER:1 && (!FLAG:70 || CFLAG:TARGET:344 == 1)
	CALL MESSAGE_TALK(2, TALKER:0, TALKER:1)
ELSEIF FLAG:宴会開催フラグ >= 98 && CFLAG:MASTER:現在位置 == TFLAG:宴会場
	CALL MESSAGE_TALK(1)
ELSEIF TALENT:MASTER:形状 == 4 && TIME > 1140 && CFLAG:MASTER:デート中 && (CFLAG:MASTER:現在位置 % 100) % 10 && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
	CALL MESSAGE_TALK(5,0,0)
ELSEIF CFLAG:MASTER:現在位置 == GET_MAP_REPLACEMENT(HATAKE)
	CALL MESSAGE_TALK(8)
;畑拡張実装までは暫定的に畑0で決め打ち
ELSEIF 家庭菜園可(CFLAG:MASTER:現在位置) && HATAKE_NAME:0 != "" && PLANT_TYPE > 0
	;家庭菜園情景描写
	CALL MESSAGE_TALK(10)
ELSEIF 物干し可(CFLAG:MASTER:現在位置) && TIME:5 < 2
	CALL MESSAGE_TALK(11)
ELSE
	RESULT = 0
	TRYCALLFORM MAP_UNIQUEMESSAGE_{MAIN_MAP}
	;どれにも引っかからなければ天気
	SIF OUTROOF(CFLAG:MASTER:現在位置) && !RESULT && !GROUPMATCH(TIME:5, 0, 2, 3, 6, 7, 10, 11)
		CALL MESSAGE_TALK(4)
	;SIF+コメント行は不具合を起こすので調整
	;SIF OUTROOF(CFLAG:MASTER:現在位置) && !RESULT
	;どれにも引っかからなければ天気
	;	CALL MESSAGE_TALK(4)
ENDIF
RETURN 0



;話者の決定処理
;渡されたTALKERを直接書き換える
@ANOTHER_TALK_SET_TALKER(TALKER)
#DIM REF     TALKER
#DIM DYNAMIC CHARA, キャラクタ数上限
#DIM DYNAMIC CNT_CHARA
VARSET TALKER
;対象キャラ選定
FOR LOCAL, 1, GET_TARGETNUM() + 1
	;MASTERの相手をしていたらスキップ
	SIF TARGET:LOCAL == TARGET:0
		CONTINUE
	SIF CFLAG:(TARGET:LOCAL):現在位置 != CFLAG:MASTER:現在位置
		CONTINUE
	;対象キャラID取得
	CHARA:(CNT_CHARA) = TARGET:LOCAL
	CNT_CHARA ++
NEXT
SIF CNT_CHARA < 2
	RETURN

LOCAL = RAND:(CNT_CHARA)
TALKER:0 = CHARA:LOCAL
ARRAYREMOVE CHARA, LOCAL, 1
CNT_CHARA --

;対象キャラ間の相性チェック
FOR LOCAL, 0, CNT_CHARA
	;相性の良いキャラが同室か
	SIF !RELATION:(TALKER:0):(CHARA:LOCAL)
		CONTINUE
	;相性を見て会話者２以上なら会話者を更新
	SIF TALKER:1 && RELATION:(TALKER:0):(TALKER:1) >= RELATION:(TALKER:0):(CHARA:LOCAL)
		CONTINUE
	TALKER:1 = CHARA:LOCAL
NEXT



;FileName_ANOTHER_TALK.ERB ------------------------- Rev1.03
;Info枠内メッセージ表示処理
;CALL		USER
;ARG		0:メッセージNO, 1:会話者１, 2:会話者２（メンバー会話時のみ）
;RETURN		VOID
;COMMENT	CASEの番号が若いほど出力優先度が高い
;-----------------------------------------------------------
@MESSAGE_TALK(ARG,ARG:1,ARG:2)
#DIMS 会場
#DIMS 子供の名前
#DIMS 子供の行動
#DIM CHARA
#DIM CHILDNUM
#DIM 同室
;機能チェック
;SIF !GETBIT(FLAG:3,2)
	;RETURN 0
PRINTFORM ◆
SELECTCASE ARG
	CASE 1
		IF CHK_DATENOW(CFLAG:MASTER:デート中) && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
			会場 = %DATENAME_PLACE()%
		ELSE
			会場 = %NAME_FROM_PLACE(TFLAG:宴会場)%
		ENDIF
		;参加者が居なければ表示されない
		IF FLAG:35 > 0
			;宴会の規模に応じて分岐
			IF FLAG:31 <= 1500 || FLAG:35 <= 3
				PRINTFORML %会場%では{FLAG:35}人程度のささやかな宴会が開かれている……
			ELSE
				PRINTFORML %会場%では{FLAG:35}人程度の宴会が開かれている……
			ENDIF
			;日を跨いだ場合
			IF DAY > FLAG:開始日
				;経過時間に応じて分岐
				IF TIME >= (FLAG:33 + 240) - 1440 * (DAY - FLAG:開始日)
					IF FLAG:35 <= 3
						PRINTFORML もはや残っているのは一部の猛者だけのようだ……
					ELSE
						PRINTFORML ちらほらと脱落者も出ているようだ……
					ENDIF
				ELSEIF TIME >= (FLAG:33 + 180) - 1440 * (DAY - FLAG:開始日)
					PRINTFORML 相当出来上がっているようだ……
				ELSEIF TIME >= (FLAG:33 + 60) - 1440 * (DAY - FLAG:開始日)
					PRINTFORML 笑い声と喧騒が途切れることはなく、盛り上がりを見せている……
				;まだ始まったばかり
				ELSE
					PRINTFORML なかなかの賑わいのようだ……
				ENDIF
			ELSE
				;経過時間に応じて分岐
				IF TIME >= (FLAG:33 + 240)
					IF FLAG:35 <= 3
						PRINTFORML もはや残っているのは一部の猛者だけのようだ……
					ELSE
						PRINTFORML ちらほらと脱落者も出ているようだ……
					ENDIF
				ELSEIF TIME >= (FLAG:33 + 180)
					PRINTFORML 相当出来上がっているようだ……
				ELSEIF TIME >= (FLAG:33 + 60)
					PRINTFORML 笑い声と喧騒が途切れることはなく、盛り上がりを見せている……
				;まだ始まったばかり
				ELSE
					PRINTFORML なかなかの賑わいのようだ……
				ENDIF
			ENDIF
		ELSE
			PRINTFORML 兵(つわもの)どもが夢の跡
			PRINTFORML %NAME_FROM_PLACE(TFLAG:宴会場)%は宴会によって荒れ果てている……
		ENDIF
	CASE 2
		;対話表示
		;どっちも相性高め
		IF RELATION:(ARG:1):(ARG:2) > 100 && RELATION:(ARG:2):(ARG:1) > 100
			IF TALENT:(ARG:1):恋慕 && TALENT:(ARG:2):恋慕
				PRINTFORML %CALLNAME:(ARG:1)%と%CALLNAME:(ARG:2)%は、うっとりとした表情でこちらを見つめてくる
			ELSEIF TALENT:(ARG:1):恋慕 || TALENT:(ARG:2):恋慕
				PRINTFORML \@ TALENT:(ARG:1):恋慕 ? %CALLNAME:(ARG:1)%は%CALLNAME:(ARG:2)% # %CALLNAME:(ARG:2)%は%CALLNAME:(ARG:1)% \@と談笑しつつ、熱っぽい視線を向けてきた
			ELSE
				IF RAND:2
					PRINTFORML \@ CFLAG:(ARG:1):310 > CFLAG:(ARG:2):310 ? %CALLNAME:(ARG:1)%は%CALLNAME:(ARG:2)% # %CALLNAME:(ARG:2)%は%CALLNAME:(ARG:1)% \@の頭を優しく撫でている
				ELSE
					PRINTFORML %CALLNAME:(ARG:1)%と%CALLNAME:(ARG:2)%は楽しそうに談笑している
				ENDIF
			ENDIF
		;どちらかが相性高め
		ELSEIF (RELATION:(ARG:1):(ARG:2) > 100 && RELATION:(ARG:2):(ARG:1) < 100) || (RELATION:(ARG:1):(ARG:2) < 100 && RELATION:(ARG:2):(ARG:1) > 100)
			IF TALENT:(ARG:1):恋慕 && TALENT:(ARG:2):恋慕
				PRINTFORML %CALLNAME:(ARG:1)%と%CALLNAME:(ARG:2)%は、会話しながら熱っぽい視線を向けてくる
			ELSEIF TALENT:(ARG:1):恋慕 || TALENT:(ARG:2):恋慕
				PRINTFORML \@ TALENT:(ARG:1):恋慕 ? %CALLNAME:(ARG:1)%は%CALLNAME:(ARG:2)% # %CALLNAME:(ARG:2)%は%CALLNAME:(ARG:1)% \@と話しながら、こちらを窺っている
			ELSE
				PRINTFORM \@ RELATION:(ARG:1):(ARG:2) > 100 ? %CALLNAME:(ARG:1)%の言葉に%CALLNAME:(ARG:2)% # %CALLNAME:(ARG:2)%の言葉に%CALLNAME:(ARG:1)% \@は
				PRINTDATA
					DATAFORM 不機嫌そうな表情を浮かべている…
					DATAFORM 明後日の方向を向いている…
					DATAFORM 適当に相槌を打っている…
				ENDDATA
			ENDIF
		;どちらも相性低め
		ELSEIF RELATION:(ARG:1):(ARG:2) < 100 && RELATION:(ARG:2):(ARG:1) < 100
			IF TALENT:(ARG:1):恋慕 && TALENT:(ARG:2):恋慕
				PRINTFORML %CALLNAME:(ARG:1)%と%CALLNAME:(ARG:2)%は、口論しつつ笑顔を向けてくる
			ELSEIF TALENT:(ARG:1):恋慕 || TALENT:(ARG:2):恋慕
				PRINTFORML \@ TALENT:(ARG:1):恋慕 ? %CALLNAME:(ARG:1)%は%CALLNAME:(ARG:2)% # %CALLNAME:(ARG:2)%は%CALLNAME:(ARG:1)% \@の話を聞き流し、手を振ってくる
			ELSE
				PRINTFORM %CALLNAME:(ARG:1)%と%CALLNAME:(ARG:2)%は
				PRINTDATA
					DATAFORM なにやら口論している…
					DATAFORM ムスッとしている…
					DATAFORM 喧嘩を始めた…
				ENDDATA
			ENDIF
		ENDIF
		PRINTL
	CASE 3
		PRINTFORML %CALLNAME:TARGET%はぐっすり眠っている…
	CASE 4
		PRINTS "   "
		IF TIME:7
			SELECTCASE TIME:7
				;虹
				CASE 1
					IF FLAG:70
						PRINTL 空にモノクロの虹が掛かっている…
					ELSE
						PRINTL 空に虹が掛かっている…
					ENDIF
				;ダイヤモンドダスト
				CASE 2
					PRINTL 雪の結晶が太陽光にキラキラと輝いている……息が白い…
				;流れ星
				CASE 3
					IF FLAG:70
						PRINTL 夜空に流星が留まっている…
					ELSE
						PRINTL 夜空に時折、流星が流れている…
					ENDIF
				;流星群
				CASE 4
					IF FLAG:70
						PRINTL 夜空に流星群が留まっている…
					ELSE
						PRINTL 夜空を埋め尽くす程の流星が流れ続けている…
					ENDIF
			ENDSELECT
		ELSE
			SELECTCASE TIME:5
				;お天道様
				CASE 1
					IF TIME:2 >= 2 && TIME:2 <= 4
						IF DAY:2 == 2
							PRINTL 雲一つない青空から太陽が容赦なく照り付けている…
						ELSE
							PRINTL 空は爽やかに晴れ渡っている
						ENDIF
					ENDIF
				;雨
				CASE 4
					IF FLAG:70
						PRINTL 雨粒が停止している…
					ELSE
						PRINTL しとしとと雨が降っている…
					ENDIF
				;大雨
				CASE 5
					IF FLAG:70
						PRINTL 雨粒が停止している…
					ELSE
						PRINTL 雨は激しく降りしきっている…
					ENDIF
				;雪
				CASE 8
					IF FLAG:70
						PRINTL 雪の結晶が停止している…
					ELSE
						PRINTL 雪がゆっくりと舞い降っている…息が白い…
					ENDIF
				;吹雪
				CASE 9
					IF FLAG:70
						PRINTL 雪の結晶が停止している…
					ELSE
						PRINTL 凍えそうな吹雪が吹き荒んでいる…熱い吐息すらも白に塗りつぶされそうだ…
					ENDIF
				;みぞれ
				CASE 12
					IF FLAG:70
						PRINTL 霙が停止している…
					ELSE
						PRINTL 冷たいみぞれが降っている…息が白い…
					ENDIF
				;あられ
				CASE 13
					IF FLAG:70
						PRINTL 霰の粒すら停止している…
					ELSE
						PRINTL 大粒のあられが吹き荒れている…息が白い…
					ENDIF
			ENDSELECT
		ENDIF
		PRINTL
	CASE 5
		PRINTFORML 暗い夜道も、%CALLNAME:MASTER%の光るペニスがあれば安心だ
	CASE 6
		IF !CHARA_HOLIDAY(67)
			;授業中
			IF TIME >= 480 && TIME <= 720 || TIME >= 780
				IF !FLAG:70
					PRINTFORML 寺小屋の子供達は%CALLNAME:TARGET%の\@ 充填率(TARGET,3) >= 10 ? %SHOW_BOTTOM(TARGET,1)%の中がどうなっているかも知らずに# \@授業を受けている…
				ELSEIF !CFLAG:MASTER:うふふ
					PRINTFORML 寺子屋の子供達は停止している…
				ELSE
					PRINTFORML %CALLNAME:TARGET%は寺子屋の子供達の前で襲われている…
				ENDIF
			ELSEIF TIME >= 720 && TIME <= 780 && !FLAG:70
				PRINTFORML 寺子屋の子供達は%CALLNAME:TARGET%と食事している…
			ENDIF
		ENDIF
	CASE 7
		IF CFLAG:MASTER:現在位置 == 210
			PRINTFORML %CALLNAME:(ARG:1)%の周りに人だかりができている
			IF 充填率(ARG:1,3) >= 10
				IF !RAND:10
					PRINTFORML %CALLNAME:MASTER%は彼女の表情に微かな強ばりを認めた　垂れてきたのだろうか？
				ELSE
					PRINTFORML 野次馬たちの知るところではないが、彼女のお腹には今も%CALLNAME:MASTER%の精液が\@ 充填率(ARG:1,3) >= 30 ? たっぷりと# \@溜まっているのだ
				ENDIF
			ENDIF
		ELSE
			IF FLAG:70
			ELSE
				PRINTL 広場の方が騒がしい
			ENDIF
		ENDIF
	CASE 8
		PRINTFORML %CALLNAME:(FLAG:地主)%の%STR_NOUMIN(FLAG:地主, "畑の種類")%がある
		SELECTCASE FLAG:畑仕事の成果
			CASE IS < 20
				PRINTFORML %STR_NOUMIN(FLAG:地主, "作物")%はまったくと言っていいほど育っていない
			CASE 20 TO 60
				PRINTFORML %STR_NOUMIN(FLAG:地主, "作物")%はあんまり育っていない
			CASE 61 TO 100
				PRINTFORML %STR_NOUMIN(FLAG:地主, "作物")%はそれなりによく育っている
			CASEELSE
				PRINTFORML %STR_NOUMIN(FLAG:地主, "作物")%が豊かに実っている
		ENDSELECT
	CASE 9
		;口上表示
		PRINTS "   "
		CALL KOJO_MESSAGE_SEND("INFO")
	CASE 10
		;家庭菜園情景描写
		PRINTFORM %HATAKE_NAME:0%
		IF PLANT_DRY > 3 || PLANT_GROW > 10
			PRINTL は枯れてしまっている
		ELSE
			SELECTCASE PLANT_GROW
				CASE 10
					IF HATAKE_NAME:0 == "ローター"
						PRINTFORM はたわわに実り振動音を立てている
					ELSE
						PRINT は熟し収穫の時を待っている
					ENDIF
				CASE 8,9
					PRINT が実り始めている
				CASE 6,7
					PRINT の花が咲いている
				CASE 4,5
					PRINT の葉が茂っている
				CASEELSE
					PRINT の苗は順調に育っている
			ENDSELECT
			IF PLANT_DRY == 2
				PRINT 　土が乾いてきている
			ELSEIF PLANT_DRY == 0
				SETCOLOR C_AQUA
				PRINT 　畑は十分に水分を吸収している
				RESETCOLOR
			ENDIF
			IF PLANT_FOOD
				SETCOLOR C_CREAM
				IF FLAG:天滴坤神
					PRINT 　畑は坤神の恵みを享けている
				ELSE
					PRINT 　畑は施肥されている
				ENDIF
				RESETCOLOR
			ENDIF
			PRINTL
		ENDIF
	CASE 11
		PRINTFORML 洗濯物が干されている
		;PRINTFORML その脇には短冊が吊るされた笹飾りがある
	CASE 12
		PRINTFORML そろそろ%CALLNAME:TARGET%の仕事の時間だ
	CASE 13
		PRINT 眠気を感じているのか、
		IF CFLAG:TARGET:うふふ
			PRINTFORML %CALLNAME:TARGET%は快感に没頭しきれていない様だ
		ELSE
			PRINTFORML %CALLNAME:TARGET%は欠伸をかみ殺している
		ENDIF
	CASE 14
		;自立後
		IF TFLAG:描写中の子供 > 1000
			CHARA = TFLAG:描写中の子供 % 1000
			CHILDNUM = TFLAG:描写中の子供 / 1000
		;育児中
		ELSE
			CHARA = TFLAG:描写中の子供
			CHILDNUM = TALENT:CHARA:育児中
		ENDIF
		子供の名前 = %CHILDNAME:CHARA:CHILDNUM%
		子供の行動 = %CHILD_ACT:CHARA:CHILDNUM%
		SELECTCASE 子供の行動
			CASE "睡眠"
				;幼少期以降は例外として自室外だと表示されず、フラグをオフにする（寝室に帰る描写があるため）
				IF CFLAG:CHARA:出産経過日 >= CHILD_語彙 && !自室自宅判定(CFLAG:MASTER:現在位置, CHARA)
					TFLAG:描写中の子供 = 0
				ELSE
					PRINTFORM %子供の名前%はすやすやと眠っている
				ENDIF
			CASE "起床"
				IF CFLAG:CHARA:出産経過日 < CHILD_語彙
					PRINTFORM %子供の名前%はお腹を空かせている
				ELSE
					PRINTDATA
						DATAFORM %子供の名前%はあくびをしている
						DATAFORM %子供の名前%は眠たそうにしている
						DATAFORM %子供の名前%は背伸びしている
						DATAFORM %子供の名前%は目をこすっている
						DATAFORM %子供の名前%は寝ぼけている
					ENDDATA
				ENDIF
			CASE "入浴"
				IF CFLAG:CHARA:出産経過日 < CHILD_語彙
					PRINTFORM %子供の名前%はお風呂に入れてもらってる
				ELSE
					PRINTDATA
						DATAFORM %子供の名前%は十かぞえている
						DATAFORM %子供の名前%は湯船ではしゃいでいる
						DATAFORM %子供の名前%は泡を作って遊んでいる
						DATAFORM %子供の名前%は潜って遊んでいる
						DATAFORM %子供の名前%は頭を洗ってもらっている
						DATAFORM %子供の名前%は身体を洗ってもらっている
					ENDDATA
				ENDIF
			CASE "食事"
				IF CFLAG:CHARA:出産経過日 < CHILD_離乳
					PRINTFORM %子供の名前%はおっぱいを吸っている
				ELSEIF CFLAG:CHARA:出産経過日 < CHILD_語彙
					PRINTFORM %子供の名前%は離乳食を口に咥えている
				ELSE
					SELECTCASE TIME
					CASE 300 TO 540 
						PRINTFORM %子供の名前%は朝ごはんを食べている
					CASE 660 TO 900
						PRINTFORM %子供の名前%はお昼ごはんを食べている
					CASE 1020 TO 1200
						PRINTFORM %子供の名前%は夕飯を食べている
					CASE 1200 TO 1440
						PRINTFORM %子供の名前%は夜ごはんを食べている
					CASE 0 TO 180
						PRINTFORM %子供の名前%は夜食を食べている
					CASEELSE
						PRINTFORM %子供の名前%はごはんを食べている
					ENDSELECT
				ENDIF
			CASE "玩具"
				IF CFLAG:CHARA:出産経過日 < CHILD_自立前
					PRINTFORM %子供の名前%は玩具で遊んでいる
				ELSE
					PRINTFORM %子供の名前%は玩具を見つめている
				ENDIF
			CASE "お絵かき"
				PRINTFORM %子供の名前%はお絵かきをしている
			CASE "遊び"
				PRINTDATA
					DATAFORM %子供の名前%は元気に走り回っている
					DATAFORM %子供の名前%は弾幕ごっこの真似事をしている
					DATAFORM %子供の名前%は飛び跳ねている
					DATAFORM %子供の名前%は転がっている
					DATAFORM %子供の名前%は駆け回っている
					DATAFORM %子供の名前%は踊り回っている
					DATAFORM %子供の名前%はチャンバラごっこをしている
					DATAFORM %子供の名前%ははしゃぎ回っている
					DATAFORM %子供の名前%はケンケンパしている
					DATAFORM %子供の名前%は縄跳びをしている
				ENDDATA
			CASE "勉強"
				PRINTFORM %子供の名前%は勉強している
			CASE "読書"
				PRINTFORM %子供の名前%は黙々と本を読み耽っている
			CASE "料理"
				PRINTDATA
					DATAFORM %子供の名前%はたどたどしく料理している
					DATAFORM %子供の名前%はぎこちなく料理をしている
					DATAFORM %子供の名前%はレシピとにらめっこしている
					DATAFORM %子供の名前%は慎重に食材を刻んでいる
					DATAFORM %子供の名前%は火加減を念入りに見ている
					DATAFORM %子供の名前%は目を凝らして計量している
				ENDDATA
			CASE "絵描き"
				PRINTDATA
					DATAFORM %子供の名前%は構想を練っている
					DATAFORM %子供の名前%は昔描いた絵を取り出した
					DATAFORM %子供の名前%は新しい紙を取り出した
					DATAFORM %子供の名前%は気ままに絵を描いている
					DATAFORM %子供の名前%は熱心に絵を描いている
					DATAFORM %子供の名前%は夢中になって絵を描いている
					DATAFORM %子供の名前%は鼻歌を歌いながら絵を描いている
					DATAFORM %子供の名前%はこちらを見ながら絵を描いている
					DATAFORM %子供の名前%は色を塗っている
				ENDDATA
			CASE "歌唱"
				PRINTDATA
					DATAFORM %子供の名前%は元気に歌っている
					DATAFORM %子供の名前%は気ままに歌っている
					DATAFORM %子供の名前%はなりきって歌っている
					DATAFORM %子供の名前%はうっとりして歌っている
					DATAFORM %子供の名前%は堂々と歌っている
					DATAFORM %子供の名前%はリズムに乗っている
					DATAFORM %子供の名前%は踊っている
					DATAFORM %子供の名前%は楽器を弾く真似をしている
					DATAFORM %子供の名前%はポーズを決めている
				ENDDATA
			CASE "掃除"
				PRINTFORM %子供の名前%は部屋の掃除をしている
			CASE "支度"
				PRINTFORM %子供の名前%は寺子屋へ行く支度をしている
			CASE "登校"
				PRINTFORM %子供の名前%が登校していく……
			CASE "授業"
				PRINTFORM %子供の名前%は授業を受けている
			CASE "帰宅"
				;寺子屋で見送った場合は表示されず、フラグをオフにする
				IF  !自室自宅判定(CFLAG:MASTER:現在位置, CHARA) && CFLAG:MASTER:現在位置 != CFLAG:CHARA:現在位置
					TFLAG:描写中の子供 = 0
				ELSE
					PRINTFORM %子供の名前%はカバンを降ろしている
				ENDIF
			CASE "仕事"
				PRINTFORM %子供の名前%は仕事に励んでいる
			CASE "散歩"
				PRINTDATA
					DATAFORM %子供の名前%は誰かと話している
					DATAFORM %子供の名前%は異性の人物と並んでいる
					DATAFORM %子供の名前%は佇んでいる
					DATAFORM %子供の名前%はお茶をすすっている
					DATAFORM %子供の名前%はあくびをしている
					DATAFORM %子供の名前%は手帳に何かを書いている
					DATAFORM %子供の名前%は辺りを見回している
					DATAFORM %子供の名前%は眠そうにしている
					DATAFORM %子供の名前%はこちらの様子を伺っている
				ENDDATA
			;その他の行動,ここでCHILD_ACTを決めちゃう
			CASEELSE
				IF CFLAG:MASTER:現在位置 == CFLAG:CHARA:現在位置
					同室 = 1
				ELSE
					同室 = 0
				ENDIF
				;描写の抽選と固定
				IF LOCAL:1 != TIME || LOCAL:2 != 同室
					IF CFLAG:CHARA:出産経過日 < CHILD_語彙
						LOCAL = RAND:16
					ELSEIF CFLAG:CHARA:出産経過日 < CHILD_自立前
						IF 同室
							LOCAL = RAND:24
						ELSE
							LOCAL = RAND:14
						ENDIF
					ELSE
						IF 同室
							LOCAL = RAND:26
						ELSE
							LOCAL = RAND:11
						ENDIF
					ENDIF
					LOCAL:1 = TIME
					LOCAL:2 = 同室
				ENDIF
				IF CFLAG:CHARA:出産経過日 < CHILD_語彙
					SELECTCASE LOCAL
					CASE 1, 2, 3, 4
						PRINTFORM %子供の名前%は%CALLNAME:CHARA%に抱かれている
						CHILD_ACT:CHARA:CHILDNUM = 幼児期_抱かれる
					CASE 5, 6
						PRINTFORM %子供の名前%は%CALLNAME:CHARA%の腕の中で眠たそうにしている
						CHILD_ACT:CHARA:CHILDNUM = 幼児期_眠たそう
					CASE 7, 8
						PRINTFORM %子供の名前%は%CALLNAME:CHARA%の腕の中で大人しくしている
						CHILD_ACT:CHARA:CHILDNUM = 幼児期_大人しい
					CASE 9, 10, 11, 12
						PRINTFORM %CALLNAME:CHARA%に抱かれた%子供の名前%がこちらを見ている
						CHILD_ACT:CHARA:CHILDNUM = 幼児期_こちらを見る
					CASE 13
						PRINTFORM %CALLNAME:CHARA%に抱かれた%子供の名前%は何かを訴えている
						CHILD_ACT:CHARA:CHILDNUM = 幼児期_何かを訴える
					CASE 14
						PRINTFORM %子供の名前%は何かを目で追っている
						CHILD_ACT:CHARA:CHILDNUM = 幼児期_何かを目で追う
					CASEELSE
						PRINTFORM %CALLNAME:CHARA%は%子供の名前%をあやしている
						CHILD_ACT:CHARA:CHILDNUM = 幼児期_あやされる
					ENDSELECT
				ELSEIF CFLAG:CHARA:出産経過日 < CHILD_自立前
					IF 同室
						SELECTCASE LOCAL
						CASE 1, 2
							PRINTFORM %CALLNAME:CHARA%と%子供の名前%は楽しそうに話している
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_会話
						CASE 3, 4
							PRINTFORM %CALLNAME:CHARA%は%子供の名前%に何かを教えている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_教わる
						CASE 5, 6
							PRINTFORM %CALLNAME:CHARA%は%子供の名前%に話をしている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_会話
						CASE 7, 8
							PRINTFORM %CALLNAME:CHARA%は%子供の名前%をしつけている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_しつけられる
						CASE 9
							PRINTFORM %子供の名前%は%CALLNAME:CHARA%と遊んでいる
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_親子遊び
						CASE 10
							PRINTFORM %子供の名前%は%CALLNAME:CHARA%と熱心に話をしている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_会話
						CASE 11
							PRINTFORM %子供の名前%は一人で遊んでいる
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_一人遊び
						CASE 12
							PRINTFORM %子供の名前%は大人しくしている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_大人しい
						CASE 13
							PRINTFORM %子供の名前%は考え事をしている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_考え事
						CASE 14
							PRINTFORM %子供の名前%は眠そうにしている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_眠そう
						CASE 15
							PRINTFORM %子供の名前%ははしゃいでいる
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_はしゃぐ
						CASE 16
							PRINTFORM %子供の名前%がうろちょろしている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_うろちょろ
						CASE 17
							PRINTFORM %子供の名前%はうずうずしている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_うずうず
						CASE 18
							PRINTFORM %子供の名前%はこちらをじっと見つめている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_見つめる
						CASE 19, 20
							PRINTFORM %子供の名前%は%CALLNAME:MASTER%と遊ぼうとしている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_遊ぼう
						CASE 21, 22
							PRINTFORM %子供の名前%がぺたぺたと触ってくる
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_ぺたぺた
						CASEELSE
							PRINTFORM %子供の名前%は%CALLNAME:MASTER%にかまってほしいようだ
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_かまって
						ENDSELECT
					;単独
					ELSE
						SELECTCASE LOCAL
						CASE 1
							PRINTFORM %子供の名前%は一人で遊んでいる
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_一人遊び
						CASE 2
							PRINTFORM %子供の名前%は大人しくしている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_大人しい
						CASE 3
							PRINTFORM %子供の名前%は考え事をしている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_考え事
						CASE 4
							PRINTFORM %子供の名前%は眠そうにしている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_眠そう
						CASE 5
							PRINTFORM %子供の名前%ははしゃいでいる
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_はしゃぐ
						CASE 6
							PRINTFORM %子供の名前%がうろちょろしている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_うろちょろ
						CASE 7
							PRINTFORM %子供の名前%はうずうずしている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_うずうず
						CASE 8
							PRINTFORM %子供の名前%はこちらをじっと見つめている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_見つめる
						CASE 9, 10
							PRINTFORM %子供の名前%は%CALLNAME:MASTER%と遊ぼうとしている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_遊ぼう
						CASE 11, 12
							PRINTFORM %子供の名前%がぺたぺたと触ってくる
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_ぺたぺた
						CASEELSE
							PRINTFORM %子供の名前%は%CALLNAME:MASTER%にかまってほしいようだ
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_かまって
						ENDSELECT
					ENDIF
				ELSE
					IF 同室
						SELECTCASE LOCAL
						CASE 1, 2
							PRINTFORM %CALLNAME:CHARA%と%子供の名前%は楽しそうに話している
							CHILD_ACT:CHARA:CHILDNUM = 自立前_会話
						CASE 3, 4
							PRINTFORM %CALLNAME:CHARA%は%子供の名前%に何かを教えている
							CHILD_ACT:CHARA:CHILDNUM = 自立前_教わる
						CASE 5, 6
							PRINTFORM %CALLNAME:CHARA%は%子供の名前%に話をしている
							CHILD_ACT:CHARA:CHILDNUM = 自立前_会話
						CASE 7
							PRINTFORM %CALLNAME:CHARA%は%子供の名前%をしつけている
							CHILD_ACT:CHARA:CHILDNUM = 自立前_しつけられる
						CASE 8
							PRINTFORM %CALLNAME:CHARA%はこちらを見ながら%子供の名前%とひそひそ話をしている
							CHILD_ACT:CHARA:CHILDNUM = 自立前_ひそひそ話
						CASE 9
							PRINTFORM %子供の名前%は%CALLNAME:CHARA%に文句を言っている
							CHILD_ACT:CHARA:CHILDNUM = 自立前_文句
						CASE 10
							PRINTFORM %子供の名前%は%CALLNAME:CHARA%に主張している
							CHILD_ACT:CHARA:CHILDNUM = 自立前_主張
						CASE 11
							PRINTFORM %子供の名前%は%CALLNAME:CHARA%と笑っている
							CHILD_ACT:CHARA:CHILDNUM = 自立前_笑い合う
						CASE 12, 13
							PRINTFORM %子供の名前%は%CALLNAME:CHARA%に教えを請いている
							CHILD_ACT:CHARA:CHILDNUM = 自立前_教えて
						CASE 14, 15
							PRINTFORM %子供の名前%は%CALLNAME:CHARA%の手伝いをしている
							CHILD_ACT:CHARA:CHILDNUM = 自立前_手伝い
						CASE 16, 17
							PRINTFORM %子供の名前%は一人で遊んでいる
							CHILD_ACT:CHARA:CHILDNUM = 自立前_一人遊び
						CASE 18
							PRINTFORM %子供の名前%は考え事をしている
							CHILD_ACT:CHARA:CHILDNUM = 自立前_考え事
						CASE 19
							PRINTFORM %子供の名前%は眠そうにしている
							CHILD_ACT:CHARA:CHILDNUM = 自立前_眠そう
						CASE 20, 21, 22, 23
							PRINTFORM %子供の名前%は%CALLNAME:MASTER%と話したいようだ
							CHILD_ACT:CHARA:CHILDNUM = 自立前_話したい
						CASE 24
							PRINTFORM %子供の名前%は%CALLNAME:MASTER%を気にしている
							CHILD_ACT:CHARA:CHILDNUM = 自立前_気にする
						CASE 25
							PRINTFORM %子供の名前%は%CALLNAME:MASTER%に遠慮している
							CHILD_ACT:CHARA:CHILDNUM = 自立前_遠慮
						CASEELSE
							PRINTFORM %子供の名前%が鋭い目でこちらを見ている
							CHILD_ACT:CHARA:CHILDNUM = 自立前_睨む
						ENDSELECT
					;単独
					ELSE
						SELECTCASE LOCAL
						CASE 1, 2
							PRINTFORM %子供の名前%は一人で遊んでいる
							CHILD_ACT:CHARA:CHILDNUM = 自立前_一人遊び
						CASE 3, 4
							PRINTFORM %子供の名前%は考え事をしている
							CHILD_ACT:CHARA:CHILDNUM = 自立前_考え事
						CASE 5
							PRINTFORM %子供の名前%は眠そうにしている
							CHILD_ACT:CHARA:CHILDNUM = 自立前_眠そう
						CASE 6, 7, 8, 9
							PRINTFORM %子供の名前%は%CALLNAME:MASTER%と話したいようだ
							CHILD_ACT:CHARA:CHILDNUM = 自立前_話したい
						CASE 10
							PRINTFORM %子供の名前%は%CALLNAME:MASTER%を気にしている
							CHILD_ACT:CHARA:CHILDNUM = 自立前_気にする
						CASE 11
							PRINTFORM %子供の名前%は%CALLNAME:MASTER%に遠慮している
							CHILD_ACT:CHARA:CHILDNUM = 自立前_遠慮
						CASEELSE
							PRINTFORM %子供の名前%が鋭い目でこちらを見ている
							CHILD_ACT:CHARA:CHILDNUM = 自立前_睨む
						ENDSELECT
					ENDIF
				ENDIF
		ENDSELECT
		PRINTL 
	CASE 15
		IF !CHARA_HOLIDAY(30) && BETWEENTIME(540, 719)
			IF !FLAG:70
				IF RAND:2
					PRINTFORML %CALLNAME:30%は\@ 充填率(30,3) >= 10 ? %SHOW_BOTTOM(TARGET,1)%の中をぐしょぐしょにしながら、# \@%TEXTR("厳かに/粛々と/しめやかに")%裁判を進めている…
				ELSE
					PRINTFORML \@ 充填率(30,3) >= 30 ? たっぷりと# \@\@ 充填率(30,3) >= 10 ? お腹に精液を溜めた # \@%CALLNAME:30%は%TEXTR("神妙な顔つき/厳粛な雰囲気の中/")%で裁判を進めている…
				ENDIF
			ELSEIF !CFLAG:MASTER:うふふ
				PRINTFORML 裁判は停止中だ…
			ELSE
				PRINTFORML %CALLNAME:TARGET%は死者の魂の前で襲われている…
			ENDIF
		ENDIF
	CASE 16
		CALL 潜伏描写
	;落とし穴描写
	CASE 17
		;穴掘り実行中の妖精が居るか見る
		同室 = 0
		LOCAL = 5, 6, 7, 14, 111
		FOR COUNT, 0, 5
			SIF CFLAG:MASTER:現在位置 != CFLAG:(LOCAL:COUNT):現在位置
				CONTINUE
			SIF Activity_Type:(LOCAL:COUNT) != 16 || !Activity_Duration:(LOCAL:COUNT)
				CONTINUE
			同室 ++
		NEXT
		;あなたが落ちた
		IF FA16_HOLE:1 == 2 && FA16_HOLE:2 == TIME
			IF 同室
				PRINTFORML イタズラ好きの妖精\@ 同室 > 1 ? 達 # \@がこちらを指差して大笑いしている…
			ELSEIF GET_TARGETNUM() > 0
				PRINTFORM 近くにいた%CALLNAME:TARGET%が%CALLNAME:MASTER%を見て
				SELECTCASE CFLAG:TARGET:性格傾向
					CASE 1, 5
						PRINTFORML クスクスと笑っている…
					CASE 2
						PRINTFORML 鼻で笑っている…
					CASE 3
						PRINTFORML \@ TARGET == 57 && !TALENT:恋慕 ? ひょっとこの面を揺らし # 楽しそうに笑っ \@ている…
					CASE 4
						PRINTFORML こらえきれずに吹き出している…
				ENDSELECT
			ELSE
				PRINTFORML 落とし穴は妖精達のイタズラに違いない…他にもいくつかあるようだ
			ENDIF
		;穴掘り中
		ELSEIF 同室
			PRINTFORML イタズラ好きの妖精\@ 同室 > 1 ? 達 # \@が何かやっている…
		ELSE
			PRINTDATA
				DATAFORM 地面が不自然に盛り上がっている…
				DATAFORM 土を掘り返した跡がわずかに残っている…
				DATAFORM 周りより妙に\@ DAY:2 == 4 ? 雪の積もった # 落ち葉の多い \@場所がある…
			ENDDATA
			PRINTDATAL
				DATAFORM 面白いものが見られそうだ
				DATAFORM 何かが起きそうだ
			ENDDATA
		ENDIF
	;にゅーぷらんの宣伝
	CASE 18
		PRINTFORM 銭湯に看板が立っている……『にゅーぷらん　
		IF DAY < 8
			PRINTFORML ぐらんどおーぷん』
		ELSE
			SELECTCASE ((GETTIME()/1000000000) % 100) % 3
				CASE 0
					PRINTFORML ９０ぷん　１００００えん～』
				CASE 1
					PRINTFORML しんじんたすう』
				CASE 2
					PRINTFORML めいろうかいけい』
			ENDSELECT
		ENDIF
ENDSELECT
;子供の描写がないならフラグをオフ,情景描写から呼ばれてるなら除外
SIF ARG != 14 && !GROUPMATCH(ARG, 1, 6, 15, 7)
	TFLAG:描写中の子供 = 0
;-----------------------------------------------------------------------------------------------------------------
@情景描写
#DIM KROOM_DESC
VARSET LOCAL
;口上内私室描写をチェック
KROOM_DESC = 0
IF CFLAG:MASTER:お招き
	KROOM_DESC = CFLAG:MASTER:お招き
ELSEIF PRIVATEROOM:(CFLAG:MASTER:現在位置 % 100)
	KROOM_DESC = PRIVATEROOM:(CFLAG:MASTER:現在位置 % 100)
ENDIF
IF KROOM_DESC
	RESULTS =
	;口上の存在判定 and RESULTSに文字列代入
	IF CFLAG:KROOM_DESC:口上セレクタ
		TRYCALLFORM M_KOJO_K{NO:KROOM_DESC}_{CFLAG:KROOM_DESC:口上セレクタ}
	ELSE
		TRYCALLFORM M_KOJO_K{NO:KROOM_DESC}
	ENDIF
	TRYCCALLFORM K{KROOM_DESC}%RESULTS%_ROOM_DESCRIPTION
	;	PRINTFORML あったよ
		RETURN
	CATCH
	;	PRINTFORML ないよ
	;関数がないなら続行
	ENDCATCH
ENDIF
;自由行動：落とし穴の描写
SIF CFLAG:MASTER:現在位置 == FA16_HOLE:0
	CALL MESSAGE_TALK(17)
;付近で宴会をやっているなら賑やかな様子が見える
IF FLAG:宴会開催フラグ >= 98 && CAN_MOVE(CFLAG:MASTER:現在位置, TFLAG:宴会場) >= 2
	CALL MESSAGE_TALK(1)
;けーね先生の寺小屋情景文章
ELSEIF CFLAG:MASTER:現在位置 == 240 && CFLAG:67:行動 == 5
		CALL MESSAGE_TALK(6)
;えーき様の裁判風景描写文章
ELSEIF ((CFLAG:MASTER:現在位置 == 640 && MAIN_MAP != 6) || (CFLAG:MASTER:現在位置 == 612 && MAIN_MAP == 6)) && CFLAG:30:行動 == 5
		CALL MESSAGE_TALK(15)
	;ELSEIF CFLAG:MASTER:うふふ && GET_TARGETNUM() > 1
	;	CALL EASY_TARGET_CHANGER
ELSEIF GROUPMATCH(CFLAG:MASTER:現在位置,210,220,230,250,270) 
	FOR LOCAL,1,CHARANUM
		IF CFLAG:LOCAL:行動 == 5 && CFLAG:LOCAL:現在位置 == 広場
			LOCAL:1 ++
			LOCAL:2 = LOCAL
		ENDIF
	NEXT
	SIF LOCAL:1
		CALL MESSAGE_TALK(7,LOCAL:2)
ELSEIF CFLAG:MASTER:現在位置 == GET_MAP_REPLACEMENT(934) && FLAG:周回数
	CALL MESSAGE_TALK(18)
ENDIF